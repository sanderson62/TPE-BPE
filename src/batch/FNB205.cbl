       IDENTIFICATION DIVISION.
       PROGRAM-ID.    FNB205.

      *:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
      *    DATE    BY  MODIFICATION                                            :
      * ========== === ========================================================:
      * 02/15/1999 DAN TO PRODUCTION                                           :
      * 03/19/2003 DJN CONVERT FROM MAINFRAME TO MICROFOCUS COBOL.             :
      * 07/28/2006 DJN DO NOT ADVANCE TO NEW PAGE ON FIRST PAGE OF REPORTS.    :
082506* 08/25/2006 AJR REMOVE FROM CLAIMS SYSTEM.                              :
      *:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

           SELECT REPORT-FILES
               ASSIGN TO EXTERNAL SYS010
               ORGANIZATION IS LINE SEQUENTIAL
               FILE STATUS IS SYS010-STATUS.

           SELECT SUMMARY-REPORT
               ASSIGN TO EXTERNAL SYS007
               ORGANIZATION IS LINE SEQUENTIAL.

082506**** PARM FILE
082506     SELECT PARM-FILE        ASSIGN       TO EXTERNAL IPARM
082506                             ORGANIZATION IS LINE SEQUENTIAL
082506                             FILE STATUS  IS PARM-STATUS.
082506

       DATA DIVISION.
       FILE SECTION.

       FD  REPORT-FILES
           LABEL RECORDS ARE STANDARD
           RECORDING MODE IS F
           RECORD CONTAINS 133 CHARACTERS
           BLOCK CONTAINS 0 RECORDS.

       01  REPORT-RECORD       PIC X(133).

       FD  SUMMARY-REPORT
           LABEL RECORDS ARE STANDARD
           RECORDING MODE IS F
           RECORD CONTAINS 132 CHARACTERS
           BLOCK CONTAINS 0 RECORDS.

       01  SUMMARY-RECORD      PIC X(132).

082506**** PARM FILE
082506 FD  PARM-FILE
082506     LABEL RECORDS ARE STANDARD
082506     RECORDING MODE IS F
082506     BLOCK CONTAINS 0 RECORDS.
082506 01  PARM.
082506     05  PARM-CYCLE-DATE PIC X(10) VALUE SPACES.
082506

       WORKING-STORAGE SECTION.

       01 RC                       PIC S9(9)   COMP-5 VALUE 0.
       01 LIST-REC                 PIC X(132).

       01  FILLER USAGE IS PACKED-DECIMAL.
           05  PAGE-COUNT          PIC S9(3)     VALUE +0.
           05  LINE-COUNT          PIC S9(3)     VALUE +0.

       01  FILLER.
           05  SYS010-STATUS       PIC XX     VALUE '00'.
               88  EOF                        VALUE '10'.
082506     05  PARM-STATUS         PIC XX     VALUE SPACES.
           05  WS-SYS-DATE         PIC 9(8)   VALUE ZERO.
           05  WS-ZERO             PIC X(15)  VALUE '            .00'.
           05  WS-TEMP             PIC X(25).
           05  SAVE-TABLE OCCURS 300 TIMES INDEXED BY SAVE-INDEX, I, J.
               10  SAVE-ACCT  PIC X(10)    VALUE HIGH-VALUES.
               10  SAVE-AMT   PIC X(15)    VALUE SPACE.

       01  FILLER.
           05  WS-CYCLE-DATE       PIC X(10)     VALUE SPACES.

082506 01  WORK-DATE.
082506     05  WRK-YR    PIC 9999.
082506     05  WRK-MO    PIC 99.
082506     05  WRK-DAY   PIC 99.
082506
082506 01  SYSTEM-DATE.
082506     05  SYS-MO    PIC 99.
082506     05  SYS-DAY   PIC 99.
082506     05  SYS-YR    PIC 9999.

       01  HEADING-LINE-1.
           05  FILLER     PIC X(6)  VALUE 'DATE: '.
           05  HDG-DATE1  PIC X(10) VALUE SPACE.
           05  FILLER     PIC X(20) VALUE SPACES.
           05  FILLER     PIC X(7)  VALUE 'AS OF: '.
           05  HDG-DATE2  PIC XX/XX/XXXX  VALUE SPACE.

       01  HEADING-LINE-2.
           05  PIC X(41)
               VALUE 'SUSPENSE ACCOUNT SUMMARY BALANCING REPORT'.
       01  HEADING-LINE-3.
           05  PIC X(32) VALUE 'DISTRIBUTION: CORPORATE FINANCE '.
       01  HEADING-LINE-4.
           05  PIC X(36)
               VALUE 'ACCOUNT NUMBER               BALANCE'.
       01  HEADING-LINE-5.
           05  PIC X(36)
               VALUE '**************       ***************'.

       01  DETAIL-LINE.
           05  DET-ACCT  PIC X(10)  VALUE SPACE.
           05  FILLER    PIC X(11)  VALUE SPACE.
           05  DET-AMT   PIC X(15)  VALUE SPACE.

082506*    EXEC SQL INCLUDE ISTDWORK.INC END-EXEC.

       PROCEDURE DIVISION.

           PERFORM 000-HOUSEKEEPING

           PERFORM UNTIL EOF
             READ REPORT-FILES
             IF NOT EOF
               IF REPORT-RECORD(25:16) = 'FREEDOM ACCOUNT:'
                 PERFORM 100-SAVE-ACCOUNT
               END-IF
               IF REPORT-RECORD(04:25) = 'REPORT TRANSACTION TOTAL:'
                 MOVE REPORT-RECORD(30:15) TO SAVE-AMT (SAVE-INDEX)
               END-IF
             END-IF
           END-PERFORM

           PERFORM 200-PRINT-SUMMARY

082506     CLOSE  REPORT-FILES
082506            SUMMARY-REPORT.
082506     CALL 'FNBLIST' USING 'C' LIST-REC.
082506     CLOSE  PARM-FILE.

082506*    GOBACK GIVING RC.
082506     STOP RUN GIVING RC.

       100-SAVE-ACCOUNT.

           SET SAVE-INDEX TO +1
           SEARCH SAVE-TABLE
             AT END
               DISPLAY 'TABLE OVERFLOW IN ROUTINE 100-SAVE-ACCOUNT'
                       UPON SYSERR
               MOVE    'TABLE OVERFLOW IN ROUTINE 100-SAVE-ACCOUNT'
                       TO LIST-REC
               CALL 'FNBLIST' USING 'W' LIST-REC
               MOVE 16 TO RC
               GOBACK GIVING RC
             WHEN SAVE-ACCT (SAVE-INDEX) = REPORT-RECORD(42:10)
               CONTINUE
             WHEN SAVE-ACCT (SAVE-INDEX) = HIGH-VALUES
               MOVE REPORT-RECORD(42:10) TO SAVE-ACCT (SAVE-INDEX)
           END-SEARCH.

       200-PRINT-SUMMARY.

           PERFORM 800-SORT-TABLE

           PERFORM 210-PRINT-HEADING

           PERFORM VARYING SAVE-INDEX FROM 1 BY 1
                    UNTIL SAVE-ACCT (SAVE-INDEX) = HIGH-VALUE

             MOVE SAVE-ACCT (SAVE-INDEX) TO DET-ACCT
             IF SAVE-AMT (SAVE-INDEX) = SPACE
               MOVE WS-ZERO TO DET-AMT
             ELSE
               MOVE SAVE-AMT (SAVE-INDEX) TO DET-AMT
             END-IF
             WRITE SUMMARY-RECORD FROM DETAIL-LINE
                   AFTER ADVANCING 1 LINE
             ADD +1 TO LINE-COUNT
             IF LINE-COUNT > +56
               PERFORM 210-PRINT-HEADING
             END-IF

           END-PERFORM.

       210-PRINT-HEADING.

           ADD 1 TO PAGE-COUNT

           IF PAGE-COUNT = 1
             WRITE SUMMARY-RECORD FROM HEADING-LINE-1
                 AFTER ADVANCING 0 LINES
           ELSE
             WRITE SUMMARY-RECORD FROM HEADING-LINE-1
                 AFTER ADVANCING PAGE
           END-IF
           WRITE SUMMARY-RECORD FROM HEADING-LINE-2
                 AFTER ADVANCING 1 LINE
           WRITE SUMMARY-RECORD FROM HEADING-LINE-3
                 AFTER ADVANCING 2 LINES
           WRITE SUMMARY-RECORD FROM HEADING-LINE-4
                 AFTER ADVANCING 3 LINES
           WRITE SUMMARY-RECORD FROM HEADING-LINE-5
                 AFTER ADVANCING 1 LINE
           MOVE SPACES TO SUMMARY-RECORD
           WRITE SUMMARY-RECORD AFTER ADVANCING 1 LINE
           MOVE +9 TO LINE-COUNT.

       000-HOUSEKEEPING.

082506     CALL 'FNBLIST' USING 'O' LIST-REC
082506*    CALL IGETPARM USING IG-P1
082506*    MOVE IG-P1 TO WS-CYCLE-DATE
082506*
082506*    MOVE WS-CYCLE-DATE TO IV-P2
082506*    MOVE 'MM/DD/YEAR'  TO IV-P3
082506*    INITIALIZE            IV-P4
082506*    CALL IVERDATE USING IV-P1 IV-P2 IV-P3 IV-P4
082506*    IF IV-P1 = 'Y'
082506     OPEN INPUT PARM-FILE.
082506     IF PARM-STATUS NOT = '00'
082506       STRING  'ERROR OPENING PARM FILE, STATUS = '
082506              PARM-STATUS
082506         DELIMITED BY SIZE INTO LIST-REC
082506       CALL 'FNBLIST' USING 'W' LIST-REC
082506       CALL 'FNBLIST' USING 'C' LIST-REC
082506       MOVE 16 TO RC
082506       STOP RUN GIVING RC
082506     END-IF.
082506     READ PARM-FILE.
082506     IF PARM-CYCLE-DATE GREATER THAN SPACES
082506       MOVE PARM-CYCLE-DATE TO WS-CYCLE-DATE
             MOVE WS-CYCLE-DATE TO HDG-DATE1
           ELSE
             MOVE SPACES TO LIST-REC
             STRING '*** INVALID DATE CARD: ' WS-CYCLE-DATE
               DELIMITED BY SIZE INTO LIST-REC
             CALL 'FNBLIST' USING 'W' LIST-REC
082506       CALL 'FNBLIST' USING 'C' LIST-REC
             MOVE 16 TO RC
082506*      GOBACK GIVING RC
082506       STOP RUN GIVING RC
           END-IF

082506     ACCEPT WORK-DATE FROM DATE YYYYMMDD
082506     MOVE WRK-YR  TO SYS-YR
082506     MOVE WRK-MO  TO SYS-MO
082506     MOVE WRK-DAY TO SYS-DAY
082506
082506     MOVE SYSTEM-DATE TO WS-SYS-DATE
082506*    CALL 'SYSDATE' USING WS-SYS-DATE
           MOVE WS-SYS-DATE TO HDG-DATE2

           OPEN INPUT REPORT-FILES
           OPEN OUTPUT SUMMARY-REPORT.

       800-SORT-TABLE.
      *
      * THIS ROUTINE SORTS THE TABLE INTO ACCOUNT NUMBER ORDER
      * (I AND J ARE INDEXES FOR THE TABLE)
      *
           PERFORM VARYING I FROM 1 BY 1
                   UNTIL SAVE-ACCT (I) = HIGH-VALUE
             IF SAVE-ACCT (I) > SAVE-ACCT (I + 1)
               MOVE SAVE-TABLE (I) TO WS-TEMP
               MOVE SAVE-TABLE (I + 1) TO SAVE-TABLE (I)
               MOVE WS-TEMP TO SAVE-TABLE (I + 1)
               PERFORM VARYING J FROM I BY -1
                 UNTIL J = 1 OR SAVE-ACCT (J) NOT < SAVE-ACCT(J - 1)
                 MOVE SAVE-TABLE (J) TO WS-TEMP
                 MOVE SAVE-TABLE (J - 1) TO SAVE-TABLE (J)
                 MOVE WS-TEMP TO SAVE-TABLE (J - 1)
               END-PERFORM
             END-IF
           END-PERFORM.
