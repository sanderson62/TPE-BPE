       IDENTIFICATION DIVISION.
       PROGRAM-ID. PEMCRC2B.
       AUTHOR.     PABLO
       DATE-COMPILED.
      *REMARKS
      *  PROGRAM READS A BALLARD EXTRACT, MATCHES IT TO A CERT,
      *  IF THE CERT IS CANCELLED THEN THE REFUND AMOUNT IS USED FOR 
      *  THE O/P RECORD, IF THE CERT IS NOT THEN THE PROGRAM 
      *  CALCULATES A REFUND BASED ON A BALLARD DATE PROVIDED
      *  ON THE I/P FILE
       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

           SELECT  CERT-IN       ASSIGN TO CERTIN.

           SELECT ELCNTL           ASSIGN TO ELCNTL
                                   ORGANIZATION IS INDEXED
                                   ACCESS IS DYNAMIC
                                   RECORD KEY IS CF-CONTROL-PRIMARY
                                   FILE STATUS IS ELCNTL-FILE-STATUS.

           SELECT ERACCT           ASSIGN TO ERACCT
                                   ORGANIZATION IS INDEXED
                                   ACCESS IS DYNAMIC
                                   RECORD KEY IS AM-CONTROL-PRIMARY
                                   FILE STATUS IS ERACCT-FILE-STATUS.

           SELECT  BALL-IN       ASSIGN TO BALLIN.
           SELECT  BALL-OUT      ASSIGN TO BALLOT
            ORGANIZATION IS LINE SEQUENTIAL.
       EJECT
       DATA DIVISION.
       FILE SECTION.

00136  FD  ELCNTL.                                                      EL517
00137                                  COPY ELCCNTL.                    EL517
00138      EJECT                                                        EL517
00139  FD  ERACCT.                                                      EL517
00140                                  COPY ERCACCT.                    EL517
00141      EJECT                                                        EL517
       FD  CERT-IN
           RECORDING MODE F
           LABEL RECORDS STANDARD
           BLOCK CONTAINS 0 RECORDS.

                                   COPY ECSCRT01.
       FD  BALL-IN
           RECORDING MODE F
           LABEL RECORDS STANDARD
           BLOCK CONTAINS 0 RECORDS.
       01  BALLARD-REC.
           05  FILLER                      PIC X(6).
           05  BALLI-STATE                 PIC XX.
           05  BALLI-EFF-DT                PIC X(8).
           05  BALLI-CERT-NO               PIC X(11).
           05  BAL-PAY-DATE                PIC X(8).
           05  FILLER                      PIC X(5).


       FD  BALL-OUT
           RECORDING MODE F
           LABEL RECORDS STANDARD
           BLOCK CONTAINS 0 RECORDS.
       01  BALL-OUT-RECORD         PIC X(196).

       EJECT
       WORKING-STORAGE SECTION.
       77  FILLER  PIC X(32) VALUE '********************************'.
       77  FILLER  PIC X(32) VALUE '   PEMCRC2  WORKING-STORAGE    '.
       77  FILLER  PIC X(32) VALUE '********************************'.

00197  77  WS-ZERO                PIC S9        COMP-3   VALUE ZERO.    EL517
00198  77  WS-RETURN-CODE         PIC S9(4)     COMP     VALUE ZERO.    EL517
       77  WS-EOF-SW1                  PIC X VALUE SPACES.
           88  END-OF-BALL               VALUE 'Y'.
           88  MORE-BALL                 VALUE ' '.
       77  WS-EOF-SW2                  PIC X VALUE SPACES.
           88  END-OF-CERT               VALUE 'Y'.
           88  MORE-CERT                 VALUE ' '.
       77  SUB3                        PIC S999 COMP-3 VALUE +0.
00199  77  WS-ABEND-MESSAGE            PIC X(80) VALUE SPACES.          EL517
00200  77  WS-ABEND-FILE-STATUS        PIC XX    VALUE ZERO.            EL517
       77  BALL-RECS-IN                PIC 9(9) VALUE ZEROS.
       77  CERT-RECS-IN            PIC 9(9) VALUE ZEROS.
       77  BALL-RECS-OUT           PIC 9(9) VALUE ZEROS.
       77  BALL-RECS-FIX           PIC 9(9) VALUE ZEROS.
       77  BALL-RECS-MATCH         PIC 9(9) VALUE ZEROS.
       01  TEXAS-REGS-WORK-AREAS.
           12  TEX-FACT-1              PIC S9(7)V9(2)    COMP-3.
           12  TEX-FACT-2              PIC S9(3)   COMP-3.
           12  TEX-FACT-3              PIC S9(3)   COMP-3.
           12  TEX-FACT-4              PIC S9(7)   COMP-3.
           12  TEX-FACT-5              PIC S9(3)   COMP-3.
           12  TEX-FACT-6              PIC S9(3)   COMP-3.
           12  TEX-FACT-7              PIC S9(7)   COMP-3.
           12  TEX-FACT-8              PIC S9V9(6) COMP-3.
           12  TEX-FACT-9              PIC S9(4)V9(11)   COMP-3.

       01  NET-PAY-INTERFACE.
           05  NP-APR                  PIC S9(3)V9(4)    COMP-3.
           05  NP-ORIG                 PIC S9(3)         COMP-3.
           05  NP-REM                  PIC S9(3)         COMP-3.
           05  NP-OPT                  PIC X(01).
           05  NP-CAP                  PIC S9(3)         COMP-3.
           05  NP-FACTOR               PIC S9(4)V9(9)    COMP-3.
           05  NP-WORK1                PIC S9(6)V9(9)    COMP-3.
           05  NP-WORK2                PIC S9(6)V9(9)    COMP-3.

       01  WS-MISC-AREA.
082103     12  LF-BAL-REMTERM1         PIC S999V99  COMP-3  VALUE +0.
082103     12  LF-BAL-REMTERM2         PIC S999V99  COMP-3  VALUE +0.
           12  LF-REM-TRM1             PIC S999V99  COMP-3  VALUE +0.
           12  AH-REM-TRM1             PIC S999V99  COMP-3  VALUE +0.
           12  LF-REM-TRM2             PIC S999V99  COMP-3  VALUE +0.
           12  AH-REM-TRM2             PIC S999V99  COMP-3  VALUE +0.
           12  REM-TRM1                PIC S9(3)V99    COMP-3.
           12  REM-TRM2                PIC S9(3)V99    COMP-3.
           12  WS-INIT-BALL            PIC X(196)   VALUE SPACES.

       01  WS-OUT-RECORD.
           12  WS-BALL-IN          PIC X(40).
           12  WS-TAB1             PIC X.
           12  WS-BALL-ACCOUNT     PIC X(10).
           12  WS-TAB2             PIC X.
           12  WS-BALL-RTBL        PIC XXX.
           12  WS-TAB3             PIC X.
           12  WS-BALL-ID          PIC X.
           12  WS-TAB4             PIC X.
           12  WS-BALL-BATCH-NO    PIC X(10).
           12  WS-TAB5             PIC X.
           12  WS-BALL-REC-DATE    PIC X(10).
           12  WS-TAB6             PIC X.
           12  WS-BALL-DUE-DATE    PIC X(10).
           12  WS-TAB7             PIC X.
           12  WS-BALL-STATUS      PIC X.
           12  WS-TAB8             PIC X.
           12  WS-BALL-LF-CNC-AMT  PIC ZZZ,ZZ9.99.
           12  WS-TAB9             PIC X.
           12  WS-BALL-LF-RTRM     PIC ZZ9.
           12  WS-TAB10            PIC X.
           12  WS-BALL-LF-RAMT     PIC ZZZ,ZZ9.99.
           12  WS-TAB11            PIC X.
           12  WS-BALL-LF-HOW      PIC X(12).
           12  WS-TAB12            PIC X.
           12  WS-BALL-LF-BEN      PIC XX.
           12  WS-TAB13            PIC X.
           12  WS-BALL-LF-RATE     PIC Z9.99999.
           12  WS-TAB14            PIC X.
           12  WS-BALL-AH-CNC-AMT  PIC ZZZ,ZZ9.99.
           12  WS-TAB15            PIC X.
           12  WS-BALL-AH-RTRM     PIC ZZ9.
           12  WS-TAB16            PIC X.
           12  WS-BALL-AH-RAMT     PIC ZZZ,ZZ9.99.
           12  WS-TAB17            PIC X.
           12  WS-BALL-AH-HOW      PIC X(12).
           12  WS-TAB18            PIC X.
           12  WS-BALL-AH-BEN      PIC XX.
           12  WS-TAB19            PIC X.
           12  WS-BALL-AH-RATE     PIC Z9.99999.
           12  WS-TAB20            PIC X.
           12  WS-BALL-RTRM-DAYS   PIC X.
           

       01  MISC-WS.
           12  WS-LF-RFND              PIC S9(7)V99 VALUE +0 COMP-3.
           12  WS-AH-RFND              PIC S9(7)V99 VALUE +0 COMP-3.
           12  INTERMED                PIC S9(9)V9(6)  COMP-3.
           12  WS-REM-AMT              PIC S9(11)V99 COMP-3 VALUE +0.
           12  WS-LF-REFUND            PIC S9(9)V99 COMP-3 VALUE +0.
           12  WS-DATE-ALPH.
               16  FILLER             PIC XXX VALUE '000'.
               16  WS-WORK-CENT       PIC XX.
               16  WS-WORK-YR         PIC XX.
               16  WS-WORK-MO         PIC XX.
               16  WS-WORK-DA         PIC XX.
           12  WS-DATE-NUM REDEFINES WS-DATE-ALPH
                                      PIC 9(11).
           12  WS-VAL-BIN-DATE          PIC XX      VALUE LOW-VALUES.
           12  WS-VAL-DATE              PIC X(8).
           12  WS-VAL-DATE-NUM REDEFINES WS-VAL-DATE PIC 9(8).
           12  WS-BIN-CR-DT             PIC XX  VALUE LOW-VALUES.
           12  ELCNTL-KEY.
               16  CNTL-COMP-ID         PIC XXX     VALUE SPACES.
               16  CNTL-REC-TYPE        PIC X       VALUE SPACE.
               16  CNTL-ACCESS          PIC X(4)    VALUE SPACES.
               16  CNTL-SEQ-NO          PIC S9(4)    COMP VALUE +0.

           12  ERACCT-KEY.
               16  ACCT-COMP-CD         PIC X     VALUE LOW-VALUES.
               16  ACCT-CARRIER         PIC X     VALUE SPACES.
               16  ACCT-GROUPING        PIC X(06) VALUE SPACES.
               16  ACCT-STATE           PIC X(02) VALUE SPACES.
               16  ACCT-ACCOUNT         PIC X(10) VALUE SPACES.
               16  ACCT-EXP-DT          PIC X(02) VALUE LOW-VALUES.

           12  WS-LF-SPECIAL-CALC-CD   PIC X   VALUE SPACES.
           12  WS-LF-EARNINGS-CALC     PIC X   VALUE SPACES.
           12  WS-LF-REFUND-CALC       PIC X   VALUE SPACES.
           12  WS-LF-COVERAGE-TYPE     PIC X   VALUE SPACES.
           12  WS-LF-REM-TERM          PIC X   VALUE SPACES.
           12  WS-AH-SPECIAL-CALC-CD   PIC X   VALUE SPACES.
           12  WS-AH-EARNINGS-CALC     PIC X   VALUE SPACES.
           12  WS-AH-REFUND-CALC       PIC X   VALUE SPACES.
           12  WS-AH-COVERAGE-TYPE     PIC X   VALUE SPACES.
           12  WS-AH-REM-TERM          PIC X   VALUE SPACES.
           12  ERACCT-FILE-STATUS      PIC XX  VALUE LOW-VALUES.
           12  ELCNTL-FILE-STATUS      PIC XX  VALUE LOW-VALUES.
           12  WS-CRT-KEY.
               16  WS-CRT-STATE        PIC XX.   
               16  WS-CRT-EFF-DT       PIC 9(11).
               16  WS-CRT-CRT-NO       PIC X(11).
           12  WS-BAL-KEY.
               16  WS-BAL-STATE        PIC XX.   
               16  WS-BAL-EFF-DT       PIC 9(11).
               16  WS-BAL-CRT-NO       PIC X(11).
           12  WS-WORK-DATE            PIC 9(11).
           12  FILLER REDEFINES WS-WORK-DATE.
               16  FILLER              PIC XXX.
               16  WS-WORK-CCYY        PIC X(4).
               16  WS-WORK-MM          PIC XX.
               16  WS-WORK-DD          PIC XX.

                                       COPY ELCCALC.

                                       COPY ELCDATE.

       PROCEDURE DIVISION.

       0000-MAIN.

           PERFORM 3600-OPEN-FILES     THRU 3600-EXIT

           PERFORM 0600-INITIALIZE     THRU 0600-EXIT

           PERFORM 0100-PROCESS-BALL   THRU 0100-EXIT UNTIL
                 END-OF-BALL

           PERFORM 0500-CLOSE-FILES    THRU 0500-EXIT

           DISPLAY ' BALL RECORDS READ    ' BALL-RECS-IN
           DISPLAY ' CERT RECORDS READ    ' CERT-RECS-IN
           DISPLAY ' BALL RECORDS WRITTEN ' BALL-RECS-OUT
           DISPLAY ' BALL RECORDS FIXED   ' BALL-RECS-FIX
           DISPLAY ' BALL RECORDS MATCHED ' BALL-RECS-MATCH
           GOBACK

           .
       0100-PROCESS-BALL.

           IF WS-CRT-KEY = WS-BAL-KEY
              ADD 1                    TO BALL-RECS-MATCH
              PERFORM 0275-COMPARE-CNC THRU 0275-EXIT
              PERFORM 0250-READ-BALL      THRU 0250-EXIT
           ELSE
              IF WS-CRT-KEY < WS-BAL-KEY
                 PERFORM 0200-READ-CERT THRU 0200-EXIT
              ELSE
                 DISPLAY ' NO MATCH ' WS-BAL-KEY '  ' WS-CRT-KEY
                 PERFORM 0250-READ-BALL THRU 0250-EXIT
              END-IF
           END-IF

           .

       0100-EXIT.
           EXIT.

       0200-READ-CERT.

           READ CERT-IN AT END
                SET END-OF-CERT TO TRUE
           END-READ

           IF MORE-CERT
              ADD 1 TO CERT-RECS-IN
              MOVE CR-STATE         TO WS-CRT-STATE   
              MOVE CR-DT            TO WS-CRT-EFF-DT
              MOVE CR-CERT-NO       TO WS-CRT-CRT-NO
           END-IF

           .

       0200-EXIT.
           EXIT.

       0250-READ-BALL.

           READ BALL-IN AT END
                SET END-OF-BALL TO TRUE
           END-READ

           IF MORE-BALL
              ADD 1 TO BALL-RECS-IN
              MOVE BALLI-STATE      TO WS-BAL-STATE  
              MOVE BALLI-EFF-DT     TO WS-DATE-ALPH (4:8)
              MOVE WS-DATE-NUM      TO WS-BAL-EFF-DT
              MOVE BALLI-CERT-NO    TO WS-BAL-CRT-NO
           END-IF

           .

       0250-EXIT.
           EXIT.

       0275-COMPARE-CNC.
      *    IF CR-CERT-NO = '0484824013 '
      *       DISPLAY ' FOUND CERT ' CR-CERT-NO
      *    END-IF
      *    IF CR-CERT-NO = '0000000239 '
      *       DISPLAY ' FOUND CERT ' CR-CERT-NO
      *    END-IF

           MOVE WS-INIT-BALL           TO WS-OUT-RECORD
           MOVE +0                     TO WS-LF-RFND
                                          WS-AH-RFND
           MOVE CR-ACCOUNT             TO WS-BALL-ACCOUNT
           MOVE CR-REIN-TABLE          TO WS-BALL-RTBL
           IF CR-LFTYP = '00' OR '  '
              CONTINUE
           ELSE
              MOVE CR-LFTYP               TO WS-BALL-LF-BEN
              IF CR-LFRFND > +0
                 MOVE 'R'                 TO WS-BALL-STATUS
              ELSE
                 IF CR-DTHAMT > +0
                    MOVE 'D'              TO WS-BALL-STATUS
                 ELSE
                    MOVE ' '              TO WS-BALL-STATUS
                 END-IF
              END-IF
              MOVE BAL-PAY-DATE           TO WS-VAL-DATE
              PERFORM 0276-DO-LIFE        THRU 0276-EXIT
              MOVE CP-CALC-REFUND         TO WS-BALL-LF-CNC-AMT
                                             WS-LF-RFND
           END-IF
           
           IF CR-AHTYP = '  ' OR '00'
              CONTINUE
           ELSE   
              MOVE CR-AHTYP               TO WS-BALL-AH-BEN
              IF CR-AHRFND > +0
                 MOVE 'R'                 TO WS-BALL-STATUS
              ELSE
                 IF CR-DISAMT > +0
                    MOVE 'H'              TO WS-BALL-STATUS
                 ELSE
                    MOVE ' '              TO WS-BALL-STATUS
                 END-IF
              END-IF
              MOVE BAL-PAY-DATE           TO WS-VAL-DATE
              PERFORM 0277-DO-AH          THRU 0277-EXIT
              MOVE CP-CALC-REFUND         TO WS-BALL-AH-CNC-AMT
                                             WS-AH-RFND
           END-IF   

           IF CR-STATE = 'CT' OR 'DE' OR 'FL' OR 'IL' OR 'IN'
                      OR 'IA' OR 'KS' OR 'LA' OR 'ME' OR 'MD'
                      OR 'MI' OR 'MN' OR 'MO' OR 'MT' OR 'NE'
                      OR 'NH' OR 'NJ' OR 'NC' OR 'OH' OR 'OK'
                      OR 'SD' OR 'TN' OR 'TX' OR 'VT' OR 'VA'
                      OR 'WA' OR 'WV' OR 'WI' OR 'WY'
              IF (WS-LF-RFND + WS-AH-RFND) < +1.00
                 MOVE 'M'              TO WS-BALL-STATUS
              END-IF
           ELSE
            IF CR-STATE = 'AR' OR 'MS'
               IF (WS-LF-RFND + WS-AH-RFND) < +2.00
                  MOVE 'M'             TO WS-BALL-STATUS
               END-IF
            ELSE
             IF CR-STATE = 'NV' OR 'NM' OR 'SC'
                IF (WS-LF-RFND + WS-AH-RFND) < +3.00
                   MOVE 'M'            TO WS-BALL-STATUS
                END-IF
             ELSE
              IF CR-STATE = 'AK' OR 'AZ' OR 'CA' OR 'CO' OR 'ID'
                         OR 'ND' OR 'OR' OR 'UT'
                 IF (WS-LF-RFND + WS-AH-RFND) < +5.00
                    MOVE 'M'           TO WS-BALL-STATUS
                 END-IF
              ELSE
               IF CR-STATE = 'GA' OR 'PA'
                 IF (WS-LF-RFND + WS-AH-RFND) < +10.00
                    MOVE 'M'           TO WS-BALL-STATUS
                 END-IF
               END-IF
              END-IF
             END-IF
            END-IF
           END-IF

           MOVE BALLARD-REC         TO WS-BALL-IN
           PERFORM 3500-WRITE-BALL  THRU 3500-EXIT

           .

       0275-EXIT.
           EXIT.
       0276-DO-LIFE.

           MOVE 'CID'                  TO CNTL-COMP-ID.
           MOVE SPACES                 TO CNTL-ACCESS.
           MOVE '4'                    TO CNTL-REC-TYPE.
           MOVE +0                     TO CNTL-SEQ-NO.
           MOVE CR-LFTYP               TO CNTL-ACCESS (3:2)

           PERFORM 9000-READ-CONTROL   THRU 9000-EXIT
           PERFORM VARYING SUB3 FROM 1 BY 1 UNTIL
                  (SUB3 GREATER 8) OR
                  (CF-BENEFIT-CODE (SUB3) = CNTL-ACCESS (3:2))
           END-PERFORM

           MOVE CF-LF-COVERAGE-TYPE (SUB3)
                                       TO WS-LF-COVERAGE-TYPE
           MOVE CF-SPECIAL-CALC-CD   (SUB3)
                                       TO WS-LF-SPECIAL-CALC-CD.
           MOVE CF-CO-EARNINGS-CALC  (SUB3)
                                       TO WS-LF-EARNINGS-CALC
                                          WS-LF-REFUND-CALC

           IF CF-CO-REFUND-CALC (SUB3) GREATER '0'
              MOVE CF-CO-REFUND-CALC (SUB3)
                                       TO WS-LF-REFUND-CALC
           END-IF
           MOVE CF-CO-REM-TERM-CALC (SUB3)
                                       TO WS-LF-REM-TERM
           PERFORM 0300-FIND-STATE     THRU 0300-EXIT
           IF CF-ST-RT-CALC > '0'
              MOVE CF-ST-RT-CALC       TO WS-LF-REM-TERM
           END-IF
           PERFORM VARYING SUB3 FROM 1 BY 1 UNTIL
              (SUB3 GREATER 50) OR
              ((CF-ST-BENEFIT-CD (SUB3) = CR-LFTYP) AND
               (CF-ST-BENEFIT-KIND (SUB3) = 'L'))
           END-PERFORM

           IF SUB3 > 50
              CONTINUE
           ELSE
              IF CF-ST-REFUND-CALC  (SUB3)   GREATER '0'
                 MOVE CF-ST-REFUND-CALC (SUB3)   TO WS-LF-REFUND-CALC
              END-IF
              IF CF-ST-REM-TERM-CALC (SUB3) > '0'
                 MOVE CF-ST-REM-TERM-CALC (SUB3) TO WS-LF-REM-TERM
              END-IF
           END-IF

           PERFORM 0400-GET-ERACCT THRU 0400-EXIT

           IF WS-LF-COVERAGE-TYPE = 'R'
              IF AM-EARN-METHOD-R = 'R'
                 MOVE '1'             TO WS-LF-REFUND-CALC
              END-IF
           END-IF
           IF WS-LF-COVERAGE-TYPE = 'R'
              IF AM-EARN-METHOD-R = 'P'
                 MOVE '2'             TO WS-LF-REFUND-CALC
              END-IF
           END-IF
           IF WS-LF-COVERAGE-TYPE = 'L' OR 'P'
              IF AM-EARN-METHOD-R = 'R'
                 MOVE '1'             TO WS-LF-REFUND-CALC
              END-IF
           END-IF
           IF WS-LF-COVERAGE-TYPE = 'L' OR 'P'
              IF AM-EARN-METHOD-R = 'P'
                 MOVE '2'             TO WS-LF-REFUND-CALC
              END-IF
           END-IF
           IF WS-LF-COVERAGE-TYPE = 'R'
              IF AM-EARN-METHOD-R = 'A'
                 MOVE '6'             TO WS-LF-REFUND-CALC
              END-IF
           END-IF
           IF WS-LF-COVERAGE-TYPE = 'R'
              IF AM-EARN-METHOD-R = 'M'
                 MOVE '8'             TO WS-LF-REFUND-CALC
              END-IF
           END-IF
           IF WS-LF-COVERAGE-TYPE = 'L' OR 'P'
              IF AM-EARN-METHOD-R = 'A'
                 MOVE '6'             TO WS-LF-REFUND-CALC
              END-IF
           END-IF
           IF WS-LF-COVERAGE-TYPE = 'L' OR 'P'
              IF AM-EARN-METHOD-R = 'M'
                 MOVE '8'             TO WS-LF-REFUND-CALC
              END-IF
           END-IF

           PERFORM 3720-GET-LF-REFUND THRU 3720-EXIT

           IF CR-LFPRM = CP-CALC-REFUND
              MOVE 'FULL '               TO  WS-BALL-LF-HOW
           ELSE
           EVALUATE TRUE
              WHEN CP-R-AS-R78
                 MOVE 'RULE 78'          TO  WS-BALL-LF-HOW
              WHEN  CP-R-AS-PRORATA
                 MOVE 'PRO RATA'         TO  WS-BALL-LF-HOW
              WHEN CP-R-AS-CALIF
                 MOVE 'CALIF'            TO  WS-BALL-LF-HOW
              WHEN CP-R-AS-TEXAS
                 MOVE 'IRREG'            TO  WS-BALL-LF-HOW
              WHEN CP-REFUND-TYPE-USED = 'S'
                 MOVE 'UTAH'             TO  WS-BALL-LF-HOW
              WHEN CP-R-AS-FARM-PLAN
                 MOVE 'FARM PLAN'        TO  WS-BALL-LF-HOW
              WHEN CP-R-AS-NET-PAY
                 MOVE 'NET PAY'          TO  WS-BALL-LF-HOW
              WHEN CP-R-AS-ANTICIPATION
                 MOVE 'ANTICIPATION'     TO  WS-BALL-LF-HOW
                 MOVE CP-PREMIUM-RATE    TO  WS-BALL-LF-RATE
              WHEN CP-R-AS-MEAN
                 MOVE 'MEAN'             TO  WS-BALL-LF-HOW
              WHEN CP-R-AS-SUM-OF-DIGITS
                 MOVE 'SUM OF DIGIT'     TO  WS-BALL-LF-HOW
           END-EVALUATE
           END-IF

           .
       0276-EXIT.
           EXIT.

       0277-DO-AH.

           MOVE 'CID'                  TO CNTL-COMP-ID.
           MOVE SPACES                 TO CNTL-ACCESS.
           MOVE '5'                    TO CNTL-REC-TYPE.
           MOVE +0                     TO CNTL-SEQ-NO.
           MOVE CR-AHTYP               TO CNTL-ACCESS (3:2)

           PERFORM 9000-READ-CONTROL   THRU 9000-EXIT
           PERFORM VARYING SUB3 FROM 1 BY 1 UNTIL
                  (SUB3 GREATER 8) OR
                  (CF-BENEFIT-CODE (SUB3) = CNTL-ACCESS (3:2))
           END-PERFORM

           MOVE CF-SPECIAL-CALC-CD   (SUB3)
                                       TO WS-AH-SPECIAL-CALC-CD.
           MOVE CF-CO-EARNINGS-CALC  (SUB3)
                                       TO WS-AH-EARNINGS-CALC
                                          WS-AH-REFUND-CALC

           IF CF-CO-REFUND-CALC (SUB3) GREATER '0'
              MOVE CF-CO-REFUND-CALC (SUB3)
                                       TO WS-AH-REFUND-CALC
           END-IF
           MOVE CF-CO-REM-TERM-CALC (SUB3)
                                       TO WS-AH-REM-TERM
           PERFORM 0300-FIND-STATE     THRU 0300-EXIT
           IF CF-ST-RT-CALC > '0'
              MOVE CF-ST-RT-CALC       TO WS-AH-REM-TERM
           END-IF
           PERFORM VARYING SUB3 FROM 1 BY 1 UNTIL
              (SUB3 GREATER 50) OR
              ((CF-ST-BENEFIT-CD (SUB3) = CR-AHTYP) AND
               (CF-ST-BENEFIT-KIND (SUB3) = 'A'))
           END-PERFORM

           IF SUB3 > 50
              CONTINUE
           ELSE
              IF CF-ST-REFUND-CALC  (SUB3)   GREATER '0'
                 MOVE CF-ST-REFUND-CALC (SUB3)   TO WS-AH-REFUND-CALC
              END-IF
              IF CF-ST-REM-TERM-CALC (SUB3) > '0'
                 MOVE CF-ST-REM-TERM-CALC (SUB3) TO WS-AH-REM-TERM
              END-IF
           END-IF

           IF CR-LFTYP NOT = '  ' AND '00'
              CONTINUE
           ELSE
              PERFORM 0400-GET-ERACCT THRU 0400-EXIT
           END-IF

           IF AM-EARN-METHOD-A = 'R'
              MOVE '1'                 TO WS-AH-REFUND-CALC
           END-IF
           IF AM-EARN-METHOD-A = 'P'
              MOVE '2'                 TO WS-AH-REFUND-CALC
           END-IF
           IF AM-EARN-METHOD-A = 'C'
              MOVE '3'                 TO WS-AH-REFUND-CALC
           END-IF
           IF AM-EARN-METHOD-A = 'A'
              MOVE '6'                 TO WS-AH-REFUND-CALC
           END-IF
           IF AM-EARN-METHOD-A = 'M'
              MOVE '8'                 TO WS-AH-REFUND-CALC
           END-IF
           IF AM-EARN-METHOD-A = 'N'
              MOVE '5'                 TO WS-AH-REFUND-CALC
           END-IF
           IF AM-EARN-METHOD-A = 'S'
              MOVE '9'                 TO WS-AH-REFUND-CALC
           END-IF

           PERFORM 3740-GET-AH-REFUND THRU 3740-EXIT

           IF CR-AHPRM = CP-CALC-REFUND
              MOVE 'FULL '               TO  WS-BALL-AH-HOW
           ELSE
           EVALUATE TRUE
              WHEN CP-R-AS-R78
                 MOVE 'RULE 78'          TO  WS-BALL-AH-HOW
              WHEN CP-R-AS-PRORATA
                 MOVE 'PRO RATA'         TO  WS-BALL-AH-HOW
              WHEN CP-REFUND-TYPE-USED = ('S' OR '3')
                 MOVE 'CALIF'            TO  WS-BALL-AH-HOW
              WHEN CP-R-AS-TEXAS
                 MOVE 'IRREG'            TO  WS-BALL-AH-HOW
              WHEN CP-R-AS-FARM-PLAN
                 MOVE 'FARM PLAN'        TO  WS-BALL-AH-HOW
              WHEN CP-R-AS-NET-PAY
                 MOVE 'NET PAY'          TO  WS-BALL-AH-HOW
              WHEN CP-R-AS-ANTICIPATION
                 MOVE 'ANTICIPATION'     TO  WS-BALL-AH-HOW
                 MOVE CP-PREMIUM-RATE    TO  WS-BALL-AH-RATE
              WHEN CP-R-AS-MEAN
                 MOVE 'MEAN'             TO  WS-BALL-AH-HOW
              WHEN CP-R-AS-SUM-OF-DIGITS
                 MOVE 'SUM OF DIGIT'     TO  WS-BALL-AH-HOW
           END-EVALUATE
           END-IF

           .
       0277-EXIT.
           EXIT.

       0300-FIND-STATE.

           MOVE 'CID'                  TO CNTL-COMP-ID.
           MOVE SPACES                 TO CNTL-ACCESS.
           MOVE '3'                    TO CNTL-REC-TYPE.
           MOVE +0                     TO CNTL-SEQ-NO.
           MOVE CR-STATE               TO CNTL-ACCESS

           PERFORM 9000-READ-CONTROL   THRU 9000-EXIT

           .
       0300-EXIT.
           EXIT.
       0400-GET-ERACCT.

           MOVE X'04'         TO AM-COMPANY-CD
           MOVE CR-CARRIER    TO AM-CARRIER
           MOVE '000000'      TO AM-GROUPING
           MOVE CR-STATE      TO AM-STATE
           MOVE CR-ACCOUNT    TO AM-ACCOUNT
           MOVE CR-DT         TO AM-EXPIRE-DT
           START ERACCT KEY IS NOT < AM-CONTROL-PRIMARY
           IF ERACCT-FILE-STATUS NOT = '00'
              DISPLAY 'ERACCT START ' ERACCT-FILE-STATUS
           END-IF
           READ ERACCT NEXT RECORD

           .

       0400-EXIT.
           EXIT.

       3720-GET-LF-REFUND.

02906      MOVE CR-DT                 TO  DC-GREG-DATE-CYMD.            ECS010
02907      MOVE 'L'                   TO  DC-OPTION-CODE.               ECS010
02908      PERFORM 8500-DATE-CONVERT  THRU 8500-EXIT                    ECS010
02909      MOVE DC-BIN-DATE-1         TO  CP-CERT-EFF-DT                ECS010
02910                                                                   ECS010
02906      MOVE WS-VAL-DATE-NUM       TO  DC-GREG-DATE-CYMD.            ECS010
02907      MOVE 'L'                   TO  DC-OPTION-CODE.               ECS010
02908      PERFORM 8500-DATE-CONVERT  THRU 8500-EXIT                    ECS010
02909      MOVE DC-BIN-DATE-1         TO  CP-VALUATION-DT               ECS010
                                          WS-VAL-BIN-DATE
02910                                                                   ECS010
           IF CR-LOAN-1ST-PMT-DT IS NOT = ZEROS
              MOVE CR-LOAN-1ST-PMT-DT  TO  DC-GREG-DATE-1-YMD
              MOVE '3'                 TO  DC-OPTION-CODE
              PERFORM 8500-DATE-CONVERT
                                       THRU 8500-EXIT
              MOVE DC-BIN-DATE-1       TO  CP-FIRST-PAY-DATE
           ELSE
              MOVE CP-CERT-EFF-DT      TO  DC-BIN-DATE-1
              MOVE +1                  TO  DC-ELAPSED-MONTHS
              MOVE +0                  TO  DC-ELAPSED-DAYS
              MOVE '6'                 TO  DC-OPTION-CODE
              PERFORM 8500-DATE-CONVERT
                                       THRU 8500-EXIT
              MOVE DC-BIN-DATE-2       TO  CP-FIRST-PAY-DATE
           END-IF
           MOVE CR-LF-TERM             TO CP-ORIGINAL-TERM
      *    COMPUTE CP-REMAINING-TERM = CR-LF-TERM - 2
      *    MOVE CP-REMAINING-TERM-1    TO CP-REMAINING-TERM.

           PERFORM 0220-CALC-LF-REM-TERM THRU 0220-EXIT
           MOVE '2'                    TO CP-PROCESS-TYPE
           MOVE WS-LF-EARNINGS-CALC    TO CP-EARNING-METHOD.
           MOVE CR-LFAMT               TO CP-ORIGINAL-BENEFIT.
           MOVE CR-APR                 TO CP-LOAN-APR.
           MOVE CR-PMT-FREQ            TO CP-PAY-FREQUENCY.

082103     IF (WS-LF-EARNINGS-CALC = 'B')
082103        AND (WS-LF-SPECIAL-CALC-CD NOT = 'L')
082103        MOVE LF-BAL-REMTERM2     TO CP-REMAINING-TERM
082103     ELSE
082103        MOVE LF-REM-TRM2         TO CP-REMAINING-TERM
082103     END-IF

082103     MOVE CP-REMAINING-TERM      TO WS-BALL-LF-RTRM

           IF CP-REMAINING-TERM > ZERO
              PERFORM 2000-CALC-REM-AMT
                                       THRU 2999-CALC-REM-AMT-X
              MOVE WS-REM-AMT          TO WS-BALL-LF-RAMT
           END-IF

      *    PERFORM 7500-GET-REMAIN-AMOUNT THRU 7500-EXIT.

      *    COMPUTE CP-REMAINING-AMT =
      *      CR-LFAMT - 1000.00

           MOVE CR-STATE               TO CP-STATE-STD-ABBRV
           MOVE WS-LF-REFUND-CALC      TO CP-EARNING-METHOD.
           MOVE WS-LF-EARNINGS-CALC    TO CP-RATING-METHOD.
           MOVE CR-RATING-CLASS        TO CP-CLASS-CODE.
           MOVE CR-LFTYP               TO CP-BENEFIT-CD.
           MOVE CR-LFAMT               TO CP-RATING-BENEFIT-AMT

           IF CP-STATE-STD-ABBRV = 'OR'
              COMPUTE CP-RATING-BENEFIT-AMT = CR-LFAMT +
                                            CR-LFAMT-ALT
           END-IF
           MOVE CR-LFPRM               TO CP-ORIGINAL-PREMIUM.
           MOVE CR-LFPRM-ALT           TO CP-ALTERNATE-PREMIUM.
           MOVE CR-AGE                 TO CP-ISSUE-AGE.
           MOVE '000'                  TO CP-DEVIATION-CODE.
           MOVE +0                     TO CP-RATE-DEV-PCT.

082103     IF (WS-LF-EARNINGS-CALC = 'B')
082103        AND (WS-LF-SPECIAL-CALC-CD NOT = 'L')
082103        MOVE LF-BAL-REMTERM1     TO CP-REMAINING-TERM
082103     ELSE
082103        MOVE LF-REM-TRM1         TO CP-REMAINING-TERM
082103     END-IF

082103*    MOVE LF-REM-TRM1            TO CP-REMAINING-TERM
           IF CR-STATE = 'OH'
              MOVE WS-LF-EARNINGS-CALC
                                       TO CP-EARNING-METHOD
           END-IF
           MOVE 'CID'                  TO CP-COMPANY-ID
           MOVE 'L'                    TO CP-LIFE-OVERRIDE-CODE
           PERFORM 7600-GET-REFUND THRU 7600-EXIT
           MOVE CP-CALC-REFUND         TO WS-LF-REFUND
06168      IF WS-LF-EARNINGS-CALC = 'B'                                 ELC50PD
082103*      IF WS-LF-SPECIAL-CALC-CD = 'L'                             ELC50PD
082103*         ADD +1                   TO                             ELC50PD
082103*                                   CP-REMAINING-TERM             ELC50PD
082103*                                   CP-ORIGINAL-TERM              ELC50PD
082103*      END-IF                                                     ELC50PD
06176        MOVE 'L'                    TO CP-BENEFIT-TYPE             ELC50PD
06177        MOVE '2'                    TO CP-EARNING-METHOD           ELC50PD
06178                                     CP-RATING-METHOD              ELC50PD
06179        MOVE CR-LFAMT-ALT           TO CP-ORIGINAL-BENEFIT         ELC50PD
06180                                     CP-REMAINING-BENEFIT          ELC50PD
06181                                     CP-RATING-BENEFIT-AMT         ELC50PD
06182        IF CP-STATE-STD-ABBRV = 'OR'                               ELC50PD
06183            COMPUTE CP-RATING-BENEFIT-AMT = CR-LFAMT +             ELC50PD
06184                                            CR-LFAMT-ALT
             END-IF
06185        MOVE CR-LFPRM-ALT           TO CP-ORIGINAL-PREMIUM         ELC50PD
06186        MOVE 'LEV'                  TO CP-DEVIATION-CODE           ELC50PD
06188        PERFORM 7600-GET-REFUND THRU 7600-EXIT                     ELC50PD
06189        COMPUTE CP-CALC-REFUND = CP-CALC-REFUND + WS-LF-REFUND     ELC50PD
06200      END-IF                                                       ELC50PD

           .
       3720-EXIT.
           EXIT.

       0220-CALC-LF-REM-TERM.

           MOVE WS-VAL-BIN-DATE        TO CP-VALUATION-DT

           MOVE WS-LF-COVERAGE-TYPE       
                                       TO CP-BENEFIT-TYPE
           MOVE WS-LF-SPECIAL-CALC-CD   
                                       TO CP-SPECIAL-CALC-CD
           MOVE CR-LF-TERM             TO CP-ORIGINAL-TERM
                                          CP-LOAN-TERM

           IF ((WS-LF-EARNINGS-CALC = 'B') AND
              WS-LF-SPECIAL-CALC-CD NOT = 'L')
              ADD +1                   TO CP-ORIGINAL-TERM
                                          CP-LOAN-TERM
           END-IF
           MOVE WS-LF-REM-TERM         TO CP-REM-TERM-METHOD
           IF CP-TERM-IS-DAYS
              MOVE CR-LF-TERM-IN-DAYS  TO CP-TERM-OR-EXT-DAYS
           ELSE
              MOVE ZEROS               TO CP-TERM-OR-EXT-DAYS
           END-IF

           MOVE '1'                    TO CP-REM-TRM-CALC-OPTION

           PERFORM 0510-GET-REMAINING-TERM
                                       THRU 0510-EXIT

           IF ((WS-LF-EARNINGS-CALC = 'B') AND
              WS-LF-SPECIAL-CALC-CD NOT = 'L')
082103        MOVE CP-REMAINING-TERM-1 TO LF-BAL-REMTERM1
082103        MOVE CP-REMAINING-TERM-2 TO LF-BAL-REMTERM2
              COMPUTE CP-REMAINING-TERM-1 =
                             CP-REMAINING-TERM-1 - 1
              COMPUTE CP-REMAINING-TERM-2 =
                             CP-REMAINING-TERM-2 - 1
           END-IF

           IF CP-REMAINING-TERM-1 NEGATIVE
               MOVE ZEROS              TO CP-REMAINING-TERM-1
           END-IF

           IF CP-REMAINING-TERM-2 NEGATIVE
               MOVE ZEROS              TO CP-REMAINING-TERM-2
           END-IF

           MOVE CP-REMAINING-TERM-1    TO LF-REM-TRM1
           MOVE CP-REMAINING-TERM-2    TO LF-REM-TRM2

           .

       0220-EXIT.
           EXIT.
       2000-CALC-REM-AMT.

           IF CR-LFTYP = 'QD'
              MOVE +15.0               TO CR-APR
           END-IF

           MOVE +0                     TO WS-REM-AMT
           IF WS-LF-COVERAGE-TYPE  = 'L' OR 'P'
              IF WS-LF-EARNINGS-CALC = 'B'
                 COMPUTE WS-REM-AMT =
                   CR-LFAMT + CR-LFAMT-ALT
              ELSE
                 MOVE CR-LFAMT         TO WS-REM-AMT
              END-IF
      *       GO TO 2999-CALC-REM-AMT-X
              GO TO 2900-DISPLAY-AMTS
           END-IF

           COMPUTE INTERMED ROUNDED = CR-LFAMT / CR-LF-TERM

           IF LF-REM-TRM2 = CR-LF-TERM
              IF WS-LF-EARNINGS-CALC = 'B'
                 COMPUTE WS-REM-AMT =
                   CR-LFAMT + CR-LFAMT-ALT
              ELSE
                 MOVE CR-LFAMT         TO WS-REM-AMT
              END-IF
      *       GO TO 2999-CALC-REM-AMT-X
              GO TO 2900-DISPLAY-AMTS
           END-IF

           IF WS-LF-EARNINGS-CALC = 'B'
              GO TO 2010-ORDINARY-REM
           END-IF

           IF WS-LF-EARNINGS-CALC = 'T' OR '4'
              GO TO 2020-CALC-TEXAS-REM
           END-IF

           IF CR-LFTYP = 'QD'
              MOVE +15.0               TO CR-APR
              MOVE 'N'                 TO
                             WS-LF-EARNINGS-CALC            
              GO TO 2030-CALC-NET-PAY-REM
           END-IF

           IF WS-LF-EARNINGS-CALC = 'N' OR '5'
              GO TO 2030-CALC-NET-PAY-REM
           END-IF

           IF CF-STATE-ABBREVIATION = 'OH'
               IF (CR-LF-TERM GREATER THAN +60) AND
                  (CR-DT GREATER THAN 19831031) AND
                  (CR-APR GREATER THAN ZERO)
                   GO TO 2030-CALC-NET-PAY-REM
              END-IF
           END-IF

           IF CF-STATE-ABBREVIATION = 'MT'
               IF (CR-LF-TERM GREATER THAN +61) AND
                  (CR-DT  GREATER THAN 19830318) AND
                  (CR-APR GREATER THAN ZERO)
                   GO TO 2030-CALC-NET-PAY-REM
              END-IF
           END-IF

           IF CF-STATE-ABBREVIATION = 'UT'
               IF (CR-LF-TERM GREATER THAN +62) AND
                  (CR-DT  GREATER THAN 19810831) AND
                  (CR-DT  LESS THAN 19830901) AND
                  (CR-APR GREATER THAN ZERO)
                   GO TO 2030-CALC-NET-PAY-REM
              END-IF
           END-IF

           IF CF-STATE-ABBREVIATION = 'RI'
               IF (CR-LF-TERM GREATER THAN +60) AND
                  (CR-DT  GREATER THAN 19831231) AND
                  (CR-APR GREATER THAN ZERO)
                   GO TO 2030-CALC-NET-PAY-REM
              END-IF
           END-IF
           .
       2010-ORDINARY-REM.

           IF WS-LF-EARNINGS-CALC = 'B'
               COMPUTE WS-REM-AMT ROUNDED =
                        (INTERMED * LF-REM-TRM2) + CR-LFAMT-ALT
           ELSE
               COMPUTE WS-REM-AMT ROUNDED = INTERMED * LF-REM-TRM2
           END-IF

      *    GO TO 2999-CALC-REM-AMT-X
           GO TO 2900-DISPLAY-AMTS

           .
       2020-CALC-TEXAS-REM.

           DIVIDE CR-LFAMT BY CR-LF-TERM
               GIVING TEX-FACT-1.
           DIVIDE LF-REM-TRM2 BY CR-PMT-FREQ
               GIVING TEX-FACT-2
               REMAINDER TEX-FACT-3.

           IF TEX-FACT-3 NOT = ZERO
               ADD +1                  TO TEX-FACT-2
           END-IF

           IF (TEX-FACT-2 * CR-PMT-FREQ) = CR-LF-TERM
               MOVE CR-LFAMT           TO WS-REM-AMT
           ELSE
               COMPUTE WS-REM-AMT ROUNDED =
                   (TEX-FACT-1 * (TEX-FACT-2 * CR-PMT-FREQ))
           END-IF

      *    GO TO 2999-CALC-REM-AMT-X
           GO TO 2900-DISPLAY-AMTS

           .
       2030-CALC-NET-PAY-REM.

           MOVE CR-DT                  TO  DC-GREG-DATE-CYMD
           MOVE 'L'                    TO  DC-OPTION-CODE
           PERFORM 8500-DATE-CONVERT
                                       THRU 8500-EXIT
           MOVE DC-BIN-DATE-1          TO  CP-CERT-EFF-DT
      *    MOVE WS-BIN-CR-DT           TO  CP-CERT-EFF-DT

           IF CR-LOAN-1ST-PMT-DT IS NOT = ZEROS
              MOVE CR-LOAN-1ST-PMT-DT  TO  DC-GREG-DATE-1-YMD
              MOVE '3'                 TO  DC-OPTION-CODE
              PERFORM 8500-DATE-CONVERT
                                       THRU 8500-EXIT
              MOVE DC-BIN-DATE-1       TO  CP-FIRST-PAY-DATE
           ELSE
              MOVE CP-CERT-EFF-DT      TO  DC-BIN-DATE-1
              MOVE +1                  TO  DC-ELAPSED-MONTHS
              MOVE +0                  TO  DC-ELAPSED-DAYS
              MOVE '6'                 TO  DC-OPTION-CODE
              PERFORM 8500-DATE-CONVERT
                                       THRU 8500-EXIT
              MOVE DC-BIN-DATE-2       TO  CP-FIRST-PAY-DATE
           END-IF

           IF WS-LF-EARNINGS-CALC = 'B' OR 'K' OR 'L'
              COMPUTE CP-ORIGINAL-BENEFIT = CR-LFAMT + CR-LFAMT-ALT
           ELSE
              MOVE CR-LFAMT            TO  CP-ORIGINAL-BENEFIT
           END-IF
           MOVE CR-APR                 TO  CP-LOAN-APR
           MOVE CR-LF-TERM             TO  CP-ORIGINAL-TERM
           MOVE CR-LOAN-TERM           TO  CP-LOAN-TERM
           MOVE LF-REM-TRM2            TO  CP-REMAINING-TERM
           MOVE WS-LF-SPECIAL-CALC-CD         
                                       TO CP-SPECIAL-CALC-CD
           MOVE WS-LF-COVERAGE-TYPE       
                                       TO  CP-BENEFIT-TYPE
           MOVE WS-LF-EARNINGS-CALC      
                                       TO  CP-EARNING-METHOD

           CALL 'ELRAMTX' USING CALCULATION-PASS-AREA

           MOVE CP-REMAINING-AMT       TO  WS-REM-AMT

           .
       2900-DISPLAY-AMTS.

      *    IF (CR-DT > 19921231) AND
      *       (CR-DT < 19940101) AND
      *    IF (WS-BIN-VAL-DATES (VYR) = X'95FF')
      *       MOVE CR-DT TO WS-DISPLAY-DATE
      *       MOVE CR-LF-TERM TO WS-DISPLAY-TERM
      *       MOVE LF-REM-TRM2 TO WS-DISPLAY-RTERM
      *       MOVE CR-LFPRM TO WS-DISPLAY-PRM
      *       IF CLAS-I-EP (CLAS-INDEXL) EQUAL 'B' OR 'K' OR 'L'
      *          ADD CR-LFAMT CR-LFAMT-ALT GIVING WS-DISPLAY-AMT
      *       ELSE
      *          MOVE CR-LFAMT     TO WS-DISPLAY-AMT
      *       END-IF
      *       MOVE WS-REM-AMT   TO WS-DISPLAY-RAMT
      *       DISPLAY '  ' CR-CERT-NO '  ' WS-DISPLAY-DATE '   '
      *        WS-DISPLAY-TERM '  ' WS-DISPLAY-RTERM '   '
      *        WS-DISPLAY-AMT '   ' WS-DISPLAY-RAMT
      *    END-IF

           .
       2999-CALC-REM-AMT-X.
           EXIT.
       EJECT
       3740-GET-AH-REFUND.

02906      MOVE CR-DT                 TO  DC-GREG-DATE-CYMD.            ECS010
02907      MOVE 'L'                   TO  DC-OPTION-CODE.               ECS010
02908      PERFORM 8500-DATE-CONVERT  THRU 8500-EXIT                    ECS010
02909      MOVE DC-BIN-DATE-1         TO  CP-CERT-EFF-DT                ECS010
02910                                                                   ECS010
02906      MOVE WS-VAL-DATE-NUM       TO  DC-GREG-DATE-CYMD.            ECS010
02907      MOVE 'L'                   TO  DC-OPTION-CODE.               ECS010
02908      PERFORM 8500-DATE-CONVERT  THRU 8500-EXIT                    ECS010
02909      MOVE DC-BIN-DATE-1         TO  CP-VALUATION-DT               ECS010
                                          WS-VAL-BIN-DATE
02910                                                                   ECS010
           IF CR-LOAN-1ST-PMT-DT IS NOT = ZEROS
              MOVE CR-LOAN-1ST-PMT-DT  TO  DC-GREG-DATE-1-YMD
              MOVE '3'                 TO  DC-OPTION-CODE
              PERFORM 8500-DATE-CONVERT
                                       THRU 8500-EXIT
              MOVE DC-BIN-DATE-1       TO  CP-FIRST-PAY-DATE
           ELSE
              MOVE CP-CERT-EFF-DT      TO  DC-BIN-DATE-1
              MOVE +1                  TO  DC-ELAPSED-MONTHS
              MOVE +0                  TO  DC-ELAPSED-DAYS
              MOVE '6'                 TO  DC-OPTION-CODE
              PERFORM 8500-DATE-CONVERT
                                       THRU 8500-EXIT
              MOVE DC-BIN-DATE-2       TO  CP-FIRST-PAY-DATE
           END-IF
           MOVE CR-AH-TERM             TO CP-ORIGINAL-TERM
           PERFORM 0420-CALC-AH-REM-TERM THRU 0420-EXIT
      *    COMPUTE CP-REMAINING-TERM = CR-AH-TERM - 1
           MOVE WS-AH-REFUND-CALC      TO CP-EARNING-METHOD.
           MOVE WS-AH-EARNINGS-CALC    TO CP-RATING-METHOD.


           MOVE '2'                    TO CP-PROCESS-TYPE
           MOVE CR-RATING-CLASS        TO CP-CLASS-CODE.
           MOVE CR-AHTYP               TO CP-BENEFIT-CD.
           MOVE CR-STATE               TO CP-STATE-STD-ABBRV
           MOVE CR-AHAMT               TO CP-ORIGINAL-BENEFIT
                                          CP-RATING-BENEFIT-AMT.
           IF CP-STATE-STD-ABBRV = 'OR'
               COMPUTE CP-RATING-BENEFIT-AMT = CR-AHAMT * CR-AH-TERM
           END-IF    

           MOVE CR-AHPRM               TO CP-ORIGINAL-PREMIUM.
           MOVE CR-PMT-FREQ            TO CP-PAY-FREQUENCY.
           MOVE CR-AGE                 TO CP-ISSUE-AGE.
           MOVE '000'                  TO CP-DEVIATION-CODE.
           MOVE +0                     TO CP-RATE-DEV-PCT.
           MOVE CR-APR                 TO CP-LOAN-APR.
           IF CP-STATE-STD-ABBRV = 'OH'
              IF CR-LFTYP = '  ' OR '00'
                 IF CP-CRITICAL-PERIOD
                    MOVE '2'       TO CP-EARNING-METHOD
                 ELSE
                    MOVE '6'       TO CP-EARNING-METHOD
                 END-IF
              ELSE
                 IF WS-LF-COVERAGE-TYPE = 'L'
                    MOVE '2'       TO CP-EARNING-METHOD
                 ELSE
                    IF (CR-LF-TERM > +60) OR
                       (WS-LF-EARNINGS-CALC = '5')
                       IF CP-CRITICAL-PERIOD
                          MOVE '2'  TO CP-EARNING-METHOD
                       ELSE
                          MOVE '6'  TO CP-EARNING-METHOD
                       END-IF
                    ELSE
                       MOVE '1'     TO CP-EARNING-METHOD
                    END-IF
                 END-IF
              END-IF
           END-IF

           IF CP-STATE-STD-ABBRV = 'VA'
              IF CR-DT > 19921231
                 IF CR-LF-TERM GREATER 61
                    MOVE '6' TO CP-EARNING-METHOD
                 ELSE
                    MOVE '1' TO CP-EARNING-METHOD.

           MOVE AH-REM-TRM1        TO CP-REMAINING-TERM
           PERFORM 7600-GET-REFUND THRU 7600-EXIT.


           .
       3740-EXIT.
           EXIT.
       0420-CALC-AH-REM-TERM.

           MOVE WS-VAL-BIN-DATE        TO CP-VALUATION-DT

           MOVE WS-AH-COVERAGE-TYPE       
                                       TO CP-BENEFIT-TYPE
           MOVE WS-AH-SPECIAL-CALC-CD   
                                       TO CP-SPECIAL-CALC-CD
           MOVE CR-AH-TERM             TO CP-ORIGINAL-TERM
                                          CP-LOAN-TERM

           MOVE WS-AH-REM-TERM         TO CP-REM-TERM-METHOD
           MOVE ZEROS                  TO CP-TERM-OR-EXT-DAYS

           MOVE '1'                      
                                       TO CP-REM-TRM-CALC-OPTION

           PERFORM 0510-GET-REMAINING-TERM
                                       THRU 0510-EXIT

           IF CP-REMAINING-TERM-1 NEGATIVE
               MOVE ZEROS              TO CP-REMAINING-TERM-1
           END-IF

           IF CP-REMAINING-TERM-2 NEGATIVE
               MOVE ZEROS              TO CP-REMAINING-TERM-2
           END-IF

           MOVE CP-REMAINING-TERM-1    TO AH-REM-TRM1
                                          WS-BALL-AH-RTRM
           MOVE CP-REMAINING-TERM-2    TO AH-REM-TRM2

           .

       0420-EXIT.
           EXIT.


       3500-WRITE-BALL.

           WRITE BALL-OUT-RECORD       FROM WS-OUT-RECORD
           ADD 1 TO BALL-RECS-OUT

           .

       3500-EXIT.
           EXIT.

       3600-OPEN-FILES.

           OPEN INPUT CERT-IN BALL-IN ELCNTL ERACCT
               OUTPUT BALL-OUT
           IF ELCNTL-FILE-STATUS NOT = '00'
              DISPLAY ' ELCNTL OPEN ' ELCNTL-FILE-STATUS
           END-IF
           IF ERACCT-FILE-STATUS NOT = '00'
              DISPLAY ' ERACCT OPEN ' ERACCT-FILE-STATUS
           END-IF

           .

       3600-EXIT.
           EXIT.

       0500-CLOSE-FILES.

           CLOSE CERT-IN BALL-IN BALL-OUT ELCNTL ERACCT

           .

       0500-EXIT.
           EXIT.

       0510-GET-REMAINING-TERM.

           CALL 'ELRTRMX' USING CALCULATION-PASS-AREA

           .

       0510-EXIT.
           EXIT.

       0600-INITIALIZE.

           MOVE SPACES                 TO WS-OUT-RECORD
           MOVE X'09'                  TO WS-TAB1
                                          WS-TAB2
                                          WS-TAB3
                                          WS-TAB4
                                          WS-TAB5
                                          WS-TAB6
                                          WS-TAB7
                                          WS-TAB8
                                          WS-TAB9
                                          WS-TAB10
                                          WS-TAB11
                                          WS-TAB12
                                          WS-TAB13
                                          WS-TAB14
                                          WS-TAB15
                                          WS-TAB16
                                          WS-TAB17
                                          WS-TAB18
                                          WS-TAB19
                                          WS-TAB20
                                          
           MOVE ZEROS                  TO WS-BALL-LF-CNC-AMT
                                          WS-BALL-LF-RTRM
                                          WS-BALL-LF-RAMT
                                          WS-BALL-LF-RATE
                                          WS-BALL-AH-CNC-AMT
                                          WS-BALL-AH-RTRM
                                          WS-BALL-AH-RAMT
                                          WS-BALL-AH-RATE

           MOVE 'AAAAAAAAAA'           TO WS-BALL-BATCH-NO
           MOVE 'BBBBBBBBBB'           TO WS-BALL-REC-DATE
           MOVE 'CCCCCCCCCC'           TO WS-BALL-DUE-DATE
           MOVE WS-OUT-RECORD          TO WS-INIT-BALL

           PERFORM 0200-READ-CERT THRU 0200-EXIT
           PERFORM 0250-READ-BALL THRU 0250-EXIT

           .

       0600-EXIT.
           EXIT.

       7600-GET-REFUND.
           CALL 'ELRFNDX' USING CALCULATION-PASS-AREA.

       7600-EXIT.
           EXIT.
           EJECT

       9000-READ-CONTROL.

           MOVE ELCNTL-KEY TO CF-CONTROL-PRIMARY.
           START ELCNTL KEY NOT LESS THAN CF-CONTROL-PRIMARY.

           IF ELCNTL-FILE-STATUS NOT = '00'
              GO TO 9000-NOTFND.

           READ ELCNTL NEXT.

           IF ELCNTL-FILE-STATUS NOT = '00'
              GO TO 9000-NOTFND.

           GO TO 9000-EXIT.

       9000-NOTFND.

           DISPLAY ' ELCNTL NOT FOUND ' ELCNTL-FILE-STATUS
           .
       9000-EXIT.
           EXIT.
01538  8500-DATE-CONVERT.                                               EL517
01539      CALL 'ELDATCX' USING DATE-CONVERSION-DATA.                   EL517
01540                                                                   EL517
01541  8500-EXIT.                                                       EL517
01542      EXIT.                                                        EL517
01543      EJECT                                                        EL517
01544                                                                   EL517
02256  ABEND-PGM.   COPY ELCABEND.                                      EL517
02257                                                                   EL517
