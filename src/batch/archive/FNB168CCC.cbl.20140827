       IDENTIFICATION DIVISION.
       PROGRAM-ID.    FNB168CCC.
       AUTHOR.        PABLO.
       DATE-COMPILED.

101708*  This program currently only runs for DCC carrier 5 and 6
070904******************************************************************
070904*                   C H A N G E   L O G
070904*
070904* CHANGES ARE MARKED BY THE CHANGE EFFECTIVE DATE.
070904*-----------------------------------------------------------------
070904*  CHANGE   CHANGE REQUEST PGMR  DESCRIPTION OF CHANGE
070904* EFFECTIVE    NUMBER
070904*-----------------------------------------------------------------
070904* 070904                   PEMA  PROCESS REIN RECS ONLY TO SYNC
070904*                                CLMRSRV INTERFACE W/ 45A & 20R
032905* 032905    2005030300002  PEMA  LIMIT FEED TO REIN CO 300 AND 500
122205* 122205    2005033100001  PEMA  ADD PROCESSING FOR CSI
101708* 101708    2008050500001  AJRA  ADD PROCESSING FOR CCC
070904******************************************************************

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

           SELECT EPEC-IN              ASSIGN TO SYS010.
           SELECT SORT-EPECS           ASSIGN TO SORTWK1.
101708     SELECT CCC-EXTRACT          ASSIGN TO SYS012
122205        ORGANIZATION IS LINE SEQUENTIAL.
           SELECT DISK-DATE            ASSIGN TO SYS019.

       DATA DIVISION.
       FILE SECTION.

       FD  EPEC-IN
                                       COPY ECSEPCFD.
                                       COPY ECSEPC01.

       SD  SORT-EPECS.

       01  SORT-EPEC-REC.
           05  SORT-KEY.
122205         10  SORT-CARRIER        PIC X.
               10  SORT-STATE          PIC XX.
           05  SORT-CLM-DU             PIC S9(9)V99 COMP-3.
           05  SORT-CLM-PV             PIC S9(9)V99 COMP-3.
           05  SORT-CLM-IBNR           PIC S9(11)V99 COMP-3.
           05  SORT-CLM-LOSS-RESV      PIC S9(11)V99 COMP-3.
           05  FILLER                  PIC X(6).

101708 FD  CCC-EXTRACT
122205     BLOCK CONTAINS 0 RECORDS
122205     RECORDING MODE IS F.
122205
       01  EXTRACT-RECORD.                                                 
           COPY FNC022.

       FD  DISK-DATE
                                       COPY ELCDTEFD.

       WORKING-STORAGE SECTION.
       77  FILLER  PIC X(32) VALUE '********************************'.
       77  FILLER  PIC X(32) VALUE '    FNB168CCC WORKING STORAGE   '.
       77  FILLER  PIC X(32) VALUE '********************************'.

       77  WS-EOF-SW                   PIC X  VALUE SPACES.
           88  END-OF-EPEC                    VALUE 'Y'.
           88  MORE-EPECS                     VALUE 'N'.
       77  WS-EPEC-IN                  PIC 9(9) VALUE ZEROS.
       77  WS-EXT-OUT                  PIC 9(9) VALUE ZEROS.

       01  WS-BIG-TABLE.
           05  WS-KEY.
122205         10  WS-CARRIER          PIC X.
               10  WS-STATE            PIC XX.
           05  WS-CLM-DU               PIC S9(9)V99 COMP-3.
           05  WS-CLM-PV               PIC S9(9)V99 COMP-3.
           05  WS-CLM-IBNR             PIC S9(11)V99 COMP-3.
           05  WS-CLM-LOSS-RESV        PIC S9(11)V99 COMP-3.
           05  FILLER                  PIC X(6) VALUE SPACES.

       01  WS-MISC.
           05  SYSTEM-DATE.
               10  SYS-MO              PIC 99  VALUE 00.
               10  SYS-DA              PIC 99  VALUE 00.
               10  SYS-CCYY            PIC 9(4) VALUE 0000.
           05  WS-BIN-AH-END-DATE      PIC XX  VALUE LOW-VALUES.
           05  WS-END-YEAR             PIC 9(11)  VALUE ZEROS.
           05  FILLER REDEFINES WS-END-YEAR.
               10  FILLER              PIC 999.
               10  WS-CCYY             PIC 9(4).
               10  WS-MMDD             PIC 9(4).

           EJECT

       01  WS-ABEND-AREA.
           05  WS-ABEND-FILE-STATUS    PIC X(02).
           05  WS-ABEND-MESSAGE        PIC X(80) VALUE SPACES.
           05  WS-RETURN-CODE          PIC S9(04)  COMP VALUE +0.
           05  WS-ZERO                 PIC S9(01) VALUE +0 COMP-3.

       01  WORK-ABEND-CODE.
           12  WAC-1                   PIC X.
           12  WAC-2                   PIC X.
           12  WAC-3-4.
               16  WAC-3               PIC X.
               16  WAC-4               PIC X.

       01  DATE-AREAS.
           05  WS-WORK-DATE            PIC 9(11).
           05  FILLER REDEFINES WS-WORK-DATE.
               10  FILLER              PIC XXX.
               10  WS-WORK-CCYY        PIC 9(4).
               10  WS-WORK-MMDD        PIC 9(4).

       01  ABEND-FIELDS.
           12  PGM-SUB                 PIC S999 COMP  VALUE +158.
           12  FIRST-TIME-SW           PIC X  VALUE 'Y'.
               88  FIRST-TIME                 VALUE 'Y'.

           EJECT
                                       COPY ELCDATE.

                                       COPY ELCDTECX.

                                       COPY ELCDTEVR.
                                       COPY ELCFUNDT.

       PROCEDURE DIVISION.

       0001-DT-CRD-READ SECTION.
                                       COPY ELCDTERX.

           SORT SORT-EPECS ON ASCENDING KEY SORT-KEY
                INPUT PROCEDURE 0002-INPUT THRU 0002-EXIT
                OUTPUT PROCEDURE 0003-OUTPUT THRU 0003-EXIT

           DISPLAY ' EPEC IN           ' WS-EPEC-IN
           DISPLAY ' EXT OUT           ' WS-EXT-OUT

           GOBACK

             .
       0002-INPUT SECTION.

           PERFORM 0010-INITIALIZE     THRU 0010-EXIT
           PERFORM 0020-OPEN-FILES     THRU 0020-EXIT
           PERFORM 0050-PROCESS-FILE   THRU 0050-EXIT

           .
       0002-EXIT.
           EXIT.

       0003-OUTPUT SECTION.

           SET MORE-EPECS              TO TRUE

           PERFORM 0070-RETURN-REC     THRU 0070-EXIT

           MOVE SORT-KEY               TO WS-KEY

           PERFORM 0075-PROCESS-RECS THRU 0075-EXIT UNTIL
                END-OF-EPEC

           PERFORM 0076-BREAK THRU 0076-EXIT

           PERFORM 0030-CLOSE-FILES    THRU 0030-EXIT

           .
       0003-EXIT.
           EXIT.


       0010-INITIALIZE.

           MOVE FUNCTION CURRENT-DATE  TO FUNCTION-DATE
           MOVE WS-FN-MO               TO SYS-MO
           MOVE WS-FN-DA               TO SYS-DA
           MOVE WS-FN-CCYY             TO SYS-CCYY
122205     MOVE SPACE                  TO WS-KEY

           MOVE +0                     TO WS-CLM-DU
                                          WS-CLM-PV
                                          WS-CLM-IBNR
                                          WS-CLM-LOSS-RESV

           .

       0010-EXIT.
           EXIT.

       0020-OPEN-FILES.

           OPEN INPUT EPEC-IN      
101708          OUTPUT CCC-EXTRACT

           .
       0020-EXIT.
           EXIT.

       0030-CLOSE-FILES.

           CLOSE EPEC-IN
101708           CCC-EXTRACT

           .
       0030-EXIT.
           EXIT.

       0050-PROCESS-FILE.

           PERFORM 0060-READ-EPEC      THRU 0060-EXIT

           PERFORM 0080-PROCESS-EPEC   THRU 0080-EXIT UNTIL
                 END-OF-EPEC

           .

       0050-EXIT.
           EXIT.

       0060-READ-EPEC.

           READ EPEC-IN AT END
               SET END-OF-EPEC         TO TRUE
           END-READ

           IF NOT END-OF-EPEC
              ADD 1                    TO WS-EPEC-IN
           END-IF
           
           .

       0060-EXIT.
           EXIT.

       0070-RETURN-REC.

           RETURN SORT-EPECS    AT END
               SET END-OF-EPEC         TO TRUE
           END-RETURN

           .
       0070-EXIT.
           EXIT.

       0075-PROCESS-RECS.

           IF SORT-KEY NOT  = WS-KEY
              PERFORM 0076-BREAK THRU 0076-EXIT
           ELSE
              ADD SORT-CLM-DU        TO WS-CLM-DU  
              ADD SORT-CLM-PV        TO WS-CLM-PV 
              ADD SORT-CLM-IBNR      TO WS-CLM-IBNR
              ADD SORT-CLM-LOSS-RESV TO WS-CLM-LOSS-RESV
           END-IF

           PERFORM 0070-RETURN-REC THRU 0070-EXIT

           .
       0075-EXIT.
           EXIT.

       0076-BREAK.

           MOVE SPACES                 TO EXTRACT-RECORD
                                                                        
103002     MOVE 'LOGIC '               TO FX-SOURCE-CODE                          
103002     MOVE '30'                   TO FX-TRAN-TYPE                            
103002     MOVE '08'                   TO FX-SUB-TYPE
101708     MOVE 'CCCDCCRSRV'           TO FX-SYSTEM
103002     MOVE '50'                   TO FX-DIVISION                             
103002     MOVE ' '                    TO FX-FY-REN                        
           MOVE SYSTEM-DATE            TO FX-JOURNAL-DATE
           STRING RUN-MO RUN-DA RUN-CCYR DELIMITED BY SIZE
              INTO FX-POSTING-DATE
           END-STRING
           MOVE WS-STATE               TO FX-STATE

           COMPUTE FX-AMOUNT = WS-CLM-DU + WS-CLM-PV
                             + WS-CLM-IBNR + WS-CLM-LOSS-RESV

           WRITE EXTRACT-RECORD

           MOVE SORT-EPEC-REC          TO WS-BIG-TABLE

           .
       0076-EXIT.
           EXIT.

       0080-PROCESS-EPEC.

           IF EP-RECORD-ID = 'EP'
070904        IF (EP-REIN = 'R')
101708           AND (EP-CARRIER = '5' OR '6')
                 AND (EP-PURGE = ' ')
                 AND (EP-RUN-DTE = RUN-DATE)
101708           AND (EP-REINCO = '800' OR '900')
                 MOVE EP-STATE         TO WS-STATE
                 MOVE EP-CLM-DU        TO WS-CLM-DU
                 MOVE EP-CLM-PV        TO WS-CLM-PV
                 MOVE EP-CLM-IBNR      TO WS-CLM-IBNR
                 MOVE EP-LOSS-RESV     TO WS-CLM-LOSS-RESV
                 MOVE ' '              TO WS-CARRIER
                 PERFORM 0090-RELEASE  THRU 0090-EXIT
              END-IF
           END-IF
           
           PERFORM 0060-READ-EPEC      THRU 0060-EXIT

           .
       0080-EXIT.
           EXIT.

       0090-RELEASE.

           RELEASE SORT-EPEC-REC       FROM WS-BIG-TABLE
           PERFORM 0010-INITIALIZE     THRU 0010-EXIT

           .
       0090-EXIT.
           EXIT.


       8510-DATE-CONVERSION.

           CALL 'ELDATCX' USING DATE-CONVERSION-DATA

           .

       8590-EXIT.
           EXIT.

       ABEND-PGM.
                                       COPY ELCABEND.

           EJECT
