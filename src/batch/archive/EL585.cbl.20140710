061814$SET SQL(dbman=ODBC, TARGETDB=MSSQLSERVER, NOAUTOCOMMIT)
      ****************************************************************
       IDENTIFICATION DIVISION.

       PROGRAM-ID.                 EL585.


      *AUTHOR.     SUZAN VUKOV.
      ******************************************************************
      *REMARKS.
      *        THIS PROGRAM CREATES THREE REPORTS:
      *            1) MONTH-TO-DATE CLAIMS PAID OVER $10,000
      *            2) MONTH-TO-DATE CLAIMS PAID ACTIVITY SUMMARY
      *            3) DAILY DRAFTS, STOP-PAYS, AND OFFLINE TRANSACTIONS
      *                SORTED BY DRAFT NUMBER
      *
      *        THIS PROGRAM ALSO CREATES A FILE TO BE USED AS INPUT TO
      *           EL586.  ELMSTR IS TRIMMED DOWN TO RECORDS
      *           WITH A LAST MAINT DATE NOT FURTHER BACK THAN 30 DAYS
      *           FROM THE FIRST OF THE CURRENT MONTH.
      *
      *        THIS PROGRAM REPLACES MAINFRAME CULPRIT REPORTS
      *        CILCLM10  CILCLM35  PDCLSUM  SPACTPR  DRACTPR
      *        PREVIOUSLY GENERATED BY JOB CICLDLY, PROGRAM CICLDLYC
      *        AND CULPRITS SPLST AND DRFTLST PREVIOUSLY GENERATED BY
      *        JOB CICLDLY, PROGRAM CICLDFTA.
      *
      *     INPUT:   ELMSTR
      *              ELTRLR
      ******************************************************************
      *                   C H A N G E   L O G
      *
      * CHANGES ARE MARKED BY THE CHANGE EFFECTIVE DATE.
      *-----------------------------------------------------------------
      *  CHANGE   CHANGE REQUEST PGMR  DESCRIPTION OF CHANGE
      * EFFECTIVE    NUMBER
      *-----------------------------------------------------------------
      * 110101    2001100100006  SMVA  INITIAL IMPL FOR UNIKIX
      *                                REWRITE OF MAINFRAME CULPRIT
121902* 121902    2001061800003  SMVA  ADD PROCESSING FOR NEW CLM TYP I
120103* 120103    2003080800002  SMVA  ADD PROCESSING FOR NEW CLM TYP G
010704* 010704                   SMVA  HANDLE FILE STATUS 23           
050604* 050604                   SMVA  ADD ACCT NO TO REPORT           
032707* 032707    2007032000003  PEMA  ELIMINATE LIFE INTEREST PAYMENTS
011810* 011810    2010011800001  AJRA  ADD SIGN TO SUMMARY AMTS
030612* 030612  CR2011120900003  AJRA  ADD AHL COMPANY CODE
043012* 043012  CR2012042500001  AJRA  HANDLE AHL EARLY MO END CUTOFF
053012* 053012  CR2012042500001  AJRA  SEPARATE VOIDS ON AHL ISSUED PMTS
061814* 061814  CR2013092700001  PEMA  UPLOAD DLY G/L INT BAL DATA
      ******************************************************************

       ENVIRONMENT DIVISION.

       INPUT-OUTPUT SECTION.

       FILE-CONTROL.
           SELECT DISK-DATE          ASSIGN TO SYS019-FBA1-S-SYS019.

           SELECT ELMSTR-INFILE      ASSIGN TO ELMSTR
                                     ORGANIZATION IS INDEXED
                                     ACCESS IS DYNAMIC
                                     RECORD KEY IS CL-CONTROL-PRIMARY
                                     FILE STATUS IS
                                       WS-ELMSTR-FILE-STATUS.

           SELECT ELTRLR-INFILE      ASSIGN TO ELTRLR
                                     ORGANIZATION IS INDEXED
                                     ACCESS IS DYNAMIC
                                     RECORD KEY IS AT-CONTROL-PRIMARY
                                     FILE STATUS IS
                                       WS-ELTRLR-FILE-STATUS.

           SELECT MTDCLMS-OUT        ASSIGN TO MTDCLMS
                                     ORGANIZATION IS LINE SEQUENTIAL.


           SELECT PRNTR              ASSIGN TO SYS008-UR-1403-S-SYS008.

           SELECT SORT-FILE          ASSIGN TO SYS001-UT-3380-S-SORTWK1.

           SELECT SEQ-FILE-ARCHIVE   ASSIGN TO SYS020-UT-2400-S-SYS020.

           SELECT ELREPT             ASSIGN TO SYS018-FBA1-ELREPT
                                     ORGANIZATION IS INDEXED
                                     ACCESS IS DYNAMIC
                                     RECORD KEY IS RF-CONTROL-PRIMARY
                                     FILE STATUS IS DTE-VSAM-FLAGS.
043012
043012     SELECT ELCNTL       ASSIGN TO ELCNTL         
043012                         ORGANIZATION IS INDEXED              
043012                         ACCESS IS DYNAMIC                    
043012                         RECORD KEY IS CF-CONTROL-PRIMARY     
043012                         FILE STATUS IS WS-ELCNTL-FILE-STATUS.


       DATA DIVISION.

       FILE SECTION.

       FD  DISK-DATE
                                COPY ELCDTEFD.

       FD  ELMSTR-INFILE.
                                COPY ELCMSTR.

       FD  ELTRLR-INFILE.
                                COPY ELCTRLR.


       FD  MTDCLMS-OUT
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE F.
       01  MTD-ELMSTR-REC                  PIC X(350).


       FD  PRNTR
                                COPY ELCPRTFD.


       SD  SORT-FILE.
       01  SORT-RECORD.
           05  FILLER                      PIC X(01).
           05  SORT-DRAFT-NO               PIC X(10).
           05  FILLER                      PIC X(122).


      *********** OLD FICHE PROCESSING USED FOR SEQUENTIAL FILE ARCHIVE
      *               IF DTE-PRT-OPT = 'B'
       FD  SEQ-FILE-ARCHIVE
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE F.

       01  SEQ-ARCHIVE-REC                 PIC X(133).



       FD  ELREPT
                                COPY ELCRPTFD.
                                COPY ELCREPT.
043012
043012 FD  ELCNTL.                                                      
043012                                                                   
043012      COPY ELCCNTL.                                                


       WORKING-STORAGE SECTION.

061814 EXEC SQL
061814    INCLUDE SQLCA
061814 END-EXEC

       01  LCP-ABND-CODE                   PIC S999 COMP   VALUE +519.

       77  FILLER  PIC X(32)   VALUE '********************************'.
       77  FILLER  PIC X(32)   VALUE '*     EL585  WORKING STORAGE   *'.
       77  FILLER  PIC X(32)   VALUE '********** V/M 2.008 ***********'.
061814 77  int-tot-stop-pays           pic s9(7)v99 comp-3 value +0.
061814 77  int-tot-offline             pic s9(7)v99 comp-3 value +0.
061814 77  int-tot-clm-pmts            pic s9(7)v99 comp-3 value +0.

061814 01  P pointer.
061814 01  KIXSYS                      pic X(7)  VALUE Z"KIXSYS".
061814 01  var-ptr pointer.
061814 01  env-var-len                 pic 9(4)  binary.
061814 01  rc                          pic 9(9)  binary.
061814
061814 01  WS-KIXSYS.
061814     05  WS-KIX-FIL1             PIC X(10).
061814     05  WS-KIX-APPS             PIC X(10).
061814     05  WS-KIX-ENV              PIC X(10).
061814     05  WS-KIX-MYENV            PIC X(10).
061814     05  WS-KIX-SYS              PIC X(10).
061814
061814 EXEC SQL BEGIN DECLARE SECTION END-EXEC
061814
061814 01  svr                         pic x(32).
061814 01  usr                         pic x(32).
061814 01  pass                        pic x(32).
061814 01  usr-pass                    pic x(64).
061814 01  ws-disp-code                pic s9(11).
061814
061814 01  ws-table-values.
061814     05  tbl-comp-id             pic x(5)   value spaces.
061814     05  tbl-batch-no            pic x(50)  value spaces.
061814     05  tbl-carrier             pic x      value spaces.
061814     05  tbl-date                pic x(10)  value spaces.
061814     05  tbl-source-rpt          pic x(8)   value 'EL585'.
061814     05  tbl-remits              pic x(10)  value spaces.
061814     05  tbl-charges             pic x(10)  value spaces.
061814     05  tbl-other               pic x(10)  value spaces.
061814     05  tbl-clm-pmts            pic x(10)  value spaces.
061814     05  em-clm-pmts redefines tbl-clm-pmts
061814                                 pic zzzzzzz.99.
061814     05  tbl-clm-stop-pays       pic x(10)  value spaces.
061814     05  em-clm-stop-pays redefines tbl-clm-stop-pays
061814                                 pic zzzzzzz.99.
061814     05  tbl-clm-offline-pmts    pic x(10)  value spaces.
061814     05  em-clm-offline-pmts redefines tbl-clm-offline-pmts
061814                                 pic zzzzzzz.99.
061814
061814 EXEC SQL END DECLARE SECTION END-EXEC

       01  FILLER                          COMP-3.
           05  WS-LINE-COUNT               PIC S9(03)    VALUE +0.
           05  WS-LINE-COUNT-MAX           PIC S9(03)    VALUE +55.
           05  WS-PAGE                     PIC S9(05)    VALUE +0.

      ******* COUNTS
           05  WS-DLY-DIS-CLMS-PD-CNT      PIC S9(06)    VALUE +0.
121902     05  WS-DLY-IU-CLMS-PD-CNT       PIC S9(06)    VALUE +0.
120103     05  WS-DLY-GAP-CLMS-PD-CNT      PIC S9(06)    VALUE +0.
           05  WS-DLY-LIFE-CLMS-PD-CNT     PIC S9(06)    VALUE +0.
           05  WS-DLY-DIS-DRFTS-CNT        PIC S9(06)    VALUE +0.
121902     05  WS-DLY-IU-DRFTS-CNT         PIC S9(06)    VALUE +0.
120103     05  WS-DLY-GAP-DRFTS-CNT        PIC S9(06)    VALUE +0.
           05  WS-DLY-LIFE-DRFTS-CNT       PIC S9(06)    VALUE +0.
           05  WS-DLY-DIS-VOID-CNT         PIC S9(06)    VALUE +0.
121902     05  WS-DLY-IU-VOID-CNT          PIC S9(06)    VALUE +0.
120103     05  WS-DLY-GAP-VOID-CNT         PIC S9(06)    VALUE +0.
           05  WS-DLY-LIFE-VOID-CNT        PIC S9(06)    VALUE +0.
           05  WS-DLY-DIS-OFFLN-CNT        PIC S9(06)    VALUE +0.
121902     05  WS-DLY-IU-OFFLN-CNT         PIC S9(06)    VALUE +0.
120103     05  WS-DLY-GAP-OFFLN-CNT        PIC S9(06)    VALUE +0.
           05  WS-DLY-LIFE-OFFLN-CNT       PIC S9(06)    VALUE +0.
053012     05  WS-DLY-DIS-AHL-VOID-CNT     PIC S9(06)    VALUE +0.
053012     05  WS-DLY-LIFE-AHL-VOID-CNT    PIC S9(06)    VALUE +0.

           05  WS-TOT-DIS-CLMS-PD-CNT      PIC S9(06)    VALUE +0.
121902     05  WS-TOT-IU-CLMS-PD-CNT       PIC S9(06)    VALUE +0.
120103     05  WS-TOT-GAP-CLMS-PD-CNT      PIC S9(06)    VALUE +0.
           05  WS-TOT-LIFE-CLMS-PD-CNT     PIC S9(06)    VALUE +0.
           05  WS-TOT-DIS-DRFTS-CNT        PIC S9(06)    VALUE +0.
121902     05  WS-TOT-IU-DRFTS-CNT         PIC S9(06)    VALUE +0.
120103     05  WS-TOT-GAP-DRFTS-CNT        PIC S9(06)    VALUE +0.
           05  WS-TOT-LIFE-DRFTS-CNT       PIC S9(06)    VALUE +0.
           05  WS-TOT-DIS-VOID-CNT         PIC S9(06)    VALUE +0.
121902     05  WS-TOT-IU-VOID-CNT          PIC S9(06)    VALUE +0.
120103     05  WS-TOT-GAP-VOID-CNT         PIC S9(06)    VALUE +0.
           05  WS-TOT-LIFE-VOID-CNT        PIC S9(06)    VALUE +0.
           05  WS-TOT-DIS-OFFLN-CNT        PIC S9(06)    VALUE +0.
121902     05  WS-TOT-IU-OFFLN-CNT         PIC S9(06)    VALUE +0.
120103     05  WS-TOT-GAP-OFFLN-CNT        PIC S9(06)    VALUE +0.
           05  WS-TOT-LIFE-OFFLN-CNT       PIC S9(06)    VALUE +0.
053012     05  WS-TOT-DIS-AHL-VOID-CNT     PIC S9(06)    VALUE +0.
053012     05  WS-TOT-LIFE-AHL-VOID-CNT    PIC S9(06)    VALUE +0.

      ******* DOLLAR AMOUNTS
           05  WS-DLY-DIS-CLMS-PD-AMT      PIC S9(08)V99 VALUE +0.
121902     05  WS-DLY-IU-CLMS-PD-AMT       PIC S9(08)V99 VALUE +0.
           05  WS-DLY-LIFE-CLMS-PD-AMT     PIC S9(08)V99 VALUE +0.
           05  WS-DLY-DIS-VOID-AMT         PIC S9(08)V99 VALUE +0.
121902     05  WS-DLY-IU-VOID-AMT          PIC S9(08)V99 VALUE +0.
           05  WS-DLY-LIFE-VOID-AMT        PIC S9(08)V99 VALUE +0.
           05  WS-DLY-DIS-OFFLN-AMT        PIC S9(08)V99 VALUE +0.
121902     05  WS-DLY-IU-OFFLN-AMT         PIC S9(08)V99 VALUE +0.
           05  WS-DLY-LIFE-OFFLN-AMT       PIC S9(08)V99 VALUE +0.
           05  WS-DLY-DIS-NETPD-AMT        PIC S9(08)V99 VALUE +0.
121902     05  WS-DLY-IU-NETPD-AMT         PIC S9(08)V99 VALUE +0.
           05  WS-DLY-LIFE-NETPD-AMT       PIC S9(08)V99 VALUE +0.
120103     05  WS-DLY-GAP-CLMS-PD-AMT      PIC S9(08)V99 VALUE +0.
120103     05  WS-DLY-GAP-VOID-AMT         PIC S9(08)V99 VALUE +0.
120103     05  WS-DLY-GAP-OFFLN-AMT        PIC S9(08)V99 VALUE +0.
120103     05  WS-DLY-GAP-NETPD-AMT        PIC S9(08)V99 VALUE +0.
053012     05  WS-DLY-DIS-AHL-VOID-AMT     PIC S9(08)V99 VALUE +0.
053012     05  WS-DLY-LIFE-AHL-VOID-AMT    PIC S9(08)V99 VALUE +0.

           05  WS-TOT-DIS-CLMS-PD-AMT      PIC S9(08)V99 VALUE +0.
121902     05  WS-TOT-IU-CLMS-PD-AMT       PIC S9(08)V99 VALUE +0.
           05  WS-TOT-LIFE-CLMS-PD-AMT     PIC S9(08)V99 VALUE +0.
           05  WS-TOT-DIS-VOID-AMT         PIC S9(08)V99 VALUE +0.
121902     05  WS-TOT-IU-VOID-AMT          PIC S9(08)V99 VALUE +0.
           05  WS-TOT-LIFE-VOID-AMT        PIC S9(08)V99 VALUE +0.
           05  WS-TOT-DIS-OFFLN-AMT        PIC S9(08)V99 VALUE +0.
121902     05  WS-TOT-IU-OFFLN-AMT         PIC S9(08)V99 VALUE +0.
           05  WS-TOT-LIFE-OFFLN-AMT       PIC S9(08)V99 VALUE +0.
           05  WS-TOT-DIS-NETPD-AMT        PIC S9(08)V99 VALUE +0.
121902     05  WS-TOT-IU-NETPD-AMT         PIC S9(08)V99 VALUE +0.
           05  WS-TOT-LIFE-NETPD-AMT       PIC S9(08)V99 VALUE +0.
120103     05  WS-TOT-GAP-CLMS-PD-AMT      PIC S9(08)V99 VALUE +0.
120103     05  WS-TOT-GAP-VOID-AMT         PIC S9(08)V99 VALUE +0.
121503     05  WS-TOT-GAP-OFFLN-AMT        PIC S9(08)V99 VALUE +0.
121503     05  WS-TOT-GAP-NETPD-AMT        PIC S9(08)V99 VALUE +0.
053012     05  WS-TOT-DIS-AHL-VOID-AMT     PIC S9(08)V99 VALUE +0.
053012     05  WS-TOT-LIFE-AHL-VOID-AMT    PIC S9(08)V99 VALUE +0.

           05  WS-GRAND-TOT-CLMS-PD-CNT    PIC S9(06)    VALUE +0.
           05  WS-GRAND-TOT-DRFTS-CNT      PIC S9(06)    VALUE +0.
           05  WS-GRAND-TOT-VOID-CNT       PIC S9(06)    VALUE +0.
           05  WS-GRAND-TOT-OFFLN-CNT      PIC S9(06)    VALUE +0.
053012     05  WS-GRAND-TOT-AHL-VOID-CNT   PIC S9(06)    VALUE +0.

           05  WS-GRAND-TOT-CLMS-PD-AMT    PIC S9(08)V99 VALUE +0.
           05  WS-GRAND-TOT-VOID-AMT       PIC S9(08)V99 VALUE +0.
           05  WS-GRAND-TOT-OFFLN-AMT      PIC S9(08)V99 VALUE +0.
           05  WS-GRAND-TOT-NETPD-AMT      PIC S9(08)V99 VALUE +0.
053012     05  WS-GRAND-TOT-AHL-VOID-AMT   PIC S9(08)V99 VALUE +0.

           05  WS-AMT-PAID-CRITERIA        PIC S9(07)V99 VALUE
                                                           +10000.
           05  WS-AMT-PD-THIS-CLM          PIC S9(07)V99 VALUE +0.
           05  WS-OVER-SETAMT-CNT          PIC S9(06)    VALUE +0.

           05  WS-DRFTLST-STOP-PAY-AMT     PIC S9(07)V99 VALUE +0.

           05  WS-RETURN-CODE              PIC S9(03)    VALUE +0.
           05  WS-ZERO                     PIC S9(01)    VALUE +0.


       01  FILLER                          COMP SYNC.
           05  PGM-SUB                     PIC S9(04)    VALUE +585.

       01  FILLER.
           05  ABEND-CODE                  PIC X(04).
           05  ABEND-OPTION                PIC X(01).
           05  OLC-REPORT-NAME             PIC X(05)     VALUE 'EL585'.

           05  WS-CYCLE-DT-BINARY          PIC X(02)     VALUE SPACES.

           05  WS-PREV-CYCLE-DT-BINARY     PIC X(02)     VALUE SPACES.

           05  WS-CURR-MNTH-STRT-DT-BIN    PIC X(02)     VALUE SPACES.

           05  WS-LAST-MAINT-CUTOFF-DT-BIN PIC X(02)     VALUE SPACES.
043012
043012     05  WS-MONTH-END-SELECT-DATE    PIC X(02)     VALUE SPACES.           

           05  WS-CYCLE-DT.
               10  WS-CYCLE-DT-CC          PIC 9(02)     VALUE ZEROS.
               10  WS-CYCLE-DT-YY          PIC 9(02)     VALUE ZEROS.
               10  WS-CYCLE-DT-MM          PIC 9(02)     VALUE ZEROS.
               10  WS-CYCLE-DT-DD          PIC 9(02)     VALUE ZEROS.

           05  WS-EDITED-CYCLE-DT.
               10  WS-EDITED-CYCLE-DT-MM   PIC X(02)     VALUE SPACES.
               10  FILLER                  PIC X(01)     VALUE '/'.
               10  WS-EDITED-CYCLE-DT-DD   PIC X(02)     VALUE SPACES.
               10  FILLER                  PIC X(01)     VALUE '/'.
               10  WS-EDITED-CYCLE-DT-YY   PIC X(02)     VALUE SPACES.

           05  WS-CURR-MNTH-STRT-DT.
               10  FILLER                  PIC X(06)     VALUE SPACES.
               10  WS-CURR-MNTH-DAY01      PIC X(02)     VALUE SPACES.

           05  WS-EOF1-SW                  PIC X(01)     VALUE SPACE.
               88  END-OF-ELMSTR                         VALUE 'Y'.

           05  WS-EOF2-SW                  PIC X(01)     VALUE SPACE.
               88  END-OF-ELTRLR                         VALUE 'Y'.

           05  WS-EOF3-SW                  PIC X(01)     VALUE SPACE.
               88  END-OF-SORTFILE                       VALUE 'Y'.

           05  WS-ELTRLR-FILE-STATUS       PIC X(02)     VALUE ZERO.
           05  WS-ELMSTR-FILE-STATUS       PIC X(02)     VALUE ZERO.
043012     05  WS-ELCNTL-FILE-STATUS       PIC X(02)     VALUE ZERO.
           
           05  WS-ELTRLR-KEY-SW            PIC X(01)     VALUE SPACE.
               88  NEW-ELTRLR-KEY                        VALUE 'N'.
               88  ELTRLR-KEY-CHANGE                     VALUE 'C'.

           05  WS-STOP-PAY-SW              PIC X(01)     VALUE SPACE.
               88  THIS-IS-A-STOP-PAY                    VALUE 'Y'.
               88  STOP-PAY-SW-OFF                       VALUE 'N'.

           05  WS-CLAIM-COUNTED-SW         PIC X(01)     VALUE SPACE.
               88  CLAIM-NOT-COUNTED                     VALUE 'N'.
               88  CLAIM-COUNTED                         VALUE 'C'.

           05  WS-MTD-OR-DAILY-SW          PIC X(01)     VALUE SPACE.
               88  DAILY                                 VALUE 'D'.
               88  MONTH-TO-DATE                         VALUE 'M'.

           05  WS-DETAIL-SW                PIC X(01)     VALUE 'N'.
               88  DETAIL-SETUP-NOT-DONE                 VALUE 'N'.
               88  DETAIL-SETUP-DONE                     VALUE 'D'.

           05  WS-REPORT-DELETE-SW         PIC X(01)     VALUE 'B'.
               88  BEGIN-REPORT-DELETE                   VALUE 'B'.
               88  REPORT-DELETE-DONE                    VALUE 'D'.

           05  WS-ABEND-MESSAGE            PIC X(80)     VALUE SPACES.

           05  WS-ABEND-FILE-STATUS        PIC X(02)     VALUE ZERO.

           05  WS-DETAIL-REPORT-TITLE      PIC X(42)     VALUE
               '  MONTH-TO-DATE CLAIMS PAID OVER $10,000  '.

           05  WS-SUMMARY-REPORT-TITLE     PIC X(42)     VALUE
               '  MONTH-TO-DATE CLAIMS ACTIVITY SUMMARY   '.

           05  WS-MTD-LITERAL              PIC X(17)     VALUE
               'MONTH-TO-DATE    '.

           05  WS-FULL-DIS-LABEL.
               10  WS-DIS-LABEL-VAR        PIC X(10)     VALUE SPACES.
               10  WS-DIS-LABEL            PIC X(17)     VALUE
                   'CREDIT DISABILITY'.

121902     05  WS-FULL-IU-LABEL.
121902         10  WS-IU-LABEL-VAR         PIC X(10)     VALUE SPACES.
121902         10  WS-IU-LABEL             PIC X(17)     VALUE
121902             'DCC UNEMPLOYMENT '.

120103     05  WS-FULL-GAP-LABEL.
120103         10  WS-GAP-LABEL-VAR        PIC X(10)     VALUE SPACES.
120103         10  WS-GAP-LABEL            PIC X(17)     VALUE
120103             'DCC   GAP        '.

           05  WS-FULL-LIFE-LABEL.
               10  WS-LIFE-LABEL-VAR       PIC X(10)     VALUE SPACES.
               10  WS-LIFE-LABEL           PIC X(17)     VALUE
                   'CREDIT LIFE      '.

121902     05  WS-GRAND-TOTALS-LABEL.
121902         10  FILLER                  PIC X(08)     VALUE
121902             '******* '.
121902         10  WS-GT-CLIENT-ID         PIC X(03)     VALUE
121902             'CID'.
121902         10  FILLER                  PIC X(16)     VALUE
121902         ' GRAND TOTALS   '.

       01  WS-HEADING1.
           05  FILLER                      PIC X(01)     VALUE '1'.
           05  FILLER                      PIC X(43)     VALUE SPACES.
           05  WS-H1-TITLE                 PIC X(42)     VALUE SPACES.
           05  FILLER                      PIC X(35)     VALUE SPACES.
           05  WS-H1-REPORT-ID             PIC X(05)     VALUE 'EL585'.
           05  FILLER                      PIC X(07)     VALUE SPACES.


       01  WS-HEADING2.
           05  FILLER                      PIC X(01)     VALUE SPACE.
           05  FILLER                      PIC X(46)     VALUE SPACES.
           05  WS-H2-COMPANY-NAME          PIC X(30)     VALUE SPACES.
           05  FILLER                      PIC X(44)     VALUE SPACES.
           05  WS-H2-DATE                  PIC X(08)     VALUE SPACES.
           05  FILLER                      PIC X(04)     VALUE SPACES.


       01  WS-HEADING3.
           05  FILLER                      PIC X(01)     VALUE SPACE.
           05  FILLER                      PIC X(51)     VALUE SPACES.
           05  FILLER                      PIC X(17)     VALUE
               'ACTIVITY THROUGH '.
           05  WS-H3-THROUGH-DT            PIC X(10)     VALUE SPACES.
           05  FILLER                      PIC X(42)     VALUE SPACES.
           05  FILLER                      PIC X(04)     VALUE 'PAGE'.
           05  FILLER                      PIC X(01)     VALUE SPACES.
           05  WS-H3-PAGE                  PIC ZZ,ZZ9.
           05  FILLER                      PIC X(01)     VALUE SPACE.


       01  WS-HEADING4.
           05  FILLER                      PIC X(01)     VALUE '-'.
           05  FILLER                      PIC X(15)     VALUE SPACES.
           05  FILLER                      PIC X(07)     VALUE
               'CLAIM #'.
           05  FILLER                      PIC X(09)     VALUE SPACES.
           05  FILLER                      PIC X(30)     VALUE
               'CLAIMANT NAME                 '.
           05  FILLER                      PIC X(02)     VALUE SPACES.
           05  FILLER                      PIC X(10)     VALUE
               ' POLICY # '.
           05  FILLER                      PIC X(09)     VALUE SPACES.
           05  FILLER                      PIC X(17)     VALUE
               ' CLAIM TYPE      '.
           05  FILLER                      PIC X(07)     VALUE SPACES.
           05  FILLER                      PIC X(11)     VALUE
               'AMOUNT PAID'.
           05  FILLER                      PIC X(15)     VALUE SPACES.


       01  WS-DETAIL1.
           05  FILLER                      PIC X(01)     VALUE SPACE.
           05  FILLER                      PIC X(15)     VALUE SPACES.
           05  WS-D1-CLAIM-NO              PIC X(07)     VALUE SPACES.
           05  FILLER                      PIC X(09)     VALUE SPACES.
           05  WS-D1-CLAIMANT-NM           PIC X(30)     VALUE SPACES.
           05  FILLER                      PIC X(02)     VALUE SPACES.
           05  WS-D1-CERT-NO               PIC X(10)     VALUE SPACES.
           05  FILLER                      PIC X(09)     VALUE SPACES.
           05  WS-D1-CLAIM-TYP-DESC        PIC X(17)     VALUE SPACES.
           05  FILLER                      PIC X(07)     VALUE SPACES.
           05  WS-D1-AMT-PD-MTD            PIC ZZZ,ZZ9.99.
           05  FILLER                      PIC X(16)     VALUE SPACES.


      *************************
       01  WS-DRFTLST-HEADING1.
           05  FILLER                      PIC X(01)     VALUE '1'.
           05  FILLER                      PIC X(42)     VALUE SPACES.
           05  FILLER                      PIC X(47)     VALUE
               'DAILY DRAFT, STOP-PAY, AND OFFLINE TRANSACTIONS'.
           05  FILLER                      PIC X(31)     VALUE SPACES.
           05  WS-DRFTLST-H1-RPT-ID        PIC X(05)     VALUE 'EL585'.
           05  FILLER                      PIC X(07)     VALUE SPACES.

       01  WS-DRFTLST-HEADING3.
           05  FILLER                      PIC X(01)     VALUE SPACE.
           05  FILLER                      PIC X(55)     VALUE SPACES.
           05  FILLER                      PIC X(11)     VALUE
               'CYCLE DATE '.
           05  WS-DRFTLST-H3-DT            PIC X(10)     VALUE SPACES.
           05  FILLER                      PIC X(44)     VALUE SPACES.
           05  FILLER                      PIC X(04)     VALUE 'PAGE'.
           05  FILLER                      PIC X(01)     VALUE SPACES.
           05  WS-DRFTLST-H3-PAGE          PIC ZZ9.
           05  FILLER                      PIC X(04)     VALUE SPACE.


       01  WS-DRFTLST-HEADING4.
           05  FILLER                      PIC X(01)     VALUE '-'.
050604     05  FILLER                      PIC X(07)     VALUE
050604         'DRAFT #'.
050604     05  FILLER                      PIC X(02)     VALUE SPACES.
           05  FILLER                      PIC X(12)     VALUE
               'DRAFT AMOUNT'.
050604     05  FILLER                      PIC X(03)     VALUE SPACES.
           05  FILLER                      PIC X(10)     VALUE
               'ENTRY TYPE'.
050604     05  FILLER                      PIC X(03)     VALUE SPACES.
           05  FILLER                      PIC X(07)     VALUE
               'CLAIM #'.
050604     05  FILLER                      PIC X(03)     VALUE SPACES.
           05  FILLER                      PIC X(10)     VALUE
               ' POLICY # '.
050604     05  FILLER                      PIC X(02)     VALUE SPACES.
           05  FILLER                      PIC X(30)     VALUE
               'CLAIMANT NAME                 '.
050604     05  FILLER                      PIC X(02)     VALUE SPACES.
           05  FILLER                      PIC X(30)     VALUE
               'PAYEE                         '.
050604     05  FILLER                      PIC X(01)     VALUE SPACES.
050604     05  FILLER                      PIC X(10)     VALUE
050604         'ACCOUNT NO'.


       01  WS-DRFTLST-DETAIL.
           05  FILLER                      PIC X(01)     VALUE SPACE.
           05  WS-DRFTLST-CHECK-NO         PIC X(07)     VALUE SPACES.
050604     05  FILLER                      PIC X(02)     VALUE SPACES.
           05  WS-DRFTLST-DRFTAMT          PIC ZZZ,ZZ9.99-.
050604     05  FILLER                      PIC X(04)     VALUE SPACES.
           05  WS-DRFTLST-ENTRY-TYPE       PIC X(08)     VALUE SPACES.
           05  FILLER                      PIC X(05)     VALUE SPACES.
           05  WS-DRFTLST-CLAIM-NO         PIC X(07)     VALUE SPACES.
050604     05  FILLER                      PIC X(03)     VALUE SPACES.
           05  WS-DRFTLST-CERT-NO          PIC X(10)     VALUE SPACES.
050604     05  FILLER                      PIC X(02)     VALUE SPACES.
           05  WS-DRFTLST-CLAIMANT-NM      PIC X(30)     VALUE SPACES.
050604     05  FILLER                      PIC X(02)     VALUE SPACES.
           05  WS-DRFTLST-PAYEE            PIC X(30)     VALUE SPACES.
050604     05  FILLER                      PIC X(01)     VALUE SPACES.
050604     05  WS-DRFTLST-ACCT-NO          PIC X(10)     VALUE SPACES.

      *************************


       01  WS-SUMMARY-HEADING4.
           05  FILLER                      PIC X(01)     VALUE '-'.
           05  FILLER                      PIC X(29)     VALUE SPACES.
           05  FILLER                      PIC X(14)     VALUE
               ' GROSS TOTAL  '.
           05  FILLER                      PIC X(02)     VALUE SPACES.
           05  FILLER                      PIC X(08)     VALUE
               '# CLAIMS'.
           05  FILLER                      PIC X(02)     VALUE SPACES.
           05  FILLER                      PIC X(08)     VALUE
               '# DRAFTS'.
           05  FILLER                      PIC X(02)     VALUE SPACES.
           05  FILLER                      PIC X(15)     VALUE
               '        TOTAL  '.
           05  FILLER                      PIC X(02)     VALUE SPACES.
           05  FILLER                      PIC X(07)     VALUE
               ' # STOP'.
           05  FILLER                      PIC X(02)     VALUE SPACES.
           05  FILLER                      PIC X(14)     VALUE
               '       TOTAL  '.
           05  FILLER                      PIC X(02)     VALUE SPACES.
           05  FILLER                      PIC X(09)     VALUE
               '# OFFLINE'.
           05  FILLER                      PIC X(02)     VALUE SPACES.
           05  FILLER                      PIC X(14)     VALUE
               '       NET    '.


       01  WS-SUMMARY-HEADING5.
           05  FILLER                      PIC X(01)     VALUE SPACE.
           05  FILLER                      PIC X(29)     VALUE SPACES.
           05  FILLER                      PIC X(14)     VALUE
               ' PAID CLAIMS  '.
           05  FILLER                      PIC X(02)     VALUE SPACES.
           05  FILLER                      PIC X(08)     VALUE
               '  PAID  '.
           05  FILLER                      PIC X(02)     VALUE SPACES.
           05  FILLER                      PIC X(08)     VALUE
               ' ISSUED '.
           05  FILLER                      PIC X(02)     VALUE SPACES.
           05  FILLER                      PIC X(15)     VALUE
               '      STOP-PAYS'.
           05  FILLER                      PIC X(02)     VALUE SPACES.
           05  FILLER                      PIC X(07)     VALUE
               '  PAYS '.
           05  FILLER                      PIC X(02)     VALUE SPACES.
           05  FILLER                      PIC X(14)     VALUE
               '      OFFLINE '.
           05  FILLER                      PIC X(03)     VALUE SPACES.
           05  FILLER                      PIC X(09)     VALUE
               '  TRANS  '.
           05  FILLER                      PIC X(01)     VALUE SPACES.
           05  FILLER                      PIC X(14)     VALUE
               '   PAID CLAIMS'.



       01  WS-SUMMARY-LINEX.
           05  FILLER                      PIC X(01)     VALUE SPACE.
           05  WS-SUMMARY-LABEL            PIC X(27)     VALUE SPACES.
           05  FILLER                      PIC X(02)     VALUE SPACES.
011810     05  WS-SUMMARY-TOT-PD-AMT       PIC $$Z,ZZZ,ZZ9.99-.
           05  FILLER                      PIC X(02)     VALUE SPACE.
           05  WS-SUMMARY-CLAIMS-PD-CNT    PIC ZZZ,ZZ9.
011810     05  FILLER                      PIC X(02)     VALUE SPACES.
           05  WS-SUMMARY-DRAFT-CNT        PIC ZZZ,ZZ9.
           05  FILLER                      PIC X(03)     VALUE SPACES.
011810     05  WS-SUMMARY-TOT-VOID-AMT     PIC $$Z,ZZZ,ZZ9.99-.
011810     05  FILLER                      PIC X(02)     VALUE SPACES.
           05  WS-SUMMARY-VOID-CNT         PIC ZZZ,ZZ9.
           05  FILLER                      PIC X(02)     VALUE SPACES.
           05  WS-SUMMARY-OFFLINE-AMT      PIC $$Z,ZZZ,ZZ9.99-.
           05  FILLER                      PIC X(02)     VALUE SPACES.
           05  WS-SUMMARY-OFFLINE-CNT      PIC ZZZ,ZZ9.
011810     05  FILLER                      PIC X(02)     VALUE SPACES.
011810     05  WS-SUMMARY-NETPD-AMT        PIC $$Z,ZZZ,ZZ9.99-.

053012
053012 01  WS-AHL-VOID-SEPARATOR.
053012     05  FILLER                      PIC X(01)     VALUE SPACES.
053012     05  FILLER                      PIC X(36)     VALUE
053012         '* PAYMENTS ORIGINALLY ISSUED BY AHL '.
053012     05  FILLER                      PIC X(52)     VALUE
053012         '(TRANS CREATED IN CLAIMS - LOAD TO FDF ONLY, NOT GL)'.
053012     05  FILLER                      PIC X(44)    VALUE SPACES.
053012
053012 01  WS-AHL-VOID-SUMMARY.
053012     05  FILLER                      PIC X(01)     VALUE SPACE.
053012     05  WS-AHL-SUMMARY-LABEL        PIC X(27)     VALUE SPACES.
053012     05  FILLER                      PIC X(38)     VALUE SPACES.
053012     05  WS-AHL-SUMMARY-VOID-AMT     PIC $$Z,ZZZ,ZZ9.99-.
053012     05  FILLER                      PIC X(02)     VALUE SPACES.
053012     05  WS-AHL-SUMMARY-VOID-CNT     PIC ZZZ,ZZ9.
053012     05  FILLER                      PIC X(43)     VALUE SPACES.

      *              *************
      *              ELCDTECX: LAYOUT FOR DISK-DATE FILE
                     COPY ELCDTECX.


                     COPY ELCDTEVR.

      *              *************
      *              ELCDATE: LAYOUT OF DATA PASSED TO DATE CONV RTN
                     COPY ELCDATE.


       LINKAGE SECTION.

061814 01  var  pic x(30).

       01  PARM.
           05  PARM-LENGTH        COMP     PIC S9(04)    VALUE ZEROS.
           05  PARM-PREV-CYCLE-DT          PIC X(08)     VALUE SPACES.
           05  PARM-CYCLE-DT               PIC X(08)     VALUE SPACES.
062512     05  PARM-AHL-CUTOFF-DT          PIC X(08)     VALUE SPACES.

      ******************************************************************
      ********************************
       PROCEDURE DIVISION USING PARM.

      ****************READ DISK-DATE FILE
       0000-DATE-CARD-READ. COPY ELCDTERX.

       1000-MAIN-LOGIC.

061814     set P to address of KIXSYS
061814     CALL "getenv" using by value P returning var-ptr
061814     if var-ptr = null then
061814        display ' kixsys not set '
061814     else
061814        set address of var to var-ptr
061814        move 0 to env-var-len
061814        inspect var tallying env-var-len
061814          for characters before X'00' 
061814        unstring var (1:env-var-len) delimited by '/'
061814           into WS-KIX-FIL1 WS-KIX-APPS WS-KIX-ENV WS-KIX-MYENV
061814              WS-KIX-SYS
061814        end-unstring
061814     end-if
061814
061814     display ' KIXSYS  ' ws-kix-myenv

           PERFORM 1500-EDIT-CYCLE-DATE       THRU 1500-EXIT

           PERFORM OPEN-FILES                 THRU OPEN-FILES-EXIT
043012
043012     PERFORM 1600-GET-SELECT-DATE       THRU 1600-EXIT

           MOVE WS-DETAIL-REPORT-TITLE        TO WS-H1-TITLE
           MOVE COMPANY-NAME                  TO WS-H2-COMPANY-NAME
           MOVE WS-CURRENT-DATE               TO WS-H2-DATE
           MOVE WS-EDITED-CYCLE-DT            TO WS-H3-THROUGH-DT

030612     MOVE DTE-CLIENT                    TO WS-GT-CLIENT-ID
030612
121902     IF DTE-CLIENT = 'DCC'
121902         MOVE 'DCC DISABILITY   '       TO WS-DIS-LABEL
121902         MOVE 'DCC LIFE         '       TO WS-LIFE-LABEL
121902         MOVE 'DCC'                     TO WS-GT-CLIENT-ID
121902     END-IF

           PERFORM 3500-PRINT-HEADINGS        THRU 3500-EXIT

           MOVE DTE-CLASIC-COMPANY-CD         TO CL-COMPANY-CD
           START ELMSTR-INFILE KEY NOT < CL-COMPANY-CD
           END-START

010704     EVALUATE TRUE
010704     WHEN WS-ELMSTR-FILE-STATUS = '00'
010704         CONTINUE

010704     WHEN WS-ELMSTR-FILE-STATUS = '23'
010704         SET END-OF-ELMSTR TO TRUE
010704         MOVE '0NO ACTIVITY FOR EL585 REPORT SERIES'
010704                                     TO PRT
010704         PERFORM 3900-WRITE          THRU 3900-EXIT

010704     WHEN OTHER
010704        DISPLAY ' ELMSTR START ' WS-ELMSTR-FILE-STATUS
              PERFORM ABEND-PGM               THRU APS-EXIT
010704     END-EVALUATE

010704     IF NOT END-OF-ELMSTR 
               SORT SORT-FILE  ASCENDING KEY SORT-DRAFT-NO
               INPUT  PROCEDURE 2000-INPUT-PROCEDURE  THRU 2000-EXIT
               OUTPUT PROCEDURE 3000-OUTPUT-PROCEDURE THRU 3000-EXIT

               IF SORT-RETURN  NOT = ZEROS
                   MOVE 'INTERNAL SORT ABORTED'
                                           TO WS-ABEND-MESSAGE
                   MOVE '0101'             TO WS-RETURN-CODE
                   PERFORM ABEND-PGM       THRU APS-EXIT
               END-IF
010704     END-IF


           PERFORM CLOSE-FILES        THRU CLOSE-FILES-EXIT

061814     if dte-client = 'CID' or 'AHL'
061814        perform 5000-build-sql-table thru 5000-exit
061814     end-if

           GOBACK.

       1500-EDIT-CYCLE-DATE.

           IF PARM-LENGTH = +0
               DISPLAY 'CYCLE DATE INPUT PARMS MISSING'
               PERFORM ABEND-PGM            THRU APS-EXIT
           END-IF

           MOVE PARM-CYCLE-DT               TO DC-GREG-DATE-CYMD
           MOVE 'L'                         TO DC-OPTION-CODE
           PERFORM 8500-DATE-CONVERSION     THRU 8500-EXIT

           IF NO-CONVERSION-ERROR
               MOVE DC-BIN-DATE-1           TO WS-CYCLE-DT-BINARY
               MOVE PARM-CYCLE-DT           TO WS-CYCLE-DT
               MOVE WS-CYCLE-DT-MM          TO WS-EDITED-CYCLE-DT-MM
               MOVE WS-CYCLE-DT-DD          TO WS-EDITED-CYCLE-DT-DD
               MOVE WS-CYCLE-DT-YY          TO WS-EDITED-CYCLE-DT-YY
061814         move dc-greg-date-a-edit     to tbl-date
           ELSE
               DISPLAY 'INVALID CYCLE DATE ' PARM-CYCLE-DT
               PERFORM ABEND-PGM            THRU APS-EXIT
           END-IF


      ************* CREATE BEGINNING OF MONTH DATE FOR MTD PROCESSING
           MOVE PARM-CYCLE-DT               TO WS-CURR-MNTH-STRT-DT
           MOVE '01'                        TO WS-CURR-MNTH-DAY01
           MOVE WS-CURR-MNTH-STRT-DT        TO DC-GREG-DATE-CYMD
           MOVE 'L'                         TO DC-OPTION-CODE
           PERFORM 8500-DATE-CONVERSION     THRU 8500-EXIT

           IF NO-CONVERSION-ERROR
               MOVE DC-BIN-DATE-1           TO WS-CURR-MNTH-STRT-DT-BIN
           ELSE
               DISPLAY 'CONVERSION ERROR ON MNTH-STRT-DT'
                                         WS-CURR-MNTH-STRT-DT
               PERFORM ABEND-PGM            THRU APS-EXIT
           END-IF
      *************

      ************* CREATE LAST MAINTENANCE CUTOFF DATE

           MOVE WS-CURR-MNTH-STRT-DT-BIN    TO DC-BIN-DATE-1
           MOVE -60                         TO DC-ELAPSED-DAYS
           MOVE ZERO                        TO DC-ELAPSED-MONTHS
           MOVE '6'                         TO DC-OPTION-CODE
           PERFORM 8500-DATE-CONVERSION     THRU 8500-EXIT

           IF NO-CONVERSION-ERROR
               MOVE DC-BIN-DATE-2        TO WS-LAST-MAINT-CUTOFF-DT-BIN
           ELSE
               DISPLAY 'CONVERSION ERROR ON LAST-MAINT-CUTOFF-DT'
               PERFORM ABEND-PGM            THRU APS-EXIT
           END-IF
      *************

           MOVE PARM-PREV-CYCLE-DT          TO DC-GREG-DATE-CYMD
           MOVE 'L'                         TO DC-OPTION-CODE
           PERFORM 8500-DATE-CONVERSION     THRU 8500-EXIT

           IF NO-CONVERSION-ERROR
               MOVE DC-BIN-DATE-1           TO WS-PREV-CYCLE-DT-BINARY
           ELSE
               DISPLAY 'INVALID PREVIOUS CYCLE DATE ' PARM-PREV-CYCLE-DT
               PERFORM ABEND-PGM            THRU APS-EXIT
           END-IF

           .
       1500-EXIT.
           EXIT.

043012
043012 1600-GET-SELECT-DATE.
043012
043012     MOVE DTE-CLIENT             TO CF-COMPANY-ID
043012     MOVE '1'                    TO CF-RECORD-TYPE
043012     MOVE ZEROS                  TO CF-SEQUENCE-NO
043012     MOVE SPACES                 TO CF-ACCESS-CD-GENL
043012
043012     READ ELCNTL
043012     IF WS-ELCNTL-FILE-STATUS = '10' OR '23'
043012        DISPLAY ' COMPANY RECORD, ELCNTL, NOT FOUND'
043012     ELSE
043012        IF WS-ELCNTL-FILE-STATUS NOT = '00'
043012           MOVE ' ELCNTL READ  ERROR '
043012                                 TO WS-ABEND-MESSAGE
043012           MOVE WS-ELCNTL-FILE-STATUS
043012                                 TO WS-ABEND-FILE-STATUS
043012           PERFORM ABEND-PGM     THRU APS-EXIT
043012        END-IF
043012     END-IF
043012
043012     MOVE CF-CURRENT-MONTH-END TO WS-MONTH-END-SELECT-DATE
062512
062512***ON AHL MONTHEND NEED PRIOR MONTH SELECT DATE
062512     IF PARM-CYCLE-DT = PARM-AHL-CUTOFF-DT
062512        MOVE WS-MONTH-END-SELECT-DATE TO DC-BIN-DATE-1
062512        MOVE ZERO                     TO DC-ELAPSED-DAYS
062512        MOVE -1                       TO DC-ELAPSED-MONTHS
062512        MOVE '1'                      TO DC-END-OF-MONTH 
062512        MOVE '6'                      TO DC-OPTION-CODE
062512        PERFORM 8500-DATE-CONVERSION  THRU 8500-EXIT
062512
062512        IF NO-CONVERSION-ERROR
062512           MOVE DC-BIN-DATE-2 TO WS-MONTH-END-SELECT-DATE
062512        ELSE
062512           DISPLAY 'CONVERSION ERROR ON LAST-MAINT-CUTOFF-DT'
062512           PERFORM ABEND-PGM  THRU APS-EXIT
062512        END-IF
062512     END-IF
043012     .
043012 1600-EXIT.
043012     EXIT.
043012

       2000-INPUT-PROCEDURE.

           PERFORM 2010-BUILD-REPORT          THRU 2010-EXIT
               UNTIL END-OF-ELMSTR


           IF WS-OVER-SETAMT-CNT = +0
               MOVE '-NO CLAIMS PAID OVER $10,000 THIS MONTH-TO-DATE'
                                              TO PRT
               PERFORM 3900-WRITE             THRU 3900-EXIT
           END-IF


           PERFORM 3200-PRINT-SUMMARY         THRU 3200-EXIT

           .
       2000-EXIT.
           EXIT.


       2010-BUILD-REPORT.

           READ ELMSTR-INFILE NEXT RECORD
               AT END
                   SET END-OF-ELMSTR        TO TRUE
                   GO TO 2010-EXIT
           END-READ

      *********************** COMPANY CODE OF HEX '04' = CID
121902*********************** COMPANY CODE OF HEX '05' = DCC 
           IF (CL-COMPANY-CD = DTE-CLASIC-COMPANY-CD AND
               CL-LAST-MAINT-DT >= WS-LAST-MAINT-CUTOFF-DT-BIN)
               WRITE MTD-ELMSTR-REC FROM CLAIM-MASTER
           ELSE
               GO TO 2010-EXIT
           END-IF

           MOVE LOW-VALUES                  TO AT-CONTROL-PRIMARY
           MOVE CL-CONTROL-PRIMARY          TO AT-CONTROL-PRIMARY (1:20)
           START ELTRLR-INFILE KEY NOT < AT-CONTROL-PRIMARY
               INVALID KEY
                   DISPLAY 'INVALID KEY ON START ELTRLR '
                                            CL-CONTROL-PRIMARY
                   PERFORM ABEND-PGM        THRU APS-EXIT
           END-START

           SET NEW-ELTRLR-KEY               TO TRUE
           SET CLAIM-NOT-COUNTED            TO TRUE
           MOVE ZEROS                       TO WS-AMT-PD-THIS-CLM
           PERFORM 2100-READ-ELTRLR         THRU 2100-EXIT
               UNTIL ELTRLR-KEY-CHANGE  OR
                     END-OF-ELTRLR

           IF DETAIL-SETUP-DONE
               ADD +1                       TO WS-OVER-SETAMT-CNT
               MOVE WS-AMT-PD-THIS-CLM      TO WS-D1-AMT-PD-MTD
               PERFORM 3100-PRINT-DETAIL    THRU 3100-EXIT
               SET DETAIL-SETUP-NOT-DONE    TO TRUE
           END-IF

           .
       2010-EXIT.
           EXIT.


       2100-READ-ELTRLR.

           SET STOP-PAY-SW-OFF              TO TRUE
           READ ELTRLR-INFILE NEXT RECORD
               AT END
                   SET END-OF-ELTRLR        TO TRUE
                   GO TO 2100-EXIT
           END-READ


           IF AT-CONTROL-PRIMARY (1:20) = CL-CONTROL-PRIMARY
               CONTINUE
           ELSE
               SET ELTRLR-KEY-CHANGE        TO TRUE
               GO TO 2100-EXIT
           END-IF

      ****************** TYPE 2 = PAYMENT TRAILER
           IF (AT-TRAILER-TYPE = '2')
032707        AND (AT-PAYMENT-TYPE NOT = 'I')           
               CONTINUE
           ELSE
               GO TO 2100-EXIT
           END-IF


           IF (AT-VOID-DT = LOW-VALUES OR
               AT-VOID-DT = SPACES)
               CONTINUE
           ELSE
               IF (AT-CHECK-WRITTEN-DT = LOW-VALUES OR
                   AT-CHECK-WRITTEN-DT = SPACES)
      ************ THIS IS A VOID  - THE CHECK IS NEVER PRINTED
                   GO TO 2100-EXIT
               END-IF

      ********** ELSE IT'S A STOP-PAY
               PERFORM 2150-PROCESS-STOPPAY THRU 2150-EXIT

      ********** WE WANT TO RECORD THE CURRENT PERIOD CHECK AS A DRAFT
      **********    ISSUED EVEN THOUGH A STOP-PAY HAS BEEN ISSUED BUT WE
      **********    DON'T WANT TO SHOW IT IN THE CLAIMS PAID COUNT
               IF ((AT-CHECK-WRITTEN-DT >= WS-CURR-MNTH-STRT-DT-BIN OR
                    AT-CHECK-WRITTEN-DT  > WS-PREV-CYCLE-DT-BINARY) AND
043012              AT-CHECK-WRITTEN-DT <= WS-CYCLE-DT-BINARY AND
043012              DTE-CLIENT NOT = 'AHL')
043012                  OR
043012             (DTE-CLIENT = 'AHL' AND 
043012              AT-PMT-SELECT-DT = WS-MONTH-END-SELECT-DATE AND
043012              AT-CHECK-WRITTEN-DT <= WS-CYCLE-DT-BINARY)
                   SET THIS-IS-A-STOP-PAY   TO TRUE
               ELSE
                   GO TO 2100-EXIT
               END-IF
           END-IF

043012     IF (DTE-CLIENT NOT = 'AHL' AND
043012         AT-CHECK-WRITTEN-DT >= WS-CURR-MNTH-STRT-DT-BIN AND
               AT-CHECK-WRITTEN-DT <= WS-PREV-CYCLE-DT-BINARY)
043012                 OR
043012        (DTE-CLIENT = 'AHL' AND
043012         AT-PMT-SELECT-DT = WS-MONTH-END-SELECT-DATE AND
043012         AT-CHECK-WRITTEN-DT <= WS-PREV-CYCLE-DT-BINARY)
               SET MONTH-TO-DATE            TO TRUE
           ELSE
               IF (AT-CHECK-WRITTEN-DT  > WS-PREV-CYCLE-DT-BINARY AND
                   AT-CHECK-WRITTEN-DT <= WS-CYCLE-DT-BINARY)
                   SET DAILY                TO TRUE
               ELSE
                   GO TO 2100-EXIT
               END-IF
           END-IF

           IF AT-PAYMENT-ORIGIN NOT = '3'
               PERFORM 2200-ACCUM-REG-SUMMARY-DATA THRU 2200-EXIT
           ELSE
               PERFORM 2175-PROCESS-OFFLINE-DATA   THRU 2175-EXIT
           END-IF

           .
       2100-EXIT.
           EXIT.

       2150-PROCESS-STOPPAY.

043012     IF (DTE-CLIENT NOT = 'AHL' AND
043012         AT-VOID-DT >= WS-CURR-MNTH-STRT-DT-BIN AND
               AT-VOID-DT <= WS-PREV-CYCLE-DT-BINARY)
043012                      OR
043012         (DTE-CLIENT = 'AHL' AND
043012          AT-VOID-SELECT-DT = WS-MONTH-END-SELECT-DATE AND
043012          AT-VOID-DT <= WS-PREV-CYCLE-DT-BINARY)
               SET MONTH-TO-DATE            TO TRUE
           ELSE
               IF (AT-VOID-DT  > WS-PREV-CYCLE-DT-BINARY AND
                   AT-VOID-DT <= WS-CYCLE-DT-BINARY)
                   SET DAILY                TO TRUE
               ELSE
                   GO TO 2150-EXIT
               END-IF
           END-IF

           COMPUTE WS-DRFTLST-STOP-PAY-AMT = AT-AMOUNT-PAID * -1

053012****LOOK FOR AHL PAYMENTS MADE PRIOR TO APRIL 1 2012
053012     IF DTE-CLIENT = 'AHL' AND
053012        AT-CHECK-WRITTEN-DT < X'A861'  AND
053012       (AT-CHECK-WRITTEN-DT NOT EQUAL LOW-VALUES AND SPACES)
053012          IF AT-CLAIM-TYPE = 'A'
053012             ADD AT-AMOUNT-PAID     TO WS-TOT-DIS-AHL-VOID-AMT
053012                                       WS-GRAND-TOT-AHL-VOID-AMT
053012             ADD +1                 TO WS-TOT-DIS-AHL-VOID-CNT
053012                                       WS-GRAND-TOT-AHL-VOID-CNT
053012             IF DAILY
053012                MOVE WS-DRFTLST-STOP-PAY-AMT  
053012                        TO WS-DRFTLST-DRFTAMT
052312                MOVE 'AHL S/P'
052312                        TO WS-DRFTLST-ENTRY-TYPE
052312                PERFORM 2400-DAILY-DRAFT-LIST THRU 2400-EXIT
052312
052312                ADD AT-AMOUNT-PAID TO WS-DLY-DIS-AHL-VOID-AMT
052312                ADD +1             TO WS-DLY-DIS-AHL-VOID-CNT
052312             END-IF
052312          ELSE
053012             ADD AT-AMOUNT-PAID     TO WS-TOT-LIFE-AHL-VOID-AMT
053012                                       WS-GRAND-TOT-AHL-VOID-AMT
053012             ADD +1                 TO WS-TOT-LIFE-AHL-VOID-CNT
053012                                       WS-GRAND-TOT-AHL-VOID-CNT
053012             IF DAILY
053012                MOVE WS-DRFTLST-STOP-PAY-AMT  
053012                        TO WS-DRFTLST-DRFTAMT
052312                MOVE 'AHL S/P'
052312                        TO WS-DRFTLST-ENTRY-TYPE
052312                PERFORM 2400-DAILY-DRAFT-LIST THRU 2400-EXIT
052312
052312                ADD AT-AMOUNT-PAID TO WS-DLY-LIFE-AHL-VOID-AMT
052312                ADD +1             TO WS-DLY-LIFE-AHL-VOID-CNT
052312             END-IF
052312          END-IF
052312          GO TO 2150-EXIT
052312     END-IF
052312
           EVALUATE TRUE
           WHEN AT-CLAIM-TYPE = 'A'

               IF MONTH-TO-DATE
                   CONTINUE
               ELSE
                   MOVE WS-DRFTLST-STOP-PAY-AMT  TO WS-DRFTLST-DRFTAMT
                   MOVE 'STOP-PAY'               TO
                                                 WS-DRFTLST-ENTRY-TYPE
                   PERFORM 2400-DAILY-DRAFT-LIST THRU 2400-EXIT

      ************** ACCUMULATE CURRENT CYCLE DAY TOTALS HERE AND GO ON
      *              TO ADD THESE FIGURES TO MTD TOTALS
                   ADD AT-AMOUNT-PAID          TO WS-DLY-DIS-VOID-AMT
                   ADD +1                      TO WS-DLY-DIS-VOID-CNT
                   ADD WS-DRFTLST-STOP-PAY-AMT TO WS-DLY-DIS-NETPD-AMT
               END-IF

               ADD AT-AMOUNT-PAID               TO WS-TOT-DIS-VOID-AMT
               ADD +1                           TO WS-TOT-DIS-VOID-CNT
               ADD WS-DRFTLST-STOP-PAY-AMT      TO WS-TOT-DIS-NETPD-AMT

120103     WHEN AT-CLAIM-TYPE = 'G'
120103
120103         IF MONTH-TO-DATE
120103             CONTINUE
120103         ELSE
120103             MOVE WS-DRFTLST-STOP-PAY-AMT  TO WS-DRFTLST-DRFTAMT
120103             MOVE 'STOP-PAY'               TO
120103                                           WS-DRFTLST-ENTRY-TYPE
120103             PERFORM 2400-DAILY-DRAFT-LIST THRU 2400-EXIT
120103
120103************** ACCUMULATE CURRENT CYCLE DAY TOTALS HERE AND GO ON
120103*              TO ADD THESE FIGURES TO MTD TOTALS
120103             ADD AT-AMOUNT-PAID          TO WS-DLY-GAP-VOID-AMT
120103             ADD +1                      TO WS-DLY-GAP-VOID-CNT
120103             ADD WS-DRFTLST-STOP-PAY-AMT TO WS-DLY-GAP-NETPD-AMT
120103         END-IF
120103
120103         ADD AT-AMOUNT-PAID               TO WS-TOT-GAP-VOID-AMT
120103         ADD +1                           TO WS-TOT-GAP-VOID-CNT
120103         ADD WS-DRFTLST-STOP-PAY-AMT      TO WS-TOT-GAP-NETPD-AMT
      

121902     WHEN AT-CLAIM-TYPE = 'I'
121902
121902         IF MONTH-TO-DATE
121902             CONTINUE
121902         ELSE
121902             MOVE WS-DRFTLST-STOP-PAY-AMT  TO WS-DRFTLST-DRFTAMT
121902             MOVE 'STOP-PAY'               TO
121902                                           WS-DRFTLST-ENTRY-TYPE
121902             PERFORM 2400-DAILY-DRAFT-LIST THRU 2400-EXIT
121902
121902************** ACCUMULATE CURRENT CYCLE DAY TOTALS HERE AND GO ON
121902*              TO ADD THESE FIGURES TO MTD TOTALS
121902             ADD AT-AMOUNT-PAID          TO WS-DLY-IU-VOID-AMT
121902             ADD +1                      TO WS-DLY-IU-VOID-CNT
121902             ADD WS-DRFTLST-STOP-PAY-AMT TO WS-DLY-IU-NETPD-AMT
121902         END-IF
121902
121902         ADD AT-AMOUNT-PAID               TO WS-TOT-IU-VOID-AMT
121902         ADD +1                           TO WS-TOT-IU-VOID-CNT
121902         ADD WS-DRFTLST-STOP-PAY-AMT      TO WS-TOT-IU-NETPD-AMT
      
           WHEN AT-CLAIM-TYPE = 'L'

               IF MONTH-TO-DATE
                   CONTINUE
               ELSE
                   MOVE WS-DRFTLST-STOP-PAY-AMT  TO WS-DRFTLST-DRFTAMT
                   MOVE 'STOP-PAY'               TO
                                                 WS-DRFTLST-ENTRY-TYPE
                   PERFORM 2400-DAILY-DRAFT-LIST THRU 2400-EXIT

                   ADD AT-AMOUNT-PAID           TO WS-DLY-LIFE-VOID-AMT
                   ADD +1                       TO WS-DLY-LIFE-VOID-CNT
                   ADD WS-DRFTLST-STOP-PAY-AMT  TO WS-DLY-LIFE-NETPD-AMT
               END-IF

               ADD AT-AMOUNT-PAID               TO WS-TOT-LIFE-VOID-AMT
               ADD +1                           TO WS-TOT-LIFE-VOID-CNT
               ADD WS-DRFTLST-STOP-PAY-AMT      TO WS-TOT-LIFE-NETPD-AMT


           WHEN OTHER
               DISPLAY 'INVALID CLAIM TYPE ' AT-CLAIM-TYPE
               PERFORM ABEND-PGM            THRU APS-EXIT
           END-EVALUATE


061814     if not month-to-date
061814        compute int-tot-stop-pays = int-tot-stop-pays +
061814           at-amount-paid
061814     end-if

           ADD AT-AMOUNT-PAID               TO WS-GRAND-TOT-VOID-AMT
           ADD +1                           TO WS-GRAND-TOT-VOID-CNT
           ADD WS-DRFTLST-STOP-PAY-AMT      TO WS-GRAND-TOT-NETPD-AMT

           .
       2150-EXIT.
           EXIT.

       2175-PROCESS-OFFLINE-DATA.

           IF (AT-CHECK-WRITTEN-DT >= WS-CURR-MNTH-STRT-DT-BIN AND
043012         AT-CHECK-WRITTEN-DT <= WS-PREV-CYCLE-DT-BINARY AND
043012         DTE-CLIENT NOT = 'AHL')
043012                      OR
043012        (DTE-CLIENT = 'AHL'   AND
043012         AT-PMT-SELECT-DT = WS-MONTH-END-SELECT-DATE AND
043012         AT-CHECK-WRITTEN-DT <= WS-PREV-CYCLE-DT-BINARY)
               SET MONTH-TO-DATE        TO TRUE
           ELSE
               IF (AT-CHECK-WRITTEN-DT  > WS-PREV-CYCLE-DT-BINARY AND
                   AT-CHECK-WRITTEN-DT <= WS-CYCLE-DT-BINARY)
                   SET DAILY            TO TRUE
               ELSE
                   GO TO 2175-EXIT
               END-IF
           END-IF

           EVALUATE TRUE
           WHEN AT-CLAIM-TYPE = 'A'

               IF MONTH-TO-DATE
                   CONTINUE
               ELSE
                   MOVE AT-AMOUNT-PAID      TO WS-DRFTLST-DRFTAMT
                   MOVE 'OFFLINE'           TO WS-DRFTLST-ENTRY-TYPE
                   PERFORM 2400-DAILY-DRAFT-LIST THRU 2400-EXIT

                   ADD AT-AMOUNT-PAID       TO WS-DLY-DIS-OFFLN-AMT
                   ADD +1                   TO WS-DLY-DIS-OFFLN-CNT
                   ADD AT-AMOUNT-PAID       TO WS-DLY-DIS-NETPD-AMT
               END-IF

               ADD AT-AMOUNT-PAID           TO WS-TOT-DIS-OFFLN-AMT
               ADD +1                       TO WS-TOT-DIS-OFFLN-CNT
               ADD AT-AMOUNT-PAID           TO WS-TOT-DIS-NETPD-AMT

120103     WHEN AT-CLAIM-TYPE = 'G'
120103
120103         IF MONTH-TO-DATE
120103             CONTINUE
120103         ELSE
120103             MOVE AT-AMOUNT-PAID      TO WS-DRFTLST-DRFTAMT
120103             MOVE 'OFFLINE'           TO WS-DRFTLST-ENTRY-TYPE
120103             PERFORM 2400-DAILY-DRAFT-LIST THRU 2400-EXIT
120103
120103             ADD AT-AMOUNT-PAID       TO WS-DLY-GAP-OFFLN-AMT
120103             ADD +1                   TO WS-DLY-GAP-OFFLN-CNT
120103             ADD AT-AMOUNT-PAID       TO WS-DLY-GAP-NETPD-AMT
120103         END-IF
120103
120103         ADD AT-AMOUNT-PAID           TO WS-TOT-GAP-OFFLN-AMT
120103         ADD +1                       TO WS-TOT-GAP-OFFLN-CNT
120103         ADD AT-AMOUNT-PAID           TO WS-TOT-GAP-NETPD-AMT
      
121902     WHEN AT-CLAIM-TYPE = 'I'
121902
121902         IF MONTH-TO-DATE
121902             CONTINUE
121902         ELSE
121902             MOVE AT-AMOUNT-PAID      TO WS-DRFTLST-DRFTAMT
121902             MOVE 'OFFLINE'           TO WS-DRFTLST-ENTRY-TYPE
121902             PERFORM 2400-DAILY-DRAFT-LIST THRU 2400-EXIT
121902
121902             ADD AT-AMOUNT-PAID       TO WS-DLY-IU-OFFLN-AMT
121902             ADD +1                   TO WS-DLY-IU-OFFLN-CNT
121902             ADD AT-AMOUNT-PAID       TO WS-DLY-IU-NETPD-AMT
121902         END-IF
121902
121902         ADD AT-AMOUNT-PAID           TO WS-TOT-IU-OFFLN-AMT
121902         ADD +1                       TO WS-TOT-IU-OFFLN-CNT
121902         ADD AT-AMOUNT-PAID           TO WS-TOT-IU-NETPD-AMT
      
      
           WHEN AT-CLAIM-TYPE = 'L'

               IF MONTH-TO-DATE
                   CONTINUE
               ELSE
                   MOVE AT-AMOUNT-PAID      TO WS-DRFTLST-DRFTAMT
                   MOVE 'OFFLINE'           TO WS-DRFTLST-ENTRY-TYPE
                   PERFORM 2400-DAILY-DRAFT-LIST THRU 2400-EXIT

                   ADD AT-AMOUNT-PAID       TO WS-DLY-LIFE-OFFLN-AMT
                   ADD +1                   TO WS-DLY-LIFE-OFFLN-CNT
                   ADD AT-AMOUNT-PAID       TO WS-DLY-LIFE-NETPD-AMT
               END-IF

               ADD AT-AMOUNT-PAID           TO WS-TOT-LIFE-OFFLN-AMT
               ADD +1                       TO WS-TOT-LIFE-OFFLN-CNT
               ADD AT-AMOUNT-PAID           TO WS-TOT-LIFE-NETPD-AMT


           WHEN OTHER
               DISPLAY 'INVALID CLAIM TYPE ' AT-CLAIM-TYPE
               PERFORM ABEND-PGM            THRU APS-EXIT
           END-EVALUATE


061814     if not month-to-date
061814        compute int-tot-offline = int-tot-offline +
061814           FUNCTION ABS(at-amount-paid)
061814     end-if

           ADD AT-AMOUNT-PAID               TO WS-GRAND-TOT-OFFLN-AMT
           ADD +1                           TO WS-GRAND-TOT-OFFLN-CNT
           ADD AT-AMOUNT-PAID               TO WS-GRAND-TOT-NETPD-AMT

           .
       2175-EXIT.
           EXIT.

       2200-ACCUM-REG-SUMMARY-DATA.

           EVALUATE TRUE
           WHEN AT-CLAIM-TYPE = 'A'

               IF MONTH-TO-DATE
                   CONTINUE
               ELSE
                   MOVE AT-AMOUNT-PAID      TO WS-DRFTLST-DRFTAMT
                   MOVE 'ONLINE'            TO WS-DRFTLST-ENTRY-TYPE
                   PERFORM 2400-DAILY-DRAFT-LIST THRU 2400-EXIT

      ************** ACCUMULATE CURRENT CYCLE DAY TOTALS HERE AND GO ON
      *              TO ADD THESE FIGURES TO MTD TOTALS ALSO
                   IF CLAIM-NOT-COUNTED
                       ADD +1               TO WS-DLY-DIS-CLMS-PD-CNT
                   END-IF
                   ADD AT-AMOUNT-PAID       TO WS-DLY-DIS-CLMS-PD-AMT
                   ADD +1                   TO WS-DLY-DIS-DRFTS-CNT
                   ADD AT-AMOUNT-PAID       TO WS-DLY-DIS-NETPD-AMT
               END-IF


               IF (CLAIM-NOT-COUNTED AND
                   STOP-PAY-SW-OFF)
                   ADD +1                   TO WS-TOT-DIS-CLMS-PD-CNT
                   ADD +1                   TO WS-GRAND-TOT-CLMS-PD-CNT
                   SET CLAIM-COUNTED        TO TRUE
               END-IF

               ADD AT-AMOUNT-PAID           TO WS-TOT-DIS-CLMS-PD-AMT
               ADD +1                       TO WS-TOT-DIS-DRFTS-CNT
               ADD AT-AMOUNT-PAID           TO WS-TOT-DIS-NETPD-AMT


120103     WHEN AT-CLAIM-TYPE = 'G'
120103  
120103         IF MONTH-TO-DATE
120103             CONTINUE
120103         ELSE
120103             MOVE AT-AMOUNT-PAID      TO WS-DRFTLST-DRFTAMT
120103             MOVE 'ONLINE'            TO WS-DRFTLST-ENTRY-TYPE
120103             PERFORM 2400-DAILY-DRAFT-LIST THRU 2400-EXIT
120103
120103************** ACCUMULATE CURRENT CYCLE DAY TOTALS HERE AND GO ON
120103*              TO ADD THESE FIGURES TO MTD TOTALS ALSO
120103             IF CLAIM-NOT-COUNTED
120103                 ADD +1               TO WS-DLY-GAP-CLMS-PD-CNT
120103             END-IF
120103             ADD AT-AMOUNT-PAID       TO WS-DLY-GAP-CLMS-PD-AMT
120103             ADD +1                   TO WS-DLY-GAP-DRFTS-CNT
120103             ADD AT-AMOUNT-PAID       TO WS-DLY-GAP-NETPD-AMT
120103         END-IF
120103
120103
120103         IF (CLAIM-NOT-COUNTED AND
120103             STOP-PAY-SW-OFF)
120103             ADD +1                   TO WS-TOT-GAP-CLMS-PD-CNT
120103             ADD +1                   TO WS-GRAND-TOT-CLMS-PD-CNT
120103             SET CLAIM-COUNTED        TO TRUE
120103         END-IF
120103
120103         ADD AT-AMOUNT-PAID           TO WS-TOT-GAP-CLMS-PD-AMT
120103         ADD +1                       TO WS-TOT-GAP-DRFTS-CNT
120103         ADD AT-AMOUNT-PAID           TO WS-TOT-GAP-NETPD-AMT

121902     WHEN AT-CLAIM-TYPE = 'I'
121902  
121902         IF MONTH-TO-DATE
121902             CONTINUE
121902         ELSE
121902             MOVE AT-AMOUNT-PAID      TO WS-DRFTLST-DRFTAMT
121902             MOVE 'ONLINE'            TO WS-DRFTLST-ENTRY-TYPE
121902             PERFORM 2400-DAILY-DRAFT-LIST THRU 2400-EXIT
121902
121902************** ACCUMULATE CURRENT CYCLE DAY TOTALS HERE AND GO ON
121902*              TO ADD THESE FIGURES TO MTD TOTALS ALSO
121902             IF CLAIM-NOT-COUNTED
121902                 ADD +1               TO WS-DLY-IU-CLMS-PD-CNT
121902             END-IF
121902             ADD AT-AMOUNT-PAID       TO WS-DLY-IU-CLMS-PD-AMT
121902             ADD +1                   TO WS-DLY-IU-DRFTS-CNT
121902             ADD AT-AMOUNT-PAID       TO WS-DLY-IU-NETPD-AMT
121902         END-IF
121902
121902
121902         IF (CLAIM-NOT-COUNTED AND
121902             STOP-PAY-SW-OFF)
121902             ADD +1                   TO WS-TOT-IU-CLMS-PD-CNT
121902             ADD +1                   TO WS-GRAND-TOT-CLMS-PD-CNT
121902             SET CLAIM-COUNTED        TO TRUE
121902         END-IF
121902
121902         ADD AT-AMOUNT-PAID           TO WS-TOT-IU-CLMS-PD-AMT
121902         ADD +1                       TO WS-TOT-IU-DRFTS-CNT
121902         ADD AT-AMOUNT-PAID           TO WS-TOT-IU-NETPD-AMT


           WHEN AT-CLAIM-TYPE = 'L'

               IF MONTH-TO-DATE
                   CONTINUE
               ELSE
                   MOVE AT-AMOUNT-PAID      TO WS-DRFTLST-DRFTAMT
                   MOVE 'ONLINE'            TO WS-DRFTLST-ENTRY-TYPE
                   PERFORM 2400-DAILY-DRAFT-LIST THRU 2400-EXIT

                   IF CLAIM-NOT-COUNTED
                       ADD +1               TO WS-DLY-LIFE-CLMS-PD-CNT
                   END-IF
                   ADD AT-AMOUNT-PAID       TO WS-DLY-LIFE-CLMS-PD-AMT
                   ADD +1                   TO WS-DLY-LIFE-DRFTS-CNT
                   ADD AT-AMOUNT-PAID       TO WS-DLY-LIFE-NETPD-AMT
               END-IF


               IF (CLAIM-NOT-COUNTED AND
                   STOP-PAY-SW-OFF)
                   ADD +1                   TO WS-TOT-LIFE-CLMS-PD-CNT
                   ADD +1                   TO WS-GRAND-TOT-CLMS-PD-CNT
                   SET CLAIM-COUNTED        TO TRUE
               END-IF

               ADD AT-AMOUNT-PAID           TO WS-TOT-LIFE-CLMS-PD-AMT
               ADD +1                       TO WS-TOT-LIFE-DRFTS-CNT
               ADD AT-AMOUNT-PAID           TO WS-TOT-LIFE-NETPD-AMT


           WHEN OTHER
               DISPLAY 'INVALID CLAIM TYPE ' AT-CLAIM-TYPE
               PERFORM ABEND-PGM            THRU APS-EXIT
           END-EVALUATE

061814     if not month-to-date
061814        compute int-tot-clm-pmts = int-tot-clm-pmts +
061814           at-amount-paid
061814     end-if

           ADD AT-AMOUNT-PAID               TO WS-GRAND-TOT-CLMS-PD-AMT
           ADD +1                           TO WS-GRAND-TOT-DRFTS-CNT
           ADD AT-AMOUNT-PAID               TO WS-GRAND-TOT-NETPD-AMT


           ADD AT-AMOUNT-PAID               TO WS-AMT-PD-THIS-CLM
           IF (WS-AMT-PD-THIS-CLM > WS-AMT-PAID-CRITERIA AND
               DETAIL-SETUP-NOT-DONE)
               PERFORM 2300-DETAIL-SETUP THRU 2300-EXIT
           END-IF

           .
       2200-EXIT.
           EXIT.

       2300-DETAIL-SETUP.

           MOVE SPACES                      TO WS-DETAIL1

           MOVE AT-CLAIM-NO                 TO WS-D1-CLAIM-NO

           MOVE SPACES                      TO WS-D1-CLAIMANT-NM
           STRING CL-INSURED-LAST-NAME      DELIMITED BY SPACE
                  ', '                      DELIMITED BY SIZE
                  CL-INSURED-1ST-NAME       DELIMITED BY SPACE
                  ' '                       DELIMITED BY SIZE
                  CL-INSURED-MID-INIT       DELIMITED BY SPACE

                  INTO WS-D1-CLAIMANT-NM


           MOVE AT-CERT-NO                  TO WS-D1-CERT-NO

121902     EVALUATE TRUE
121902     WHEN AT-CLAIM-TYPE = 'A'
               MOVE WS-DIS-LABEL            TO WS-D1-CLAIM-TYP-DESC

120103     WHEN AT-CLAIM-TYPE = 'G'
120103         MOVE WS-GAP-LABEL            TO WS-D1-CLAIM-TYP-DESC
120103
121902     WHEN AT-CLAIM-TYPE = 'I'
121902         MOVE WS-IU-LABEL             TO WS-D1-CLAIM-TYP-DESC

121902     WHEN AT-CLAIM-TYPE = 'L'
               MOVE WS-LIFE-LABEL           TO WS-D1-CLAIM-TYP-DESC

121902     END-EVALUATE

           SET DETAIL-SETUP-DONE            TO TRUE

           .
       2300-EXIT.
           EXIT.

       2400-DAILY-DRAFT-LIST.

           MOVE AT-CHECK-NO                 TO WS-DRFTLST-CHECK-NO

           MOVE AT-CLAIM-NO                 TO WS-DRFTLST-CLAIM-NO
           MOVE AT-CERT-NO                  TO WS-DRFTLST-CERT-NO

           MOVE SPACES                      TO WS-DRFTLST-CLAIMANT-NM
           STRING CL-INSURED-LAST-NAME      DELIMITED BY SPACE
                  ', '                      DELIMITED BY SIZE
                  CL-INSURED-1ST-NAME       DELIMITED BY SPACE
                  ' '                       DELIMITED BY SIZE
                  CL-INSURED-MID-INIT       DELIMITED BY SPACE

                  INTO WS-DRFTLST-CLAIMANT-NM

           MOVE AT-PAYEES-NAME              TO WS-DRFTLST-PAYEE
050604     MOVE CL-CERT-ACCOUNT             TO WS-DRFTLST-ACCT-NO

           PERFORM 2900-RELEASE-SORT        THRU 2900-EXIT

           .
       2400-EXIT.
           EXIT.


       2900-RELEASE-SORT.

           RELEASE SORT-RECORD  FROM  WS-DRFTLST-DETAIL

           .
       2900-EXIT.
           EXIT.


       3000-OUTPUT-PROCEDURE.

           MOVE WS-LINE-COUNT-MAX           TO WS-LINE-COUNT
           MOVE +0                          TO WS-PAGE
           MOVE WS-EDITED-CYCLE-DT          TO WS-DRFTLST-H3-DT

           PERFORM 3020-RETURN-SORT THRU 3020-EXIT
               UNTIL END-OF-SORTFILE

           .
       3000-EXIT.
           EXIT.

       3020-RETURN-SORT.

           RETURN SORT-FILE
               AT END
                   SET END-OF-SORTFILE TO TRUE
                   GO TO 3020-EXIT
           END-RETURN

           IF WS-LINE-COUNT >= WS-LINE-COUNT-MAX
               PERFORM 3600-PRINT-DRFTLST-HEADINGS THRU 3600-EXIT
           END-IF

           MOVE SORT-RECORD                 TO PRT
           PERFORM 3900-WRITE               THRU 3900-EXIT

           .
       3020-EXIT.
           EXIT.


       3100-PRINT-DETAIL.

           IF WS-LINE-COUNT >= WS-LINE-COUNT-MAX
               PERFORM 3500-PRINT-HEADINGS  THRU 3500-EXIT
           END-IF

           MOVE WS-DETAIL1                  TO PRT
           PERFORM 3900-WRITE               THRU 3900-EXIT

           .
       3100-EXIT.
           EXIT.


       3200-PRINT-SUMMARY.

           MOVE WS-SUMMARY-REPORT-TITLE     TO WS-H1-TITLE
           MOVE WS-HEADING1                 TO PRT
           PERFORM 3900-WRITE               THRU 3900-EXIT

           MOVE WS-HEADING2                 TO PRT
           PERFORM 3900-WRITE               THRU 3900-EXIT

           MOVE +1                          TO WS-H3-PAGE
           MOVE WS-HEADING3                 TO PRT
           PERFORM 3900-WRITE               THRU 3900-EXIT

           MOVE WS-SUMMARY-HEADING4         TO PRT
           PERFORM 3900-WRITE               THRU 3900-EXIT

           MOVE WS-SUMMARY-HEADING5         TO PRT
           PERFORM 3900-WRITE               THRU 3900-EXIT

           MOVE SPACES                      TO PRT
           MOVE '-'                         TO P-CTL
           PERFORM 3900-WRITE               THRU 3900-EXIT


      ************ SUMMARY LINE 1
           MOVE WS-EDITED-CYCLE-DT          TO WS-DIS-LABEL-VAR
           MOVE WS-FULL-DIS-LABEL           TO WS-SUMMARY-LABEL
           MOVE WS-DLY-DIS-CLMS-PD-AMT      TO WS-SUMMARY-TOT-PD-AMT
           MOVE WS-DLY-DIS-CLMS-PD-CNT      TO WS-SUMMARY-CLAIMS-PD-CNT
           MOVE WS-DLY-DIS-DRFTS-CNT        TO WS-SUMMARY-DRAFT-CNT
           MOVE WS-DLY-DIS-VOID-AMT         TO WS-SUMMARY-TOT-VOID-AMT
           MOVE WS-DLY-DIS-VOID-CNT         TO WS-SUMMARY-VOID-CNT
           MOVE WS-DLY-DIS-OFFLN-AMT        TO WS-SUMMARY-OFFLINE-AMT
           MOVE WS-DLY-DIS-OFFLN-CNT        TO WS-SUMMARY-OFFLINE-CNT
           MOVE WS-DLY-DIS-NETPD-AMT        TO WS-SUMMARY-NETPD-AMT

           MOVE WS-SUMMARY-LINEX            TO PRT
           PERFORM 3900-WRITE               THRU 3900-EXIT


           INITIALIZE WS-SUMMARY-LINEX
      ************ SUMMARY LINE 2
           MOVE SPACES                      TO WS-DIS-LABEL-VAR
           MOVE WS-MTD-LITERAL              TO WS-DIS-LABEL
           MOVE WS-FULL-DIS-LABEL           TO WS-SUMMARY-LABEL
           MOVE WS-TOT-DIS-CLMS-PD-AMT      TO WS-SUMMARY-TOT-PD-AMT
           MOVE WS-TOT-DIS-CLMS-PD-CNT      TO WS-SUMMARY-CLAIMS-PD-CNT
           MOVE WS-TOT-DIS-DRFTS-CNT        TO WS-SUMMARY-DRAFT-CNT
           MOVE WS-TOT-DIS-VOID-AMT         TO WS-SUMMARY-TOT-VOID-AMT
           MOVE WS-TOT-DIS-VOID-CNT         TO WS-SUMMARY-VOID-CNT
           MOVE WS-TOT-DIS-OFFLN-AMT        TO WS-SUMMARY-OFFLINE-AMT
           MOVE WS-TOT-DIS-OFFLN-CNT        TO WS-SUMMARY-OFFLINE-CNT
           MOVE WS-TOT-DIS-NETPD-AMT        TO WS-SUMMARY-NETPD-AMT

           MOVE WS-SUMMARY-LINEX            TO PRT
           PERFORM 3900-WRITE               THRU 3900-EXIT


121902     IF DTE-CLIENT = 'DCC' 
121902************ SUMMARY LINE FOR IU DAILY 
121902         MOVE WS-EDITED-CYCLE-DT      TO WS-IU-LABEL-VAR
121902         MOVE WS-FULL-IU-LABEL        TO WS-SUMMARY-LABEL
121902         MOVE WS-DLY-IU-CLMS-PD-AMT   TO WS-SUMMARY-TOT-PD-AMT
121902         MOVE WS-DLY-IU-CLMS-PD-CNT   TO WS-SUMMARY-CLAIMS-PD-CNT
121902         MOVE WS-DLY-IU-DRFTS-CNT     TO WS-SUMMARY-DRAFT-CNT
121902         MOVE WS-DLY-IU-VOID-AMT      TO WS-SUMMARY-TOT-VOID-AMT
121902         MOVE WS-DLY-IU-VOID-CNT      TO WS-SUMMARY-VOID-CNT
121902         MOVE WS-DLY-IU-OFFLN-AMT     TO WS-SUMMARY-OFFLINE-AMT
121902         MOVE WS-DLY-IU-OFFLN-CNT     TO WS-SUMMARY-OFFLINE-CNT
121902         MOVE WS-DLY-IU-NETPD-AMT     TO WS-SUMMARY-NETPD-AMT
121902
121902         MOVE WS-SUMMARY-LINEX        TO PRT
121902         MOVE '-'                     TO P-CTL
121902         PERFORM 3900-WRITE           THRU 3900-EXIT
121902
121902
121902         INITIALIZE WS-SUMMARY-LINEX
121902************ SUMMARY LINE FOR IU MTD
121902         MOVE SPACES                  TO WS-IU-LABEL-VAR
121902         MOVE WS-MTD-LITERAL          TO WS-IU-LABEL
121902         MOVE WS-FULL-IU-LABEL        TO WS-SUMMARY-LABEL
121902         MOVE WS-TOT-IU-CLMS-PD-AMT   TO WS-SUMMARY-TOT-PD-AMT
121902         MOVE WS-TOT-IU-CLMS-PD-CNT   TO WS-SUMMARY-CLAIMS-PD-CNT
121902         MOVE WS-TOT-IU-DRFTS-CNT     TO WS-SUMMARY-DRAFT-CNT
121902         MOVE WS-TOT-IU-VOID-AMT      TO WS-SUMMARY-TOT-VOID-AMT
121902         MOVE WS-TOT-IU-VOID-CNT      TO WS-SUMMARY-VOID-CNT
121902         MOVE WS-TOT-IU-OFFLN-AMT     TO WS-SUMMARY-OFFLINE-AMT
121902         MOVE WS-TOT-IU-OFFLN-CNT     TO WS-SUMMARY-OFFLINE-CNT
121902         MOVE WS-TOT-IU-NETPD-AMT     TO WS-SUMMARY-NETPD-AMT
121902
121902         MOVE WS-SUMMARY-LINEX        TO PRT
121902         PERFORM 3900-WRITE           THRU 3900-EXIT


120103************ SUMMARY LINE FOR GAP (SECURE PAY) DAILY 
120103         MOVE WS-EDITED-CYCLE-DT      TO WS-GAP-LABEL-VAR
120103         MOVE WS-FULL-GAP-LABEL       TO WS-SUMMARY-LABEL
120103         MOVE WS-DLY-GAP-CLMS-PD-AMT  TO WS-SUMMARY-TOT-PD-AMT
120103         MOVE WS-DLY-GAP-CLMS-PD-CNT  TO WS-SUMMARY-CLAIMS-PD-CNT
120103         MOVE WS-DLY-GAP-DRFTS-CNT    TO WS-SUMMARY-DRAFT-CNT
120103         MOVE WS-DLY-GAP-VOID-AMT     TO WS-SUMMARY-TOT-VOID-AMT
120103         MOVE WS-DLY-GAP-VOID-CNT     TO WS-SUMMARY-VOID-CNT
120103         MOVE WS-DLY-GAP-OFFLN-AMT    TO WS-SUMMARY-OFFLINE-AMT
120103         MOVE WS-DLY-GAP-OFFLN-CNT    TO WS-SUMMARY-OFFLINE-CNT
120103         MOVE WS-DLY-GAP-NETPD-AMT    TO WS-SUMMARY-NETPD-AMT
120103
120103         MOVE WS-SUMMARY-LINEX        TO PRT
120103         MOVE '-'                     TO P-CTL
120103         PERFORM 3900-WRITE           THRU 3900-EXIT
120103
120103
120103         INITIALIZE WS-SUMMARY-LINEX
120103************ SUMMARY LINE FOR GAP (SECURE PAY) MTD
120103         MOVE SPACES                  TO WS-GAP-LABEL-VAR
120103         MOVE WS-MTD-LITERAL          TO WS-GAP-LABEL
120103         MOVE WS-FULL-GAP-LABEL       TO WS-SUMMARY-LABEL
120103         MOVE WS-TOT-GAP-CLMS-PD-AMT  TO WS-SUMMARY-TOT-PD-AMT
120103         MOVE WS-TOT-GAP-CLMS-PD-CNT  TO WS-SUMMARY-CLAIMS-PD-CNT
120103         MOVE WS-TOT-GAP-DRFTS-CNT    TO WS-SUMMARY-DRAFT-CNT
120103         MOVE WS-TOT-GAP-VOID-AMT     TO WS-SUMMARY-TOT-VOID-AMT
120103         MOVE WS-TOT-GAP-VOID-CNT     TO WS-SUMMARY-VOID-CNT
120103         MOVE WS-TOT-GAP-OFFLN-AMT    TO WS-SUMMARY-OFFLINE-AMT
120103         MOVE WS-TOT-GAP-OFFLN-CNT    TO WS-SUMMARY-OFFLINE-CNT
120103         MOVE WS-TOT-GAP-NETPD-AMT    TO WS-SUMMARY-NETPD-AMT
120103
120103         MOVE WS-SUMMARY-LINEX        TO PRT
120103         PERFORM 3900-WRITE           THRU 3900-EXIT
120103     END-IF

           INITIALIZE WS-SUMMARY-LINEX
      ************ SUMMARY LINE 3
           MOVE WS-EDITED-CYCLE-DT          TO WS-LIFE-LABEL-VAR
           MOVE WS-FULL-LIFE-LABEL          TO WS-SUMMARY-LABEL
           MOVE WS-DLY-LIFE-CLMS-PD-AMT     TO WS-SUMMARY-TOT-PD-AMT
           MOVE WS-DLY-LIFE-CLMS-PD-CNT     TO WS-SUMMARY-CLAIMS-PD-CNT
           MOVE WS-DLY-LIFE-DRFTS-CNT       TO WS-SUMMARY-DRAFT-CNT
           MOVE WS-DLY-LIFE-VOID-AMT        TO WS-SUMMARY-TOT-VOID-AMT
           MOVE WS-DLY-LIFE-VOID-CNT        TO WS-SUMMARY-VOID-CNT
           MOVE WS-DLY-LIFE-OFFLN-AMT       TO WS-SUMMARY-OFFLINE-AMT
           MOVE WS-DLY-LIFE-OFFLN-CNT       TO WS-SUMMARY-OFFLINE-CNT
           MOVE WS-DLY-LIFE-NETPD-AMT       TO WS-SUMMARY-NETPD-AMT

           MOVE WS-SUMMARY-LINEX            TO PRT
           MOVE '-'                         TO P-CTL
           PERFORM 3900-WRITE               THRU 3900-EXIT


           INITIALIZE WS-SUMMARY-LINEX
      ************ SUMMARY LINE 4
           MOVE SPACES                      TO WS-LIFE-LABEL-VAR
           MOVE WS-MTD-LITERAL              TO WS-LIFE-LABEL
           MOVE WS-FULL-LIFE-LABEL          TO WS-SUMMARY-LABEL
           MOVE WS-TOT-LIFE-CLMS-PD-AMT     TO WS-SUMMARY-TOT-PD-AMT
           MOVE WS-TOT-LIFE-CLMS-PD-CNT     TO WS-SUMMARY-CLAIMS-PD-CNT
           MOVE WS-TOT-LIFE-DRFTS-CNT       TO WS-SUMMARY-DRAFT-CNT
           MOVE WS-TOT-LIFE-VOID-AMT        TO WS-SUMMARY-TOT-VOID-AMT
           MOVE WS-TOT-LIFE-VOID-CNT        TO WS-SUMMARY-VOID-CNT
           MOVE WS-TOT-LIFE-OFFLN-AMT       TO WS-SUMMARY-OFFLINE-AMT
           MOVE WS-TOT-LIFE-OFFLN-CNT       TO WS-SUMMARY-OFFLINE-CNT
           MOVE WS-TOT-LIFE-NETPD-AMT       TO WS-SUMMARY-NETPD-AMT

           MOVE WS-SUMMARY-LINEX            TO PRT
           PERFORM 3900-WRITE               THRU 3900-EXIT


           INITIALIZE WS-SUMMARY-LINEX
      ************ GRAND TOTAL LINE
           MOVE WS-GRAND-TOTALS-LABEL       TO WS-SUMMARY-LABEL
           MOVE WS-GRAND-TOT-CLMS-PD-AMT    TO WS-SUMMARY-TOT-PD-AMT
           MOVE WS-GRAND-TOT-CLMS-PD-CNT    TO WS-SUMMARY-CLAIMS-PD-CNT
           MOVE WS-GRAND-TOT-DRFTS-CNT      TO WS-SUMMARY-DRAFT-CNT
           MOVE WS-GRAND-TOT-VOID-AMT       TO WS-SUMMARY-TOT-VOID-AMT
           MOVE WS-GRAND-TOT-VOID-CNT       TO WS-SUMMARY-VOID-CNT
           MOVE WS-GRAND-TOT-OFFLN-AMT      TO WS-SUMMARY-OFFLINE-AMT
           MOVE WS-GRAND-TOT-OFFLN-CNT      TO WS-SUMMARY-OFFLINE-CNT
           MOVE WS-GRAND-TOT-NETPD-AMT      TO WS-SUMMARY-NETPD-AMT

           MOVE WS-SUMMARY-LINEX            TO PRT
           MOVE '-'                         TO P-CTL
           PERFORM 3900-WRITE               THRU 3900-EXIT

053012     IF DTE-CLIENT NOT = 'AHL'
053012         GO TO 3200-EXIT
053012     END-IF
053012     
053012     MOVE SPACES                      TO PRT
053012     MOVE '-'                         TO P-CTL
053012     PERFORM 3900-WRITE               THRU 3900-EXIT
053012     MOVE WS-AHL-VOID-SEPARATOR       TO PRT
053012     MOVE '-'                         TO P-CTL
053012     PERFORM 3900-WRITE               THRU 3900-EXIT
053012
053012     MOVE WS-EDITED-CYCLE-DT          TO WS-DIS-LABEL-VAR
053012     MOVE 'CREDIT DISABILITY'         TO WS-DIS-LABEL
053012     MOVE WS-FULL-DIS-LABEL           TO WS-AHL-SUMMARY-LABEL
053012     MOVE WS-DLY-DIS-AHL-VOID-AMT     
053012                         TO WS-AHL-SUMMARY-VOID-AMT
053012     MOVE WS-DLY-DIS-AHL-VOID-CNT        
053012                         TO WS-AHL-SUMMARY-VOID-CNT
053012
053012     MOVE WS-AHL-VOID-SUMMARY         TO PRT
053012     MOVE '-'                         TO P-CTL
053012     PERFORM 3900-WRITE               THRU 3900-EXIT
053012
053012     MOVE SPACES                      TO WS-DIS-LABEL-VAR
053012     MOVE WS-MTD-LITERAL              TO WS-DIS-LABEL
053012     MOVE WS-FULL-DIS-LABEL           TO WS-AHL-SUMMARY-LABEL
053012     MOVE WS-TOT-DIS-AHL-VOID-AMT 
053012                        TO WS-AHL-SUMMARY-VOID-AMT
053012     MOVE WS-TOT-DIS-AHL-VOID-CNT         
053012                        TO WS-AHL-SUMMARY-VOID-CNT
053012
053012     MOVE WS-AHL-VOID-SUMMARY         TO PRT
053012     PERFORM 3900-WRITE               THRU 3900-EXIT
053012
053012     MOVE WS-EDITED-CYCLE-DT          TO WS-LIFE-LABEL-VAR
053012     MOVE 'CREDIT LIFE      '         TO WS-LIFE-LABEL
053012     MOVE WS-FULL-LIFE-LABEL          TO WS-AHL-SUMMARY-LABEL
053012     MOVE WS-DLY-LIFE-AHL-VOID-AMT
053012                        TO WS-AHL-SUMMARY-VOID-AMT
053012     MOVE WS-DLY-LIFE-AHL-VOID-CNT   
053012                        TO WS-AHL-SUMMARY-VOID-CNT
053012
053012     MOVE WS-AHL-VOID-SUMMARY         TO PRT
053012     MOVE '-'                         TO P-CTL
053012     PERFORM 3900-WRITE               THRU 3900-EXIT
053012
053012     MOVE SPACES                      TO WS-LIFE-LABEL-VAR
053012     MOVE WS-MTD-LITERAL              TO WS-LIFE-LABEL
053012     MOVE WS-FULL-LIFE-LABEL          TO WS-AHL-SUMMARY-LABEL
053012     MOVE WS-TOT-LIFE-AHL-VOID-AMT
053012                       TO WS-AHL-SUMMARY-VOID-AMT
053012     MOVE WS-TOT-LIFE-AHL-VOID-CNT
053012                       TO WS-AHL-SUMMARY-VOID-CNT
053012
053012     MOVE WS-AHL-VOID-SUMMARY         TO PRT
053012     PERFORM 3900-WRITE               THRU 3900-EXIT
053012
053012     MOVE WS-GRAND-TOTALS-LABEL       TO WS-AHL-SUMMARY-LABEL
053012     MOVE WS-GRAND-TOT-AHL-VOID-AMT   
053012                      TO WS-AHL-SUMMARY-VOID-AMT
053012     MOVE WS-GRAND-TOT-AHL-VOID-CNT   
053012                      TO WS-AHL-SUMMARY-VOID-CNT
053012
053012     MOVE WS-AHL-VOID-SUMMARY         TO PRT
053012     MOVE '-'                         TO P-CTL
053012     PERFORM 3900-WRITE               THRU 3900-EXIT
053012
           .
       3200-EXIT.
           EXIT.


       3500-PRINT-HEADINGS.

           MOVE WS-HEADING1                 TO PRT
           PERFORM 3900-WRITE               THRU 3900-EXIT

           MOVE WS-HEADING2                 TO PRT
           PERFORM 3900-WRITE               THRU 3900-EXIT

           ADD +1                           TO WS-PAGE
           MOVE WS-PAGE                     TO WS-H3-PAGE
           MOVE WS-HEADING3                 TO PRT
           PERFORM 3900-WRITE               THRU 3900-EXIT

           MOVE WS-HEADING4                 TO PRT
           PERFORM 3900-WRITE               THRU 3900-EXIT

           MOVE SPACES                      TO PRT
           MOVE '-'                         TO P-CTL
           PERFORM 3900-WRITE               THRU 3900-EXIT

           .
       3500-EXIT.
           EXIT.

       3600-PRINT-DRFTLST-HEADINGS.

           MOVE WS-DRFTLST-HEADING1         TO PRT
           PERFORM 3900-WRITE               THRU 3900-EXIT

           MOVE WS-HEADING2                 TO PRT
           PERFORM 3900-WRITE               THRU 3900-EXIT

           ADD +1                           TO WS-PAGE
           MOVE WS-PAGE                     TO WS-DRFTLST-H3-PAGE
           MOVE WS-DRFTLST-HEADING3         TO PRT
           PERFORM 3900-WRITE               THRU 3900-EXIT

           MOVE WS-DRFTLST-HEADING4         TO PRT
           PERFORM 3900-WRITE               THRU 3900-EXIT

           MOVE SPACES                      TO PRT
           PERFORM 3900-WRITE               THRU 3900-EXIT

           .
       3600-EXIT.
           EXIT.

       3900-WRITE.

           EVALUATE TRUE
           WHEN P-CTL = '1'
               MOVE +1                      TO WS-LINE-COUNT

           WHEN P-CTL = SPACE
               ADD +1                       TO WS-LINE-COUNT

           WHEN P-CTL = '0'
               ADD +2                       TO WS-LINE-COUNT

           WHEN OTHER
               ADD +3                       TO WS-LINE-COUNT
           END-EVALUATE



           EVALUATE TRUE

      **************************** P = HARDCOPY ONLY
           WHEN DTE-PRT-OPT = 'P'
               WRITE PRT

      **************************** B = SEQUENTIAL FILE ARCHIVE (USED TO
      *                                 BE FICHE) AND HARDCOPY
           WHEN DTE-PRT-OPT = 'B'
               PERFORM 4000-SEQ-FILE-ARCHIVE THRU 4000-EXIT
               WRITE PRT

      **************************** S = ONLINE REPORT FILE ONLY (ELREPT)
           WHEN DTE-PRT-OPT = 'S'
               PERFORM 4300-ONLINE-REPORT    THRU 4300-EXIT

      **************************** T = ONLINE REPORT FILE AND HARDCOPY
           WHEN DTE-PRT-OPT = 'T'
               PERFORM 4300-ONLINE-REPORT    THRU 4300-EXIT
               WRITE PRT

           END-EVALUATE

           .
       3900-EXIT.
           EXIT.


       4000-SEQ-FILE-ARCHIVE.
      ******************************************************************
      * WHILE FICHE IS NO LONGER BEING PRODUCED, THE FICHE LOGIC IS
      * BEING USED TO CREATE A SEQUENTIAL FILE REPORT ARCHIVE WHEN
      * THE DTE-PRT-OPT IS SET TO 'B'
      ******************************************************************
           IF FICH-NO
               GO TO 4000-EXIT
           ELSE
               IF FICH-OPEN = SPACE
                   MOVE 'X'                 TO FICH-OPEN
                   OPEN OUTPUT SEQ-FILE-ARCHIVE
               END-IF
           END-IF

           MOVE SPACE                       TO P-CTL
           WRITE SEQ-ARCHIVE-REC FROM PRT

           .
       4000-EXIT.
           EXIT.


       4300-ONLINE-REPORT.

           IF (REPT-OPEN      = SPACE) AND
              (DTE-ABEND-CD-1 = SPACE)
               OPEN I-O ELREPT

               IF (DTE-F-1        NOT = ZERO)  AND
                  (DTE-VSAM-FLAGS NOT = '97')
                   MOVE DTE-VSAM-FLAGS        TO WS-ABEND-FILE-STATUS
                   MOVE 'ERROR OPENING ELREPT'
                                              TO WS-ABEND-MESSAGE
                   PERFORM ABEND-PGM          THRU APS-EXIT
               ELSE
                   MOVE '1'                   TO REPT-OPEN
                   MOVE DTE-CLASIC-COMPANY-CD TO RF-COMPANY-CD
                   MOVE '1'                   TO RF-RECORD-TYPE
                   MOVE OLC-REPORT-NAME       TO RF-REPORT-ID
                   MOVE ZERO                  TO RF-LINE-NUMBER
                   START ELREPT KEY NOT LESS RF-CONTROL-PRIMARY
                   SET BEGIN-REPORT-DELETE    TO TRUE
                   PERFORM 4500-REPORT-DELETE THRU 4500-EXIT
                       UNTIL REPORT-DELETE-DONE

                   MOVE DTE-CLASIC-COMPANY-CD TO RF-COMPANY-CD
                   MOVE '2'                   TO RF-RECORD-TYPE
                   MOVE OLC-REPORT-NAME       TO RF-REPORT-ID
                   MOVE ZERO                  TO RF-LINE-NUMBER
                   START ELREPT KEY NOT LESS RF-CONTROL-PRIMARY
                   SET BEGIN-REPORT-DELETE    TO TRUE
                   PERFORM 4500-REPORT-DELETE THRU 4500-EXIT
                       UNTIL REPORT-DELETE-DONE

                   MOVE DTE-CLASIC-COMPANY-CD TO RF-COMPANY-CD
                   MOVE '1'                   TO RF-RECORD-TYPE
                   MOVE OLC-REPORT-NAME       TO RF-REPORT-ID
                   MOVE SPACES                TO RF-REPORT-LINE-133
               END-IF
           END-IF


           IF DTE-ABEND-CD-1 = '81' AND
              DTE-PRT-OPT    = 'S'
               MOVE +0302                     TO WS-RETURN-CODE
               PERFORM ABEND-PGM              THRU APS-EXIT
           END-IF


           MOVE P-CTL                         TO RF-CTL-CHAR-133
           MOVE P-DATA                        TO RF-DATA-133

           IF DTE-ABEND-CD-1 = SPACES
               ADD +1                         TO DTE-TOT-LINES
               MOVE DTE-TOT-LINES             TO RF-LINE-NUMBER
               WRITE REPORT-SAVE-FILE
                   INVALID KEY
                       MOVE '88'              TO DTE-ABEND-CD-1
                       CLOSE ELREPT
                       MOVE SPACE             TO REPT-OPEN
           END-IF

           .
       4300-EXIT.
           EXIT.

       4500-REPORT-DELETE.

           IF DTE-F-1 NOT = ZERO
               MOVE ZERO                      TO DTE-VSAM-FLAGS
               SET REPORT-DELETE-DONE         TO TRUE
               GO TO 4500-EXIT
           END-IF

           READ ELREPT NEXT RECORD
               AT END
                   SET REPORT-DELETE-DONE     TO TRUE
                   GO TO 4500-EXIT
           END-READ

           IF (DTE-CLASIC-COMPANY-CD = RF-COMPANY-CD)  AND
              (OLC-REPORT-NAME       = RF-REPORT-ID)
               DELETE ELREPT RECORD
           END-IF

           .
       4500-EXIT.
           EXIT.

061814 5000-build-sql-table.
061814
061814     move 'NTSQLTST2_Logic'      to svr
061814     move 'sa'                   to usr
061814     move 'sql2008r2'               to pass
061814
061814*    if ws-kix-myenv = 'cid1p'
061814*       move 'NTCSO2_Logic'      to svr
061814*       move 'sa'                to usr
061814*       move 'ntcso2'            to pass
061814*    end-if
061814
061814     display 'Begin connect to DB '
061814     string
061814         usr delimited space
061814         "." delimited size
061814         pass delimited space into usr-pass
061814     end-string
061814
061814     display ' usr pass ' usr-pass
061814
061814     EXEC SQL
061814        CONNECT TO :svr USER :usr-pass
061814     END-EXEC
061814
061814     if sqlcode not = 0
061814        display "Error: cannot connect "
061814        display sqlcode
061814        display sqlerrmc
061814        goback
061814     end-if
061814
061814     display " connect to DB successful "
061814
061814*    perform 5010-drop-table     thru 5010-exit
061814*    perform 5020-truncate-table thru 5020-exit
061814*    perform 5030-create-table   thru 5030-exit
061814     perform 5040-insert-row     thru 5040-exit
061814     perform 5050-finish-up      thru 5050-exit
061814
061814     .
061814 5000-exit.
061814     exit.
061814
061814 5010-drop-table.
061814
061814     display 'Begin Drop table'
061814     EXEC SQL
061814         drop table DAILYGLINT
061814     END-EXEC
061814     if sqlcode not = 0
061814        display "Error(anticipated) : cannot drop table "
061814        display sqlcode
061814        display sqlerrmc
061814*       goback
061814     end-if
061814
061814     display ' sql return code ' sqlcode
061814     display ' sql err mess    ' sqlerrmc
061814
061814     .
061814 5010-exit.
061814     exit.
061814
061814 5020-truncate-table.
061814
061814     display '*** Begin Truncate table ***'
061814     EXEC SQL
061814         truncate table DAILYGLINT
061814     END-EXEC
061814     if sqlcode not = 0
061814        display "Error : cannot truncate table "
061814        display sqlcode
061814        display sqlerrmc
061814        goback
061814     end-if
061814
061814     display ' sql return code ' sqlcode
061814     display ' sql err mess    ' sqlerrmc
061814     display '***  End  Truncate table ***'
061814
061814     .
061814 5020-exit.
061814     exit.
061814
061814 5030-create-table.
061814
061814     display ' Begin Create table'
061814     EXEC SQL
061814        create table DAILYGLINT (
061814         compid char(5),
061814         batchno char(50),
061814         carrier char(1),
061814         procdate datetime,
061814         sourcerpt char(8),
061814         dlyremits decimal(9,2),
061814         dlycharges decimal(9,2),
061814         dlyclmpmts decimal(9,2),
061814         dlystoppays decimal(9,2),
061814         dlyoffline decimal(9,2))
061814     END-EXEC
061814     if sqlcode not = 0
061814        display "Error: cannot create table "
061814        move sqlcode             to ws-disp-code
061814        display ' sql return code ' ws-disp-code
061814        display ' sql err mess    ' sqlerrmc
061814        goback
061814     end-if
061814
061814     display " Create table successful "
061814
061814     .
061814 5030-exit.
061814     exit.
061814
061814 5040-insert-row.
061814
061814*    display ' Begin Insert row '
061814
061814     move dte-client             to tbl-comp-id
061814     if dte-client = 'CID'
061814        move 'Batch 96 CPS Claims' to tbl-batch-no
061814     else
061814        move 'Batch 4 AHL Claims'  to tbl-batch-no
061814     end-if
061814
061814*    move current-date           to tbl-date
061814     move 'EL585'                to tbl-source-rpt
061814     move '0000000.00'           to tbl-remits
061814                                    tbl-charges
061814                                    tbl-other
061814     move int-tot-stop-pays      to em-clm-stop-pays
061814     move int-tot-offline        to em-clm-offline-pmts
061814     move int-tot-clm-pmts       to em-clm-pmts
061814
061814     EXEC SQL
061814        insert into DAILYGLINT (
061814         compid,
061814         batchno,
061814         carrier,
061814         procdate,
061814         sourcerpt,
061814         dlyremits,
061814         dlycharges,
061814         dlyclmpmts,
061814         dlystoppays,
061814         dlyoffline)
061814         values (
061814            :tbl-comp-id,
061814            :tbl-batch-no,
061814            :tbl-carrier,
061814            :tbl-date,
061814            :tbl-source-rpt,
061814            :tbl-remits,
061814            :tbl-charges,
061814            :tbl-clm-pmts,
061814            :tbl-clm-stop-pays,
061814            :tbl-clm-offline-pmts)
061814     END-EXEC
061814     if sqlcode not = 0
061814        display "Error: cannot insert row "
061814        display ' sql return code ' sqlcode
061814        display ' sql err mess    ' sqlerrmc
061814        goback
061814     end-if
061814
061814     .
061814 5040-exit.
061814     exit.
061814
061814 5050-finish-up.
061814
061814     display ' Begin Commit '
061814     EXEC SQL
061814         commit transaction
061814     END-EXEC
061814     if sqlcode not = 0
061814        display "Error: commit "
061814        display ' sql return code ' sqlcode
061814        display ' sql err mess    ' sqlerrmc
061814        goback
061814     end-if
061814
061814     display " Commit trans successful "
061814
061814     display ' Begin Disconnect '
061814     EXEC SQL
061814         commit work release
061814     END-EXEC
061814     if sqlcode not = 0
061814        display "Error: commit release "
061814        display ' sql return code ' sqlcode
061814        display ' sql err mess    ' sqlerrmc
061814     end-if
061814
061814     .
061814 5050-exit.
061814     exit.

       8500-DATE-CONVERSION.

           CALL 'ELDATCX' USING DATE-CONVERSION-DATA

           .
       8500-EXIT.
           EXIT.


       OPEN-FILES.

           OPEN INPUT  ELMSTR-INFILE
                       ELTRLR-INFILE
043012                 ELCNTL
                OUTPUT PRNTR
                       MTDCLMS-OUT

           IF WS-ELMSTR-FILE-STATUS  = '00' OR '97'
              CONTINUE
           ELSE
              MOVE 'OPEN ERROR OCCURRED ON ELMSTR'
                                              TO WS-ABEND-MESSAGE
              MOVE WS-ELMSTR-FILE-STATUS      TO WS-ABEND-FILE-STATUS
              PERFORM ABEND-PGM               THRU APS-EXIT
           END-IF


           IF WS-ELTRLR-FILE-STATUS  = '00' OR '97'
              CONTINUE
           ELSE
              MOVE 'OPEN ERROR OCCURRED ON ELTRLR'
                                              TO WS-ABEND-MESSAGE
              MOVE WS-ELTRLR-FILE-STATUS      TO WS-ABEND-FILE-STATUS
              PERFORM ABEND-PGM               THRU APS-EXIT
           END-IF
043012
043012     IF WS-ELCNTL-FILE-STATUS  = '00' OR '97'
043012        CONTINUE
043012     ELSE
043012        MOVE 'OPEN ERROR OCCURRED ON ELCNTL'
043012                                        TO WS-ABEND-MESSAGE
043012        MOVE WS-ELCNTL-FILE-STATUS      TO WS-ABEND-FILE-STATUS
043012        PERFORM ABEND-PGM               THRU APS-EXIT
043012     END-IF
043012
           .
       OPEN-FILES-EXIT.
           EXIT.


       CLOSE-FILES.

           IF FICH-OPEN NOT = SPACE
               CLOSE SEQ-FILE-ARCHIVE
           END-IF

           IF REPT-OPEN NOT = SPACE
               MOVE '2'                       TO RF-RECORD-TYPE
               ADD +1                         TO DTE-TOT-LINES
               MOVE DTE-TOT-LINES             TO RF-LINE-NUMBER
               MOVE SPACES                    TO RF-TRAILER-RECORD
               MOVE WS-TIME                   TO RF-PRINT-HH-MM-SS
               MOVE WS-CURRENT-DATE           TO RF-CURRENT-DATE
               WRITE REPORT-SAVE-FILE
               IF DTE-VSAM-FLAGS NOT = ZEROS
                   DISPLAY 'ERROR DURING WRITE OF TYPE 2 REPORT REC'
                   DISPLAY 'VSAM RETURN CODE = ' DTE-VSAM-FLAGS
                   DISPLAY 'ERROR DURING WRITE OF TYPE 2 REPORT REC'
                       UPON CONSOLE
                   DISPLAY 'VSAM RETURN CODE = ' DTE-VSAM-FLAGS
                       UPON CONSOLE
               ELSE
                   DISPLAY '***************************************'
                   DISPLAY '***************************************'
                   DISPLAY '**               ' OLC-REPORT-NAME
                   DISPLAY '**  REPORT HAS BEEN SAVED IN THE     **'
                   DISPLAY '**  CLAS-IC ONLINE REPORT FILE       **'
                   DISPLAY '**                                   **'
                   DISPLAY '***************************************'
                   DISPLAY '***************************************'
                   CLOSE ELREPT
               END-IF
           END-IF

           IF DTE-ABEND-CD-1 = '81'
               DISPLAY '***************************************'
               DISPLAY '***************************************'
               DISPLAY '**               ' OLC-REPORT-NAME
               DISPLAY '**  UNABLE TO OPEN ONLINE FILE -     **'
               DISPLAY '**  REPORT PRINTED BUT NOT SAVED     **'
               DISPLAY '**                                   **'
               DISPLAY '***************************************'
               DISPLAY '***************************************'
               DISPLAY 'VSAM RETURN CODE WAS - ', DTE-VSAM-FLAGS
           END-IF

           IF DTE-ABEND-CD-1 = '88'
               DISPLAY '***************************************'
               DISPLAY '***************************************'
               DISPLAY '**               ' OLC-REPORT-NAME
               DISPLAY '**  REPORT FILE IS FULL-  REPORT WAS **'
               DISPLAY '**  PRINTED AND PARTIALLY SAVED      **'
               DISPLAY '**                                   **'
               DISPLAY '***************************************'
               DISPLAY '***************************************'
               DISPLAY 'VSAM RETURN CODE WAS - ', DTE-VSAM-FLAGS
           END-IF


           CLOSE ELMSTR-INFILE
                 ELTRLR-INFILE
043012           ELCNTL
                 PRNTR
                 MTDCLMS-OUT

            .
       CLOSE-FILES-EXIT.
           EXIT.

       ABEND-PGM. COPY ELCABEND.
