       IDENTIFICATION DIVISION.
       PROGRAM-ID. CIDNAPEX.
       AUTHOR.     PABLO.
       DATE-COMPILED.
033110******************************************************************
033110*                   C H A N G E   L O G
033110*
033110* CHANGES ARE MARKED BY THE CHANGE EFFECTIVE DATE.
033110*-----------------------------------------------------------------
033110*  CHANGE   CHANGE REQUEST PGMR  DESCRIPTION OF CHANGE
033110* EFFECTIVE    NUMBER
033110*-----------------------------------------------------------------
033110* 033110    2009122800001  AJRA  NEW PROGRAM
102810* 102810    2009122800001  AJRA  BYPASS LETTER IF STOP DATE EXISTS
102810*                                ADD PROCESSOR ID AND COMPANY ID
031912* 031912    2011120900003  AJRA  ADD AHL CLAIM NO TO EXTRACT
033110******************************************************************
       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

           SELECT ELNAPS           ASSIGN TO ELNAPS
                                   ORGANIZATION IS INDEXED
                                   ACCESS IS DYNAMIC
                                   RECORD KEY IS NA-CONTROL-PRIMARY
                                   FILE STATUS IS ELNAPS-FILE-STATUS.

           SELECT ERACCT           ASSIGN TO ERACCT
                                   ORGANIZATION IS INDEXED
                                   ACCESS IS DYNAMIC
                                   RECORD KEY IS AM-CONTROL-PRIMARY
                                   FILE STATUS IS ERACCT-FILE-STATUS.

           SELECT ELMSTR           ASSIGN TO ELMSTR
                                   ORGANIZATION IS INDEXED
                                   ACCESS IS DYNAMIC
                                   RECORD KEY IS CL-CONTROL-PRIMARY
                                   FILE STATUS IS ELMSTR-FILE-STATUS.

           SELECT ELTRLR           ASSIGN TO ELTRLR
                                   ORGANIZATION IS INDEXED
                                   ACCESS IS DYNAMIC
                                   RECORD KEY IS AT-CONTROL-PRIMARY
                                   FILE STATUS IS ELTRLR-FILE-STATUS.

           SELECT ELCERT           ASSIGN TO ELCERT
                                   ORGANIZATION IS INDEXED
                                   ACCESS IS DYNAMIC
                                   RECORD KEY IS CM-CONTROL-PRIMARY
                                   FILE STATUS IS ELCERT-FILE-STATUS.

           SELECT ELBENE           ASSIGN TO ELBENE
                                   ORGANIZATION IS INDEXED
                                   ACCESS IS DYNAMIC
                                   RECORD KEY IS BE-CONTROL-PRIMARY
                                   FILE STATUS IS ELBENE-FILE-STATUS.

           SELECT NAPER-OUT       ASSIGN TO NAPEROT
               ORGANIZATION IS LINE SEQUENTIAL.

           SELECT DISK-DATE        ASSIGN TO SYS019.

           EJECT
       DATA DIVISION.
       FILE SECTION.

       FD  ELNAPS.
       
           COPY ELCNAPS.

       FD  ERACCT.

           COPY ERCACCT.

       FD  ELMSTR.

           COPY ELCMSTR.

       FD  ELTRLR.

           COPY ELCTRLR.

       FD  ELCERT.

           COPY ELCCERT.

       FD  ELBENE.

           COPY ELCBENE.

       FD  NAPER-OUT
           RECORDING MODE F
           LABEL RECORDS STANDARD
           BLOCK CONTAINS 0 RECORDS.

       01  NAPER-OUT-REC              PIC X(1100).


       FD  DISK-DATE
           COPY ELCDTEFD.

       WORKING-STORAGE SECTION.
       77  FILLER  PIC X(32) VALUE '********************************'.
       77  FILLER  PIC X(32) VALUE '   PEMCLX2  WORKING STORAGE     '.
       77  FILLER  PIC X(32) VALUE '********************************'.

       77  WS-EOF-SW               PIC X VALUE SPACES.
           88  END-OF-ELNAPS             VALUE 'Y'.
       77  NAPS-RECS-IN            PIC 9(9) VALUE ZEROS.
       77  CLM-RECS-IN             PIC 9(9) VALUE ZEROS.
       77  CRT-RECS-IN             PIC 9(9) VALUE ZEROS.
       77  TRL-RECS-IN             PIC 9(9) VALUE ZEROS.
       77  CLM-RECS-OUT            PIC 9(9) VALUE ZEROS.
       77  WS-DIAGNOSIS            PIC X(60) VALUE SPACES.
       77  WS-LAST-ACT-DT          PIC XX    VALUE LOW-VALUES.
       77  WS-ACTIVITY-DT          PIC XX    VALUE LOW-VALUES.
       77  WS-AUTO-LAST-SCHED-DT   PIC XX    VALUE LOW-VALUES.
       77  WS-LAST-ACT-TYPE        PIC X(15) VALUE SPACES.
       77  WS-ACTIVITY-TYPE        PIC X(15) VALUE SPACES.
       77  WS-FORM                 PIC X(4)  VALUE SPACES.
       77  WS-LETTER-ID            PIC XXXX  VALUE SPACES.
       77  WS-GOT-IT-SW            PIC X     VALUE ' '.
           88  WE-GOT-IT-ALL                 VALUE 'Y'.
       77  WS-DONE-WITH-ACCT       PIC X     VALUE 'N'.
           88  DONE-WITH-ACCT                VALUE 'Y'.
           88  NOT-DONE-WITH-ACCT            VALUE 'N'.

       01  WORK-AREA.
           05  WRK-END-FOUND           PIC X   VALUE 'N'.
               88 END-FOUND                    VALUE 'Y'.
       
           05  WS-INT-RATE             PIC S99V9(5)  COMP-3.

       01  WS-LOAN-NO                  PIC X(25).
       01  WS-INS-NAME-AND-ADDRESS.
           05  WS-INS-NAME             PIC X(30).
           05  WS-INS-ADDR1            PIC X(30).
           05  WS-INS-ADDR2            PIC X(30).
           05  WS-INS-CITY             PIC X(30).
           05  WS-INS-STATE            PIC X(2).
           05  WS-INS-ZIP              PIC X(9).
           05  WS-INS-PHONE            PIC X(13).
           05  WS-WORK-PHONE           PIC X(11).

       01  WS-BEN-NAME-AND-ADDRESS.
           05  WS-BEN-NAME             PIC X(30).
           05  WS-BEN-ADDR1            PIC X(30).
           05  WS-BEN-ADDR2            PIC X(30).
           05  WS-BEN-CITY             PIC X(30).
           05  WS-BEN-STATE            PIC X(2).
           05  WS-BEN-ZIP              PIC X(9).
           05  WS-BEN-PHONE            PIC X(13).

       01  WS-ACCT-NAME-AND-ADDRESS.
           05  WS-ORIG-ACCT-NAME        PIC X(30).
           05  WS-ACCT-NAME             PIC X(30).
           05  WS-ACCT-ADDR1            PIC X(30).
           05  WS-ACCT-ADDR2            PIC X(30).
           05  WS-ACCT-CITY             PIC X(30).
           05  WS-ACCT-STATE            PIC X(2).
           05  WS-ACCT-ZIP              PIC X(9).
           05  WS-ACCT-PHONE            PIC X(13).

       01  WS-SAVE-NAPER              PIC X(1100) VALUE LOW-VALUES.
031912****currently 1037 
       01  NAPER-DETAIL-RECORD.
           12  EX-CYCLE-DATE           PIC X(8).
           12  EX-TAB00                PIC X.
           12  EX-LETTER-ID            PIC X(4).
           12  EX-TAB01                PIC X.
           12  EX-CARRIER              PIC X.
           12  EX-TAB02                PIC X.
           12  EX-CLAIM-NO             PIC X(7).
           12  EX-TAB03                PIC X.
           12  EX-CERT-NO              PIC X(11).
           12  EX-TAB04                PIC X.
           12  EX-ACCOUNT              PIC X(10).
           12  EX-TAB05                PIC X.
           12  EX-INSURED-LAST-NAME    PIC X(15).
           12  EX-TAB06                PIC X.
           12  EX-INSURED-1ST-NAME     PIC X(11).
           12  EX-TAB07                PIC X.
           12  EX-INSURED-SEX-CD       PIC X.
           12  EX-TAB08                PIC X.
           12  EX-SSN                  PIC X(11).
           12  EX-TAB09                PIC X.
           12  EX-CLAIM-STATUS         PIC X.
           12  EX-TAB10                PIC X.
           12  EX-CLAIM-TYPE           PIC X.
           12  EX-TAB11                PIC X.
           12  EX-FILE-ESTABLISH-DT    PIC X(10).
           12  EX-TAB12                PIC X.
           12  EX-INCURRED-DT          PIC X(10).
           12  EX-TAB13                PIC X.
           12  EX-REPORTED-DT          PIC X(10).
           12  EX-TAB14                PIC X.
           12  EX-PAID-THRU-DT         PIC X(10).
           12  EX-TAB15                PIC X.
           12  EX-LAST-PMT-DT          PIC X(10).
           12  EX-TAB16                PIC X.
           12  EX-NO-OF-PMTS           PIC ZZ9.
           12  EX-TAB17                PIC X.
           12  EX-TOTAL-PAID-AMT       PIC -9(7).99.
           12  EX-TAB18                PIC X.
           12  EX-LAST-PAID-AMT        PIC -9(7).99.
           12  EX-TAB19                PIC X.
           12  EX-ACCOUNT-ADDR-CNT     PIC 9.
           12  EX-TAB20                PIC X.
           12  EX-CERT-STATE           PIC XX.
           12  EX-TAB21                PIC X.
           12  EX-CERT-ACCOUNT         PIC X(10).
           12  EX-TAB22                PIC X.
           12  EX-CERT-EFF-DT          PIC X(10).
           12  EX-TAB23                PIC X.
           12  EX-LAST-MAINT-DT        PIC X(10).
           12  EX-TAB24                PIC X.
           12  EX-LAST-MAINT-USER      PIC X(4).
           12  EX-TAB25                PIC X.
           12  EX-LAST-MAINT-TYPE      PIC X.
           12  EX-TAB26                PIC X.
           12  EX-DIAG                 PIC X(60).
           12  EX-TAB27                PIC X.
           12  EX-EXP-DT               PIC X(10).
           12  EX-TAB28                PIC X.
           12  EX-LAST-CLOSE-REASON    PIC X.
           12  EX-TAB29                PIC X.
           12  EX-BIRTH-DT             PIC X(10).
           12  EX-TAB30                PIC X.
           12  EX-TERM                 PIC 999.
           12  EX-TAB31                PIC X.
           12  EX-BEN-CODE             PIC XX.
           12  EX-TAB32                PIC X.
           12  EX-BENEFIT-AMT          PIC -9(7).99.
           12  EX-TAB33                PIC X.
           12  EX-CRIT-PERIOD          PIC 99.
           12  EX-TAB34                PIC X.
           12  EX-WAIT-PERIOD          PIC XX.
           12  EX-TAB35                PIC X.
           12  EX-LF-INTEREST-RATE     PIC 99.9(5).
           12  EX-TAB36                PIC X.
           12  EX-AUTOPYDT             PIC X(10).
           12  EX-TAB37                PIC X.
           12  EX-AUTO-LAST-SCHED-DT   PIC X(10).
           12  EX-TAB38                PIC X.
           12  EX-LOAN-NO              PIC X(25).
           12  EX-TAB39                PIC X.
           12  EX-APR                  PIC Z9.9999.
           12  EX-TAB40                PIC X.
           12  EX-LAST-ACT-DT          PIC X(10).
           12  EX-TAB41                PIC X.
           12  EX-ACTIVITY-DT          PIC X(10).
           12  EX-TAB42                PIC X.
           12  EX-LAST-ACT-TYPE        PIC X(15).
           12  EX-TAB43                PIC X.
           12  EX-ACTIVITY-TYPE        PIC X(15).
           12  EX-TAB44                PIC X.
           12  EX-FORM                 PIC X(4).
           12  EX-TAB45                PIC X.
           12  EX-INS-NAME             PIC X(30).
           12  EX-TAB46                PIC X.
           12  EX-INS-ADDR1            PIC X(30).
           12  EX-TAB47                PIC X.
           12  EX-INS-ADDR2            PIC X(30).
           12  EX-TAB48                PIC X.
           12  EX-INS-CITY             PIC X(30).
           12  EX-TAB49                PIC X.
           12  EX-INS-STATE            PIC X(2).
           12  EX-TAB50                PIC X.
           12  EX-INS-ZIP              PIC X(9).
           12  EX-TAB51                PIC X.
           12  EX-INS-PHONE            PIC X(13).
           12  EX-TAB52                PIC X.
           12  EX-BEN-NAME             PIC X(30).
           12  EX-TAB53                PIC X.
           12  EX-BEN-ADDR1            PIC X(30).
           12  EX-TAB54                PIC X.
           12  EX-BEN-ADDR2            PIC X(30).
           12  EX-TAB55                PIC X.
           12  EX-BEN-CITY             PIC X(30).
           12  EX-TAB56                PIC X.
           12  EX-BEN-STATE            PIC X(2).
           12  EX-TAB57                PIC X.
           12  EX-BEN-ZIP              PIC X(9).
           12  EX-TAB58                PIC X.
           12  EX-BEN-PHONE            PIC X(13).
           12  EX-TAB59                PIC X.
           12  EX-ACCT-NAME            PIC X(30).
           12  EX-TAB60                PIC X.
           12  EX-ACCT-ADDR1           PIC X(30).
           12  EX-TAB61                PIC X.
           12  EX-ACCT-ADDR2           PIC X(30).
           12  EX-TAB62                PIC X.
           12  EX-ACCT-CITY            PIC X(30).
           12  EX-TAB63                PIC X.
           12  EX-ACCT-STATE           PIC X(2).
           12  EX-TAB64                PIC X.
           12  EX-ACCT-ZIP             PIC X(9).
           12  EX-TAB65                PIC X.
           12  EX-ACCT-PHONE           PIC X(13).
           12  EX-TAB66                PIC X.
           12  EX-ENCLOSURE-CD         PIC X(3).
           12  EX-TAB67                PIC X.
           12  EX-ENC-STATE            PIC XX.
           12  EX-TAB68                PIC X.
           12  EX-ARCHIVE-NO           PIC 9(8).
           12  EX-TAB69                PIC X.
           12  EX-1ST-LTR-PRINT-DT     PIC X(10).
           12  EX-TAB70                PIC X.
           12  EX-NEXT-DUE-DT          PIC X(10).
           12  EX-TAB71                PIC X.
           12  EX-JT-BORROWER-LAST-NAME PIC X(15).
           12  EX-TAB72                PIC X.
           12  EX-JT-BORROWER-1ST-NAME PIC X(11).
           12  EX-TAB73                PIC X.
           12  EX-ORIG-ACCT-NAME       PIC X(30).
           12  EX-TAB74                PIC X.
           12  EX-ORIG-ARCHIVE-NO      PIC 9(9).
           12  EX-TAB75                PIC X.
           12  EX-RESEND-PROMPT-IND    PIC X.
           12  EX-TAB76                PIC X.
102810     12  EX-PROCESSOR-ID         PIC X(4).
102810     12  EX-TAB77                PIC X.
102810     12  EX-COMPANY-ID           PIC X(3).
102810     12  EX-TAB78                PIC X.
031912     12  EX-AHL-CLAIM-NO         PIC X(9).
031912     12  EX-TAB79                PIC X.
           12  EX-LAST-BYTE            PIC X.

      ******************************************************************
       01  WS-MISC.
           05  ELNAPS-FILE-STATUS      PIC XX     VALUE ZEROS.
           05  ERACCT-FILE-STATUS      PIC XX     VALUE ZEROS.
           05  ELMSTR-FILE-STATUS      PIC XX     VALUE ZEROS.
           05  ELTRLR-FILE-STATUS      PIC XX     VALUE ZEROS.
           05  ELCERT-FILE-STATUS      PIC XX     VALUE ZEROS.
           05  ELBENE-FILE-STATUS      PIC XX     VALUE ZEROS.
           05  WS-CURRENT-BIN-DT       PIC X(02)  VALUE LOW-VALUES.
           05  WS-DATE                 PIC 9(11)  VALUE ZEROS.
           05  PGM-SUB          COMP-3 PIC S9(04) VALUE +585.
           05  WS-RETURN-CODE   COMP   PIC S9(03) VALUE +0.
           05  WS-ABEND-MESSAGE        PIC X(80)  VALUE SPACES.
           05  WS-ZERO          COMP-3 PIC S9(01) VALUE +0.
           05  WS-ABEND-FILE-STATUS    PIC X(02)  VALUE ZERO.

                                       COPY ELCDTECX.

                                       COPY ELCDTEVR.

                                       COPY ELCDATE.
       LINKAGE SECTION.

       01  PARM.
           05  PARM-LENGTH        COMP     PIC S9(04)    VALUE ZEROS.
           05  PARM-CYCLE-DATE             PIC X(08)     VALUE SPACES.
       

       PROCEDURE DIVISION USING PARM.

            IF PARM-CYCLE-DATE = SPACES
                DISPLAY 'MISSING CYCLE DATE PARM'
                GO TO ABEND-PGM
            END-IF.

       0000-DATE-CARD-READ. COPY ELCDTERX.

           PERFORM 0400-OPEN-FILES     THRU 0400-EXIT

           PERFORM 0600-INITIALIZE     THRU 0600-EXIT

           PERFORM 0050-PROCESS-FILE   THRU 0050-EXIT UNTIL
                 (END-OF-ELNAPS)

           PERFORM 0500-CLOSE-FILES    THRU 0500-EXIT

           DISPLAY ' CLAIM RECORDS READ    '  NAPS-RECS-IN
           DISPLAY ' TRLR  RECORDS READ    '  TRL-RECS-IN
           DISPLAY ' CLAIM RECORDS WRITTEN '  CLM-RECS-OUT
           GOBACK

           .
       0050-PROCESS-FILE.

           IF NA-INITIAL-PRINT-DT EQUAL LOW-VALUES OR SPACES 
              PERFORM 0100-PROCESS-ELNAPS THRU 0100-EXIT
           END-IF

           PERFORM 0200-READ-ELNAPS    THRU 0200-EXIT

           .
       0050-EXIT.
           EXIT.

       0100-PROCESS-ELNAPS.

           IF NA-COMPANY-CD NOT = DTE-CLASIC-COMPANY-CD
               SET END-OF-ELNAPS TO TRUE
               GO TO 0100-EXIT
           END-IF
           
           MOVE NA-COMPANY-CD      TO AT-COMPANY-CD.
           MOVE NA-CARRIER         TO AT-CARRIER.
           MOVE NA-CLAIM-NO        TO AT-CLAIM-NO.
           MOVE NA-CERT-NO         TO AT-CERT-NO.
           MOVE NA-CORR-TRLR-SEQ   TO AT-SEQUENCE-NO.

           READ ELTRLR RECORD INVALID KEY
               GO TO 0100-EXIT
           END-READ.

           IF AT-LETTER-ANSWERED-DT IS NOT EQUAL TO LOW-VALUES
               GO TO 0100-EXIT
           END-IF.
102810
102810     IF AT-STOP-LETTER-DT NOT EQUAL LOW-VALUES AND SPACES
102810         GO TO 0100-EXIT
102810     END-IF.
           
           IF NA-LETTER-ID = SPACES
              GO TO 0100-EXIT
           END-IF.
           
           IF NA-LETTER-ID = 'HOHI' OR 'CI74'
              GO TO 0100-EXIT
           END-IF.

           MOVE WS-CURRENT-BIN-DT   TO NA-INITIAL-PRINT-DT
                                       AT-INITIAL-PRINT-DATE.
                                       
           REWRITE NAPERSOFT-FILE
                INVALID KEY
                    DISPLAY ' CIDNAPEX REWRITE ERROR - ELNAPS'
                    SET END-OF-ELNAPS TO TRUE
                    GO TO ABEND-PGM.

           REWRITE ACTIVITY-TRAILERS
                INVALID KEY
                    DISPLAY ' CIDNAPEX REWRITE ERROR - ELTRLR'
                    SET END-OF-ELNAPS TO TRUE
                    GO TO ABEND-PGM.

           IF NA-CREATED-IN-NAPERSOFT = 'Y'
               GO TO 0100-EXIT
           END-IF.                                       
       
           PERFORM 0205-READ-ELMSTR THRU 0205-EXIT
           PERFORM 0275-READ-ELCERT THRU 0275-EXIT
           PERFORM 0210-GET-ELTRLRS THRU 0210-EXIT

           MOVE WS-SAVE-NAPER         TO NAPER-DETAIL-RECORD

           MOVE PARM-CYCLE-DATE        TO EX-CYCLE-DATE
           MOVE NA-LETTER-ID           TO EX-LETTER-ID
           MOVE NA-CARRIER             TO EX-CARRIER
           MOVE NA-CLAIM-NO            TO EX-CLAIM-NO
           MOVE NA-CERT-NO             TO EX-CERT-NO
           MOVE NA-ARCHIVE-NO          TO EX-ARCHIVE-NO
102810     MOVE NA-PROCESSOR-ID        TO EX-PROCESSOR-ID
102810     MOVE DTE-CLIENT             TO EX-COMPANY-ID
           MOVE CL-CERT-ACCOUNT        TO EX-ACCOUNT
           MOVE CL-INSURED-LAST-NAME   TO EX-INSURED-LAST-NAME
           MOVE CL-INSURED-1ST-NAME    TO EX-INSURED-1ST-NAME
           MOVE CL-CLAIM-STATUS        TO EX-CLAIM-STATUS
           MOVE CL-CLAIM-TYPE          TO EX-CLAIM-TYPE
           MOVE CL-TOTAL-PAID-AMT      TO EX-TOTAL-PAID-AMT
           MOVE CL-NO-OF-PMTS-MADE     TO EX-NO-OF-PMTS
           MOVE CL-LAST-PMT-AMT        TO EX-LAST-PAID-AMT
           MOVE CL-INSURED-SEX-CD      TO EX-INSURED-SEX-CD
           MOVE CL-ACCOUNT-ADDR-CNT    TO EX-ACCOUNT-ADDR-CNT
           MOVE CL-CERT-STATE          TO EX-CERT-STATE
                                          EX-ENC-STATE
           MOVE CL-CERT-ACCOUNT        TO EX-CERT-ACCOUNT
           MOVE CL-LAST-MAINT-USER     TO EX-LAST-MAINT-USER
           MOVE CL-LAST-MAINT-TYPE     TO EX-LAST-MAINT-TYPE
           MOVE CL-LAST-CLOSE-REASON   TO EX-LAST-CLOSE-REASON
           MOVE CL-SOC-SEC-NO          TO EX-SSN
031912     MOVE CL-CCN (1:9)           TO EX-AHL-CLAIM-NO

           MOVE CL-FILE-ESTABLISH-DT   TO DC-BIN-DATE-1
           MOVE ' '                    TO DC-OPTION-CODE
           PERFORM 8510-DATE-CONVERSION
                                       THRU 8590-EXIT
           IF NO-CONVERSION-ERROR
              MOVE DC-GREG-DATE-A-EDIT TO EX-FILE-ESTABLISH-DT
           END-IF

           MOVE CL-CERT-EFF-DT         TO DC-BIN-DATE-1
           MOVE ' '                    TO DC-OPTION-CODE
           PERFORM 8510-DATE-CONVERSION
                                       THRU 8590-EXIT
           IF NO-CONVERSION-ERROR
              MOVE DC-GREG-DATE-A-EDIT TO EX-CERT-EFF-DT
           END-IF

           MOVE CL-PAID-THRU-DT        TO DC-BIN-DATE-1
           MOVE ' '                    TO DC-OPTION-CODE
           PERFORM 8510-DATE-CONVERSION
                                       THRU 8590-EXIT
           IF NO-CONVERSION-ERROR
              MOVE DC-GREG-DATE-A-EDIT TO EX-PAID-THRU-DT
           END-IF

           MOVE CL-LAST-PMT-DT         TO DC-BIN-DATE-1
           MOVE ' '                    TO DC-OPTION-CODE
           PERFORM 8510-DATE-CONVERSION
                                       THRU 8590-EXIT
           IF NO-CONVERSION-ERROR
              MOVE DC-GREG-DATE-A-EDIT TO EX-LAST-PMT-DT
           END-IF

           MOVE CL-INCURRED-DT         TO DC-BIN-DATE-1
           MOVE ' '                    TO DC-OPTION-CODE
           PERFORM 8510-DATE-CONVERSION
                                       THRU 8590-EXIT
           IF NO-CONVERSION-ERROR
              MOVE DC-GREG-DATE-A-EDIT TO EX-INCURRED-DT
           END-IF

           MOVE CL-REPORTED-DT         TO DC-BIN-DATE-1
           MOVE ' '                    TO DC-OPTION-CODE
           PERFORM 8510-DATE-CONVERSION
                                       THRU 8590-EXIT
           IF NO-CONVERSION-ERROR
              MOVE DC-GREG-DATE-A-EDIT TO EX-REPORTED-DT
           END-IF

           MOVE CL-LAST-MAINT-DT       TO DC-BIN-DATE-1
           MOVE ' '                    TO DC-OPTION-CODE
           PERFORM 8510-DATE-CONVERSION
                                       THRU 8590-EXIT
           IF NO-CONVERSION-ERROR
              MOVE DC-GREG-DATE-A-EDIT TO EX-LAST-MAINT-DT
           END-IF

           MOVE CL-INSURED-BIRTH-DT    TO DC-BIN-DATE-1
           MOVE ' '                    TO DC-OPTION-CODE
           PERFORM 8510-DATE-CONVERSION
                                       THRU 8590-EXIT
           IF NO-CONVERSION-ERROR
              MOVE DC-GREG-DATE-A-EDIT TO EX-BIRTH-DT
           END-IF

           MOVE NA-1ST-LTR-PRINT-DT       TO DC-BIN-DATE-1
           MOVE ' '                    TO DC-OPTION-CODE
           PERFORM 8510-DATE-CONVERSION THRU 8590-EXIT
           IF NO-CONVERSION-ERROR
              MOVE DC-GREG-DATE-A-EDIT TO EX-1ST-LTR-PRINT-DT
           END-IF

           MOVE NA-NEXT-DUE-DT         TO DC-BIN-DATE-1
           MOVE ' '                    TO DC-OPTION-CODE
           PERFORM 8510-DATE-CONVERSION THRU 8590-EXIT
           IF NO-CONVERSION-ERROR
              MOVE DC-GREG-DATE-A-EDIT TO EX-NEXT-DUE-DT
           END-IF

           MOVE NA-AUTOPYDT            TO DC-BIN-DATE-1
           MOVE ' '                    TO DC-OPTION-CODE
           PERFORM 8510-DATE-CONVERSION THRU 8590-EXIT
           IF NO-CONVERSION-ERROR
              MOVE DC-GREG-DATE-A-EDIT TO EX-AUTOPYDT
           END-IF

           INSPECT WS-DIAGNOSIS
              REPLACING ALL X'00' BY SPACES

           MOVE WS-DIAGNOSIS           TO EX-DIAG

           MOVE WS-LAST-ACT-DT         TO DC-BIN-DATE-1
           MOVE ' '                    TO DC-OPTION-CODE
           PERFORM 8510-DATE-CONVERSION
                                       THRU 8590-EXIT
           IF NO-CONVERSION-ERROR
              MOVE DC-GREG-DATE-A-EDIT TO EX-LAST-ACT-DT
           END-IF

           MOVE WS-ACTIVITY-DT         TO DC-BIN-DATE-1
           MOVE ' '                    TO DC-OPTION-CODE
           PERFORM 8510-DATE-CONVERSION
                                       THRU 8590-EXIT
           IF NO-CONVERSION-ERROR
              MOVE DC-GREG-DATE-A-EDIT TO EX-ACTIVITY-DT
           END-IF

           MOVE WS-AUTO-LAST-SCHED-DT  TO DC-BIN-DATE-1
           MOVE ' '                    TO DC-OPTION-CODE
           PERFORM 8510-DATE-CONVERSION
                                       THRU 8590-EXIT
           IF NO-CONVERSION-ERROR
              MOVE DC-GREG-DATE-A-EDIT TO EX-AUTO-LAST-SCHED-DT
           END-IF

           MOVE WS-LAST-ACT-TYPE       TO EX-LAST-ACT-TYPE
           MOVE WS-ACTIVITY-TYPE       TO EX-ACTIVITY-TYPE
           MOVE WS-FORM                TO EX-FORM

           IF WS-ACCT-NAME-AND-ADDRESS NOT = SPACES
              MOVE WS-ORIG-ACCT-NAME      TO EX-ORIG-ACCT-NAME
              MOVE WS-ACCT-NAME           TO EX-ACCT-NAME
              MOVE WS-ACCT-ADDR1          TO EX-ACCT-ADDR1
              MOVE WS-ACCT-ADDR2          TO EX-ACCT-ADDR2
              MOVE WS-ACCT-CITY           TO EX-ACCT-CITY
              MOVE WS-ACCT-STATE          TO EX-ACCT-STATE
              MOVE WS-ACCT-ZIP            TO EX-ACCT-ZIP
              MOVE WS-ACCT-PHONE          TO EX-ACCT-PHONE
           END-IF
           
           IF WS-LOAN-NO NOT = SPACES
              MOVE WS-LOAN-NO      TO EX-LOAN-NO
           END-IF

           IF WS-INS-NAME-AND-ADDRESS NOT = SPACES
              MOVE WS-INS-NAME         TO EX-INS-NAME
              MOVE WS-INS-ADDR1        TO EX-INS-ADDR1
              MOVE WS-INS-ADDR2        TO EX-INS-ADDR2
              MOVE WS-INS-CITY         TO EX-INS-CITY
              MOVE WS-INS-STATE        TO EX-INS-STATE
              MOVE WS-INS-ZIP          TO EX-INS-ZIP
              MOVE WS-INS-PHONE        TO EX-INS-PHONE
           END-IF

           IF WS-BEN-NAME-AND-ADDRESS NOT = SPACES
              MOVE WS-BEN-NAME         TO EX-BEN-NAME
              MOVE WS-BEN-ADDR1        TO EX-BEN-ADDR1
              MOVE WS-BEN-ADDR2        TO EX-BEN-ADDR2
              MOVE WS-BEN-CITY         TO EX-BEN-CITY
              MOVE WS-BEN-STATE        TO EX-BEN-STATE
              MOVE WS-BEN-ZIP          TO EX-BEN-ZIP
              MOVE WS-BEN-PHONE        TO EX-BEN-PHONE
           ELSE
              IF CL-BENEFICIARY NOT = SPACES
                 MOVE CL-COMPANY-CD    TO BE-COMPANY-CD
                 MOVE 'B'              TO BE-RECORD-TYPE
                 MOVE CL-BENEFICIARY   TO BE-BENEFICIARY
                 READ ELBENE
                 IF ELBENE-FILE-STATUS = '00'
                    MOVE BE-MAIL-TO-NAME TO EX-BEN-NAME
                    MOVE BE-ADDRESS-LINE-1 TO EX-BEN-ADDR1
                    MOVE BE-ADDRESS-LINE-2 TO EX-BEN-ADDR2
                    MOVE BE-CITY       TO EX-BEN-CITY
                    MOVE BE-STATE      TO EX-BEN-STATE
                    MOVE BE-ZIP-CODE   TO EX-BEN-ZIP
                    MOVE BE-PHONE-NO   TO EX-BEN-PHONE
                 ELSE
                    DISPLAY ' ELBENE NOT FOUND ' CL-BENEFICIARY
                    ' ' ELBENE-FILE-STATUS
                 END-IF
              END-IF
           END-IF


           IF ELCERT-FILE-STATUS = '00'
              IF CL-INSURED-1ST-NAME = CM-INSURED-FIRST-NAME
                  MOVE CM-JT-FIRST-NAME TO EX-JT-BORROWER-1ST-NAME
                  MOVE CM-JT-LAST-NAME  TO EX-JT-BORROWER-LAST-NAME
              ELSE
                  MOVE CM-INSURED-FIRST-NAME TO EX-JT-BORROWER-1ST-NAME
                  MOVE CM-INSURED-LAST-NAME TO EX-JT-BORROWER-LAST-NAME
              END-IF
              IF CL-CLAIM-TYPE = 'L' OR 'P'
                 MOVE CM-LF-LOAN-EXPIRE-DT
                                       TO DC-BIN-DATE-1
                 MOVE CM-LF-ORIG-TERM  TO EX-TERM
                 MOVE CM-LF-BENEFIT-CD TO EX-BEN-CODE
                 MOVE CM-LF-BENEFIT-AMT TO EX-BENEFIT-AMT
                 MOVE CM-LF-CRITICAL-PERIOD
                                       TO EX-CRIT-PERIOD
                 MOVE CM-LOAN-APR      TO EX-APR
              ELSE
                 MOVE CM-AH-LOAN-EXPIRE-DT 
                                       TO DC-BIN-DATE-1
                 MOVE CM-AH-ORIG-TERM  TO EX-TERM
                 MOVE CM-AH-BENEFIT-CD TO EX-BEN-CODE
                 MOVE CM-AH-BENEFIT-AMT TO EX-BENEFIT-AMT
                 MOVE CM-AH-CRITICAL-PERIOD
                                       TO EX-CRIT-PERIOD
                 PERFORM VARYING CLAS-INDEXA
                    FROM CLAS-STARTA BY +1 UNTIL
                    (CLAS-I-BEN (CLAS-INDEXA) = EX-BEN-CODE)
                    OR (CLAS-INDEXA > CLAS-MAXA)
                 END-PERFORM
                 IF CLAS-INDEXA > CLAS-MAXA
                    DISPLAY ' ERROR AH BEN CODE ' EX-BEN-CODE ' '
                      CL-CLAIM-NO ' ' CL-CARRIER ' ' CL-CERT-NO
                 ELSE
                    MOVE CLAS-EXCLUSION (CLAS-INDEXA)
                                       TO EX-WAIT-PERIOD
                 END-IF
              END-IF
              MOVE ' '                 TO DC-OPTION-CODE
              PERFORM 8510-DATE-CONVERSION
                                       THRU 8590-EXIT
              IF NO-CONVERSION-ERROR
                 MOVE DC-GREG-DATE-A-EDIT
                                       TO EX-EXP-DT
              END-IF
           END-IF
           
           MOVE WS-INT-RATE            TO EX-LF-INTEREST-RATE
           IF NA-ENCLOSURE-CD EQUAL LOW-VALUES
               MOVE SPACES             TO EX-ENCLOSURE-CD
           ELSE
               MOVE NA-ENCLOSURE-CD    TO EX-ENCLOSURE-CD
           END-IF
           
           IF NA-ORIG-ARCHIVE-NO EQUAL LOW-VALUES
               MOVE ZEROS              TO EX-ORIG-ARCHIVE-NO
           ELSE
               MOVE NA-ORIG-ARCHIVE-NO TO EX-ORIG-ARCHIVE-NO
           END-IF
           
           IF NA-RESEND-PROMPT-IND EQUAL LOW-VALUES
               MOVE SPACES             TO EX-RESEND-PROMPT-IND
           ELSE
               MOVE NA-RESEND-PROMPT-IND TO EX-RESEND-PROMPT-IND
           END-IF

           PERFORM 0300-WRITE-MSTR     THRU 0300-EXIT

           .
       0100-EXIT.
           EXIT.

       0200-READ-ELNAPS.

           READ ELNAPS NEXT RECORD

           IF (ELNAPS-FILE-STATUS = '10' OR '23')
              OR (NA-COMPANY-CD NOT = DTE-CLASIC-COMPANY-CD)
              SET END-OF-ELNAPS        TO TRUE
           ELSE
              IF ELNAPS-FILE-STATUS NOT = '00'
                 DISPLAY 'ELNAPS READ NEXT ' ELNAPS-FILE-STATUS
                 SET END-OF-ELNAPS     TO TRUE
              END-IF
           END-IF

           IF NOT END-OF-ELNAPS
              ADD 1 TO NAPS-RECS-IN
           END-IF

           .
       0200-EXIT.
           EXIT.

       0205-READ-ELMSTR.

           MOVE LOW-VALUES      TO  CLAIM-MASTER
           MOVE NA-COMPANY-CD   TO  CL-COMPANY-CD
           MOVE NA-CARRIER      TO  CL-CARRIER 
           MOVE NA-CLAIM-NO     TO  CL-CLAIM-NO
           MOVE NA-CERT-NO      TO  CL-CERT-NO
           
           READ ELMSTR

           IF ELMSTR-FILE-STATUS = '10' OR '23'
              DISPLAY ' CLAIM NOT FOUND ' NA-CLAIM-NO
                 ' ' NA-CERT-NO 
           ELSE
              IF ELMSTR-FILE-STATUS NOT = '00'
                 DISPLAY 'ELMSTR -ERROR - READ ' ELMSTR-FILE-STATUS
              ELSE
                 ADD 1 TO CLM-RECS-IN   
              END-IF
           END-IF

           .
       0205-EXIT.
           EXIT.


       0210-GET-ELTRLRS.

           MOVE SPACES                 TO WS-DIAGNOSIS
                                          WS-LAST-ACT-TYPE
                                          WS-ACTIVITY-TYPE
                                          WS-FORM
                                          WS-LETTER-ID
                                          WS-GOT-IT-SW
                                          WS-LOAN-NO
                                          WS-LETTER-ID
           MOVE LOW-VALUES             TO WS-LAST-ACT-DT
                                          WS-ACTIVITY-DT
                                          WS-AUTO-LAST-SCHED-DT
           MOVE ZEROS                  TO WS-INT-RATE

           PERFORM 0215-GET-INS-ADDR-TRLR
                                       THRU 0215-EXIT

           PERFORM 0216-GET-BEN-ADDR-TRLR
                                       THRU 0216-EXIT

           PERFORM 0220-START-ELTRLR   THRU 0220-EXIT
           IF (ELTRLR-FILE-STATUS = '00')
              AND (CL-CONTROL-PRIMARY = AT-CONTROL-PRIMARY (1:20))
              PERFORM 0230-READ-ELTRLR THRU 0230-EXIT UNTIL
                 WE-GOT-IT-ALL
           END-IF

           .
       0210-EXIT.
           EXIT.

       0215-GET-INS-ADDR-TRLR.

           MOVE SPACES                 TO WS-INS-NAME-AND-ADDRESS
           MOVE CL-CONTROL-PRIMARY     TO AT-CONTROL-PRIMARY
           MOVE +1                     TO AT-SEQUENCE-NO

           READ ELTRLR

           IF (ELTRLR-FILE-STATUS = '00')
              AND (CL-CONTROL-PRIMARY = AT-CONTROL-PRIMARY (1:20))
              IF AT-SEQUENCE-NO = +1
                 MOVE AT-MAIL-TO-NAME  TO WS-INS-NAME
                 MOVE AT-ADDRESS-LINE-1
                                       TO WS-INS-ADDR1
                 MOVE AT-ADDRESS-LINE-2
                                       TO WS-INS-ADDR2
                 MOVE AT-CITY          TO WS-INS-CITY
                 MOVE AT-STATE         TO WS-INS-STATE
                 MOVE AT-ZIP           TO WS-INS-ZIP
                 IF AT-PHONE-NO NOT = ZEROS
                    MOVE AT-PHONE-NO   TO WS-WORK-PHONE
                    STRING '(' WS-WORK-PHONE (2:3) ')'
                       WS-WORK-PHONE (5:3) '-' WS-WORK-PHONE (8:4)
                       DELIMITED BY SIZE INTO WS-INS-PHONE
                    END-STRING
                 END-IF
              END-IF
           END-IF

           .
       0215-EXIT.
           EXIT.

       0216-GET-BEN-ADDR-TRLR.

           MOVE SPACES                 TO WS-BEN-NAME-AND-ADDRESS
           MOVE CL-CONTROL-PRIMARY     TO AT-CONTROL-PRIMARY
           MOVE +11                    TO AT-SEQUENCE-NO

           READ ELTRLR

           IF (ELTRLR-FILE-STATUS = '00')
              AND (CL-CONTROL-PRIMARY = AT-CONTROL-PRIMARY (1:20))
              IF AT-SEQUENCE-NO = +11
                 MOVE AT-MAIL-TO-NAME  TO WS-BEN-NAME
                 MOVE AT-ADDRESS-LINE-1
                                       TO WS-BEN-ADDR1
                 MOVE AT-ADDRESS-LINE-2
                                       TO WS-BEN-ADDR2
                 MOVE AT-CITY          TO WS-BEN-CITY
                 MOVE AT-STATE         TO WS-BEN-STATE
                 MOVE AT-ZIP           TO WS-BEN-ZIP
                 IF AT-PHONE-NO NOT = ZEROS
                    MOVE AT-PHONE-NO   TO WS-WORK-PHONE
                    STRING '(' WS-WORK-PHONE (2:3) ')'
                       WS-WORK-PHONE (5:3) '-' WS-WORK-PHONE (8:4)
                       DELIMITED BY SIZE INTO WS-BEN-PHONE
                    END-STRING
                 END-IF
              END-IF
           END-IF

           .
       0216-EXIT.
           EXIT.

       0220-START-ELTRLR.

           MOVE CL-CONTROL-PRIMARY     TO AT-CONTROL-PRIMARY
           MOVE +90                    TO AT-SEQUENCE-NO
           START ELTRLR KEY >= AT-CONTROL-PRIMARY
           IF ELTRLR-FILE-STATUS NOT = '00'
              DISPLAY ' ERROR - ELTRLR - START ' ELTRLR-FILE-STATUS
                 ' ' CL-CONTROL-PRIMARY (2:19)
           END-IF

           .
       0220-EXIT.
           EXIT.

           
       0230-READ-ELTRLR.

           READ ELTRLR NEXT RECORD

           IF (ELTRLR-FILE-STATUS = '00')
              AND (CL-CONTROL-PRIMARY = AT-CONTROL-PRIMARY (1:20))
              EVALUATE TRUE
                 WHEN AT-SEQUENCE-NO = +90
                    MOVE AT-INFO-LINE-1   TO WS-DIAGNOSIS
                 WHEN AT-SEQUENCE-NO = +91
                    MOVE AT-INFO-LINE-1 TO WS-LOAN-NO
                 WHEN AT-TRAILER-TYPE = '2' AND AT-PAYMENT-TYPE = 'I'
                    IF AT-INT-RATE NUMERIC AND
                       AT-INT-RATE NOT EQUAL ZEROS
                          MOVE AT-INT-RATE TO WS-INT-RATE
                    END-IF
                 WHEN AT-TRAILER-TYPE = '3'
                    IF WS-CURRENT-BIN-DT NOT LESS THAN 
                             AT-SCHEDULE-START-DT  AND
                       WS-CURRENT-BIN-DT NOT GREATER THAN
                             AT-SCHEDULE-END-DT  AND
                      (AT-TERMINATED-DT EQUAL LOW-VALUES OR SPACES)
                         MOVE AT-SCHEDULE-END-DT TO 
                                      WS-AUTO-LAST-SCHED-DT
                    END-IF
                 WHEN AT-TRAILER-TYPE = '8'
                    IF AT-RECORDED-DT > WS-LAST-ACT-DT
                       MOVE AT-RECORDED-DT
                                    TO WS-LAST-ACT-DT
                       MOVE 'DENIAL'
                                    TO WS-LAST-ACT-TYPE
                    END-IF
                 WHEN AT-TRAILER-TYPE = 'A'
                    IF AT-RECORDED-DT > WS-LAST-ACT-DT
                       MOVE AT-RECORDED-DT
                                    TO WS-LAST-ACT-DT
                       MOVE 'FORMS' TO WS-LAST-ACT-TYPE
                       IF INITIAL-FORM
                          MOVE 'INIT'
                                    TO WS-FORM
                       END-IF
                       IF PROGRESS-FORM
                          MOVE 'PROG'
                                    TO WS-FORM
                       END-IF
                    END-IF
                    IF AT-FORM-SEND-ON-DT > WS-ACTIVITY-DT
                       MOVE AT-FORM-SEND-ON-DT
                                    TO WS-ACTIVITY-DT
                    END-IF
                    IF AT-FORM-REPRINT-DT > WS-ACTIVITY-DT
                       MOVE AT-FORM-REPRINT-DT
                                    TO WS-ACTIVITY-DT
                    END-IF
                    IF AT-FORM-ANSWERED-DT > WS-ACTIVITY-DT
                       MOVE AT-FORM-ANSWERED-DT
                                    TO WS-ACTIVITY-DT
                    END-IF
              END-EVALUATE
           ELSE
              SET WE-GOT-IT-ALL        TO TRUE
           END-IF

           .
       0230-EXIT.
           EXIT.


       0275-READ-ELCERT.

           MOVE CL-CERT-KEY-DATA       TO CM-CONTROL-PRIMARY (2:21)
           MOVE DTE-CLASIC-COMPANY-CD  TO CM-COMPANY-CD
           MOVE CL-CERT-NO             TO CM-CERT-NO
           
           READ ELCERT

           IF ELCERT-FILE-STATUS = '10' OR '23'
              DISPLAY ' CERT NOT FOUND ' CL-CERT-KEY-DATA (1:19)
                 ' ' CL-CERT-NO ' ' CL-CLAIM-NO
           ELSE
              IF ELCERT-FILE-STATUS NOT = '00'
                 DISPLAY 'ELCERT -ERROR - READ ' ELCERT-FILE-STATUS
              ELSE
                 PERFORM 0280-GET-ERACCT
                                       THRU 0280-EXIT
                 ADD 1 TO CRT-RECS-IN   
              END-IF
           END-IF

           .
       0275-EXIT.
           EXIT.

       0280-GET-ERACCT.

           MOVE SPACES                 TO WS-ACCT-NAME-AND-ADDRESS
           MOVE LOW-VALUES             TO AM-CONTROL-PRIMARY
           MOVE CM-CONTROL-PRIMARY (1:20)
                                       TO AM-CONTROL-PRIMARY (1:20)
           MOVE CM-CERT-EFF-DT         TO AM-EXPIRATION-DT
           SET NOT-DONE-WITH-ACCT      TO TRUE
           START ERACCT KEY >= AM-CONTROL-PRIMARY
           IF ERACCT-FILE-STATUS = '00'
              READ ERACCT NEXT RECORD
              IF ERACCT-FILE-STATUS = '00'
                 IF CM-CONTROL-PRIMARY (1:20) =
                    AM-CONTROL-PRIMARY (1:20)
                    MOVE AM-NAME       TO WS-ORIG-ACCT-NAME
                    PERFORM UNTIL DONE-WITH-ACCT
                        MOVE AM-NAME   TO WS-ACCT-NAME
                        MOVE AM-ADDRS  TO WS-ACCT-ADDR1
                        MOVE SPACES    TO WS-ACCT-ADDR2
                        MOVE AM-ADDR-CITY  TO WS-ACCT-CITY
                        MOVE AM-ADDR-STATE  TO WS-ACCT-STATE
                        MOVE AM-ZIP    TO WS-ACCT-ZIP
                        MOVE AM-TEL-NO TO WS-ACCT-PHONE
                        READ ERACCT NEXT RECORD
                        IF ERACCT-FILE-STATUS = '00'
                           IF CM-CONTROL-PRIMARY (1:20) <>
                              AM-CONTROL-PRIMARY (1:20)
                                 SET DONE-WITH-ACCT TO TRUE
                           END-IF
                        END-IF
                    END-PERFORM
                 END-IF
              END-IF
           END-IF

           .
       0280-EXIT.
           EXIT.

       0300-WRITE-MSTR.

           INSPECT NAPER-DETAIL-RECORD REPLACING ALL ';' BY ' '
           INSPECT NAPER-DETAIL-RECORD REPLACING ALL X'A2' BY ';'
           WRITE NAPER-OUT-REC        FROM NAPER-DETAIL-RECORD
           ADD 1 TO CLM-RECS-OUT

           .
       0300-EXIT.
           EXIT.

       0400-OPEN-FILES.

           OPEN I-O ELNAPS ELTRLR 
                INPUT ELMSTR ELCERT ERACCT ELBENE
                OUTPUT NAPER-OUT

           .
       0400-EXIT.
           EXIT.

       0500-CLOSE-FILES.

           CLOSE ELNAPS ELMSTR NAPER-OUT ELTRLR ELCERT ERACCT ELBENE

           .
       0500-EXIT.
           EXIT.

       0550-START-ELNAPS.

           MOVE LOW-VALUES             TO NA-CONTROL-PRIMARY
           MOVE DTE-CLASIC-COMPANY-CD  TO NA-COMPANY-CD

           START ELNAPS KEY IS NOT < NA-CONTROL-PRIMARY

           IF ELNAPS-FILE-STATUS = '10' OR '23'
              SET END-OF-ELNAPS        TO TRUE
           ELSE
              IF ELNAPS-FILE-STATUS NOT = '00'
                 DISPLAY 'ELNAPS START     ' ELNAPS-FILE-STATUS
                 SET END-OF-ELNAPS     TO TRUE
              END-IF
           END-IF

           .
       0550-EXIT.
           EXIT.

       0600-INITIALIZE.

           INITIALIZE NAPER-DETAIL-RECORD
           MOVE X'A2'                  TO EX-TAB00
                                          EX-TAB01
                                          EX-TAB02
                                          EX-TAB03
                                          EX-TAB04
                                          EX-TAB05
                                          EX-TAB06
                                          EX-TAB07
                                          EX-TAB08
                                          EX-TAB09
                                          EX-TAB10
                                          EX-TAB11
                                          EX-TAB12
                                          EX-TAB13
                                          EX-TAB14
                                          EX-TAB15
                                          EX-TAB16
                                          EX-TAB17
                                          EX-TAB18
                                          EX-TAB19
                                          EX-TAB20
                                          EX-TAB21
                                          EX-TAB22
                                          EX-TAB23
                                          EX-TAB24
                                          EX-TAB25
                                          EX-TAB26
                                          EX-TAB27
                                          EX-TAB28
                                          EX-TAB29
                                          EX-TAB30
                                          EX-TAB31
                                          EX-TAB32
                                          EX-TAB33
                                          EX-TAB34
                                          EX-TAB35
                                          EX-TAB36
                                          EX-TAB37
                                          EX-TAB38
                                          EX-TAB39
                                          EX-TAB40
                                          EX-TAB41
                                          EX-TAB42
                                          EX-TAB43
                                          EX-TAB44
                                          EX-TAB45
                                          EX-TAB46
                                          EX-TAB47
                                          EX-TAB48
                                          EX-TAB49
                                          EX-TAB50
                                          EX-TAB51
                                          EX-TAB52
                                          EX-TAB53
                                          EX-TAB54
                                          EX-TAB55
                                          EX-TAB56
                                          EX-TAB57
                                          EX-TAB58
                                          EX-TAB59
                                          EX-TAB60
                                          EX-TAB61
                                          EX-TAB62
                                          EX-TAB63
                                          EX-TAB64
                                          EX-TAB65
                                          EX-TAB66
                                          EX-TAB67
                                          EX-TAB68
                                          EX-TAB69
                                          EX-TAB70
                                          EX-TAB71
                                          EX-TAB72
                                          EX-TAB73
                                          EX-TAB74
                                          EX-TAB75
                                          EX-TAB76
102810                                    EX-TAB77
102810                                    EX-TAB78
031612                                    EX-TAB79
           MOVE 'E'                    TO EX-LAST-BYTE

           MOVE WS-CURRENT-DATE        TO  DC-GREG-DATE-1-EDIT.
           MOVE '2'                    TO  DC-OPTION-CODE.
           DISPLAY '* * * * * * * * * * * * * * * * * * * * * '.
           DISPLAY 'CURRENT DATE USED FOR RUN IS - - ' WS-CURRENT-DATE.
           DISPLAY '* * * * * * * * * * * * * * * * * * * * * '.

           PERFORM 8510-DATE-CONVERSION.
           IF NO-CONVERSION-ERROR
               MOVE DC-BIN-DATE-1      TO  WS-CURRENT-BIN-DT
           END-IF

           MOVE NAPER-DETAIL-RECORD   TO WS-SAVE-NAPER
           PERFORM 0550-START-ELNAPS   THRU 0550-EXIT
           PERFORM 0200-READ-ELNAPS    THRU 0200-EXIT

           .
       0600-EXIT.
           EXIT.

       8510-DATE-CONVERSION.

           CALL 'ELDATCX' USING DATE-CONVERSION-DATA

           .
       8590-EXIT.
           EXIT.

       ABEND-PGM. COPY ELCABEND.
