       IDENTIFICATION DIVISION.

       PROGRAM-ID.                 EL353.


      *AUTHOR.     AJRA.
      ******************************************************************
      *REMARKS.
      *        THIS PROGRAM CREATES A REPORT OF CLAIM PAYMENTS THAT
      *        ARE UNAPPROVED.
      *
      *     INPUT:   ELACTQ
      *              ELTRLR
      ******************************************************************
      *                   C H A N G E   L O G
      *
      * CHANGES ARE MARKED BY THE CHANGE EFFECTIVE DATE.
      *-----------------------------------------------------------------
      *  CHANGE   CHANGE REQUEST PGMR  DESCRIPTION OF CHANGE
      * EFFECTIVE    NUMBER
      *-----------------------------------------------------------------
      * 081007    2006062200004  AJRA  INITIAL PROGRAM
      ******************************************************************

       ENVIRONMENT DIVISION.

       INPUT-OUTPUT SECTION.

       FILE-CONTROL.
           SELECT DISK-DATE          ASSIGN TO SYS019-FBA1-S-SYS019.

           SELECT ELACTQ-INFILE      ASSIGN TO ELACTQ
                                     ORGANIZATION IS INDEXED
                                     ACCESS IS DYNAMIC
                                     RECORD KEY IS AQ-CONTROL-PRIMARY
                                     FILE STATUS IS
                                       WS-ELACTQ-FILE-STATUS.

           SELECT ELTRLR-INFILE      ASSIGN TO ELTRLR
                                     ORGANIZATION IS INDEXED
                                     ACCESS IS DYNAMIC
                                     RECORD KEY IS AT-CONTROL-PRIMARY
                                     FILE STATUS IS
                                       WS-ELTRLR-FILE-STATUS.

           SELECT PRNTR              ASSIGN TO SYS008-UR-1403-S-SYS008.

           SELECT SORT-FILE          ASSIGN TO SYS001-UT-3380-S-SORTWK1.

       DATA DIVISION.

       FILE SECTION.

       FD  DISK-DATE
                                COPY ELCDTEFD.

       FD  ELACTQ-INFILE.
                                COPY ELCACTQ.

       FD  ELTRLR-INFILE.
                                COPY ELCTRLR.
                                
       FD  PRNTR
                                COPY ELCPRTFD.

       SD  SORT-FILE.
       01  SORT-RECORD.
           05  SORT-CLAIM-NO               PIC X(07).
           05  SORT-CERT-NO                PIC X(11).
           05  SORT-RECORDED-BY            PIC X(04).
           05  SORT-RECORDED-DT            PIC X(02).
           05  SORT-AMOUNT-PAID            PIC S9(7)V99.
           05  FILLER                      PIC X(47).


       WORKING-STORAGE SECTION.

       77  FILLER  PIC X(32)   VALUE '********************************'.
       77  FILLER  PIC X(32)   VALUE '*     EL353  WORKING STORAGE   *'.
       77  FILLER  PIC X(32)   VALUE '********** V/M 2.008 ***********'.

       01  FILLER                          COMP-3.
           05  WS-LINE-COUNT               PIC S9(03)    VALUE +0.
           05  WS-LINE-COUNT-MAX           PIC S9(03)    VALUE +55.
           05  WS-PAGE                     PIC S9(05)    VALUE +0.
           05  WS-SORT-RTN-COUNT           PIC S9(05)    VALUE +0.

           05  WS-RETURN-CODE              PIC S9(03)    VALUE +0.
           05  WS-ZERO                     PIC S9(01)    VALUE +0.


       01  FILLER                          COMP SYNC.
           05  PGM-SUB                     PIC S9(04)    VALUE +353.

       01  FILLER.
           05  ABEND-CODE                  PIC X(04).
           05  ABEND-OPTION                PIC X(01).
           05  OLC-REPORT-NAME             PIC X(05)     VALUE 'EL353'.

           05  WS-CYCLE-DT-BINARY          PIC X(02)     VALUE SPACES.

           05  WS-LAST-MAINT-CUTOFF-DT-BIN PIC X(02)     VALUE SPACES.

           05  WS-CYCLE-DT.
               10  WS-CYCLE-DT-CC          PIC 9(02)     VALUE ZEROS.
               10  WS-CYCLE-DT-YY          PIC 9(02)     VALUE ZEROS.
               10  WS-CYCLE-DT-MM          PIC 9(02)     VALUE ZEROS.
               10  WS-CYCLE-DT-DD          PIC 9(02)     VALUE ZEROS.

           05  WS-EDITED-CYCLE-DT.
               10  WS-EDITED-CYCLE-DT-MM   PIC X(02)     VALUE SPACES.
               10  FILLER                  PIC X(01)     VALUE '/'.
               10  WS-EDITED-CYCLE-DT-DD   PIC X(02)     VALUE SPACES.
               10  FILLER                  PIC X(01)     VALUE '/'.
               10  WS-EDITED-CYCLE-DT-CC   PIC X(02)     VALUE SPACES.
               10  WS-EDITED-CYCLE-DT-YY   PIC X(02)     VALUE SPACES.

           05  WS-EOF1-SW                  PIC X(01)     VALUE SPACE.
               88  END-OF-ELACTQ                         VALUE 'Y'.

           05  WS-EOF2-SW                  PIC X(01)     VALUE SPACE.
               88  END-OF-ELTRLR                         VALUE 'Y'.

           05  WS-EOF3-SW                  PIC X(01)     VALUE SPACE.
               88  END-OF-SORTFILE                       VALUE 'Y'.
               

           05  WS-ELTRLR-FILE-STATUS       PIC X(02)     VALUE ZERO.
           05  WS-ELACTQ-FILE-STATUS       PIC X(02)     VALUE ZERO.

           05  WS-ELTRLR-KEY-SW            PIC X(01)     VALUE SPACE.
               88  NEW-ELTRLR-KEY                        VALUE 'N'.
               88  ELTRLR-KEY-CHANGE                     VALUE 'C'.

           05  WS-ABEND-MESSAGE            PIC X(80)     VALUE SPACES.

           05  WS-ABEND-FILE-STATUS        PIC X(02)     VALUE ZERO.

           05  WS-DETAIL-REPORT-TITLE      PIC X(42)     VALUE
               '    CHECKS WAITING FOR PAYMENT APPROVAL   '.

           05  WS-MTD-LITERAL              PIC X(17)     VALUE
               'MONTH-TO-DATE    '.
                   
           05  WS-NO-CHECK-MESSAGE.
               10  FILLER                  PIC X(45)    VALUE SPACES.
               10  FILLER                  PIC X(41)    VALUE
                   'THERE ARE NO CHECKS WAITING FOR APPROVAL.'.
               10  FILLER                  PIC X(47)    VALUE SPACES.

       01  WS-HEADING1.
           05  FILLER                      PIC X(01)     VALUE '1'.
           05  FILLER                      PIC X(43)     VALUE SPACES.
           05  WS-H1-TITLE                 PIC X(42)     VALUE SPACES.
           05  FILLER                      PIC X(35)     VALUE SPACES.
           05  WS-H1-REPORT-ID             PIC X(05)     VALUE 'EL353'.
           05  WS-H1-REPORT-ID2            PIC X(01)     VALUE SPACES.
           05  FILLER                      PIC X(06)     VALUE SPACES.


       01  WS-HEADING2.
           05  FILLER                      PIC X(01)     VALUE SPACE.
           05  FILLER                      PIC X(50)     VALUE SPACES.
           05  WS-H2-COMPANY-NAME          PIC X(30)     VALUE SPACES.
           05  FILLER                      PIC X(40)     VALUE SPACES.
           05  WS-H2-DATE                  PIC X(08)     VALUE SPACES.
           05  FILLER                      PIC X(04)     VALUE SPACES.


       01  WS-HEADING3.
           05  FILLER                      PIC X(01)     VALUE SPACE.
           05  FILLER                      PIC X(51)     VALUE SPACES.
           05  FILLER                      PIC X(17)     VALUE
               'ACTIVITY THROUGH '.
           05  WS-H3-THROUGH-DT            PIC X(10)     VALUE SPACES.
           05  FILLER                      PIC X(42)     VALUE SPACES.
           05  FILLER                      PIC X(04)     VALUE 'PAGE'.
           05  FILLER                      PIC X(01)     VALUE SPACES.
           05  WS-H3-PAGE                  PIC ZZ,ZZ9.
           05  FILLER                      PIC X(01)     VALUE SPACE.


       01  WS-HEADING4.
           05  FILLER                      PIC X(01)     VALUE '-'.
           05  FILLER                      PIC X(27)     VALUE SPACES.
           05  FILLER                      PIC X(05)     VALUE
               'CLAIM'.
           05  FILLER                      PIC X(12)     VALUE SPACES.
           05  FILLER                      PIC X(04)     VALUE
               'CERT'.
           05  FILLER                      PIC X(10)     VALUE SPACES.
           05  FILLER                      PIC X(08)     VALUE
               'RECORDED'.
           05  FILLER                      PIC X(08)     VALUE SPACES.
           05  FILLER                      PIC X(08)     VALUE
               'RECORDED'.
           05  FILLER                      PIC X(10)     VALUE SPACES.
           05  FILLER                      PIC X(06)     VALUE
               'AMOUNT'.
           05  FILLER                      PIC X(34)     VALUE SPACES.



       01  WS-HEADING4B.
           05  FILLER                      PIC X(01)     VALUE ' '.
           05  FILLER                      PIC X(27)     VALUE SPACES.
           05  FILLER                      PIC X(06)     VALUE
               'NUMBER'.
           05  FILLER                      PIC X(10)     VALUE SPACES.
           05  FILLER                      PIC X(06)     VALUE
               'NUMBER'.
           05  FILLER                      PIC X(12)     VALUE SPACES.
           05  FILLER                      PIC X(02)     VALUE
               'BY'.
           05  FILLER                      PIC X(13)     VALUE SPACES.
           05  FILLER                      PIC X(04)     VALUE
               'DATE'.
           05  FILLER                      PIC X(13)     VALUE SPACES.
           05  FILLER                      PIC X(04)     VALUE
               'PAID'.
           05  FILLER                      PIC X(35)     VALUE SPACES.


       01  WS-DETAIL.
           05  FILLER                      PIC X(01)     VALUE SPACE.
           05  FILLER                      PIC X(26)     VALUE SPACES.
           05  WS-DET-CLAIM-NO             PIC X(07).
           05  FILLER                      PIC X(08)     VALUE SPACES.
           05  WS-DET-CERT-NO              PIC X(11).
           05  FILLER                      PIC X(08)     VALUE SPACES.
           05  WS-DET-RECORDED-BY          PIC X(04).
           05  FILLER                      PIC X(10)     VALUE SPACES.
           05  WS-DET-RECORDED-DT          PIC X(10).
           05  FILLER                      PIC X(07)     VALUE SPACES.
           05  WS-DET-AMOUNT-PAID          PIC -(7).99.
           05  FILLER                      PIC X(31)     VALUE SPACES.

      *              *************
      *              ELCDTECX: LAYOUT FOR DISK-DATE FILE
                     COPY ELCDTECX.


                     COPY ELCDTEVR.

      *              *************
      *              ELCDATE: LAYOUT OF DATA PASSED TO DATE CONV RTN
                     COPY ELCDATE.


       LINKAGE SECTION.

       01  PARM.
           05  PARM-LENGTH        COMP     PIC S9(04)    VALUE ZEROS.
           05  PARM-CYCLE-DT               PIC X(08)     VALUE SPACES.

      ******************************************************************
      ********************************
       PROCEDURE DIVISION USING PARM.

      ****************READ DISK-DATE FILE
       0000-DATE-CARD-READ. COPY ELCDTERX.

       1000-MAIN-LOGIC.

           PERFORM 1500-EDIT-CYCLE-DATE       THRU 1500-EXIT.

           PERFORM OPEN-FILES                 THRU OPEN-FILES-EXIT.

           MOVE WS-DETAIL-REPORT-TITLE        TO WS-H1-TITLE.
           MOVE COMPANY-NAME                  TO WS-H2-COMPANY-NAME.
           MOVE WS-CURRENT-DATE               TO WS-H2-DATE.
           MOVE WS-EDITED-CYCLE-DT            TO WS-H3-THROUGH-DT.
           
           PERFORM 3500-PRINT-HEADINGS THRU 3500-EXIT.

           SORT SORT-FILE  ASCENDING KEY 
                          SORT-CLAIM-NO SORT-CERT-NO SORT-RECORDED-BY
           INPUT  PROCEDURE 2000-INPUT-PROCEDURE  THRU 2000-EXIT
           OUTPUT PROCEDURE 3000-OUTPUT-PROCEDURE THRU 3000-EXIT.

           IF SORT-RETURN  NOT = ZEROS
               MOVE 'INTERNAL SORT ABORTED'
                                       TO WS-ABEND-MESSAGE
               MOVE '0101'             TO WS-RETURN-CODE
               PERFORM ABEND-PGM       THRU APS-EXIT
           END-IF.

           PERFORM CLOSE-FILES        THRU CLOSE-FILES-EXIT.


           GOBACK.


       1500-EDIT-CYCLE-DATE.

           IF PARM-LENGTH = +0
               DISPLAY 'CYCLE DATE INPUT PARMS MISSING'
               PERFORM ABEND-PGM            THRU APS-EXIT
           END-IF.

           MOVE PARM-CYCLE-DT               TO DC-GREG-DATE-CYMD.
           MOVE 'L'                         TO DC-OPTION-CODE.
           PERFORM 8500-DATE-CONVERSION     THRU 8500-EXIT.

           IF NO-CONVERSION-ERROR
               MOVE DC-BIN-DATE-1           TO WS-CYCLE-DT-BINARY
               MOVE PARM-CYCLE-DT           TO WS-CYCLE-DT
               MOVE WS-CYCLE-DT-MM          TO WS-EDITED-CYCLE-DT-MM
               MOVE WS-CYCLE-DT-DD          TO WS-EDITED-CYCLE-DT-DD
               MOVE WS-CYCLE-DT-CC          TO WS-EDITED-CYCLE-DT-CC
               MOVE WS-CYCLE-DT-YY          TO WS-EDITED-CYCLE-DT-YY
           ELSE
               DISPLAY 'INVALID CYCLE DATE ' PARM-CYCLE-DT
               PERFORM ABEND-PGM            THRU APS-EXIT
           END-IF.


       1500-EXIT.
           EXIT.


       2000-INPUT-PROCEDURE.

           MOVE LOW-VALUES             TO AQ-CONTROL-PRIMARY.
           MOVE DTE-CLASIC-COMPANY-CD  TO AQ-COMPANY-CD.
           
           START ELACTQ-INFILE KEY NOT < AQ-CONTROL-PRIMARY
           END-START.

           EVALUATE TRUE
           WHEN WS-ELACTQ-FILE-STATUS = '00'
               CONTINUE

           WHEN WS-ELACTQ-FILE-STATUS = '23'
               SET END-OF-ELACTQ TO TRUE
               GO TO 2000-EXIT

           WHEN OTHER
              DISPLAY ' ELACTQ START ' WS-ELACTQ-FILE-STATUS
              PERFORM ABEND-PGM             THRU APS-EXIT
           END-EVALUATE.                      

           PERFORM 2010-BUILD-REPORT          THRU 2010-EXIT
               UNTIL END-OF-ELACTQ.

           MOVE SPACES TO SORT-RECORD.
           

       2000-EXIT.
           EXIT.


       2010-BUILD-REPORT.

           READ ELACTQ-INFILE NEXT RECORD
               AT END
                   SET END-OF-ELACTQ        TO TRUE
                   GO TO 2010-EXIT
           END-READ.

      *********************** COMPANY CODE OF HEX '04' = CID
      *********************** COMPANY CODE OF HEX '05' = DCC 
           IF AQ-COMPANY-CD NOT EQUAL DTE-CLASIC-COMPANY-CD
               GO TO 2010-EXIT
           END-IF.
           
           IF AQ-PMT-UNAPPROVED-COUNT NOT NUMERIC
               GO TO 2010-EXIT
           END-IF.
           
           IF AQ-PMT-UNAPPROVED-COUNT NOT GREATER THAN +0
               GO TO 2010-EXIT
           END-IF.
           
           MOVE LOW-VALUES           TO AT-CONTROL-PRIMARY.
           MOVE AQ-CONTROL-PRIMARY   TO AT-CONTROL-PRIMARY (1:20).
           MOVE ZERO                 TO AT-SEQUENCE-NO.
           
           START ELTRLR-INFILE KEY NOT < AT-CONTROL-PRIMARY
               INVALID KEY
                   DISPLAY 'INVALID KEY ON START ELTRLR '
                                            AT-CONTROL-PRIMARY
                   PERFORM ABEND-PGM        THRU APS-EXIT
           END-START.

           SET NEW-ELTRLR-KEY               TO TRUE.
           PERFORM 2100-READ-ELTRLR         THRU 2100-EXIT
               UNTIL ELTRLR-KEY-CHANGE  OR
                     END-OF-ELTRLR.

           
       2010-EXIT.
           EXIT.


       2100-READ-ELTRLR.

           READ ELTRLR-INFILE NEXT RECORD
               AT END
                   SET END-OF-ELTRLR        TO TRUE
                   GO TO 2100-EXIT
           END-READ.

           IF AT-CONTROL-PRIMARY (1:20) = AQ-CONTROL-PRIMARY
               CONTINUE
           ELSE
               SET ELTRLR-KEY-CHANGE        TO TRUE
               GO TO 2100-EXIT
           END-IF.

      ******************CHECK FOR UNAPPROVED PMT
           IF AT-PAYMENT-APPROVAL-SW = 'U'
               PERFORM 2300-RELEASE-DETAIL THRU 2300-EXIT
           END-IF.
           
           
       2100-EXIT.
           EXIT.


       2300-RELEASE-DETAIL.
               
           MOVE AT-CLAIM-NO    TO SORT-CLAIM-NO.
           MOVE AT-CERT-NO     TO SORT-CERT-NO.
           MOVE AT-RECORDED-BY TO SORT-RECORDED-BY.
           MOVE AT-RECORDED-DT TO SORT-RECORDED-DT.          
           MOVE AT-AMOUNT-PAID TO SORT-AMOUNT-PAID.          
               
           RELEASE SORT-RECORD.

       2300-EXIT.
           EXIT.


       3000-OUTPUT-PROCEDURE.

           PERFORM 3020-RETURN-SORT THRU 3020-EXIT
               UNTIL END-OF-SORTFILE.

           IF WS-SORT-RTN-COUNT = 0
               MOVE WS-NO-CHECK-MESSAGE TO PRT
               PERFORM 3900-WRITE  THRU 3900-EXIT
           END-IF.
           
           
       3000-EXIT.
           EXIT.

       3020-RETURN-SORT.

           RETURN SORT-FILE
               AT END
                   SET END-OF-SORTFILE TO TRUE
                   GO TO 3020-EXIT
           END-RETURN
 
           ADD +1 TO WS-SORT-RTN-COUNT
                                            
           IF WS-LINE-COUNT >= WS-LINE-COUNT-MAX
               PERFORM 3500-PRINT-HEADINGS THRU 3500-EXIT
           END-IF

           MOVE SORT-CLAIM-NO      TO WS-DET-CLAIM-NO.
           MOVE SORT-CERT-NO       TO WS-DET-CERT-NO.
           MOVE SORT-RECORDED-BY   TO WS-DET-RECORDED-BY.
           MOVE SORT-AMOUNT-PAID   TO WS-DET-AMOUNT-PAID.          
           MOVE SORT-RECORDED-DT   TO DC-BIN-DATE-1.
           MOVE ' '                TO DC-OPTION-CODE.
           PERFORM 8500-DATE-CONVERSION THRU 8500-EXIT.

           IF NO-CONVERSION-ERROR
               MOVE DC-GREG-DATE-B-EDIT TO WS-DET-RECORDED-DT
           ELSE
               MOVE SPACES         TO WS-DET-RECORDED-DT
           END-IF.
                

           MOVE WS-DETAIL                   TO PRT.
           PERFORM 3900-WRITE               THRU 3900-EXIT.

           
       3020-EXIT.
           EXIT.


       3500-PRINT-HEADINGS.

           MOVE 'A'                         TO WS-H1-REPORT-ID2.
           MOVE WS-HEADING1                 TO PRT.
           PERFORM 3900-WRITE               THRU 3900-EXIT.

           MOVE WS-HEADING2                 TO PRT.
           PERFORM 3900-WRITE               THRU 3900-EXIT.

           ADD +1                           TO WS-PAGE.
           MOVE WS-PAGE                     TO WS-H3-PAGE.
           MOVE WS-HEADING3                 TO PRT.
           PERFORM 3900-WRITE               THRU 3900-EXIT.

           MOVE WS-HEADING4                 TO PRT.
           PERFORM 3900-WRITE               THRU 3900-EXIT.

           MOVE WS-HEADING4B                TO PRT.
           PERFORM 3900-WRITE               THRU 3900-EXIT.

           MOVE SPACES                      TO PRT.
           MOVE ' '                         TO P-CTL.
           PERFORM 3900-WRITE               THRU 3900-EXIT.

          
       3500-EXIT.
           EXIT.


       3900-WRITE.

           EVALUATE TRUE
           WHEN P-CTL = '1'
               MOVE +1                      TO WS-LINE-COUNT

           WHEN P-CTL = SPACE
               ADD +1                       TO WS-LINE-COUNT

           WHEN P-CTL = '0'
               ADD +2                       TO WS-LINE-COUNT

           WHEN OTHER
               ADD +3                       TO WS-LINE-COUNT
           END-EVALUATE


           WRITE PRT

           .
       3900-EXIT.
           EXIT.



       8500-DATE-CONVERSION.

           CALL 'ELDATCX' USING DATE-CONVERSION-DATA

           .
       8500-EXIT.
           EXIT.


       OPEN-FILES.

           OPEN INPUT  ELACTQ-INFILE
                       ELTRLR-INFILE
                OUTPUT PRNTR.

           IF WS-ELACTQ-FILE-STATUS  = '00' OR '97'
              CONTINUE
           ELSE
              MOVE 'OPEN ERROR OCCURRED ON ELACTQ'
                                              TO WS-ABEND-MESSAGE
              MOVE WS-ELACTQ-FILE-STATUS      TO WS-ABEND-FILE-STATUS
              PERFORM ABEND-PGM               THRU APS-EXIT
           END-IF.


           IF WS-ELTRLR-FILE-STATUS  = '00' OR '97'
              CONTINUE
           ELSE
              MOVE 'OPEN ERROR OCCURRED ON ELTRLR'
                                              TO WS-ABEND-MESSAGE
              MOVE WS-ELTRLR-FILE-STATUS      TO WS-ABEND-FILE-STATUS
              PERFORM ABEND-PGM               THRU APS-EXIT
           END-IF.
           
           
       OPEN-FILES-EXIT.
           EXIT.


       CLOSE-FILES.



           CLOSE ELACTQ-INFILE
                 ELTRLR-INFILE
                 PRNTR.

            
       CLOSE-FILES-EXIT.
           EXIT.

       ABEND-PGM. COPY ELCABEND.
ABEND.

ABEND.
ABEND.
BEND.
BEND.
ABEND.
