       IDENTIFICATION DIVISION.

       PROGRAM-ID.                 EL583.


      *AUTHOR.     SUZAN VUKOV.
      ******************************************************************
      *REMARKS.
      *        THIS PROGRAM CREATES A PRINTED CHECK LIST FOR CID.
      *
      *     INPUT:   CI.DD.LG.ELCHKQ
      *     REPORT: - SORTED BY CONTROL NUMBER
      ******************************************************************
      *                   C H A N G E   L O G
      *
      * CHANGES ARE MARKED BY THE CHANGE EFFECTIVE DATE.
      *-----------------------------------------------------------------
      *  CHANGE   CHANGE REQUEST PGMR  DESCRIPTION OF CHANGE
      * EFFECTIVE    NUMBER
      *-----------------------------------------------------------------
      * 110101    2001100100006  SMVA  INITIAL IMPL FOR UNIKIX
      *                              REWRITE OF MAINFRAME CULPRIT
010704* 010704                   SMVA  HANDLE ELCHKQ STATUS 23
050307* 050307    2007032100007  PEMA  ADD FLAG TO ID PMTS WITH INTEREST
111709* 111709    2009111700001  AJRA  ADD PARM TO LIMIT CARRIER
      ******************************************************************

       ENVIRONMENT DIVISION.

       INPUT-OUTPUT SECTION.

       FILE-CONTROL.
           SELECT DISK-DATE        ASSIGN TO SYS019-FBA1-S-SYS019.

           SELECT ELCHKQ           ASSIGN TO SYS027-FBA1-ELCHKQ
                                   ORGANIZATION IS INDEXED
                                   ACCESS IS DYNAMIC
                                   RECORD KEY IS CQ-CONTROL-PRIMARY
                                   FILE STATUS IS ELCHKQ-FILE-STATUS.

050307     SELECT ELTRLR           ASSIGN TO ELTRLR
050307                             ORGANIZATION IS INDEXED
050307                             ACCESS IS DYNAMIC
050307                             RECORD KEY IS AT-CONTROL-PRIMARY
050307                             FILE STATUS IS ELTRLR-FILE-STATUS.

           SELECT PRNTR            ASSIGN TO SYS008-UR-1403-S-SYS008.

           SELECT FICH             ASSIGN TO SYS020-UT-2400-S-SYS020.

           SELECT ELREPT           ASSIGN TO SYS018-FBA1-ELREPT
                                   ORGANIZATION IS INDEXED
                                   ACCESS IS DYNAMIC
                                   RECORD KEY IS RF-CONTROL-PRIMARY
                                   FILE STATUS IS DTE-VSAM-FLAGS.

       DATA DIVISION.

       FILE SECTION.

       FD  DISK-DATE
                                COPY ELCDTEFD.

       FD  ELCHKQ.
                                COPY ELCCHKQ.

050307 FD  ELTRLR.
050307                          COPY ELCTRLR.

       FD  PRNTR
                                COPY ELCPRTFD.

      *********** OLD FICHE PROCESSING USED FOR SEQUENTIAL FILE ARCHIVE
      *               IF DTE-PRT-OPT = 'B'
       FD  FICH
                                COPY ELCFCHFD.


       FD  ELREPT
                                COPY ELCRPTFD.
                                COPY ELCREPT.

       WORKING-STORAGE SECTION.
       01  LCP-ABND-CODE                 PIC S999 COMP   VALUE +519.

       77  FILLER  PIC X(32)   VALUE '********************************'.
       77  FILLER  PIC X(32)   VALUE '*     EL583  WORKING STORAGE   *'.
       77  FILLER  PIC X(32)   VALUE '********** V/M 2.008 ***********'.

       01  FILLER                          COMP-3.
           05  WS-LINE-COUNT               PIC S9(03)    VALUE +0.
           05  WS-LINE-COUNT-MAX           PIC S9(03)    VALUE +45.
           05  WS-PAGE                     PIC S9(05)    VALUE +0.
           05  WS-CONTROL-TOTAL-CHECK-CNT  PIC S9(06)    VALUE +0.
           05  WS-GRAND-TOTAL-CHECK-CNT    PIC S9(06)    VALUE +0.
           05  WS-CONTROL-TOTAL-CHECK-AMT  PIC S9(08)V99 VALUE +0.
           05  WS-GRAND-TOTAL-CHECK-AMT    PIC S9(08)V99 VALUE +0.
           05  WS-RETURN-CODE              PIC S9(03)    VALUE +0.
           05  WS-ZERO                     PIC S9        VALUE +0.


       01  FILLER                          COMP SYNC.
           05  PGM-SUB                     PIC S9(04)    VALUE +583.

       01  FILLER.
           05  ABEND-CODE                  PIC X(04).
           05  ABEND-OPTION                PIC X(01).
           05  OLC-REPORT-NAME             PIC X(05)     VALUE 'EL583'.

           05  WS-HOLD-CONTROL-NUMBER      PIC S9(08) COMP VALUE ZEROS.

           05  WS-CYCLE-DT-BINARY          PIC X(02)     VALUE SPACES.

           05  WS-CYCLE-DT.
               10  WS-CYCLE-DT-CC          PIC 9(02)     VALUE ZEROS.
               10  WS-CYCLE-DT-YY          PIC 9(02)     VALUE ZEROS.
               10  WS-CYCLE-DT-MM          PIC 9(02)     VALUE ZEROS.
               10  WS-CYCLE-DT-DD          PIC 9(02)     VALUE ZEROS.

           05  WS-EDITED-CYCLE-DT.
               10  WS-EDITED-CYCLE-DT-MM   PIC X(02)     VALUE SPACES.
               10  FILLER                  PIC X(01)     VALUE '/'.
               10  WS-EDITED-CYCLE-DT-DD   PIC X(02)     VALUE SPACES.
               10  FILLER                  PIC X(01)     VALUE '/'.
               10  WS-EDITED-CYCLE-DT-YY   PIC X(02)     VALUE SPACES.

           05  WS-EOF-SW                   PIC X(01)     VALUE SPACE.
               88  END-OF-ELCHKQ                         VALUE '1'.

           05  ELCHKQ-FILE-STATUS          PIC X(02)     VALUE ZERO.
050307     05  ELTRLR-FILE-STATUS          PIC XX        VALUE ZERO.

           05  WS-FIRST-TIME-SW            PIC X(01)     VALUE 'Y'.
               88  FIRST-TIME-THROUGH                    VALUE 'Y'.
               88  NOT-FIRST-TIME                        VALUE 'N'.

           05  WS-REPORT-DELETE-SW         PIC X(01)     VALUE 'B'.
               88  BEGIN-REPORT-DELETE                   VALUE 'B'.
               88  REPORT-DELETE-DONE                    VALUE 'D'.

           05  WS-ABEND-MESSAGE            PIC X(80)     VALUE SPACES.

           05  WS-ABEND-FILE-STATUS        PIC X(02)     VALUE ZERO.

      *    05  WS-FILE-ERROR-MESSAGE.
      *        10  FILLER                  PIC X(24)     VALUE
      *            'ERROR OCCURED OPENING - '.
      *        10  WS-FEM-FILE-NAME        PIC X(08).


       01  WS-HEADING1.
           05  FILLER                      PIC X(01)     VALUE '1'.
           05  FILLER                      PIC X(32)     VALUE SPACES.
           05  WS-H1-TITLE                 PIC X(14)     VALUE
               'PRINTED CHECKS'.
           05  FILLER                      PIC X(21)     VALUE SPACES.
           05  WS-H1-REPORT-ID             PIC X(05)     VALUE 'EL583'.
           05  FILLER                      PIC X(07)     VALUE SPACES.


       01  WS-HEADING2.
           05  FILLER                      PIC X(01)     VALUE SPACE.
           05  FILLER                      PIC X(25)     VALUE SPACES.
           05  WS-H2-COMPANY-NAME          PIC X(30)     VALUE SPACES.
           05  FILLER                      PIC X(12)     VALUE SPACES.
           05  WS-H2-DATE                  PIC X(08)     VALUE SPACES.
           05  FILLER                      PIC X(04)     VALUE SPACES.


       01  WS-HEADING3.
           05  FILLER                      PIC X(01)     VALUE SPACE.
           05  FILLER                      PIC X(26)     VALUE SPACES.
           05  FILLER                      PIC X(12)     VALUE
               'CYCLE DATE: '.
           05  WS-H3-CYCLE-DT              PIC X(10)     VALUE SPACES.
           05  FILLER                      PIC X(19)     VALUE SPACES.
           05  FILLER                      PIC X(04)     VALUE 'PAGE'.
           05  FILLER                      PIC X(02)     VALUE SPACES.
           05  WS-H3-PAGE                  PIC ZZ,ZZ9.


       01  WS-HEADING4.
           05  FILLER                      PIC X(01)     VALUE '-'.
050307     05  FILLER                      PIC X         VALUE ' '.
           05  FILLER                      PIC X(10)     VALUE
               'CONTROL   '.
           05  FILLER                      PIC X(09)     VALUE
               'CHECK    '.
           05  FILLER                      PIC X(09)     VALUE
               'CHECK    '.
           05  FILLER                      PIC X(10)     VALUE
               'PAYMENT   '.
           05  FILLER                      PIC X(08)     VALUE
               'CLAIM   '.
           05  FILLER                      PIC X(08)     VALUE SPACES.
           05  FILLER                      PIC X(12)     VALUE
               'CERT        '.
           05  FILLER                      PIC X(09)     VALUE
               'PAYMENT  '.
           05  FILLER                      PIC X(04)     VALUE
               'USER'.


       01  WS-HEADING5.
           05  FILLER                      PIC X(01)     VALUE SPACE.
050307     05  FILLER                      PIC X         VALUE ' '.
           05  FILLER                      PIC X(10)     VALUE
               'NUMBER    ' .
           05  FILLER                      PIC X(09)     VALUE
               'NUMBER   '.
           05  FILLER                      PIC X(10)     VALUE
               'DATE      '.
           05  FILLER                      PIC X(09)     VALUE
               'TYPE     '.
           05  FILLER                      PIC X(08)     VALUE
               'NUMBER  '.
           05  FILLER                      PIC X(07)     VALUE
               'CAR    '.
           05  FILLER                      PIC X(13)     VALUE
               'NUMBER       '.
           05  FILLER                      PIC X(10)     VALUE
               'AMOUNT    '.
           05  FILLER                      PIC X(03)     VALUE
               'ID '.


       01  WS-DETAIL1.
           05  FILLER                      PIC X(01)     VALUE SPACE.
050307     05  WS-D1-INT-PMT-PRESENT       PIC X         VALUE SPACES.
           05  WS-D1-CONTROL-NUMBER        PIC X(08)     VALUE SPACES.
           05  FILLER                      PIC X(02)     VALUE SPACES.
           05  WS-D1-CHECK-NUMBER          PIC X(07)     VALUE SPACES.
           05  FILLER                      PIC X(01)     VALUE SPACES.
           05  WS-D1-CHECK-DT              PIC X(08)     VALUE SPACES.
           05  FILLER                      PIC X(02)     VALUE SPACES.
           05  WS-D1-PAYMENT-TYPE          PIC X(07)     VALUE SPACES.
           05  FILLER                      PIC X(02)     VALUE SPACES.
           05  WS-D1-CLAIM-NUMBER          PIC X(07)     VALUE SPACES.
           05  FILLER                      PIC X(03)     VALUE SPACES.
           05  WS-D1-CARRIER               PIC X(01)     VALUE SPACE.
           05  FILLER                      PIC X(03)     VALUE SPACES.
           05  WS-D1-CERT-NUMBER           PIC X(10)     VALUE SPACES.
           05  WS-D1-CHECK-AMT             PIC Z,ZZZ,ZZ9.99.
           05  FILLER                      PIC X(02)     VALUE SPACES.
           05  WS-D1-USER-ID               PIC X(04)     VALUE SPACES.


       01  WS-TOTAL-LINE1.
           05  FILLER                      PIC X(01)     VALUE '-'.
           05  FILLER                      PIC X(12)     VALUE SPACES.
           05  FILLER                      PIC X(13)     VALUE
               'NO. CHECKS:  '.
           05  WS-T1-CHECK-CNT             PIC ZZZ,ZZ9.
           05  FILLER                      PIC X(12)     VALUE SPACES.
           05  FILLER                      PIC X(16)     VALUE
               'CONTROL TOTAL:  '.
           05  WS-T1-TOTAL-CHECK-AMT       PIC $$Z,ZZZ,ZZ9.99.
           05  FILLER                      PIC X(05)     VALUE SPACES.


       01  WS-GRAND-TOTAL-LINE1.
           05  FILLER                      PIC X(01)     VALUE '0'.
           05  FILLER                      PIC X(25)     VALUE
               'GRAND TOTAL NO. CHECKS:  '.
           05  WS-GT1-CHECK-CNT            PIC ZZZ,ZZ9.
           05  FILLER                      PIC X(14)     VALUE SPACES.
           05  FILLER                      PIC X(14)     VALUE
               'GRAND TOTAL:  '.
           05  WS-GT1-TOTAL-CHECK-AMT      PIC $$Z,ZZZ,ZZ9.99.
           05  FILLER                      PIC X(05)     VALUE SPACES.


      *              *************
      *              ELCDTECX: LAYOUT FOR DISK-DATE FILE
                     COPY ELCDTECX.


                     COPY ELCDTEVR.

      *              *************
      *              ELCDATE: LAYOUT OF DATA PASSED TO DATE CONV RTN
                     COPY ELCDATE.


       LINKAGE SECTION.

       01  PARM.
           05  PARM-LENGTH        COMP     PIC S9(04)    VALUE ZEROS.
           05  PARM-CYCLE-DATE             PIC X(08)     VALUE SPACES.
111709     05  FILLER                      PIC X(01)     VALUE SPACES.           
111709     05  PARM-COMPANY-CD             PIC X(03)     VALUE SPACES.

      ******************************************************************
      ********************************
       PROCEDURE DIVISION USING PARM.

      ****************READ DISK-DATE FILE
       0000-DATE-CARD-READ. COPY ELCDTERX.

       1000-MAIN-LOGIC.

           PERFORM 1500-EDIT-CYCLE-DATE     THRU 1500-EXIT

           PERFORM OPEN-FILES

           MOVE COMPANY-NAME                TO WS-H2-COMPANY-NAME
           MOVE WS-CURRENT-DATE             TO WS-H2-DATE
           MOVE WS-EDITED-CYCLE-DT          TO WS-H3-CYCLE-DT
           PERFORM 3500-PRINT-HEADINGS      THRU 3500-EXIT

           MOVE DTE-CLASIC-COMPANY-CD       TO CQ-COMPANY-CD
           START ELCHKQ KEY NOT < CQ-COMPANY-CD
           END-START

010704     EVALUATE TRUE
010704     WHEN ELCHKQ-FILE-STATUS = '00'
               PERFORM 2000-BUILD-REPORT-DETAIL THRU 2000-EXIT
                   UNTIL END-OF-ELCHKQ

010704     WHEN ELCHKQ-FILE-STATUS = '23'
010704         SET END-OF-ELCHKQ TO TRUE

010704     WHEN OTHER
010704        DISPLAY ' ELCHKQ START ' ELCHKQ-FILE-STATUS
              PERFORM ABEND-PGM THRU APS-EXIT
010704     END-EVALUATE


           IF WS-GRAND-TOTAL-CHECK-CNT > +0
               IF WS-CONTROL-TOTAL-CHECK-CNT > +0
                   PERFORM 3700-PRINT-CONTROL-TOTAL THRU 3700-EXIT
               END-IF
               PERFORM 3800-PRINT-GRAND-TOTAL       THRU 3800-EXIT

           ELSE
               MOVE '0NO CHECKS PRINTED FOR THE CURRENT CYCLE DATE'
                                                    TO PRT
               PERFORM 3900-WRITE                   THRU 3900-EXIT
           END-IF


           PERFORM CLOSE-FILES THRU CLOSE-FILES-EXIT


           GOBACK.


       1500-EDIT-CYCLE-DATE.

           IF PARM-LENGTH = 0
               DISPLAY 'CYCLE DATE INPUT PARM MISSING '
               PERFORM ABEND-PGM         THRU APS-EXIT
           END-IF

           MOVE PARM-CYCLE-DATE          TO DC-GREG-DATE-CYMD
           MOVE 'L'                      TO DC-OPTION-CODE
           PERFORM 8500-DATE-CONVERSION  THRU 8500-EXIT

           IF NO-CONVERSION-ERROR
               MOVE DC-BIN-DATE-1        TO WS-CYCLE-DT-BINARY
               MOVE PARM-CYCLE-DATE      TO WS-CYCLE-DT
               MOVE WS-CYCLE-DT-MM       TO WS-EDITED-CYCLE-DT-MM
               MOVE WS-CYCLE-DT-DD       TO WS-EDITED-CYCLE-DT-DD
               MOVE WS-CYCLE-DT-YY       TO WS-EDITED-CYCLE-DT-YY
           ELSE
               DISPLAY 'INVALID CYCLE DATE ' PARM-CYCLE-DATE
               PERFORM ABEND-PGM         THRU APS-EXIT
           END-IF
           .
       1500-EXIT.
           EXIT.


       2000-BUILD-REPORT-DETAIL.

           READ ELCHKQ NEXT RECORD
               AT END SET END-OF-ELCHKQ         TO TRUE
                   GO TO 2000-EXIT
           END-READ


      ********* COMPANY CODE OF HEX '04' = CID
           IF CQ-COMPANY-CD = DTE-CLASIC-COMPANY-CD
               CONTINUE
           ELSE
               SET END-OF-ELCHKQ                TO TRUE
               GO TO 2000-EXIT
           END-IF


           IF CQ-CHECK-WRITTEN-DT = WS-CYCLE-DT-BINARY
               CONTINUE
           ELSE
               GO TO 2000-EXIT
           END-IF
           
111709
111709     IF PARM-COMPANY-CD = 'DCC'
111709        IF CQ-CARRIER = '1' OR '2'
111709          CONTINUE
111709        ELSE
111709          GO TO 2000-EXIT
111709        END-IF
111709     END-IF
111709     IF PARM-COMPANY-CD = 'CSI' 
111709        IF CQ-CARRIER = '3' OR '4'
111709          CONTINUE
111709        ELSE
111709          GO TO 2000-EXIT
111709        END-IF
111709     END-IF
111709     IF PARM-COMPANY-CD = 'CCC' 
111709        IF CQ-CARRIER = '5' OR '6'
111709          CONTINUE
111709     ELSE
111709          GO TO 2000-EXIT
111709        END-IF
111709     END-IF

           INITIALIZE WS-DETAIL1

           MOVE CQ-CONTROL-NUMBER        TO WS-D1-CONTROL-NUMBER
           MOVE CQ-CHECK-NUMBER          TO WS-D1-CHECK-NUMBER


           MOVE CQ-CHECK-WRITTEN-DT      TO DC-BIN-DATE-1
           MOVE SPACE                    TO DC-OPTION-CODE
           PERFORM 8500-DATE-CONVERSION  THRU 8500-EXIT

           IF NO-CONVERSION-ERROR
               MOVE DC-GREG-DATE-1-EDIT  TO WS-D1-CHECK-DT
           ELSE
               DISPLAY 'CHECK DATE CONVERSION ERROR ' DC-ERROR-CODE
               MOVE SPACES               TO WS-D1-CHECK-DT
           END-IF


           EVALUATE TRUE
           WHEN  CQ-PARTIAL-PAYMENT
                 MOVE 'PARTIAL'          TO WS-D1-PAYMENT-TYPE

           WHEN  CQ-FINAL-PAYMENT
                 MOVE 'FINAL'            TO WS-D1-PAYMENT-TYPE

           WHEN  CQ-LUMP-SUM-PAYMENT
                 MOVE 'LUMPSUM'          TO WS-D1-PAYMENT-TYPE

           WHEN  CQ-ADDITIONAL-PAYMENT
                 MOVE 'ADDL'             TO WS-D1-PAYMENT-TYPE

           WHEN  OTHER
                 MOVE SPACES             TO WS-D1-PAYMENT-TYPE
           END-EVALUATE


           MOVE CQ-CLAIM-NO              TO WS-D1-CLAIM-NUMBER
           MOVE CQ-CARRIER               TO WS-D1-CARRIER
           MOVE CQ-CERT-NO               TO WS-D1-CERT-NUMBER
           MOVE CQ-CHECK-AMOUNT          TO WS-D1-CHECK-AMT
           MOVE CQ-CHECK-BY-USER         TO WS-D1-USER-ID


           IF FIRST-TIME-THROUGH
               MOVE CQ-CONTROL-NUMBER    TO WS-HOLD-CONTROL-NUMBER
               MOVE '0'                  TO P-CTL
               SET NOT-FIRST-TIME        TO TRUE
           END-IF

050307     IF CQ-LIFE-CLAIM
050307        MOVE CQ-COMPANY-CD       TO AT-COMPANY-CD
050307        MOVE CQ-CARRIER          TO AT-CARRIER
050307        MOVE CQ-CLAIM-NO         TO AT-CLAIM-NO
050307        MOVE CQ-CERT-NO          TO AT-CERT-NO
050307        MOVE CQ-PMT-TRLR-SEQUENCE TO AT-SEQUENCE-NO
050307        SUBTRACT +1              FROM AT-SEQUENCE-NO
050307        READ ELTRLR
050307        IF (ELTRLR-FILE-STATUS = '00')
050307           AND (AT-TRAILER-TYPE = '2')
050307           AND (AT-PAYMENT-TYPE = 'I')
050307           AND (AT-VOID-DT = LOW-VALUES)
050307           MOVE '*'              TO WS-D1-INT-PMT-PRESENT
050307        END-IF
050307     END-IF

           PERFORM 3000-PRINT-DETAIL     THRU 3000-EXIT

           .
       2000-EXIT.
           EXIT.

       3000-PRINT-DETAIL.

           IF CQ-CONTROL-NUMBER = WS-HOLD-CONTROL-NUMBER
               CONTINUE
           ELSE
               PERFORM 3700-PRINT-CONTROL-TOTAL THRU 3700-EXIT
               PERFORM 3500-PRINT-HEADINGS      THRU 3500-EXIT
               MOVE +0                   TO WS-CONTROL-TOTAL-CHECK-CNT
               MOVE +0                   TO WS-CONTROL-TOTAL-CHECK-AMT
               MOVE CQ-CONTROL-NUMBER    TO WS-HOLD-CONTROL-NUMBER
           END-IF

           IF WS-LINE-COUNT = WS-LINE-COUNT-MAX
               PERFORM 3500-PRINT-HEADINGS THRU 3500-EXIT
           END-IF

           MOVE WS-DETAIL1               TO PRT
           PERFORM 3900-WRITE            THRU 3900-EXIT

           ADD +1                        TO WS-CONTROL-TOTAL-CHECK-CNT
           ADD +1                        TO WS-GRAND-TOTAL-CHECK-CNT
           ADD CQ-CHECK-AMOUNT           TO WS-CONTROL-TOTAL-CHECK-AMT
           ADD CQ-CHECK-AMOUNT           TO WS-GRAND-TOTAL-CHECK-AMT

           .
       3000-EXIT.
           EXIT.


       3500-PRINT-HEADINGS.

           MOVE WS-HEADING1              TO PRT
           PERFORM 3900-WRITE            THRU 3900-EXIT

           MOVE WS-HEADING2              TO PRT
           PERFORM 3900-WRITE            THRU 3900-EXIT

           ADD +1                        TO WS-PAGE
           MOVE WS-PAGE                  TO WS-H3-PAGE
           MOVE WS-HEADING3              TO PRT
           PERFORM 3900-WRITE            THRU 3900-EXIT

           MOVE WS-HEADING4              TO PRT
           PERFORM 3900-WRITE            THRU 3900-EXIT

           MOVE WS-HEADING5              TO PRT
           PERFORM 3900-WRITE            THRU 3900-EXIT

           MOVE SPACES                   TO PRT
           PERFORM 3900-WRITE            THRU 3900-EXIT

           .
       3500-EXIT.
           EXIT.


       3700-PRINT-CONTROL-TOTAL.

           MOVE '0'                         TO P-CTL
           MOVE SPACES                      TO P-DATA
           PERFORM 3900-WRITE               THRU 3900-EXIT

           MOVE WS-CONTROL-TOTAL-CHECK-CNT  TO WS-T1-CHECK-CNT
           MOVE WS-CONTROL-TOTAL-CHECK-AMT  TO WS-T1-TOTAL-CHECK-AMT
           MOVE WS-TOTAL-LINE1              TO PRT
           PERFORM 3900-WRITE               THRU 3900-EXIT

           .
       3700-EXIT.
           EXIT.


       3800-PRINT-GRAND-TOTAL.

           MOVE WS-GRAND-TOTAL-CHECK-CNT    TO WS-GT1-CHECK-CNT
           MOVE WS-GRAND-TOTAL-CHECK-AMT    TO WS-GT1-TOTAL-CHECK-AMT
           MOVE WS-GRAND-TOTAL-LINE1        TO PRT
           PERFORM 3900-WRITE               THRU 3900-EXIT

           MOVE '0    * - INDICATES INTEREST PAYMENT PRESENT '
                                       TO WS-GRAND-TOTAL-LINE1
           MOVE WS-GRAND-TOTAL-LINE1        TO PRT
           PERFORM 3900-WRITE          THRU 3900-EXIT

           .
       3800-EXIT.
           EXIT.


       3900-WRITE.

           EVALUATE TRUE
           WHEN P-CTL = '1'
               MOVE +1                     TO WS-LINE-COUNT

           WHEN P-CTL = SPACE
               ADD +1                      TO WS-LINE-COUNT

           WHEN P-CTL = '0'
               ADD +2                      TO WS-LINE-COUNT

           WHEN OTHER
               ADD +3                      TO WS-LINE-COUNT
           END-EVALUATE


           EVALUATE TRUE
      **************************** P = HARDCOPY ONLY
           WHEN DTE-PRT-OPT = 'P'
               WRITE PRT

      **************************** B = FICH (NOW USED FOR SEQUENTIAL
      *                                 FILE ARCHIVE) AND HARDCOPY
           WHEN DTE-PRT-OPT = 'B'
               PERFORM 4000-SEQ-FILE-ARCHIVE THRU 4000-EXIT
               WRITE PRT

      **************************** S = ONLINE REPORT FILE ONLY (ELREPT)
           WHEN DTE-PRT-OPT = 'S'
               PERFORM 4300-ONLINE-REPORT    THRU 4300-EXIT

      **************************** T = ONLINE REPORT FILE AND HARDCOPY
           WHEN DTE-PRT-OPT = 'T'
               PERFORM 4300-ONLINE-REPORT    THRU 4300-EXIT
               WRITE PRT

           WHEN OTHER
               MOVE 'INVALID DTE-PRT-OPT'    TO WS-ABEND-MESSAGE
               PERFORM ABEND-PGM             THRU APS-EXIT
           END-EVALUATE

           .
       3900-EXIT.
           EXIT.


       4000-SEQ-FILE-ARCHIVE.
      ******************************************************************
      * WHILE FICHE IS NO LONGER BEING PRODUCED, THE FICHE LOGIC IS
      * BEING USED TO CREATE A SEQUENTIAL FILE REPORT ARCHIVE WHEN
      * THE DTE-PRT-OPT IS SET TO 'B'
      ******************************************************************
           IF FICH-NO
               GO TO 4000-EXIT
           ELSE
               IF FICH-OPEN = SPACE
                   MOVE 'X'                TO FICH-OPEN
                   OPEN OUTPUT FICH
               END-IF
           END-IF

           MOVE SPACE                      TO P-CTL
           WRITE FICH-REC                  FROM PRT

           .
       4000-EXIT.
           EXIT.


       4300-ONLINE-REPORT.

           IF (REPT-OPEN = SPACE) AND (DTE-ABEND-CD-1 = SPACE)
               OPEN I-O ELREPT

               IF DTE-F-1 NOT = ZERO AND
                   DTE-VSAM-FLAGS NOT = '97'
                   MOVE DTE-VSAM-FLAGS TO WS-ABEND-FILE-STATUS
                   MOVE 'ERROR OPENING ELREPT'
                                       TO WS-ABEND-MESSAGE
                   PERFORM ABEND-PGM   THRU APS-EXIT
               ELSE
                   MOVE '1'                   TO REPT-OPEN
                   MOVE DTE-CLASIC-COMPANY-CD TO RF-COMPANY-CD
                   MOVE '1'                   TO RF-RECORD-TYPE
                   MOVE OLC-REPORT-NAME       TO RF-REPORT-ID
                   MOVE ZERO                  TO RF-LINE-NUMBER
                   START ELREPT KEY NOT LESS RF-CONTROL-PRIMARY
                   SET BEGIN-REPORT-DELETE    TO TRUE
                   PERFORM 4500-REPORT-DELETE THRU 4500-EXIT
                       UNTIL REPORT-DELETE-DONE

                   MOVE DTE-CLASIC-COMPANY-CD TO RF-COMPANY-CD
                   MOVE '2'                   TO RF-RECORD-TYPE
                   MOVE OLC-REPORT-NAME       TO RF-REPORT-ID
                   MOVE ZERO                  TO RF-LINE-NUMBER
                   START ELREPT KEY NOT LESS RF-CONTROL-PRIMARY
                   SET BEGIN-REPORT-DELETE    TO TRUE
                   PERFORM 4500-REPORT-DELETE THRU 4500-EXIT
                       UNTIL REPORT-DELETE-DONE

                   MOVE DTE-CLASIC-COMPANY-CD TO RF-COMPANY-CD
                   MOVE '1'                   TO RF-RECORD-TYPE
                   MOVE OLC-REPORT-NAME       TO RF-REPORT-ID
                   MOVE SPACES                TO RF-REPORT-LINE-133
               END-IF
           END-IF


           IF DTE-ABEND-CD-1 = '81' AND
              DTE-PRT-OPT = 'S'
               MOVE +0302                     TO WS-RETURN-CODE
               PERFORM ABEND-PGM              THRU APS-EXIT
           END-IF


           MOVE P-CTL                         TO RF-CTL-CHAR-133
           MOVE P-DATA                        TO RF-DATA-133

           IF DTE-ABEND-CD-1 = SPACES
               ADD +1                         TO DTE-TOT-LINES
               MOVE DTE-TOT-LINES             TO RF-LINE-NUMBER
               WRITE REPORT-SAVE-FILE
                   INVALID KEY
                       MOVE '88'              TO DTE-ABEND-CD-1
                       CLOSE ELREPT
                       MOVE SPACE             TO REPT-OPEN
           END-IF

           .
       4300-EXIT.
           EXIT.

       4500-REPORT-DELETE.

           IF DTE-F-1 NOT = ZERO
               MOVE ZERO                          TO DTE-VSAM-FLAGS
               SET REPORT-DELETE-DONE             TO TRUE
               GO TO 4500-EXIT
           END-IF

           READ ELREPT NEXT RECORD
               AT END
                   SET REPORT-DELETE-DONE         TO TRUE
                   GO TO 4500-EXIT
           END-READ

           IF DTE-CLASIC-COMPANY-CD = RF-COMPANY-CD  AND
              OLC-REPORT-NAME = RF-REPORT-ID
               DELETE ELREPT RECORD
           END-IF

           .
       4500-EXIT.
           EXIT.


       8500-DATE-CONVERSION.

           CALL 'ELDATCX' USING DATE-CONVERSION-DATA

           .
       8500-EXIT.
           EXIT.

       OPEN-FILES.

           OPEN INPUT  ELCHKQ ELTRLR
                OUTPUT PRNTR

           IF ELCHKQ-FILE-STATUS  = '00' OR '97'
              CONTINUE
           ELSE
              MOVE 'OPEN INPUT ERROR OCCURRED ON ELCHKQ' TO
                                            WS-ABEND-MESSAGE
              MOVE ELCHKQ-FILE-STATUS    TO WS-ABEND-FILE-STATUS
              PERFORM ABEND-PGM          THRU APS-EXIT
           END-IF

           IF ELTRLR-FILE-STATUS  = '00' OR '97'
              CONTINUE
           ELSE
              MOVE 'OPEN INPUT ERROR OCCURRED ON ELTRLR' TO
                                            WS-ABEND-MESSAGE
              MOVE ELTRLR-FILE-STATUS    TO WS-ABEND-FILE-STATUS
              PERFORM ABEND-PGM          THRU APS-EXIT
           END-IF

           .
       OPEN-FILES-EXIT.
           EXIT.

       CLOSE-FILES.

       CFS-010. COPY ELCPRTCX.

           CLOSE ELCHKQ ELTRLR
                 PRNTR

           .
       CLOSE-FILES-EXIT.
           EXIT.

       ABEND-PGM. COPY ELCABEND.

