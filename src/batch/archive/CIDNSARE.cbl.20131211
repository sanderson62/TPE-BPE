       IDENTIFICATION DIVISION.
       PROGRAM-ID.                CIDNSARE.
      *AUTHOR.     AJRA.
      *REMARKS.
      * THIS PROGRAM GENERATES RESEND LETTERS FOR ACCT SERVICES
103012******************************************************************
103012*                   C H A N G E   L O G
103012*
103012* CHANGES ARE MARKED BY THE CHANGE EFFECTIVE DATE.
103012*-----------------------------------------------------------------
103012*  CHANGE   CHANGE REQUEST PGMR  DESCRIPTION OF CHANGE
103012* EFFECTIVE    NUMBER
103012*-----------------------------------------------------------------
103012* 103012  CR2012101700002  AJRA  STOP RESEND ON OPEN CLAIM
103012*                                CHANGE STOP DATE ON AUTO STOPS
100713* 100713  IR2013100700002  AJRA  DON'T ABEND IF NO Z RECORD
103012******************************************************************
       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

           SELECT ARCH-IN          ASSIGN TO SYS010.

           SELECT ERARCH           ASSIGN TO ERARCH
                                   ORGANIZATION IS INDEXED
                                   ACCESS IS DYNAMIC
                                   RECORD KEY IS LA-CONTROL-PRIMARY
                                   FILE STATUS IS ERARCH-FILE-STATUS.

           SELECT ERARCT           ASSIGN TO ERARCT
                                   ORGANIZATION IS INDEXED
                                   ACCESS IS DYNAMIC
                                   RECORD KEY IS LT-CONTROL-PRIMARY
                                   FILE STATUS IS ERARCT-FILE-STATUS.

           SELECT NSASEXTR         ASSIGN TO NSASEXTR
                                   ORGANIZATION IS INDEXED
                                   ACCESS IS DYNAMIC
                                   RECORD KEY IS NSAS-CONTROL-PRIMARY
                                   FILE STATUS IS NSAS-FILE-STATUS.

           SELECT ELCNTL           ASSIGN TO ELCNTL
                                   ACCESS IS DYNAMIC
                                   ORGANIZATION IS INDEXED
                                   FILE STATUS IS ELCNTL-FILE-STATUS
                                   RECORD KEY IS CF-CONTROL-PRIMARY.

           SELECT ELLETR           ASSIGN TO ELLETR
                                   ACCESS IS DYNAMIC
                                   ORGANIZATION IS INDEXED
                                   FILE STATUS IS ELLETR-FILE-STATUS
                                   RECORD KEY IS TX-CONTROL-PRIMARY.

           SELECT ELCERT           ASSIGN TO ELCERT
                                   ACCESS IS DYNAMIC
                                   ORGANIZATION IS INDEXED
                                   FILE STATUS IS ELCERT-FILE-STATUS
                                   RECORD KEY IS CM-CONTROL-PRIMARY.

           SELECT ERPNDB           ASSIGN TO ERPNDB2
                                   ACCESS IS DYNAMIC
                                   ORGANIZATION IS INDEXED
                                   FILE STATUS IS ERPNDB-FILE-STATUS
                                   RECORD KEY IS PB-CONTROL-BY-ACCOUNT.

           SELECT DISK-DATE        ASSIGN TO SYS019.
           
103012     SELECT RESEND-REPORT    ASSIGN TO SYS011
                                   ORGANIZATION IS LINE SEQUENTIAL. 
103012
103012     SELECT ELMSTR           ASSIGN TO SYS023-3380-ELMSTR5        
103012                             ORGANIZATION IS INDEXED              
103012                             ACCESS IS DYNAMIC                    
103012                             RECORD KEY IS CL-CONTROL-BY-CERT-NO
103012                             FILE STATUS IS ELMSTR-FILE-STATUS.   
           
                   

       DATA DIVISION.

       FILE SECTION.

       FD  ARCH-IN
           RECORDING MODE F
           BLOCK CONTAINS 0.
           
       01  ARCH-IN-RECORD              PIC X(250).

       FD  ERARCH.
                                       COPY ERCARCH.

       FD  ERARCT.
                                       COPY ERCARCT.

       FD  NSASEXTR.
                                       COPY NSCASEXTR.                         

       FD  ELCNTL.
                                       COPY ELCCNTL.

       FD  ELLETR.
                                       COPY ELCTEXT.

       FD  ELCERT.
                                       COPY ELCCERT.

       FD  ERPNDB.
                                       COPY ERCPNDB.


       FD  DISK-DATE
           COPY ELCDTEFD.

103012 FD  RESEND-REPORT
           RECORDING MODE F
           LABEL RECORDS STANDARD
           BLOCK CONTAINS 0 RECORDS.
103012 01  RESEND-RPT-RECORD                 PIC X(132).
103012
103012 FD  ELMSTR.
103012                                 COPY ELCMSTR.


         WORKING-STORAGE SECTION.
       77  FILLER  PIC X(32) VALUE '********************************'.
       77  FILLER  PIC X(32) VALUE '   CIDNSARE  WORKING-STORAGE     '.
       77  FILLER  PIC X(32) VALUE '*********** VMOD=2.001. ********'.

       77  WS-ABEND-FILE-STATUS        PIC XX            VALUE ZERO.    
       77  WS-ABEND-MESSAGE            PIC X(80)         VALUE SPACES.  
       77  WS-ABEND-PROGRAM            PIC X(8)          VALUE SPACES.  
       77  WS-RETURN-CODE              PIC S9(4)         VALUE +0.      
       77  WS-ZERO                     PIC S9     COMP-3 VALUE +0.      
       77  WS-EOF-SW                   PIC X VALUE SPACES.
           88  END-OF-ARCH             VALUE 'Y'.
       77  WS-DONE-SW                  PIC X VALUE SPACES.
           88  DONE-WITH-ARCH          VALUE 'Y'.
       77  WS-NEW-ARCH-SW              PIC X VALUE 'N'.
           88 HAVE-NEW-ARCH-NO               VALUE 'Y'.
           88 NEED-NEW-ARCH-NO               VALUE 'N'.
       77  WS-CERT-FOUND-SW            PIC X VALUE ' '.
           88  WS-CERT-FOUND                 VALUE 'Y'.
       77  WS-CERT-STATUS-SW           PIC X VALUE ' '.
           88  CERT-CANCELLED                VALUE 'C'.
103012 77  WS-CERT-CLAIM-SW            PIC X VALUE ' '.
103012     88  CERT-HAS-OPEN-CLM             VALUE 'Y'.
       77  ARCH-RECS-IN                PIC 9(9) VALUE ZEROS.
       77  NSAS-RECS-OUT               PIC 9(9) VALUE ZEROS.
       77  NSAS-RECS-DEL               PIC 9(9) VALUE ZEROS.
       77  ERARCH-FILE-STATUS          PIC XX  VALUE LOW-VALUES.
       77  ERARCT-FILE-STATUS          PIC XX  VALUE LOW-VALUES.
       77  NSAS-FILE-STATUS            PIC XX  VALUE LOW-VALUES.
       77  ELCNTL-FILE-STATUS          PIC XX  VALUE LOW-VALUES.
       77  ELLETR-FILE-STATUS          PIC XX  VALUE LOW-VALUES.
       77  ELCERT-FILE-STATUS          PIC XX  VALUE LOW-VALUES.
       77  ERPNDB-FILE-STATUS          PIC XX  VALUE LOW-VALUES.
103012 77  ELMSTR-FILE-STATUS          PIC XX  VALUE LOW-VALUES.
       77  PGM-SUB                     COMP-3 PIC S9(04) VALUE +585.
       77  WS-CURRENT-TIME             PIC S9(7)   VALUE ZERO.
       77  WS-CURRENT-BIN-DT           PIC X(02)  VALUE LOW-VALUES.
       77  WS-ARCHIVE-NO               PIC S9(8)   COMP VALUE +0.
       77  WS-RESEND-DATE              PIC XX  VALUE LOW-VALUES.
       77  WS-FOLLOW-UP-DATE           PIC XX  VALUE LOW-VALUES.
       77  WS-COV-CNT                  PIC S999 COMP-3 VALUE +0.
       77  WS-STOP-DATE                PIC XX  VALUE LOW-VALUES.
       77  WS-ZEROS                    PIC S9(03) VALUE +0 COMP-3.
103012 77  WS-LINE-CNT                 PIC S9(03) VALUE +0 COMP-3.
103012 77  WS-PAGE-CNT                 PIC S9(03) VALUE +0 COMP-3.
100713 77  WS-BYPASS-Z-REC             PIC X  VALUE ' '.
100713     88  BYPASS-REC                 VALUE 'Y'.

      
       01  MISC.
           05  WRK-RUN-DTE             PIC 9(11) COMP-3.
           05  WRK-ORIG-COMPANY-CD     PIC X.
           05  WRK-ORIG-ARCHIVE-NO     PIC S9(8) COMP.
           05  WRK-ORIG-RESEND-LETR    PIC X(5).
           05  WRK-ORIG-CERT-NO        PIC X(11).
           05  WRK-ORIG-CARRIER        PIC X(01).
           05  WRK-ORIG-GROUPING       PIC X(06).
           05  WRK-ORIG-STATE          PIC X(02).
           05  WRK-ORIG-ACCOUNT        PIC X(10).
           05  WRK-ORIG-EFFECT-DATE    PIC X(02).
           05  WRK-ORIG-ENTRY          PIC X(06).
           05  WRK-ORIG-PROCESSOR-CD   PIC X(04).
           
           05  WS-COMMENT-INDEX        PIC S9(4) COMP.
           05  WS-COMMENT-LINE         PIC X(69) VALUE SPACES.
           05  WS-ADD-ARCT             PIC  X      VALUE 'N'.    
           
       01  WS-REASONS.
103012     05  WS-CANCELLED            PIC X(19)
103012         VALUE 'CERT CANCELLED     '.
103012     05  WS-DEATH                PIC X(19)
103012         VALUE 'DEATH              '.
103012     05  WS-EXPIRED              PIC X(19)
103012         VALUE 'CERT EXPIRED       '.
103012     05  WS-SETTLEMENT           PIC X(19)
103012         VALUE 'A&H SETTLEMENT     '.
103012     05  WS-NOT-FOUND            PIC X(19)
103012         VALUE 'CERT NOT FOUND     '.
103012     05  WS-OPEN-CLAIM           PIC X(19)
103012         VALUE 'CERT HAS OPEN CLAIM'.
100713     05  WS-Z-RECORD             PIC X(19)
100713         VALUE 'Z REC WAS NOT FOUND'.
103012     05  WS-MISSING-MSG.
103012         10  FILLER              PIC X(28)
103012             VALUE 'PLEASE CREATE RESEND LETTER '.
103012         10  MISSING-RESEND-LETTERID PIC X(4).
103012     05  WS-RESEND-MSG.
103012         10  FILLER              PIC X(14)
103012             VALUE 'RESEND LETTER '.
103012         10  RESEND-LETTERID PIC X(4).
103012         10  FILLER              PIC X(14)
103012             VALUE ' NOT CREATED, '.
103012         10  RESEND-MSG-REASON   PIC X(19).
           
      ****  Z RECORD LAYOUT MOVED TO COPYBOOK ELCZREC
                                           COPY ELCZREC.

       01  EXT-REC.
           05 EXT-CONTROL-PRIMARY.
              10  EXT-COMPANY-CD       PIC X.
              10  EXT-ARCHIVE-NO       PIC 9(8) BINARY.
              10  EXT-SEQ-NO           PIC 9(4) BINARY.
           05  EXT-LETTER-VARIABLES.
               10  EXT-LETTER-ID       PIC X(5).
               10  FILLER              PIC X(4345).
           05  FILLER                  PIC X(143).

       01  BLANK-LINE.
           05  FILLER                  PIC X(132) VALUE SPACES.

       01  REPORT-HEAD1.
103012     05  FILLER                  PIC X(08) VALUE 'CIDNSARE'.
103012     05  FILLER                  PIC X(22) VALUE SPACES.
           05  FILLER                  PIC X(23)
               VALUE 'CENTRAL STATES OF OMAHA'.
           05  FILLER                  PIC X(23) VALUE SPACES.
           05  RPT-DATE                PIC X(08) VALUE SPACES.
           05  FILLER                  PIC X(48)  VALUE SPACES.

       01  REPORT-HEAD2.
           05  FILLER                  PIC X(27) VALUE SPACES.
           05  FILLER                  PIC X(29)
103012         VALUE 'RESEND LETTER ERROR MESSAGES '.
103012     05  FILLER                  PIC X(20) VALUE SPACES.
103012     05  FILLER                  PIC X(05) VALUE 'PAGE '.
103012     05  RPT-PAGE                PIC ZZ9   VALUE ZEROS.
103012     05  FILLER                  PIC X(48) VALUE SPACES.

       01  REPORT-HEADER.
           05  FILLER                  PIC X(2) VALUE SPACES.
           05  FILLER                  PIC X(11)
               VALUE 'CERT NUMBER'.
           05  FILLER                  PIC X(2) VALUE SPACES.
           05  FILLER                  PIC X(8)
               VALUE 'ARCH NUM'.
           05  FILLER                  PIC X(2) VALUE SPACES.
           05  FILLER                  PIC X(9)
               VALUE 'LETTER ID'.
           05  FILLER                  PIC X(2) VALUE SPACES.
           05  FILLER                  PIC X(10)
               VALUE 'CREATED BY'.
           05  FILLER                  PIC X(2) VALUE SPACES.
           05  FILLER                  PIC X(11)
               VALUE 'RESEND DATE'.
           05  FILLER                  PIC X(5) VALUE SPACES.
           05  FILLER                  PIC X(7)
               VALUE 'MESSAGE'.
           05  FILLER                  PIC X(62) VALUE SPACES.

103012 01  REPORT-REC.
           05  FILLER                  PIC X(2) VALUE SPACES.
103012     05  REPORT-CERTNO          PIC X(11).
           05  FILLER                  PIC X(2) VALUE SPACES.
103012     05  REPORT-ARCHIVENO       PIC 9(8).
           05  FILLER                  PIC X(4) VALUE SPACES.
103012     05  REPORT-LETTERID        PIC X(4).
           05  FILLER                  PIC X(8) VALUE SPACES.
103012     05  REPORT-PROCID          PIC X(4).
           05  FILLER                  PIC X(6) VALUE SPACES.
103012     05  REPORT-RESEND-DT       PIC X(10).
           05  FILLER                  PIC X(2) VALUE SPACES.
103012     05  REPORT-MSG             PIC X(52).
           05  FILLER                  PIC X(20) VALUE SPACES.

                                       COPY ELCDTECX.

                                       COPY ELCDTEVR.

                                       COPY ELCDATE.

       LINKAGE SECTION.
       
       01  PARM.
           05  PARM-LENGTH        COMP     PIC S9(04)    VALUE ZEROS.
           05  PARM-CYCLE-DATE             PIC X(08)     VALUE SPACES.

       PROCEDURE DIVISION USING PARM.

           IF PARM-CYCLE-DATE = SPACES
               DISPLAY 'MISSING CYCLE DATE PARM'
               PERFORM ABEND-PGM
           END-IF.
            
       0000-DATE-CARD-READ. COPY ELCDTERX.

           PERFORM 0040-OPEN-FILES     THRU 0040-EXIT.
           PERFORM 0050-INIT           THRU 0050-EXIT.

           MOVE LOW-VALUES             TO LA-CONTROL-PRIMARY.
           MOVE DTE-CLASIC-COMPANY-CD  TO LA-COMPANY-CD.
           MOVE ZEROS                  TO LA-ARCHIVE-NO.

           PERFORM 0020-PROCESS        THRU 0020-EXIT UNTIL
              (END-OF-ARCH).
           PERFORM 0060-CLOSE-FILES    THRU 0060-EXIT.

           DISPLAY ' ARCH RECS READ    ' ARCH-RECS-IN.
           DISPLAY ' LETTER RECS WRITE ' NSAS-RECS-OUT.

           GOBACK.

       0020-PROCESS.
           
           PERFORM 0024-READ-ARCH-IN THRU 0024-EXIT.
           IF END-OF-ARCH
               GO TO 0020-EXIT
           END-IF.
           
           IF LA-COMPANY-CD NOT EQUAL DTE-CLASIC-COMPANY-CD
               GO TO 0020-EXIT
           END-IF
           
           IF LA-TEMP
               GO TO 0020-EXIT
           END-IF.
                      
           IF (LA-RESEND-DATE EQUAL LOW-VALUES OR SPACES) OR
              (LA-RESEND-LETR EQUAL LOW-VALUES OR SPACES) OR
              (LA-INITIAL-PRINT-DATE EQUAL LOW-VALUES OR SPACES)
                 GO TO 0020-EXIT
           END-IF.

           IF  LA-RESEND-DATE GREATER THAN WS-CURRENT-BIN-DT
               GO TO 0020-EXIT
           END-IF.
           
           IF (LA-SENT-DATE NOT EQUAL LOW-VALUES AND SPACES) OR
              (LA-REPLY-DATE NOT EQUAL LOW-VALUES AND SPACES) OR
              (LA-VOIDED-DATE NOT EQUAL LOW-VALUES AND SPACES) OR
              (LA-PURGED-DATE NOT EQUAL LOW-VALUES AND SPACES) 
                 GO TO 0020-EXIT
           END-IF.
           
           PERFORM 0025-READ-ERARCH THRU 0025-EXIT.
           
           MOVE SPACES TO WS-CERT-FOUND-SW
                          WS-CERT-STATUS-SW.
100713     MOVE SPACES TO WS-BYPASS-Z-REC.
                          
           PERFORM 0200-CHECK-CERT-STATUS THRU 0200-EXIT.
           IF (CERT-CANCELLED)
             OR (NOT WS-CERT-FOUND)
103012       OR (CERT-HAS-OPEN-CLM)
100713       OR (BYPASS-REC)
103012        DISPLAY 'RESEND LETTER NOT CREATED '
                 LA-CERT-NO-A2 ' ' LA-ARCHIVE-NO ' ' LA-FORM-A3 
103012           ' ' WS-COMMENT-LINE
103012        MOVE LA-RESEND-LETR TO RESEND-LETTERID
103012        MOVE WS-COMMENT-LINE (1:19) TO RESEND-MSG-REASON
103012        MOVE WS-RESEND-MSG TO REPORT-MSG
103012        PERFORM 0038-WRITE-RESEND-REPORT THRU 0038-EXIT
              MOVE 'C'                 TO LA-STATUS
              MOVE WS-STOP-DATE        TO LA-VOIDED-DATE
              MOVE 'SYST'              TO LA-LAST-UPDATED-BY
              MOVE WS-CURRENT-BIN-DT   TO LA-LAST-MAINT-DATE
              MOVE WS-CURRENT-TIME     TO LA-LAST-MAINT-TIME
              REWRITE LETTER-ARCHIVE
              IF ERARCH-FILE-STATUS NOT = '00'
                 DISPLAY ' ERROR ON ERARCH - REWRITE - CANCEL '
                    ERARCH-FILE-STATUS  '  CERT '  LA-CERT-NO-A2
              END-IF
              
              PERFORM 0900-READ-ERARCT THRU 0900-EXIT
              IF WS-COMMENT-INDEX = +20
                  DISPLAY ' MAX COMMENTS ON CERT ' LA-CERT-NO-A2
                  GO TO 0020-EXIT
              END-IF
              SET WS-COMMENT-INDEX UP BY +1
              SET LC-NDX TO WS-COMMENT-INDEX
              MOVE WS-COMMENT-LINE TO  LT-COMMENT-LINE (LC-NDX)
              MOVE WS-CURRENT-BIN-DT TO LT-COMMENT-CHG-DT (LC-NDX)
              MOVE 'SYST' TO LT-COMMENT-CHG-BY (LC-NDX)
              ADD +1      TO LT-NUM-LINES-ON-RECORD
              IF WS-ADD-ARCT = 'N'
                 REWRITE LETTER-ARCHIVE-TEXT
              ELSE
                 WRITE LETTER-ARCHIVE-TEXT
              END-IF

              GO TO 0020-EXIT
           END-IF.

           PERFORM 0030-PROCESS-ARCHIVE  THRU 0030-EXIT.
                      
       0020-EXIT.
           EXIT.


       0024-READ-ARCH-IN.

           READ ARCH-IN INTO LETTER-ARCHIVE
               AT END
               SET END-OF-ARCH     TO TRUE.
           
           IF NOT END-OF-ARCH 
              ADD 1                TO ARCH-RECS-IN
           END-IF.

           
       0024-EXIT.
           EXIT.


       0025-READ-ERARCH.

           READ ERARCH.

           IF ERARCH-FILE-STATUS NOT = '00'
              DISPLAY 'BAD READ ERARCH ' LA-CERT-NO-A2 ' ' 
              LA-ARCHIVE-NO '-' ERARCH-FILE-STATUS
              PERFORM ABEND-PGM
           END-IF.
                      
       0025-EXIT.
           EXIT.


       0030-PROCESS-ARCHIVE.
                  
           MOVE 'N'              TO WS-DONE-SW.
           MOVE 'N'              TO WS-NEW-ARCH-SW.
           MOVE LA-COMPANY-CD    TO NSAS-COMPANY-CD
                                    WRK-ORIG-COMPANY-CD.
           MOVE LA-ARCHIVE-NO    TO NSAS-ARCHIVE-NO
                                    WRK-ORIG-ARCHIVE-NO.
           MOVE LA-RESEND-LETR   TO WRK-ORIG-RESEND-LETR.
           MOVE LA-CERT-NO-A2    TO WRK-ORIG-CERT-NO.
           MOVE LA-CARRIER-A2    TO WRK-ORIG-CARRIER.
           MOVE LA-GROUPING-A2   TO WRK-ORIG-GROUPING.
           MOVE LA-STATE-A2      TO WRK-ORIG-STATE.
           MOVE LA-ACCOUNT-A2    TO WRK-ORIG-ACCOUNT.
           MOVE LA-EFFECT-DATE-A2 TO WRK-ORIG-EFFECT-DATE.
           MOVE LA-PROCESSOR-CD  TO WRK-ORIG-PROCESSOR-CD.
           MOVE LA-ENTRY-A6      TO WRK-ORIG-ENTRY.
           MOVE ZEROS            TO NSAS-SEQ-NO.
           
           PERFORM 0125-READ-NSASEXTR    THRU 0125-EXIT
           IF NSAS-FILE-STATUS NOT = '00'
103012         MOVE LA-RESEND-LETR TO MISSING-RESEND-LETTERID
103012         MOVE WS-MISSING-MSG TO REPORT-MSG
               PERFORM 0038-WRITE-RESEND-REPORT THRU 0038-EXIT
               DISPLAY 'NAPERSOFT EXTRACT NOT FOUND FOR ' 
                                  WRK-ORIG-ARCHIVE-NO
               GO TO 0030-EXIT
           END-IF.
           IF NSAS-COMPANY-CD NOT EQUAL WRK-ORIG-COMPANY-CD  OR
              NSAS-ARCHIVE-NO NOT EQUAL WRK-ORIG-ARCHIVE-NO
103012            MOVE LA-RESEND-LETR TO MISSING-RESEND-LETTERID
103012            MOVE WS-MISSING-MSG TO REPORT-MSG
                  PERFORM 0038-WRITE-RESEND-REPORT THRU 0038-EXIT
                  DISPLAY 'NAPERSOFT EXTRACT NOT FOUND FOR ' 
                                  WRK-ORIG-ARCHIVE-NO
                  GO TO 0030-EXIT
           END-IF.

           MOVE WS-CURRENT-BIN-DT   TO LA-SENT-DATE
                                       LA-LAST-RESENT-PRINT-DATE.
           MOVE 'C'                 TO LA-STATUS.
           MOVE 'SYST'              TO LA-LAST-UPDATED-BY.
           MOVE WS-CURRENT-BIN-DT   TO LA-LAST-MAINT-DATE.
           MOVE WS-CURRENT-TIME     TO LA-LAST-MAINT-TIME.
                                     
           REWRITE LETTER-ARCHIVE
                INVALID KEY
                    DISPLAY ' CIDNSARE REWRITE ERROR - ERARCH'
                    SET END-OF-ARCH TO TRUE
                    PERFORM ABEND-PGM.

           PERFORM 0035-WRITE-EXT THRU 0035-EXIT.

       0030-EXIT.
            EXIT.

              
       0035-WRITE-EXT.
           
           PERFORM 0500-GET-ARCH-NO THRU 0500-EXIT
           PERFORM 1000-BUILD-ERARCH THRU 1000-EXIT

           MOVE WS-ARCHIVE-NO TO EXT-ARCHIVE-NO.
           MOVE WRK-ORIG-RESEND-LETR TO EXT-LETTER-ID 
           WRITE NSAS-EXTRACT-RECORD FROM EXT-REC.
           ADD 1                    TO NSAS-RECS-OUT.
           
           IF W-LETTER-TO-ACCT NOT EQUAL SPACES
               MOVE W-LETTER-TO-ACCT TO EXT-LETTER-ID (5:1)
               MOVE +1               TO EXT-SEQ-NO
               WRITE NSAS-EXTRACT-RECORD FROM EXT-REC
               ADD 1                 TO NSAS-RECS-OUT
           END-IF.
           
           IF W-LETTER-TO-BENE NOT EQUAL SPACES
               MOVE W-LETTER-TO-BENE TO EXT-LETTER-ID (5:1)
               MOVE +2               TO EXT-SEQ-NO
               WRITE NSAS-EXTRACT-RECORD FROM EXT-REC
               ADD 1                 TO NSAS-RECS-OUT
           END-IF.
           
       0035-EXIT.
            EXIT.

              
       0038-WRITE-RESEND-REPORT.

103012     IF WS-LINE-CNT > +55
103012        ADD +1 TO WS-PAGE-CNT
103012        MOVE WS-PAGE-CNT TO RPT-PAGE
103012        WRITE RESEND-RPT-RECORD FROM REPORT-HEAD1
103012        WRITE RESEND-RPT-RECORD FROM REPORT-HEAD2
103012        WRITE RESEND-RPT-RECORD FROM BLANK-LINE
103012        WRITE RESEND-RPT-RECORD FROM REPORT-HEADER
103012        MOVE +4 TO WS-LINE-CNT
103012     END-IF
       
103012     MOVE LA-CERT-NO-A2       TO REPORT-CERTNO.
103012     MOVE LA-ARCHIVE-NO       TO REPORT-ARCHIVENO.
103012     MOVE LA-FORM-A3          TO REPORT-LETTERID.
103012     MOVE LA-PROCESSOR-CD     TO REPORT-PROCID
           MOVE ' '                 TO DC-OPTION-CODE.
           MOVE LA-RESEND-DATE      TO DC-BIN-DATE-1.
           PERFORM 8510-DATE-CONVERSION THRU 8590-EXIT.
           IF NO-CONVERSION-ERROR
103012        MOVE DC-GREG-DATE-1-EDIT TO REPORT-RESEND-DT
           ELSE
              DISPLAY ' RESEND DATE ERROR '
           END-IF.
           
103012     WRITE RESEND-RPT-RECORD FROM REPORT-REC.
103012     ADD +1 TO WS-LINE-CNT.

       0038-EXIT.
            EXIT.

              
       0040-OPEN-FILES.
       
           OPEN INPUT ARCH-IN.
           OPEN I-O ERARCH.
           OPEN I-O ERARCT.
           OPEN I-O NSASEXTR.
           OPEN I-O ELCNTL.
103012     OPEN INPUT ELLETR ELCERT ERPNDB ELMSTR.
103012     OPEN OUTPUT RESEND-REPORT.

           IF ERARCH-FILE-STATUS NOT = '00' AND '97'
              DISPLAY 'ERARCH OPEN ERR  ' ERARCH-FILE-STATUS
              MOVE ' ERARCH OPEN ERROR ' TO WS-ABEND-MESSAGE
              MOVE ERARCH-FILE-STATUS    TO WS-ABEND-FILE-STATUS
              PERFORM ABEND-PGM
           END-IF.

           IF ERARCT-FILE-STATUS NOT = '00' AND '97'
              DISPLAY 'ERARCT OPEN ERR  ' ERARCT-FILE-STATUS
              MOVE ' ERARCT OPEN ERROR ' TO WS-ABEND-MESSAGE
              MOVE ERARCT-FILE-STATUS    TO WS-ABEND-FILE-STATUS
              PERFORM ABEND-PGM
           END-IF.

           IF NSAS-FILE-STATUS NOT = '00' AND '97'
              DISPLAY ' NSASEXTR OPEN ' NSAS-FILE-STATUS
              MOVE ' NSASEXTR OPEN ERROR ' TO WS-ABEND-MESSAGE
              MOVE NSAS-FILE-STATUS    TO WS-ABEND-FILE-STATUS
              PERFORM ABEND-PGM
           END-IF.

           IF ELCNTL-FILE-STATUS NOT = '00' AND '97'
              MOVE ' ELCNTL OPEN ERROR ' TO WS-ABEND-MESSAGE
              MOVE ELCNTL-FILE-STATUS    TO WS-ABEND-FILE-STATUS
              PERFORM ABEND-PGM
           END-IF.

           IF ELLETR-FILE-STATUS NOT = '00' AND '97'
              MOVE ' ELLETR OPEN ERROR ' TO WS-ABEND-MESSAGE
              MOVE ELLETR-FILE-STATUS    TO WS-ABEND-FILE-STATUS
              PERFORM ABEND-PGM
           END-IF.

           IF ELCERT-FILE-STATUS NOT = '00' AND '97'
              MOVE ' ELCERT OPEN ERROR ' TO WS-ABEND-MESSAGE
              MOVE ELCERT-FILE-STATUS    TO WS-ABEND-FILE-STATUS
              PERFORM ABEND-PGM
           END-IF.

           IF ERPNDB-FILE-STATUS NOT = '00' AND '97'
              MOVE ' ERPNDB OPEN ERROR ' TO WS-ABEND-MESSAGE
              MOVE ERPNDB-FILE-STATUS    TO WS-ABEND-FILE-STATUS
              PERFORM ABEND-PGM
           END-IF.
103012
103012     IF ELMSTR-FILE-STATUS NOT = '00' AND '97'
103012        MOVE ' ELMSTR OPEN ERROR ' TO WS-ABEND-MESSAGE
103012        MOVE ELMSTR-FILE-STATUS    TO WS-ABEND-FILE-STATUS
103012        PERFORM ABEND-PGM
103012     END-IF.

       0040-EXIT.
           EXIT.

       0050-INIT.
           
           ACCEPT WS-TIME-OF-DAY       FROM  TIME
           MOVE WS-TIME                TO  WS-CURRENT-TIME
           MOVE WS-CURRENT-DATE        TO  DC-GREG-DATE-1-EDIT.
           MOVE '2'                    TO  DC-OPTION-CODE.
           DISPLAY '* * * * * * * * * * * * * * * * * * * * * '.
           DISPLAY 'CURRENT DATE USED FOR RUN IS - - ' WS-CURRENT-DATE.
           DISPLAY '* * * * * * * * * * * * * * * * * * * * * '.

           PERFORM 8510-DATE-CONVERSION.
           IF NO-CONVERSION-ERROR
               MOVE DC-BIN-DATE-1      TO  WS-CURRENT-BIN-DT
           END-IF.
           
           MOVE WS-CURRENT-DATE TO RPT-DATE.
103012     MOVE +1 TO WS-PAGE-CNT.
103012     MOVE WS-PAGE-CNT TO RPT-PAGE.
103012     WRITE RESEND-RPT-RECORD FROM REPORT-HEAD1.
103012     WRITE RESEND-RPT-RECORD FROM REPORT-HEAD2.
103012     WRITE RESEND-RPT-RECORD FROM BLANK-LINE.
103012     WRITE RESEND-RPT-RECORD FROM REPORT-HEADER.
103012     MOVE +4 TO WS-LINE-CNT.

       0050-EXIT.
           EXIT.


       0060-CLOSE-FILES.
       
           CLOSE ARCH-IN.
103012     CLOSE RESEND-REPORT.
           CLOSE ERARCH ERARCT NSASEXTR ELCNTL ELLETR ELCERT ERPNDB.
103012     CLOSE ELMSTR.

           IF ERARCH-FILE-STATUS NOT = '00'
              DISPLAY ' ERARCH CLOSE ' ERARCH-FILE-STATUS
              PERFORM ABEND-PGM
           END-IF.

           IF ERARCT-FILE-STATUS NOT = '00'
              DISPLAY ' ERARCT CLOSE ' ERARCT-FILE-STATUS
              PERFORM ABEND-PGM
           END-IF.

           IF NSAS-FILE-STATUS NOT = '00'
              DISPLAY ' NSASEXTR CLOSE ' NSAS-FILE-STATUS
              PERFORM ABEND-PGM
           END-IF.

           IF ELCNTL-FILE-STATUS NOT = '00'
              DISPLAY ' ELCNTL CLOSE ' ELCNTL-FILE-STATUS
              PERFORM ABEND-PGM
           END-IF.

           IF ELLETR-FILE-STATUS NOT = '00'
              DISPLAY ' ELLETR CLOSE ' ELLETR-FILE-STATUS
              PERFORM ABEND-PGM
           END-IF.

           IF ELCERT-FILE-STATUS NOT = '00' 
              MOVE ' ELCERT CLOSE ERROR ' TO WS-ABEND-MESSAGE
              MOVE ELCERT-FILE-STATUS    TO WS-ABEND-FILE-STATUS
              PERFORM ABEND-PGM
           END-IF.

           IF ERPNDB-FILE-STATUS NOT = '00' 
              MOVE ' ERPNDB CLOSE ERROR ' TO WS-ABEND-MESSAGE
              MOVE ERPNDB-FILE-STATUS    TO WS-ABEND-FILE-STATUS
              PERFORM ABEND-PGM
           END-IF.
103012
103012     IF ELMSTR-FILE-STATUS NOT = '00' AND '97'
103012        MOVE ' ELMSTR CLOSE ERROR ' TO WS-ABEND-MESSAGE
103012        MOVE ELMSTR-FILE-STATUS    TO WS-ABEND-FILE-STATUS
103012        PERFORM ABEND-PGM
103012     END-IF.

           
       0060-EXIT.
           EXIT.


       0125-READ-NSASEXTR.

           READ NSASEXTR INTO EXT-REC.

           IF NSAS-FILE-STATUS NOT = '00'
              DISPLAY 'BAD READ NSASEXTR ' NSAS-ARCHIVE-NO '-'
                  NSAS-FILE-STATUS
           END-IF.

       0125-EXIT.
           EXIT.


       0200-CHECK-CERT-STATUS.
      
           MOVE +0                     TO WS-COV-CNT
           MOVE LOW-VALUES             TO WS-STOP-DATE
           PERFORM 0250-READ-CERT THRU 0250-EXIT
103012     IF NOT WS-CERT-FOUND
103012        MOVE WS-CURRENT-BIN-DT   TO WS-STOP-DATE
103012        MOVE WS-NOT-FOUND        TO WS-COMMENT-LINE
103012        DISPLAY ' ERROR READING ELCERT ' LA-CERT-NO-A2
103012        GO TO 0200-EXIT
103012     END-IF.
103012
103012     IF CM-LF-BENEFIT-CD NOT = '00' AND SPACES
103012         ADD +1                TO WS-COV-CNT
103012     END-IF
103012     IF CM-AH-BENEFIT-CD NOT = '00' AND SPACES
103012         ADD +1                TO WS-COV-CNT
103012     END-IF
103012     
103012     IF (CERT-PEND-ISSUE-ERROR)
103012                  AND
103012        ((CERT-CANCELLED-ONLINE)
103012        OR (CERT-PEND-CANCEL-ERROR))
103012        MOVE CM-CONTROL-PRIMARY TO PB-CONTROL-BY-ACCOUNT
103012        MOVE +0               TO PB-ALT-CHG-SEQ-NO
103012        MOVE '2'              TO PB-RECORD-TYPE
103012        READ ERPNDB
103012        IF ERPNDB-FILE-STATUS = '00'
103012           IF PB-C-LF-CANCEL-DT NOT = LOW-VALUES AND SPACES
103012              SUBTRACT +1     FROM WS-COV-CNT
103012*              MOVE PB-C-LF-CANCEL-DT TO WS-STOP-DATE
103012              MOVE WS-CURRENT-BIN-DT TO WS-STOP-DATE
103012              MOVE WS-CANCELLED      TO WS-COMMENT-LINE
103012           ELSE
103012              IF CM-LF-DEATH-DT NOT = LOW-VALUES AND SPACES
103012                 MOVE +0      TO WS-COV-CNT
103012*                 MOVE CM-LF-DEATH-DT TO WS-STOP-DATE
103012                 MOVE WS-CURRENT-BIN-DT TO WS-STOP-DATE
103012                 MOVE WS-DEATH       TO WS-COMMENT-LINE
103012              ELSE
103012                 IF (CM-LF-LOAN-EXPIRE-DT NOT = 
103012                    LOW-VALUES AND SPACES)
103012                    AND (CM-LF-LOAN-EXPIRE-DT < BIN-RUN-DATE)
103012                      SUBTRACT +1 FROM WS-COV-CNT
103012*                      MOVE CM-LF-LOAN-EXPIRE-DT TO WS-STOP-DATE
103012                      MOVE WS-CURRENT-BIN-DT TO WS-STOP-DATE
103012                      MOVE WS-EXPIRED        TO WS-COMMENT-LINE
103012                 END-IF
103012              END-IF
103012           END-IF
103012           IF PB-C-AH-CANCEL-DT NOT = LOW-VALUES AND SPACES
103012              SUBTRACT +1     FROM WS-COV-CNT
103012*              MOVE PB-C-AH-CANCEL-DT TO WS-STOP-DATE
103012              MOVE WS-CURRENT-BIN-DT TO WS-STOP-DATE
103012              MOVE WS-CANCELLED      TO WS-COMMENT-LINE
103012           ELSE
103012              IF (CM-AH-SETTLEMENT-DT NOT =
103012                 LOW-VALUES AND SPACES)
103012                 SUBTRACT +1        FROM WS-COV-CNT
103012*                 MOVE CM-AH-SETTLEMENT-DT TO WS-STOP-DATE
103012                 MOVE WS-CURRENT-BIN-DT   TO WS-STOP-DATE
103012                 MOVE WS-SETTLEMENT       TO WS-COMMENT-LINE
103012              ELSE
103012                 IF (CM-AH-LOAN-EXPIRE-DT NOT =
103012                    LOW-VALUES AND SPACES)
103012                    AND (CM-AH-LOAN-EXPIRE-DT < BIN-RUN-DATE)
103012                    SUBTRACT +1 FROM WS-COV-CNT
103012*                    MOVE CM-AH-LOAN-EXPIRE-DT TO WS-STOP-DATE
103012                    MOVE WS-CURRENT-BIN-DT TO WS-STOP-DATE
103012                    MOVE WS-EXPIRED        TO WS-COMMENT-LINE
103012                 END-IF
103012              END-IF
103012           END-IF
103012        ELSE
103012           DISPLAY ' BAD READ ON ERPNDB ' CM-STATE ' '
103012              CM-ACCOUNT ' ' CM-CERT-NO
103012        END-IF
103012     ELSE
103012        IF CM-LF-CANCEL-DT NOT = LOW-VALUES AND SPACES
103012           SUBTRACT +1        FROM WS-COV-CNT
103012*           MOVE CM-LF-CANCEL-DT TO WS-STOP-DATE
103012           MOVE WS-CURRENT-BIN-DT TO WS-STOP-DATE
103012           MOVE WS-CANCELLED    TO WS-COMMENT-LINE
103012        ELSE
103012           IF CM-LF-DEATH-DT NOT = LOW-VALUES AND SPACES
103012              MOVE +0         TO WS-COV-CNT
103012*              MOVE CM-LF-DEATH-DT TO WS-STOP-DATE
103012              MOVE WS-CURRENT-BIN-DT TO WS-STOP-DATE
103012              MOVE WS-DEATH       TO WS-COMMENT-LINE
103012           ELSE
103012              IF (CM-LF-LOAN-EXPIRE-DT NOT =
103012                 LOW-VALUES AND SPACES)
103012                 AND (CM-LF-LOAN-EXPIRE-DT < BIN-RUN-DATE)
103012                 SUBTRACT +1 FROM WS-COV-CNT
103012*                 MOVE CM-LF-LOAN-EXPIRE-DT TO WS-STOP-DATE
103012                 MOVE WS-CURRENT-BIN-DT TO WS-STOP-DATE
103012                 MOVE WS-EXPIRED        TO WS-COMMENT-LINE
103012              END-IF
103012           END-IF
103012        END-IF
103012        IF CM-AH-CANCEL-DT NOT = LOW-VALUES AND SPACES
103012           SUBTRACT +1        FROM WS-COV-CNT
103012*           MOVE CM-AH-CANCEL-DT TO WS-STOP-DATE
103012           MOVE WS-CURRENT-BIN-DT TO WS-STOP-DATE
103012           MOVE WS-CANCELLED    TO WS-COMMENT-LINE
103012        ELSE
103012           IF CM-AH-SETTLEMENT-DT NOT = LOW-VALUES AND SPACES
103012              SUBTRACT +1        FROM WS-COV-CNT
103012*              MOVE CM-AH-SETTLEMENT-DT TO WS-STOP-DATE
103012              MOVE WS-CURRENT-BIN-DT TO WS-STOP-DATE
103012              MOVE WS-SETTLEMENT       TO WS-COMMENT-LINE
103012           ELSE
103012              IF (CM-AH-LOAN-EXPIRE-DT NOT =
103012                 LOW-VALUES AND SPACES)
103012                 AND (CM-AH-LOAN-EXPIRE-DT < BIN-RUN-DATE)
103012                 SUBTRACT +1 FROM WS-COV-CNT
103012*                 MOVE CM-AH-LOAN-EXPIRE-DT TO WS-STOP-DATE
103012                 MOVE WS-CURRENT-BIN-DT TO WS-STOP-DATE
103012                 MOVE WS-EXPIRED        TO WS-COMMENT-LINE
103012              END-IF
103012           END-IF
103012        END-IF
103012     END-IF
103012     
103012     IF WS-COV-CNT <= +0
103012        SET CERT-CANCELLED    TO TRUE
103012        GO TO 0200-EXIT
103012     END-IF
103012     
103012     MOVE SPACES TO WS-CERT-CLAIM-SW
103012     IF CM-CLAIM-ATTACHED-COUNT > 0
103012        PERFORM 0700-CHECK-FOR-OPEN-CLMS THRU 0700-EXIT
103012        IF CERT-HAS-OPEN-CLM
103012           MOVE WS-CURRENT-BIN-DT   TO WS-STOP-DATE
103012           MOVE WS-OPEN-CLAIM       TO WS-COMMENT-LINE
100713           GO TO 0200-EXIT
103012        END-IF
103012     END-IF.
100713     
100713     PERFORM 0750-GET-Z-RECORD THRU 0750-EXIT
100713     IF BYPASS-REC
100713        MOVE WS-CURRENT-BIN-DT   TO WS-STOP-DATE
100713        MOVE WS-Z-RECORD         TO WS-COMMENT-LINE
100713        GO TO 0200-EXIT
100713     END-IF.
100713
100713     .

       0200-EXIT.
           EXIT.

       0250-READ-CERT.

           MOVE LA-COMPANY-CD          TO CM-COMPANY-CD
           MOVE LA-CARRIER-A2          TO CM-CARRIER
           MOVE LA-GROUPING-A2         TO CM-GROUPING
           MOVE LA-STATE-A2            TO CM-STATE
           MOVE LA-ACCOUNT-A2          TO CM-ACCOUNT
           MOVE LA-EFFECT-DATE-A2      TO CM-CERT-EFF-DT
           MOVE LA-CERT-NO-A2          TO CM-CERT-NO

           READ ELCERT
           IF ELCERT-FILE-STATUS = '00'
              MOVE 'Y'                 TO WS-CERT-FOUND-SW
           ELSE
              DISPLAY ' ERROR ON ELCERT - READ - 0250 '
                 ELCERT-FILE-STATUS
           END-IF.

       0250-EXIT.
           EXIT.

       0500-GET-ARCH-NO.

           MOVE DTE-CLIENT             TO CF-COMPANY-ID
           MOVE '1'                    TO CF-RECORD-TYPE
           MOVE ZEROS                  TO CF-SEQUENCE-NO
           MOVE SPACES                 TO CF-ACCESS-CD-GENL

           READ ELCNTL

           IF ELCNTL-FILE-STATUS NOT = '00'
               DISPLAY 'BAD READ ELCNTL ' ELCNTL-FILE-STATUS
               MOVE ' ELCNTL READ  ERROR ' TO WS-ABEND-MESSAGE
               MOVE ELCNTL-FILE-STATUS TO WS-ABEND-FILE-STATUS
               PERFORM ABEND-PGM
           END-IF
           
           ADD +1                        TO CF-CREDIT-LAST-ARCH-NUM
           MOVE CF-CREDIT-LAST-ARCH-NUM  TO WS-ARCHIVE-NO

           REWRITE CONTROL-FILE
           IF ELCNTL-FILE-STATUS NOT = '00'
              MOVE ' ELCNTL REWRITE ERROR ' TO WS-ABEND-MESSAGE
              MOVE ELCNTL-FILE-STATUS TO WS-ABEND-FILE-STATUS
              PERFORM ABEND-PGM
           END-IF

          .
       0500-EXIT.
           EXIT.
103012
103012
103012 0700-CHECK-FOR-OPEN-CLMS.                                        
103012
103012     MOVE CM-CONTROL-BY-CERT-NO TO CL-CONTROL-BY-CERT-NO.
103012
103012     START ELMSTR KEY NOT LESS THAN CL-CONTROL-BY-CERT-NO.
103012
103012     IF ELMSTR-FILE-STATUS NOT EQUAL '00' AND '02'
103012         GO TO 0700-EXIT
103012     END-IF.
103012
103012 0700-NEXT-CLAIM.
103012
103012     READ ELMSTR NEXT
103012
103012     IF ELMSTR-FILE-STATUS NOT = '00' AND '02'                
103012         GO TO 0700-EXIT
103012     END-IF
103012
103012     IF CM-COMPANY-CD-A4 NOT = CL-COMPANY-CD-A4  OR                   
103012        CM-CERT-NO-A4 NOT = CL-CERT-NO-A4                          
103012         GO TO 0700-EXIT
103012     END-IF
103012
103012     IF CM-CARRIER  NOT = CL-CARRIER       OR
103012        CM-GROUPING NOT = CL-CERT-GROUPING OR
103012        CM-STATE    NOT = CL-CERT-STATE    OR
103012        CM-ACCOUNT  NOT = CL-CERT-ACCOUNT  OR
103012        CM-CERT-EFF-DT NOT = CL-CERT-EFF-DT
103012         GO TO 0700-NEXT-CLAIM
103012     END-IF
103012
103012    IF CLAIM-IS-OPEN
103012        SET CERT-HAS-OPEN-CLM TO TRUE
103012        GO TO 0700-EXIT
103012    END-IF
103012
103012    GO TO 0700-NEXT-CLAIM.
103012
103012    .
103012 0700-EXIT.
103012     EXIT.
103012
           
       0750-GET-Z-RECORD.

           MOVE LOW-VALUES             TO TX-CONTROL-PRIMARY.
           MOVE DTE-CLASIC-COMPANY-CD  TO TX-COMPANY-CD.
           MOVE LA-RESEND-LETR         TO TX-LETTER-NO.
           
           START ELLETR KEY IS NOT LESS THAN TX-CONTROL-PRIMARY.

           IF ELLETR-FILE-STATUS NOT = '00'
100713        DISPLAY 'ERROR ON START ELLETR'
100713                  '  BYPASSING RECORD '
100713        GO TO 0750-SET-BYPASS
100713*       MOVE ' ELLETR START ERROR ' TO WS-ABEND-MESSAGE
100713*       MOVE ELLETR-FILE-STATUS TO WS-ABEND-FILE-STATUS
100713*       PERFORM ABEND-PGM
           END-IF.
           
       0750-READ-NEXT.
       
           READ ELLETR NEXT RECORD.

           IF ELLETR-FILE-STATUS NOT = '00'
100713        DISPLAY 'ERROR READING ELLETR NEXT RECORD'
100713                          '  BYPASSING RECORD  '
100713        GO TO 0750-SET-BYPASS
100713*       MOVE ' ELLETR READ  ERROR ' TO WS-ABEND-MESSAGE
100713*       MOVE ELLETR-FILE-STATUS TO WS-ABEND-FILE-STATUS
100713*       PERFORM ABEND-PGM
           END-IF.

           IF (TX-COMPANY-CD NOT = DTE-CLASIC-COMPANY-CD) OR
100713        (TX-LETTER-NO NOT = LA-RESEND-LETR)
                 DISPLAY 'LETTER Z RECORD NOT FOUND  - ' 
100713                           LA-RESEND-LETR
100713                          '  BYPASSING RECORD  '
100713           GO TO 0750-SET-BYPASS
100713*          MOVE ' Z RECORD NOT FOUND ' TO WS-ABEND-MESSAGE
100713*          PERFORM ABEND-PGM
           END-IF.

           IF TX-LINE-SQUEEZE-CONTROL NOT EQUAL 'Z'
               GO TO 0750-READ-NEXT
           END-IF.
           
           INITIALIZE W-Z-CONTROL-DATA
           MOVE LOW-VALUES             TO WS-RESEND-DATE
                                       WS-FOLLOW-UP-DATE.
           PERFORM 0800-PROCESS-Z-CONTROLS THRU 0800-EXIT.
100713     GO TO 0750-EXIT.
100713
100713 0750-SET-BYPASS.
100713
100713       SET BYPASS-REC TO TRUE.

       0750-EXIT.
           EXIT.
           
           
       0800-PROCESS-Z-CONTROLS.

           MOVE TX-TEXT-LINE           TO W-Z-CONTROL-DATA
           
           IF W-NUMBER-OF-COPIES NOT NUMERIC
              MOVE 1               TO W-NUMBER-OF-COPIES
           END-IF

           IF (W-DAYS-TO-RESEND NUMERIC) AND
              (W-DAYS-TO-RESEND > ZEROS)
              MOVE '6'              TO DC-OPTION-CODE
              MOVE WS-CURRENT-BIN-DT TO DC-BIN-DATE-1
              MOVE ZEROS            TO DC-ELAPSED-MONTHS
              MOVE W-DAYS-TO-RESEND
                                    TO DC-ELAPSED-DAYS
              PERFORM 8510-DATE-CONVERSION
                                    THRU 8590-EXIT
              IF NO-CONVERSION-ERROR
                 MOVE DC-BIN-DATE-2 TO WS-RESEND-DATE
              ELSE
                 DISPLAY ' RESEND DATE ERROR '
              END-IF
           END-IF

           IF (W-DAYS-TO-FOLLOW-UP NUMERIC) AND
              (W-DAYS-TO-FOLLOW-UP >= ZEROS)
              MOVE '6'              TO DC-OPTION-CODE
              MOVE WS-CURRENT-BIN-DT TO DC-BIN-DATE-1
              MOVE ZEROS            TO DC-ELAPSED-MONTHS
              MOVE W-DAYS-TO-FOLLOW-UP
                                    TO DC-ELAPSED-DAYS
              PERFORM 8510-DATE-CONVERSION
                                    THRU 8590-EXIT
              IF NO-CONVERSION-ERROR
                 MOVE DC-BIN-DATE-2 TO WS-FOLLOW-UP-DATE
              ELSE
                 DISPLAY 'FOLLOW UP DATE ERROR'
              END-IF
           END-IF

           .
       0800-EXIT.
           EXIT.


       0900-READ-ERARCT.
       
           MOVE LA-COMPANY-CD    TO LT-COMPANY-CD
           MOVE LA-ARCHIVE-NO    TO LT-ARCHIVE-NO
           MOVE '3'              TO LT-RECORD-TYPE
           MOVE +0               TO LT-LINE-SEQ-NO
           
           READ ERARCT
           IF ERARCT-FILE-STATUS = '00'
               MOVE 'N' TO WS-ADD-ARCT
               SET LC-NDX TO WS-ZEROS
               SET WS-COMMENT-INDEX TO WS-ZEROS
               PERFORM 20 TIMES
                  SET LC-NDX UP BY +1
                  IF LT-COMMENT-LINE (LC-NDX) > SPACES
                     SET WS-COMMENT-INDEX TO LC-NDX
                  ELSE
                     EXIT PERFORM
                  END-IF
               END-PERFORM
               GO TO 0900-EXIT
           END-IF
           
           MOVE 'Y'              TO WS-ADD-ARCT.
           MOVE LOW-VALUES       TO LETTER-ARCHIVE-TEXT.
           MOVE 'LT'             TO LT-RECORD-ID.
           MOVE LA-COMPANY-CD    TO LT-COMPANY-CD.
           MOVE LA-ARCHIVE-NO    TO LT-ARCHIVE-NO.
           MOVE '3'              TO LT-RECORD-TYPE.
           MOVE +0               TO LT-LINE-SEQ-NO.
           MOVE +0               TO LT-NUM-LINES-ON-RECORD.
           SET WS-COMMENT-INDEX  TO WS-ZEROS.

       0900-EXIT.
           EXIT.
           

       1000-BUILD-ERARCH.
      
           MOVE 'LA'                   TO LETTER-ARCHIVE

           MOVE WS-ARCHIVE-NO          TO LA-ARCHIVE-NO
                                          LA-ARCHIVE-NO-A2
                                          LA-ARCHIVE-NO-A3
                                          LA-ARCHIVE-NO-A4
                                          LA-ARCHIVE-NO-A5
                                          LA-ARCHIVE-NO-A6

           MOVE DTE-CLASIC-COMPANY-CD  TO LA-COMPANY-CD
                                          LA-COMPANY-CD-A2
                                          LA-COMPANY-CD-A3
                                          LA-COMPANY-CD-A4
                                          LA-COMPANY-CD-A5
                                          LA-COMPANY-CD-A6
           MOVE WRK-ORIG-CARRIER       TO LA-CARRIER-A2
                                          LA-CARRIER-A3
                                          LA-CARRIER-A4
                                          LA-CARRIER-A5
           MOVE WRK-ORIG-GROUPING      TO LA-GROUPING-A2
                                          LA-GROUPING-A3
                                          LA-GROUPING-A4
                                          LA-GROUPING-A5
           MOVE WRK-ORIG-ACCOUNT       TO LA-ACCOUNT-A2
                                          LA-ACCOUNT-A3
                                          LA-ACCOUNT-A4
                                          LA-ACCOUNT-A5
           MOVE WRK-ORIG-STATE         TO LA-STATE-A2
                                          LA-STATE-A3
                                          LA-STATE-A4
                                          LA-STATE-A5
           MOVE WRK-ORIG-EFFECT-DATE   TO LA-EFFECT-DATE-A2
           MOVE WRK-ORIG-CERT-NO       TO LA-CERT-NO-A2
           MOVE WRK-ORIG-ENTRY         TO LA-ENTRY-A6

           MOVE WRK-ORIG-PROCESSOR-CD  TO LA-PROCESSOR-CD
      
           MOVE LOW-VALUES             TO LA-LAST-RESENT-PRINT-DATE
                                          LA-INITIAL-PRINT-DATE
                                          LA-SENT-DATE
                                          LA-REPLY-DATE
                                          LA-PURGED-DATE
                                          LA-VOIDED-DATE

           MOVE 'A'                    TO LA-STATUS
           MOVE W-NUMBER-OF-COPIES     TO LA-NO-OF-COPIES
           MOVE WRK-ORIG-RESEND-LETR   TO LA-FORM-A3
           MOVE 'B'                    TO LA-ARCHIVE-STATUS
           MOVE WS-CURRENT-BIN-DT      TO LA-CREATION-DATE
           MOVE WS-RESEND-DATE         TO LA-RESEND-DATE
           MOVE WS-FOLLOW-UP-DATE      TO LA-FOLLOW-UP-DATE
           MOVE W-FORM-TO-RESEND       TO LA-RESEND-LETR
           MOVE W-AUTO-CLOSE-IND       TO LA-FINAL-ACT-IND

           MOVE ZEROS                  TO LA-NUMBER-LABEL-LINES
                                          LA-NO-OF-TEXT-RECORDS
                                          
           MOVE 'SYST'                 TO LA-LAST-UPDATED-BY
           MOVE WS-CURRENT-BIN-DT      TO LA-LAST-MAINT-DATE  
           MOVE WS-CURRENT-TIME        TO LA-LAST-MAINT-TIME                                        

           WRITE LETTER-ARCHIVE 
       
           IF ERARCH-FILE-STATUS = '22'
              DISPLAY ' ERARCH DUP RECORD  ' LA-ARCHIVE-NO
           ELSE
              IF ERARCH-FILE-STATUS NOT = '00'
                 MOVE ' ERARCH  WRITE  ERROR ' TO WS-ABEND-MESSAGE
                 MOVE ERARCH-FILE-STATUS TO WS-ABEND-FILE-STATUS
                 PERFORM ABEND-PGM
              END-IF
           END-IF

           .
       1000-EXIT.
           EXIT.


       8510-DATE-CONVERSION.

           CALL 'ELDATCX' USING DATE-CONVERSION-DATA

           .
       8590-EXIT.
           EXIT.


       ABEND-PGM.   COPY ELCABEND.

