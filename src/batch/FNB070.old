       IDENTIFICATION DIVISION.
       PROGRAM-ID.    FNB070.
       DATE-WRITTEN.  MAY, 2000.

      ******************************************************************
      *
      *       CREATE FIRST NATIONAL BANK FILE OF ISSUED CHECKS
070902*        THIS FILE IS TRANSFERRED TO FNB BY OPERATIONS
070902******************************************************************
070902*                   C H A N G E   L O G
070902*
070902* CHANGES ARE MARKED BY THE CHANGE EFFECTIVE DATE.
070902*-----------------------------------------------------------------
070902*  CHANGE   CHANGE REQUEST PGMR  DESCRIPTION OF CHANGE
070902* EFFECTIVE    NUMBER
070902*-----------------------------------------------------------------
070902* 070902    2002031800003  SMVA  MOVE FROM MAINFRAME TO LAN
092302* 092302                   SMVA  CHANGE COMPARISON VALUE FOR CHECK
092302*                        AMOUNT FROM ZERO TO 0.00 W/LEADING SPACES
070902******************************************************************

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

           SELECT MICR-DRAFT-FILE
               ASSIGN TO MICRDRFT
070902*        ORGANIZATION IS INDEXED
070902         ORGANIZATION IS LINE SEQUENTIAL
070902*        ACCESS IS SEQUENTIAL
070902*        RECORD KEY IS MICR-KEY
               FILE STATUS IS MICR-STATUS.

           SELECT FNB-ISSUE-FILE
               ASSIGN TO SYS011
070902         ORGANIZATION IS LINE SEQUENTIAL
               FILE STATUS IS SYS011-STATUS.


       DATA DIVISION.
       FILE SECTION.

       FD  FNB-ISSUE-FILE
           LABEL RECORDS ARE STANDARD
           RECORDING MODE IS F
           RECORD CONTAINS 54 CHARACTERS
           BLOCK CONTAINS 0 RECORDS.
       01  FNB-ISSUE-RECORD   PIC X(54).

       FD  MICR-DRAFT-FILE
           RECORD IS VARYING FROM 650 TO 3000 CHARACTERS.
       01  MICR-DRAFT-RECORD.
070902*    COPY MICR0031.
070902     COPY FNMICR.
       01  FILLER.
           05  MICR-KEY         PIC X(0019).
           05  FILLER           PIC X(2981).


      /
       WORKING-STORAGE SECTION.

       01  FILLER.
070902*    05  S0C7                PIC X      VALUE ' '.
070902*    05  FORCE-DUMP REDEFINES S0C7 PIC S9 COMP-3.
           05  MICR-STATUS         PIC XX     VALUE '00'.
               88  EOF                        VALUE '10'.
           05  SYS011-STATUS       PIC XX     VALUE '00'.
           05  TOT-COUNT           PIC S9(7)  COMP-3 VALUE +0.
070902*    05  ED-CNT              PIC ZZ,ZZZ,ZZZ,ZZ9.
070902*    05  ED-AMT              PIC ZZZ,ZZZ,ZZZ.99.
           05  WS-DATE.
               10  WS-MO           PIC XX.
               10  FILLER          PIC X.
               10  WS-DAY          PIC XX.
               10  FILLER          PIC X.
               10  WS-YEAR         PIC XXXX.
           05  WS-CHECK-DATE.
               10  WS-CHECK-YEAR   PIC XXXX.
               10  WS-CHECK-MO     PIC XX.
               10  WS-CHECK-DAY    PIC XX.

       01  FNB-WORK-RECORD.
           05  FNB-VOID-IND    PIC X         VALUE '0'.
           05  FNB-ACCOUNT     PIC X(10)     VALUE '0013238089'.
           05  FNB-CHECK-NO    PIC 9(10)     VALUE ZERO.
           05  FNB-ISSUE-DATE  PIC 9(8)      VALUE ZERO.
           05  FNB-CHECK-AMT   PIC 9(8)V99   VALUE ZERO.
           05  FILLER          PIC X(15)     VALUE ZERO.

       01  FNB-TOTAL-RECORD.
           05  FNB-TOTAL-IND   PIC X         VALUE 'T'.
           05  FNB-TOTAL-AMT   PIC 9(10)V99  VALUE ZERO.
           05  FILLER          PIC X(41)     VALUE ZERO.



      *
       PROCEDURE DIVISION.
      *
           PERFORM 0000-INIT THRU 0000-EXIT

           PERFORM 1000-PROCESS-FILE THRU 1000-EXIT
             UNTIL EOF

           PERFORM 9000-END THRU 9000-EXIT

070902*    STOP RUN.
070902     GOBACK.

       0000-INIT.
      *
           OPEN INPUT MICR-DRAFT-FILE
           IF MICR-STATUS = '00' OR '97'
              CONTINUE
           ELSE
              DISPLAY 'OPEN ERROR ' MICR-STATUS ' ON MICRDRFT'
070902*       ADD +1 TO FORCE-DUMP
070902     END-IF

           OPEN OUTPUT FNB-ISSUE-FILE
           IF SYS011-STATUS NOT = '00'
              DISPLAY 'OPEN ERROR ' SYS011-STATUS ' ON SYS011'
070902*       ADD +1 TO FORCE-DUMP
070902     END-IF

           .
       0000-EXIT.
           EXIT.

       1000-PROCESS-FILE.
      *
           READ MICR-DRAFT-FILE
             AT END GO TO 1000-EXIT
070902     END-READ

           IF FNMICR-FORM NOT = '0031'
              GO TO 1000-EXIT
070902     END-IF

092302     IF FNMICR-AMOUNT-PAID = '          0.00'
              GO TO 1000-EXIT
070902     END-IF

           MOVE FNMICR-DRAFT-NO    TO FNB-CHECK-NO
           MOVE FNMICR-CHECK-DATE  TO WS-DATE
           MOVE WS-YEAR           TO WS-CHECK-YEAR
           MOVE WS-MO             TO WS-CHECK-MO
           MOVE WS-DAY            TO WS-CHECK-DAY
           MOVE WS-CHECK-DATE     TO FNB-ISSUE-DATE
           MOVE FNMICR-AMOUNT-PAID TO FNB-CHECK-AMT
           WRITE FNB-ISSUE-RECORD FROM FNB-WORK-RECORD
           ADD +1 TO TOT-COUNT
           ADD FNB-CHECK-AMT TO FNB-TOTAL-AMT

           .
       1000-EXIT.
           EXIT.

       9000-END.
      *
           WRITE FNB-ISSUE-RECORD FROM FNB-TOTAL-RECORD
           CLOSE MICR-DRAFT-FILE
           CLOSE FNB-ISSUE-FILE
070902*    MOVE TOT-COUNT TO ED-CNT
070902*    MOVE FNB-TOTAL-AMT TO ED-AMT
070902*    DISPLAY 'FNB ISSUED CHECK FILE ' WS-DATE
070902*    DISPLAY 'RECS:' ED-CNT '   AMT:' ED-AMT
           .
       9000-EXIT.
           EXIT.

