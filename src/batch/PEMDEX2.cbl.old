       IDENTIFICATION DIVISION.
       PROGRAM-ID. PEMDEX2.
       AUTHOR.     PABLO.
       DATE-COMPILED.
       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

           SELECT EXTR-FILE-IN     ASSIGN TO EXTRIN.

           SELECT EXTR-FILE-OUT    ASSIGN TO EXTROT
               organization is line sequential.

           SELECT ERACCT          ASSIGN TO ERACCTT
                   ORGANIZATION INDEXED
                   ACCESS DYNAMIC
                   RECORD KEY AM-CONTROL-PRIMARY
                   FILE STATUS ERACCT-FILE-STATUS.

           SELECT eltrlr          ASSIGN TO eltrlr
                   ORGANIZATION INDEXED
                   ACCESS DYNAMIC
                   RECORD KEY AT-CONTROL-PRIMARY
                   FILE STATUS eltrlr-FILE-STATUS.

           SELECT PLAN-CODE-FILE                                        00160000
               ASSIGN TO PLNCODE                                        00170000
               ORGANIZATION IS INDEXED                                  00180000
               ACCESS IS RANDOM                                         00190000
               RECORD KEY IS VCL-PLAN                                   00200000
               STATUS IS PLAN-STATUS.                                   00210000
                                                                        00220000
           SELECT LOGIC-CONTROL-FILE
               ASSIGN TO ELCNTL
               ORGANIZATION IS INDEXED
               ACCESS IS SEQUENTIAL
               RECORD KEY IS CF-CONTROL-PRIMARY
               FILE STATUS IS ELCNTL-STATUS.

           EJECT
       DATA DIVISION.
       FILE SECTION.

       FD  EXTR-FILE-IN
           RECORDING MODE F
           LABEL RECORDS STANDARD
           BLOCK CONTAINS 0 RECORDS.

           COPY ECSEXT01.
      /

       FD  EXTR-FILE-OUT
           RECORDING MODE F
           LABEL RECORDS STANDARD
           BLOCK CONTAINS 0 RECORDS.

       01  EXTR-FILE-OUT-REC           PIC X(310).

           EJECT

       FD  ERACCT.

                                       COPY ERCACCT.

       FD  eltrlr.

                                       COPY Elctrlr.
       FD  PLAN-CODE-FILE.                                              00450000
           COPY AIRL0007.                                               00460000
                                                                        00470000
       FD  LOGIC-CONTROL-FILE.
           COPY ELCCNTL.

       EJECT
       WORKING-STORAGE SECTION.
       77  FILLER  PIC X(32) VALUE '********************************'.
       77  FILLER  PIC X(32) VALUE '   PEMDEX2  WORKING STORAGE     '.
       77  FILLER  PIC X(32) VALUE '********************************'.

       77  WS-EOF-SW               PIC X VALUE SPACES.
           88  END-OF-EXTR               VALUE 'Y'.
       77  EXT-RECS-IN             PIC 9(9) VALUE ZEROS.
       77  EXT-RECS-OUT            PIC 9(9) VALUE ZEROS.
       77  SUB1                    PIC S9(5) VALUE +0 COMP-3.
       77  ERACCT-FILE-STATUS      PIC XX    VALUE '00'.
       77  eltrlr-FILE-STATUS      PIC XX    VALUE '00'.
       77  PLAN-STATUS             PIC XX    VALUE '00'.
       77  ELCNTL-STATUS           PIC XX    VALUE '00'.
      /


       01  EXTR-DETAIL-RECORD.
           12  EX-CARRIER              PIC X.
           12  EX-TAB1                 PIC X.
           12  EX-GROUPING             PIC X(6).
           12  EX-TAB2                 PIC X.
           12  EX-STATE                PIC XX.
           12  EX-TAB3                 PIC X.
           12  EX-ACCOUNT              PIC X(10).
           12  EX-TAB4                 PIC X.
           12  EX-EFF                  PIC X(10).
           12  EX-TAB5                 PIC X.
           12  EX-CERT                 PIC X(11).
           12  EX-TAB6                 PIC X.
           12  EX-REPORT-CODE-1        PIC X(10).
           12  EX-TAB7                 PIC X.
           12  EX-LNAME                PIC X(15).
           12  EX-TAB8                 PIC X.
           12  EX-FNAME                PIC X(10).
           12  EX-TAB9                 PIC X.
           12  EX-INIT                 PIC X.
           12  EX-TAB10                PIC X.
           12  EX-AGE                  PIC 99.
           12  EX-TAB11                PIC X.
           12  EX-LF-TYPE              PIC XX.
           12  EX-TAB12                PIC X.
           12  EX-LF-TERM              PIC 999.
           12  EX-TAB13                PIC X.
           12  EX-LF-BEN               PIC -9(9).99.
           12  EX-TAB14                PIC X.
           12  EX-AH-TYPE              PIC XX.
           12  EX-TAB15                PIC X.
           12  EX-AH-TERM              PIC 999.
           12  EX-TAB16                PIC X.
           12  EX-AH-BEN               PIC -9(7).99.
           12  EX-TAB17                PIC X.
           12  EX-LN-OFFICER           PIC XXX.
           12  EX-TAB18                PIC X.
           12  EX-TYPE                 PIC X.
           12  EX-TAB19                PIC X.
           12  EX-CLAIM-AMT            PIC -9(9).99.
           12  EX-TAB20                PIC X.
           12  EX-REI-CLAIM-AMT        PIC -9(9).99.
           12  EX-TAB21                PIC X.
           12  EX-INCUR                PIC X(10).
           12  EX-TAB22                PIC X.
           12  EX-REPORTED             PIC X(8).
           12  EX-TAB23                PIC X.
           12  EX-PAY                  PIC X(10).
           12  EX-TAB24                PIC X.
           12  EX-PAID-TO              PIC X(10).
           12  EX-TAB25                PIC X.
           12  EX-CNUM                 PIC X(7).
           12  EX-TAB26                PIC X.
           12  EX-CHECK                PIC X(7).
           12  EX-TAB27                PIC X.
           12  EX-CLM-PROC-DT          PIC X(10).
           12  EX-TAB28                PIC X.
           12  EX-REIN                 PIC X.
           12  EX-TAB29                PIC X.
           12  EX-REPORT-CODE-2        PIC X(10).
           12  EX-TAB30                PIC X.
           12  EX-REINCO               PIC XXX.
           12  EX-TAB31                PIC X.
           12  EX-REINCO-SUB           PIC XXX.
           12  EX-TAB32                PIC X.
           12  EX-pay-code             PIC X.
           12  ex-tab33                pic x.
           12  ex-acc-name             pic x(30).
           12  ex-tab34                pic x.
           12  ex-paid-from-dt         pic x(10).
           12  EX-TAB35                PIC X.
           12  EX-VOID-DT              PIC X(10).
           12  EX-TAB36                PIC X.
           12  EX-RES-CAT              PIC XX.
      ******************************************************************
       01  WS-MISC.
           05  SUB             PIC S9(3)  COMP-3 VALUE +0.
           05  DUMP                PIC X     VALUE ' '.
           05  FORCE-DUMP REDEFINES DUMP PIC S9 COMP-3.
           05  WS-ERACCT-SW            PIC X VALUE ' '.
               88  ERACCT-FINISHED           VALUE 'Y'.
           05  WS-SAVE-EXTR            PIC X(310) VALUE LOW-VALUES.
           05  WS-DATE                 PIC 9(11) VALUE ZEROS.
           05  FILLER REDEFINES WS-DATE.
               10  FILLER              PIC 999.
               10  WS-CCYY             PIC 9999.
               10  WS-MM               PIC 99.
               10  WS-DD               PIC 99.
           05  WS-BEN-TYPE         PIC XX.
           05  WS-LF-PLAN.
               10  WS-LF-PLAN1     PIC X.
               10  WS-LF-PLAN2     PIC XXX.
           05  WS-AH-PLAN.
               10  WS-AH-PLAN1     PIC X.
               10  WS-AH-PLAN2     PIC XXX.

       01  FILLER.
           05  BENEFIT-CODE-TABLE OCCURS 100 TIMES INDEXED BY BEN-INDEX.
               10  BEN-NUM       PIC XX    VALUE HIGH-VALUE.
               10  BEN-ALPHA     PIC XXX   VALUE HIGH-VALUE.

       01  WS-ABEND-FIELDS.
           12  WS-RETURN-CODE          PIC S9(4)       VALUE ZERO.
           12  WS-ZERO                 PIC S9          VALUE ZERO.
           12  WS-ABEND-MESSAGE        PIC X(80)       VALUE SPACES.
           12  WS-ABEND-FILE-STATUS    PIC XX          VALUE ZERO.


                                       COPY ELCDATE.

           EJECT
       PROCEDURE DIVISION.

           PERFORM 0400-OPEN-FILES     THRU 0400-EXIT

           PERFORM 0600-INITIALIZE     THRU 0600-EXIT

           PERFORM 0100-PROCESS-EXTR   THRU 0100-EXIT UNTIL
                 (END-OF-EXTR)
PEMTST*          OR (EXT-RECS-IN > 5000)

           PERFORM 0500-CLOSE-FILES    THRU 0500-EXIT

           DISPLAY ' EXTR RECORDS READ    '  EXT-RECS-IN
           DISPLAY ' EXTR RECORDS WRITTEN '  EXT-RECS-OUT
           GOBACK
           .

       0100-PROCESS-EXTR.

           MOVE WS-SAVE-EXTR           TO EXTR-DETAIL-RECORD

           IF (DE-CNTRL1 = AM-CONTROL-A) AND
              ((DE-EFF < AM-EXPIRE-DT) AND
              (DE-EFF NOT < AM-EFFECT-DT))
              CONTINUE
           ELSE
              MOVE X'04'               TO AM-COMPANY-CD
              MOVE DE-CONTROL (1:25)   TO AM-CONTROL-PRIMARY (2:25)
              PERFORM 0360-STARTBR-ERACCT
                                       THRU 0360-EXIT
              PERFORM 0370-READNEXT-ERACCT
                                       THRU 0370-EXIT
              PERFORM 0350-MATCH-TO-ERACCT
                                       THRU 0350-EXIT UNTIL
                 ERACCT-FINISHED
           END-IF

           MOVE AM-REPORT-CODE-1       TO EX-REPORT-CODE-1
           MOVE AM-REPORT-CODE-2       TO EX-REPORT-CODE-2

           MOVE DE-REIN                TO EX-REIN
           MOVE DE-CARRIER             TO EX-CARRIER
           MOVE DE-GROUPING            TO EX-GROUPING
           MOVE DE-STATE               TO EX-STATE
           MOVE DE-ACCOUNT             TO EX-ACCOUNT
           MOVE DE-REINCO              TO EX-REINCO
           MOVE DE-REINCO-SUB          TO EX-REINCO-SUB
           MOVE DE-EFF                 TO WS-DATE
           STRING WS-MM '/' WS-DD '/' WS-CCYY DELIMITED BY SIZE
               INTO EX-EFF
           END-STRING

           MOVE DE-CERT                TO EX-CERT
      *    MOVE DE-REPORT-CODE-1       TO EX-REPORT-CODE-1
           MOVE DE-LNAME               TO EX-LNAME
           MOVE DE-FNAME               TO EX-FNAME
           MOVE DE-INIT                TO EX-INIT
           MOVE DE-AGE                 TO EX-AGE
           MOVE DE-LF-TYPE             TO EX-LF-TYPE
           MOVE DE-LF-TERM             TO EX-LF-TERM
           MOVE DE-LF-BEN              TO EX-LF-BEN
           MOVE DE-AH-TYPE             TO EX-AH-TYPE
           MOVE DE-AH-TERM             TO EX-AH-TERM
           MOVE DE-AH-BEN              TO EX-AH-BEN
           MOVE DE-LN-OFFICER          TO EX-LN-OFFICER
           MOVE DE-TYPE                TO EX-TYPE
           MOVE DE-CLAIM-AMT           TO EX-CLAIM-AMT
           MOVE DE-REI-CLAIM-AMT       TO EX-REI-CLAIM-AMT
           move de-pay-code            to ex-pay-code

           MOVE DE-INCUR               TO WS-DATE
           STRING WS-MM '/' WS-DD '/' WS-CCYY DELIMITED BY SIZE
               INTO EX-INCUR
           END-STRING

           STRING DE-RPT-MO '/' DE-RPT-DA '/' DE-RPT-YR
                      DELIMITED BY SIZE
               INTO EX-REPORTED
           END-STRING

           MOVE DE-PAY                 TO WS-DATE
           STRING WS-MM '/' WS-DD '/' WS-CCYY DELIMITED BY SIZE
               INTO EX-PAY
           END-STRING

           MOVE DE-PAID-TO             TO WS-DATE
           STRING WS-MM '/' WS-DD '/' WS-CCYY DELIMITED BY SIZE
               INTO EX-PAID-TO
           END-STRING

           MOVE DE-CNUM                TO EX-CNUM
           MOVE DE-CHECK               TO EX-CHECK

           MOVE DE-CLM-PROC-DT         TO WS-DATE
           STRING WS-MM '/' WS-DD '/' WS-CCYY DELIMITED BY SIZE
               INTO EX-CLM-PROC-DT
           END-STRING
           if de-pmt-trailer-seq not numeric
              move +0                  to de-pmt-trailer-seq
           end-if
           if de-days-disab not numeric
              move zeros               to de-days-disab
           end-if
           move de-company-cd          to at-company-cd
           move de-carrier             to at-carrier
           move de-cnum                to at-claim-no
           move de-cert                to at-cert-no
           move de-pmt-trailer-seq     to at-sequence-no

           IF DE-DEATH
              MOVE EX-INCUR            TO EX-PAID-FROM-DT
           ELSE
           read eltrlr
           if (eltrlr-file-status = '00')
              and (at-trailer-type = '2')
              MOVE at-paid-from-dt     TO DC-BIN-DATE-1
              MOVE ' '                 TO DC-OPTION-CODE
              PERFORM 8510-DATE-CONVERSION
                                       THRU 8590-EXIT
              IF NO-CONVERSION-ERROR
                 MOVE DC-GREG-DATE-A-EDIT TO EX-paid-from-dt
              END-IF
              IF AT-VOID-DT NOT = LOW-VALUES
                 MOVE AT-VOID-DT          TO DC-BIN-DATE-1
                 MOVE ' '                 TO DC-OPTION-CODE
                 PERFORM 8510-DATE-CONVERSION
                                       THRU 8590-EXIT
                 IF NO-CONVERSION-ERROR
                    MOVE DC-GREG-DATE-A-EDIT TO EX-VOID-DT
                 END-IF
              END-IF
           else
              move de-paid-to          to dc-greg-date-cymd
              move 'L'                 to dc-option-code
              PERFORM 8510-DATE-CONVERSION
                                       THRU 8590-EXIT
              if no-conversion-error
                 move +0               to dc-elapsed-months
                 compute dc-elapsed-days = de-days-disab * -1
                 MOVE '6'              TO DC-OPTION-CODE
                 perform 8510-date-conversion thru 8590-exit
                 if no-conversion-error
                    move dc-greg-date-b-edit to ex-paid-from-dt
                 end-if
              end-if
           end-if
           END-IF
           move de-acc-name            to ex-acc-name
           PERFORM 1200-GET-PLAN       THRU 1200-EXIT
           IF (DE-TYPE = '1' OR '3')
              AND (DE-LF-TYPE NOT = ZERO)
              MOVE WS-LF-PLAN TO VCL-PLAN
              PERFORM 0150-GET-PLAN-DATA  THRU 0150-EXIT
              IF PLAN-STATUS = '00'                                     03410000
                MOVE VCL-CLM-RESERVE-CATEGORY-CODE TO EX-RES-CAT
              END-IF
           END-IF
           IF (DE-TYPE = '2' OR '4')
              AND (DE-AH-TYPE NOT = ZERO)
              MOVE WS-AH-PLAN TO VCL-PLAN
              PERFORM 0150-GET-PLAN-DATA  THRU 0150-EXIT
              IF PLAN-STATUS = '00'                                     03410000
                MOVE VCL-CLM-RESERVE-CATEGORY-CODE TO EX-RES-CAT
              END-IF
           END-IF
           PERFORM 0300-WRITE-EXTR     THRU 0300-EXIT

           PERFORM 0200-READ-EXTR      THRU 0200-EXIT

           .

       0100-EXIT.
           EXIT.

       0150-GET-PLAN-DATA.                                              03360000
      *                                                                 03370000
           READ PLAN-CODE-FILE.                                         03390000
                                                                        03400000
           IF PLAN-STATUS = '00'                                        03410000
              CONTINUE                                                  03420000
           ELSE                                                         03460000
           IF PLAN-STATUS = '23'                                        03470000
              DISPLAY 'PLAN CODE NOT FOUND: ' VCL-PLAN                  03480000
           ELSE                                                         03490000
              DISPLAY 'READ ERROR ' PLAN-STATUS ' ON PLNCODE'           03500000
              ADD +1 TO FORCE-DUMP.                                     03510000
                                                                        03520000
       0150-EXIT.                                                       03530000
           EXIT.                                                        03540000
                                                                        03550000
       0200-READ-EXTR.

           READ EXTR-FILE-IN AT END
              SET END-OF-EXTR          TO TRUE
           END-READ


           IF NOT END-OF-EXTR
              ADD 1 TO EXT-RECS-IN
           END-IF

           .

       0200-EXIT.
           EXIT.

       0300-WRITE-EXTR.

           WRITE EXTR-FILE-OUT-REC     FROM EXTR-DETAIL-RECORD
           ADD 1 TO EXT-RECS-OUT

           .

       0300-EXIT.
           EXIT.

       0350-MATCH-TO-ERACCT.

           IF (DE-CNTRL1 > AM-CONTROL-A)
              PERFORM 0370-READNEXT-ERACCT
                                       THRU 0370-EXIT
           ELSE
              IF (DE-CNTRL1 = AM-CONTROL-A) AND
                 ((DE-EFF < AM-EXPIRE-DT)   AND
                 (DE-EFF NOT < AM-EFFECT-DT))
                 SET ERACCT-FINISHED TO TRUE
              ELSE
                 IF DE-CNTRL1 < AM-CONTROL-A
                    MOVE 'NOT FOUND'   TO AM-REPORT-CODE-1
                                          AM-REPORT-CODE-2
                    SET ERACCT-FINISHED TO TRUE
                 ELSE
                    PERFORM 0370-READNEXT-ERACCT
                                       THRU 0370-EXIT
                 END-IF
              END-IF
           END-IF

           .
       0350-EXIT.
           EXIT.

       0360-STARTBR-ERACCT.

           MOVE ' '                    TO WS-ERACCT-SW
      *    MOVE LOW-VALUES             TO AM-CONTROL-PRIMARY
      *    MOVE X'04'                  TO AM-COMPANY-CD
           START ERACCT KEY IS NOT < AM-CONTROL-PRIMARY

           IF ERACCT-FILE-STATUS = '00'
              CONTINUE
           ELSE
              DISPLAY ' ERROR ON STARTBR ERACCT '
                            ERACCT-FILE-STATUS
              PERFORM ABEND-PGM
           END-IF

           .
       0360-EXIT.
           EXIT.

       0370-READNEXT-ERACCT.

           READ ERACCT NEXT RECORD

           IF ERACCT-FILE-STATUS = '00'
              if am-company-cd > x'04'
                 move high-values      to am-control-primary
              end-if
           ELSE
              IF ERACCT-FILE-STATUS = '10' OR '23'
                 MOVE HIGH-VALUES      TO AM-CONTROL-PRIMARY
              ELSE
                 DISPLAY ' ERROR ON READNEXT ERACCT '
                            ERACCT-FILE-STATUS
                 PERFORM ABEND-PGM
              END-IF
           END-IF

           .
       0370-EXIT.
           EXIT.


       0400-OPEN-FILES.

           OPEN INPUT EXTR-FILE-IN
                      ERACCT PLAN-CODE-FILE
                      eltrlr
               OUTPUT EXTR-FILE-OUT

           IF PLAN-STATUS = '00' OR '97'                                01390000
               CONTINUE                                                 01400000
           ELSE                                                         01410000
               DISPLAY 'OPEN ERROR ' PLAN-STATUS ' ON PLNCODE'          01420000
               DISPLAY '*** JOB CANCELLED ***'                          01430000
               ADD +1 TO FORCE-DUMP                                     01440000
           END-IF                                                       01450000
           IF ERACCT-FILE-STATUS = '00' OR '97'
              CONTINUE
           ELSE
              DISPLAY ' ERROR ON OPEN  - ERACCT ' ERACCT-FILE-STATUS
              PERFORM ABEND-PGM
           END-IF

           IF eltrlr-FILE-STATUS = '00' OR '97'
              CONTINUE
           ELSE
              DISPLAY ' ERROR ON OPEN  - eltrlr ' eltrlr-FILE-STATUS
              PERFORM ABEND-PGM
           END-IF

           .

       0400-EXIT.
           EXIT.

       0500-CLOSE-FILES.

           CLOSE EXTR-FILE-IN EXTR-FILE-OUT ERACCT eltrlr
               PLAN-CODE-FILE
           IF ERACCT-FILE-STATUS = '00' OR '97'
              CONTINUE
           ELSE
              DISPLAY ' ERROR ON CLOSE - ERACCT ' ERACCT-FILE-STATUS
              PERFORM ABEND-PGM
           END-IF

           IF eltrlr-FILE-STATUS = '00' OR '97'
              CONTINUE
           ELSE
              DISPLAY ' ERROR ON CLOSE - eltrlr ' eltrlr-FILE-STATUS
              PERFORM ABEND-PGM
           END-IF

           .

       0500-EXIT.
           EXIT.


       0600-INITIALIZE.

           MOVE SPACES                 TO EXTR-DETAIL-RECORD
           MOVE X'09'                  TO EX-TAB1
                                          EX-TAB2
                                          EX-TAB3
                                          EX-TAB4
                                          EX-TAB5
                                          EX-TAB6
                                          EX-TAB7
                                          EX-TAB8
                                          EX-TAB9
                                          EX-TAB10
                                          EX-TAB11
                                          EX-TAB12
                                          EX-TAB13
                                          EX-TAB14
                                          EX-TAB15
                                          EX-TAB16
                                          EX-TAB17
                                          EX-TAB18
                                          EX-TAB19
                                          EX-TAB20
                                          EX-TAB21
                                          EX-TAB22
                                          EX-TAB23
                                          EX-TAB24
                                          EX-TAB25
                                          EX-TAB26
                                          EX-TAB27
                                          EX-TAB28
                                          EX-TAB29
                                          EX-TAB30
                                          EX-TAB31
                                          ex-tab32
                                          ex-tab33
                                          ex-tab34
                                          EX-TAB35
                                          EX-TAB36

           MOVE EXTR-DETAIL-RECORD     TO WS-SAVE-EXTR
           PERFORM 9000-LOAD-BENEFIT-TABLE THRU 9000-EXIT
           MOVE LOW-VALUES             TO AM-CONTROL-PRIMARY
           MOVE X'04'                  TO AM-COMPANY-CD
           PERFORM 0360-STARTBR-ERACCT THRU 0360-EXIT
           PERFORM 0370-READNEXT-ERACCT
                                       THRU 0370-EXIT
           PERFORM 0200-READ-EXTR      THRU 0200-EXIT
           .

       0600-EXIT.
           EXIT.

       1200-GET-PLAN.
      *
           MOVE SPACES TO WS-LF-PLAN
           MOVE SPACES TO WS-AH-PLAN

           IF DE-LF-TYPE NOT = ZERO
              IF DE-IG = '1'
                 MOVE 'I' TO WS-LF-PLAN1
              ELSE
                 MOVE 'G' TO WS-LF-PLAN1
              END-IF
              SET BEN-INDEX TO +1
              SEARCH BENEFIT-CODE-TABLE
                 WHEN BEN-NUM (BEN-INDEX) = DE-LF-TYPE
                   MOVE BEN-ALPHA (BEN-INDEX) TO WS-LF-PLAN2
                 WHEN BEN-NUM (BEN-INDEX) = HIGH-VALUE
                   MOVE ZEROS TO WS-LF-PLAN2
                   DISPLAY 'INVALID LIFE BEN TYPE: '
                            DE-LF-TYPE '  ' DE-CONTROL
               END-SEARCH
           END-IF

           IF DE-AH-TYPE NOT = ZERO
              IF DE-IG = '1'
                 MOVE 'I' TO WS-AH-PLAN1
              ELSE
                 MOVE 'G' TO WS-AH-PLAN1
              END-IF
              MOVE DE-AH-TYPE TO WS-AH-PLAN2
           END-IF
           .
       1200-EXIT.
           EXIT.

       8510-DATE-CONVERSION.

           CALL 'ELDATCX' USING DATE-CONVERSION-DATA

           .

       8590-EXIT.
           EXIT.

       9000-LOAD-BENEFIT-TABLE.
      *
           OPEN INPUT LOGIC-CONTROL-FILE
           IF ELCNTL-STATUS = '00' OR '97'
               MOVE '00' TO ELCNTL-STATUS
           ELSE
               DISPLAY 'OPEN ERROR ' ELCNTL-STATUS ' ON ELCNTL'
               ADD +1 TO FORCE-DUMP.

           SET BEN-INDEX TO +1.
           READ LOGIC-CONTROL-FILE

           PERFORM UNTIL ELCNTL-STATUS NOT = '00'
             IF (CF-COMPANY-ID = 'CID') AND (CF-RECORD-TYPE = '4')
                PERFORM VARYING SUB FROM 1 BY 1 UNTIL SUB > 8
                  MOVE CF-BENEFIT-NUMERIC (SUB) TO BEN-NUM (BEN-INDEX)
                  MOVE CF-BENEFIT-ALPHA (SUB) TO BEN-ALPHA (BEN-INDEX)
                  SET BEN-INDEX UP BY +1
                  IF BEN-INDEX > 100 DISPLAY
                    'TABLE OVERFLOW IN ROUTINE 0100-LOAD-BENEFIT-TABLE'
                    ADD +1 TO FORCE-DUMP
                  END-IF
                END-PERFORM
             END-IF
             READ LOGIC-CONTROL-FILE
           END-PERFORM

           CLOSE LOGIC-CONTROL-FILE
           .
       9000-EXIT.
           EXIT.

       ABEND-PGM SECTION.
                                     COPY ELCABEND.

