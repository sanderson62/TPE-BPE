00001  IDENTIFICATION DIVISION.
00002
00003  PROGRAM-ID.                 EL650 .
00004 *              PROGRAM CONVERTED BY
00005 *              COBOL CONVERSION AID PO 5785-ABJ
00006 *              CONVERSION DATE 07/14/94 14:53:46.
00007 *            PROGRAM REVIEWED FOR YEAR 2000 COMPLIANCE
00008 *                            VMOD=2.048.
00009
00010 *AUTHOR.        LOGIC,INC.
00011 *               DALLAS, TEXAS.
00012
00013 *DATE-COMPILED.
00014
00015 *SECURITY.   *****************************************************
00016 *            *                                                   *
00017 *            *   THIS PROGRAM IS THE PROPERTY OF LOGIC, INC.     *
00018 *            *                                                   *
00019 *            *   USE OF THIS PROGRAM BY OTHER THAN THE EMPLOYEES *
00020 *            *   OF LOGIC, INC. IS EXPRESSLY PROHIBITED WITHOUT  *
00021 *            *   THE PRIOR WRITTEN PERMISSION OF LOGIC INC.      *
00022 *            *                                                   *
00023 *            *****************************************************
00024 *
00025 *REMARKS.
00026 *        TRANSACTION - EXC4 - ACCOUNT MASTER MAINTENANCE.
00024 *
101101******************************************************************
101101*                   C H A N G E   L O G
101101*
101101* CHANGES ARE MARKED BY THE CHANGE EFFECTIVE DATE.
101101*-----------------------------------------------------------------
101101*  CHANGE   CHANGE REQUEST PGMR  DESCRIPTION OF CHANGE
101101* EFFECTIVE    NUMBER
101101*-----------------------------------------------------------------
101101* 101101    2001100100006  SMVA  ADD USERID & COMPANY ID(CMPNYID)
101101*                              ADJUSTED REDEFINES EL650AI FILLER
100703* 100703    2003080800002  PEMA  ADD SUPER GAP PROCESSING
110706* 110706  CR2006071700004  PEMA  ADD BRANCH LOCATIONS
110706*           AND SHIPPING ADDRESS TO ACCOUNT NOTES FILE
121207* 121207  CR2007121100001  PEMA  SPACE OUT ORIG DLR NO ON COPY
011410* 011410  CR2009050700003  PEMA  ADD SPP DEALER DIRECT
080612* 080612  CR2012042700005  PEMA  ADD OVER 120 DAYS FOR AHL
111513* 111513  IR2013110500003  PEMA  CORRECT MAINT FUNC OF "N"
052814* 052814  CR2014012300001  PEMA  DCC CREDIT UNION CHANGES
020816* 020816  CR2015082500001  PEMA  ADD PROCESSING FOR NEW COMP VPP
101101******************************************************************
00027
00028  ENVIRONMENT DIVISION.
00029  DATA DIVISION.
00030  EJECT
00031  WORKING-STORAGE SECTION.
00032  77  FILLER  PIC  X(32) VALUE '********************************'.
00033  77  FILLER  PIC  X(32) VALUE '*    EL650 WORKING STORAGE     *'.
00034  77  FILLER  PIC  X(32) VALUE '********* VMOD=2.048 ***********'.
111513 77  d1                      pic s999 value +0 comp-3.
00035
00036  01  WS-COMM-LENGTH          PIC S9(4) COMP VALUE +1500.
00037
111513 01  ws-display-eff-date     pic x(10) value spaces.
111513 01  ws-display-exp-date     pic x(10) value spaces.
00038  01  WS-DATE-AREA.
00039      12  SAVE-DATE           PIC  X(8)       VALUE SPACES.
00040      12  SAVE-BIN-DATE       PIC  XX         VALUE SPACES.
00041
00042  01  STANDARD-AREAS.
00043      12  GETMAIN-SPACE           PIC  X      VALUE SPACE.
00044      12  MAP-NAME                PIC  X(8)   VALUE 'EL650A'.
00045      12  MAPSET-NAME             PIC  X(8)   VALUE 'EL650S'.
00046      12  SCREEN-NUMBER           PIC  X(4)   VALUE '650A'.
00047      12  TRANS-ID                PIC  X(4)   VALUE 'EXC4'.
00048      12  EL6311-TRANS-ID         PIC  X(4)   VALUE 'EXB1'.
020816     12  vp6311-trans-id         pic  x(4)   value 'VPB1'.
CIDMOD     12  EL1273-TRANS-ID         PIC  X(4)   VALUE 'EXX3'.
CIDMOD     12  EL652-TRANS-ID          PIC  X(4)   VALUE 'EXD4'.
100703     12  EL6523-TRANS-ID         PIC  X(4)   VALUE 'EXDD'.
00049      12  EL6311NC-TRANS-ID       PIC  X(4)   VALUE 'NXB1'.
00050      12  EL640-TRANS-ID          PIC  X(4)   VALUE 'EXC1'.
00051      12  EL658-TRANS-ID          PIC  X(4)   VALUE 'EXJ3'.
00052      12  EL130-TRANS-ID          PIC  X(4)   VALUE 'EX19'.
110706     12  EL6503-TRANS-ID         PIC  X(4)   VALUE 'EXC7'.
00053      12  EL6509-TRANS-ID         PIC  X(4)   VALUE 'EXDA'.
00054      12  EL6592-TRANS-ID         PIC  X(4)   VALUE 'EX66'.
00055      12  EL608-TRANS-ID          PIC  X(4)   VALUE 'EX1E'.
00056      12  THIS-PGM                PIC  X(8)   VALUE 'EL650'.
00057      12  PGM-NAME                PIC  X(8).
00058      12  RETURNED-FROM           PIC  X(8)   VALUE SPACES.
00059      12  TIME-IN                 PIC S9(7)   VALUE ZEROS.
00060      12  TIME-OUT-R  REDEFINES  TIME-IN.
00061          16  FILLER              PIC  X.
00062          16  TIME-OUT            PIC  99V99.
00063          16  FILLER              PIC  XX.
00064      12  XCTL-005                PIC  X(8)   VALUE 'EL005'.
00065      12  XCTL-010                PIC  X(8)   VALUE 'EL010'.
00066      12  XCTL-126                PIC  X(8)   VALUE 'EL126'.
00067      12  XCTL-EL601              PIC  X(8)   VALUE 'EL601 '.
00068      12  XCTL-6501               PIC  X(8)   VALUE 'EL6501'.
00069      12  XCTL-6502               PIC  X(8)   VALUE 'EL6502'.
110706     12  XCTL-6503               PIC  X(8)   VALUE 'EL6503'.
00071      12  XCTL-6504               PIC  X(8)   VALUE 'EL6504'.
00072      12  XCTL-6505               PIC  X(8)   VALUE 'EL6505'.
00073      12  XCTL-6506               PIC  X(8)   VALUE 'EL6506'.
00074      12  XCTL-6508               PIC  X(8)   VALUE 'EL6508'.
00075      12  XCTL-6509               PIC  X(8)   VALUE 'EL6509'.
00076      12  LINK-001                PIC  X(8)   VALUE 'EL001'.
00077      12  LINK-004                PIC  X(8)   VALUE 'EL004'.
00078      12  LINK-ELDATCV            PIC  X(8)   VALUE 'ELDATCV'.
00079      12  FILE-ID                 PIC  X(8)   VALUE SPACES.
00080      12  CNTL-FILE-ID            PIC  X(8)   VALUE 'ELCNTL'.
00081      12  ACCT-FILE-ID            PIC  X(8)   VALUE 'ERACCT'.
00082      12  COMP-FILE-ID            PIC  X(8)   VALUE 'ERCOMP'.
00083      12  NAME-FILE-ID            PIC  X(8)   VALUE 'ERNAME'.
00084      12  PEND2-FILE-ID           PIC  X(8)   VALUE 'ERPNDB2'.
00085      12  PLAN-FILE-ID            PIC  X(8)   VALUE 'ERPLAN'.
00086      12  ACCT2-FILE-ID           PIC  X(8)   VALUE 'ERACCT2'.
00087      12  ACCT-REC-LEN            PIC S9(4)   VALUE +2000  COMP.
00088      12  PLAN-REC-LEN            PIC S9(4)   VALUE +420   COMP.
00089      12  COMP-REC-LEN            PIC S9(4)   VALUE +700   COMP.
00090      12  NAME-REC-LEN            PIC S9(4)   VALUE +160   COMP.
00091      12  SC-ITEM                 PIC S9(4)   VALUE +1     COMP.
00092      12  ERGXRF-FILE-ID          PIC  X(8)   VALUE 'ERGXRF'.
00093      12  ERGXRF-REC-LEN          PIC S9(4)   VALUE +0     COMP.
00094      12  ERGXRF-INC-LEN          PIC S9(4)   VALUE +31    COMP.
00095      12  ERGXRF-MIN-LEN          PIC S9(4)   VALUE +109   COMP.
00096      12  ERGXRF-MAX-LEN          PIC S9(8)   VALUE +31264 COMP.
00097      12  BIN-MIN-DATE            PIC  XX     VALUE LOW-VALUES.
00098      12  BIN-MAX-DATE            PIC  XX     VALUE LOW-VALUES.
00099      12  B4-ACCT-KEY.
00100          16  B4-ACCT-CO          PIC  X      VALUE LOW-VALUES.
00101          16  B4-ACCT-CARRIER     PIC  X      VALUE SPACES.
00102          16  B4-ACCT-GROUPING    PIC  X(6)   VALUE SPACES.
00103          16  B4-ACCT-STATE       PIC  XX     VALUE SPACES.
00104          16  B4-ACCT-ACCOUNT     PIC  X(10)  VALUE SPACES.
00105          16  B4-ACCT-EXP-DT      PIC  XX     VALUE LOW-VALUES.
00106          16  B4-ACCT-EFF-DT      PIC  XX     VALUE LOW-VALUES.
00107      12  AF-ACCT-KEY.
00108          16  AF-ACCT-CO          PIC  X      VALUE LOW-VALUES.
00109          16  AF-ACCT-CARRIER     PIC  X      VALUE SPACES.
00110          16  AF-ACCT-GROUPING    PIC  X(6)   VALUE SPACES.
00111          16  AF-ACCT-STATE       PIC  XX     VALUE SPACES.
00112          16  AF-ACCT-ACCOUNT     PIC  X(10)  VALUE SPACES.
00113          16  AF-ACCT-EXP-DT      PIC  XX     VALUE LOW-VALUES.
00114          16  AF-ACCT-EFF-DT      PIC  XX     VALUE LOW-VALUES.
00115      12  GX-AM-CONTROL-PRIMARY.
00116          16  GX-AM-COMPANY-CD    PIC  X      VALUE LOW-VALUES.
00117          16  GX-REST-OF-KEY      PIC  X(21)  VALUE SPACES.
00118          16  GX-AM-CNTL-FILLER   PIC  X(4)   VALUE SPACES.
00119  EJECT
                                       copy ERCGXRF.
00120  01  WORK-AREAS-AND-SWITCHES.
00121      12  WS-PAGE-LINES               PIC  S99    VALUE ZEROS.
00122      12  WS-ACCT2-FOUND              PIC  X      VALUE 'N'.
00123      12  GETMAIN-SW                  PIC  X      VALUE SPACES.
00124      12  NEW-KEY-SAVE.
00125          16  NEW-KEY-COMPANY-CD      PIC  X      VALUE LOW-VALUES.
00126          16  NEW-KEY-CNTRL           PIC  X(19)  VALUE SPACES.
00127      12  WS-PLAN-KEY.
00128          16  EK-CCGSA-KEY.
00129            18  EK-COMPANY-CD         PIC  X     VALUE LOW-VALUES.
00130            18  EK-CARRIER            PIC  X     VALUE SPACES.
00131            18  EK-GROUPING           PIC  X(6)  VALUE SPACES.
00132            18  EK-STATE              PIC  XX    VALUE SPACES.
00133            18  EK-ACCOUNT            PIC  X(10) VALUE SPACES.
00134          16  EK-BTYPE                PIC  X     VALUE SPACES.
00135          16  EK-BCODE                PIC  XX    VALUE SPACES.
00136          16  EK-REVISION             PIC  XXX   VALUE SPACES.
00137      12  WS-EXP-SAVE                 PIC  XX    VALUE LOW-VALUES.
00138      12  CURRENT-SAVE                PIC  X(6)  VALUE SPACES.
00139      12  SAVE-ACCT-KEY               PIC  X(26).
00140      12  SAVE-CURR-EXP-DT            PIC  XX    VALUE LOW-VALUES.
00141      12  SAVE-CURR-EFF-DT            PIC  XX    VALUE LOW-VALUES.
00142      12  WS-EFFCHG-SAVE              PIC  9(11) VALUE ZEROS.
00143      12  WS-EXPCHG-SAVE              PIC  9(11) VALUE ZEROS.
00144      12  BIN-CURRENT-SAVE            PIC  XX    VALUE LOW-VALUES.
00145      12  WS-VERIFY-AREA.
00146          16  FILLER      OCCURS  33  TIMES
00147                              INDEXED  BY  WS-INDEX.
00148              20  WS-ACCT-KEY         PIC  X(20).
00149              20  WS-ACCT-EXP-DT      PIC  XX.
00150              20  WS-ACCT-EFF-DT      PIC  XX.
00151              20  WS-ACCT-HI-CERT     PIC  XX.
00152              20  WS-ACCT-LO-CERT     PIC  XX.
00153      12  WS-TEST-KEY                 PIC  X(20)  VALUE SPACES.
00154      12  WS-SAVE-VERIFY-KEY          PIC  X(20)  VALUE SPACES.
00155      12  WS-ACCT-VERIFY-KEY.
00156          16  WS-ACCT-VER-COMP-CD     PIC  X      VALUE LOW-VALUES.
00157          16  WS-ACCT-VER-CARR        PIC  X      VALUE SPACES.
00158          16  WS-ACCT-VER-GROUP       PIC  X(6)   VALUE SPACES.
00159          16  WS-ACCT-VER-STATE       PIC  XX     VALUE SPACES.
00160          16  WS-ACCT-VER-ACCOUNT     PIC  X(10)  VALUE SPACES.
00161          16  WS-ACCT-VER-EXP-DT      PIC  XX     VALUE LOW-VALUES.
pemtst     12  filler                      pic x(10)   value spaces.
00162      12  KEY-SAVE.
00163          16  SV-CO                   PIC  X      VALUE LOW-VALUES.
00164          16  SV-CARRIER              PIC  X      VALUE SPACES.
00165          16  SV-GROUPING             PIC  X(6)   VALUE SPACES.
00166          16  SV-STATE                PIC  XX     VALUE SPACES.
00167          16  SV-ACCOUNT              PIC  X(10)  VALUE SPACES.
00168
00169      12  WS-DEEDIT-FIELD              PIC X(10)  VALUE SPACES.
00170      12  WS-DT-DEEDIT-FIELD REDEFINES WS-DEEDIT-FIELD
00171                                       PIC X(10).
00172      12  WS-DEEDIT-FIELD-DATE REDEFINES WS-DT-DEEDIT-FIELD.
00173          16  FILLER                   PIC X(4).
00174          16  WS-DEEDIT-FIELD-DATE-OUT PIC X(6).
00175
00176      12  WS-AXRF-KEY.
00177          16  WS-AXRF-COMPANY-CD      PIC  X     VALUE LOW-VALUES.
00178          16  WS-AXRF-CARRIER         PIC  X     VALUE SPACES.
00179          16  WS-AXRF-GROUPING        PIC  X(6)  VALUE SPACES.
00180          16  WS-AXRF-AGENT-NO        PIC  X(10) VALUE SPACES.
00181      12  AXRF-SUB                    PIC S9(4) COMP VALUE +0.
00182      12  GX-AGENT-SUB                PIC S9(4) COMP VALUE +0.
00183      12  COMM-WORK-SUB               PIC S9(4) COMP VALUE +0.
00184      12  COMM-WORK-SUB2              PIC S9(4) COMP VALUE +0.
00185      12  ELCNTL-KEY.
00186          16  CNTL-COMP-ID            PIC  X(3) VALUE SPACES.
00187          16  CNTL-REC-TYPE           PIC  X    VALUE SPACES.
00188          16  CNTL-ACCESS.
00189              20  CNTL-STATE          PIC  XX   VALUE SPACES.
00190              20  FILLER              PIC  X    VALUE SPACES.
00191              20  CNTL-CARRIER        PIC  X    VALUE SPACES.
00192          16  CNTL-SEQ-NO             PIC S9(4) COMP VALUE +0.
00193      12  ERPNDB-KEY.
00194          16  PNDB-COMPANY-CD-A1      PIC  X  VALUE LOW-VALUES.
00195          16  PNDB-CARRIER            PIC  X    VALUE SPACES.
00196          16  PNDB-GROUPING           PIC  X(6) VALUE SPACES.
00197          16  PNDB-STATE              PIC  XX   VALUE SPACES.
00198          16  PNDB-ACCOUNT            PIC  X(10) VALUE SPACES.
00199          16  PNDB-CERT-EFF-DT        PIC  XX    VALUE LOW-VALUES.
00200          16  PNDB-CERT-NO            PIC  X(11) VALUE SPACES.
00201          16  PNDB-ALT-CHG-SEQ-NO     PIC S9(4)   COMP VALUE +0.
00202          16  PNDB-RECORD-TYPE        PIC  X   VALUE SPACES.
00203      12  REC-SW                      PIC  9          VALUE 0.
00204          88  REC-FOUND                               VALUE 1.
00205      12  K-WITH-LINE-USED            PIC  9          VALUE 0.
00206          88  K-WITH-LINE-NO                          VALUE 1.
00207      12  RETURN-FROM-SW              PIC  9          VALUE 0.
00208          88  RETURNING-FROM-6501                     VALUE 1.
00209      12  BROWSE-STARTED-SW           PIC  X          VALUE ' '.
00210          88  BROWSE-STARTED                          VALUE 'Y'.
00211      12  FIRST-TIME-SW               PIC  X          VALUE ' '.
00212          88  FIRST-TIME                              VALUE 'Y'.
00213      12  FROM-WHERE-SW               PIC  X          VALUE ' '.
00214          88  FROM-DELETE                             VALUE '1'.
00215          88  FROM-CHANGE                             VALUE '2'.
00216      12  FROM-EL608-SW               PIC  X          VALUE 'N'.
00217      12  PENDING-BUSINESS-SW         PIC  X          VALUE 'N'.
00218          88  THERE-IS-PENDING-BUSINESS               VALUE 'Y'.
00219      12  PENDING-BROWSE-SW           PIC  X          VALUE 'N'.
00220          88  PENDING-BROWSE-STARTED                  VALUE 'Y'.
00221      12  TS-QUEUE.
00222          16  TS-SCREEN               PIC  X(4)   VALUE SPACES.
00223          16  TS-TERM                 PIC  X(4)   VALUE SPACES.
00224      12  TS-LENGTH                   PIC S9(4) COMP VALUE +640.
00225      12  TS-ITEM                     PIC S9(4) COMP VALUE +0.
00226      12  AGT-SUB                     PIC 99      VALUE ZEROS.
00227      12  VALID-AGT-SW                PIC X       VALUE 'N'.
00228
00229  01  ERROR-MESSAGES.
00230      12  ER-0000             PIC  X(4)       VALUE '0000'.
00231      12  ER-0004             PIC  X(4)       VALUE '0004'.
00232      12  ER-0008             PIC  X(4)       VALUE '0008'.
00233      12  ER-0022             PIC  X(4)       VALUE '0022'.
00234      12  ER-0023             PIC  X(4)       VALUE '0023'.
00235      12  ER-0029             PIC  X(4)       VALUE '0029'.
00236      12  ER-0042             PIC  X(4)       VALUE '0042'.
00237      12  ER-0050             PIC  X(4)       VALUE '0050'.
00238      12  ER-0052             PIC  X(4)       VALUE '0052'.
00239      12  ER-0066             PIC  X(4)       VALUE '0066'.
00240      12  ER-0067             PIC  X(4)       VALUE '0067'.
00241      12  ER-0070             PIC  X(4)       VALUE '0070'.
00242      12  ER-0144             PIC  X(4)       VALUE '0144'.
00243      12  ER-0168             PIC  X(4)       VALUE '0168'.
00244      12  ER-0193             PIC  X(4)       VALUE '0193'.
00245      12  ER-0195             PIC  X(4)       VALUE '0195'.
00246      12  ER-0216             PIC  X(4)       VALUE '0216'.
00247      12  ER-0226             PIC  X(4)       VALUE '0226'.
00248      12  ER-0348             PIC  X(4)       VALUE '0348'.
00249      12  ER-0453             PIC  X(4)       VALUE '0453'.
00250      12  ER-0454             PIC  X(4)       VALUE '0454'.
00251      12  ER-0455             PIC  X(4)       VALUE '0455'.
00252      12  ER-0456             PIC  X(4)       VALUE '0456'.
00253      12  ER-0457             PIC  X(4)       VALUE '0457'.
00254      12  ER-0470             PIC  X(4)       VALUE '0470'.
00255      12  ER-0471             PIC  X(4)       VALUE '0471'.
00256      12  ER-0472             PIC  X(4)       VALUE '0472'.
00257      12  ER-0482             PIC  X(4)       VALUE '0482'.
00258      12  ER-0561             PIC  X(4)       VALUE '0561'.
00259      12  ER-0619             PIC  X(4)       VALUE '0619'.
00260      12  ER-0630             PIC  X(4)       VALUE '0630'.
00261      12  ER-0668             PIC  X(4)       VALUE '0668'.
00262      12  ER-0899             PIC  X(4)       VALUE '0899'.
00263      12  ER-1220             PIC  X(4)       VALUE '1220'.
00264      12  ER-1228             PIC  X(4)       VALUE '1228'.
00265      12  ER-1238             PIC  X(4)       VALUE '1238'.
00265      12  ER-1299             PIC  X(4)       VALUE '1299'.
00266      12  ER-1612             PIC  X(4)       VALUE '1612'.
00267      12  ER-2051             PIC  X(4)       VALUE '2051'.
00268      12  ER-2052             PIC  X(4)       VALUE '2052'.
00269      12  ER-2053             PIC  X(4)       VALUE '2053'.
00270      12  ER-2054             PIC  X(4)       VALUE '2054'.
00271      12  ER-2058             PIC  X(4)       VALUE '2058'.
00272      12  ER-2059             PIC  X(4)       VALUE '2059'.
00273      12  ER-2060             PIC  X(4)       VALUE '2060'.
00274      12  ER-2061             PIC  X(4)       VALUE '2061'.
00275      12  ER-2062             PIC  X(4)       VALUE '2062'.
00276      12  ER-2063             PIC  X(4)       VALUE '2063'.
00277      12  ER-2064             PIC  X(4)       VALUE '2064'.
00278      12  ER-2065             PIC  X(4)       VALUE '2065'.
00279      12  ER-2066             PIC  X(4)       VALUE '2066'.
00280      12  ER-2068             PIC  X(4)       VALUE '2068'.
00281      12  ER-2069             PIC  X(4)       VALUE '2069'.
00282      12  ER-2070             PIC  X(4)       VALUE '2070'.
00283      12  ER-2072             PIC  X(4)       VALUE '2072'.
00284      12  ER-2073             PIC  X(4)       VALUE '2073'.
00285      12  ER-2074             PIC  X(4)       VALUE '2074'.
00286      12  ER-2075             PIC  X(4)       VALUE '2075'.
00287      12  ER-2087             PIC  X(4)       VALUE '2087'.
00288      12  ER-2113             PIC  X(4)       VALUE '2113'.
00289      12  ER-2151             PIC  X(4)       VALUE '2151'.
00290      12  ER-2370             PIC  X(4)       VALUE '2370'.
00291      12  ER-2572             PIC  X(4)       VALUE '2572'.
00292      12  ER-3155             PIC  X(4)       VALUE '3155'.
00293      12  ER-3785             PIC  X(4)       VALUE '3785'.
00294      12  ER-9638             PIC  X(4)       VALUE '9638'.
00294      12  ER-9999             PIC  X(4)       VALUE '9999'.
00295
00296  01  TEMP-STORAGE-RECORD.
00297      12  TS-ACCT-KEY     OCCURS  32  TIMES
00298                              INDEXED  BY  TS-INDEX
00299                                  PIC  X(20).
00300      EJECT
00301      COPY ELCSCTM.
00302      EJECT
00303      COPY ELCSCRTY.
00304      EJECT
00305      COPY ELCLOGOF.
00306      EJECT
00307      COPY ELCDATE.
00308      EJECT
00309      COPY ELCATTR.
00310      EJECT
00311      COPY ELCEMIB.
00312      EJECT
00313      COPY ELCINTF.
00314      COPY ELC650PI.
00315 *          16  PI-NAMEFLG       PIC X.
00316
00317      COPY ELCJPFX.
00318                                  PIC  X(2000).
00319  EJECT
00320      COPY ELCAID.
00321
00322  01  FILLER  REDEFINES  DFHAID.
00323      12  FILLER              PIC  X(8).
00324      12  PF-VALUES           PIC  X      OCCURS  24  TIMES.
00325      12  FILLER              PIC  X(3).
00326  EJECT
00327      COPY EL650S.
00328  EJECT
00329  01  EL650R  REDEFINES  EL650AI.
101101     12  FILLER                      PIC  X(153).
00331      12  MAP-RECORD-AREA.
00332          16  DATE-RANGES     OCCURS  8  TIMES
00333                                  INDEXED  BY  M-INDEX.
00334              20  FILLER              PIC  X(3).
00335              20  MAP-EFF-DT          PIC  X(8).
00336              20  FILLER              PIC  X(6).
00337              20  MAP-EXP-DT          PIC  X(8).
00338              20  MAP-EXP-DT-ED  REDEFINES
00339                  MAP-EXP-DT          PIC 99B99B99.
00340              20  FILLER              PIC  X(4).
00341              20  MAP-LST-MAINT-DT    PIC  X(8).
00342              20  FILLER              PIC  X(3).
00343              20  MAP-LOW-CERT        PIC  X(8).
00344              20  FILLER              PIC  X(3).
00345              20  MAP-HIGH-CERT       PIC  X(8).
00346              20  FILLER              PIC  X(4).
00347              20  MAP-AR-DATE         PIC  X(8).
00348              20  FILLER              PIC  XX.
00349      12  FILLER                      PIC  X(286).
00350  EJECT
00351  LINKAGE SECTION.
00352  01  DFHCOMMAREA             PIC  X(1500).
00353  EJECT
00354 *01 PARMLIST .
00355 *    12  FILLER              PIC S9(8)   COMP.
00356 *    12  ERACCT-POINTER      PIC S9(8)   COMP.
00357 *    12  ERCOMP-POINTER      PIC S9(8)   COMP.
00358 *    12  ERNAME-POINTER      PIC S9(8)   COMP.
00359 *    12  ERPLAN-POINTER      PIC S9(8)   COMP.
00360 *    12  ELCNTL-POINTER      PIC S9(8)   COMP.
00361 *    12  ERPNDB-POINTER      PIC S9(8)   COMP.
00362 *    12  AXRF-POINTER        PIC S9(8)   COMP.
00363 *    12  AXRF2-POINTER       PIC S9(8)   COMP.
00364 *    12  AXRF3-POINTER       PIC S9(8)   COMP.
00365 *    12  AXRF4-POINTER       PIC S9(8)   COMP.
00366 *    12  AXRF5-POINTER       PIC S9(8)   COMP.
00367 *    12  AXRF6-POINTER       PIC S9(8)   COMP.
00368 *    12  AXRF7-POINTER       PIC S9(8)   COMP.
00369 *    12  AXRF8-POINTER       PIC S9(8)   COMP.
00370  EJECT
00371      COPY ERCACCT.
00372  EJECT
00373      COPY ERCCOMP.
00374  EJECT
00375      COPY ERCNAME.
00376  EJECT
00377      COPY ERCPLAN.
00378  EJECT
00379      COPY ELCCNTL.
00380  EJECT
00381      COPY ERCPNDB.
00382 *EJECT
00383 *    COPY ERCGXRF.
00384  EJECT
00385  PROCEDURE DIVISION.
00386
00387      MOVE LOW-VALUES             TO  WS-VERIFY-AREA.
00388      INITIALIZE  TEMP-STORAGE-RECORD.
00389
00390      MOVE EIBDATE                TO  DC-JULIAN-YYDDD.
00391      MOVE '5'                    TO  DC-OPTION-CODE.
00392
00393      PERFORM 9700-DATE-CONVERSION.
00394
00395      MOVE DC-GREG-DATE-1-EDIT    TO  SAVE-DATE.
00396      MOVE DC-BIN-DATE-1          TO  SAVE-BIN-DATE.
00397      MOVE DFHCOMMAREA            TO  PROGRAM-INTERFACE-BLOCK.
00398      MOVE 2                      TO  EMI-NUMBER-OF-LINES.

pemcp *    display ' beginning of program el650   '

00399
00400      IF EIBCALEN  =  0
00401          GO TO 8800-UNAUTHORIZED-ACCESS.
00402
00403      MOVE SCREEN-NUMBER          TO  TS-SCREEN.
00404      MOVE EIBTRMID               TO  TS-TERM.
00405      MOVE LOW-VALUES             TO  EL650AI.
00406
00407      IF PI-RETURN-TO-PROGRAM = THIS-PGM
00408          MOVE PI-CALLING-PROGRAM        TO  RETURNED-FROM.
00409
00410      IF PI-CALLING-PROGRAM  NOT = THIS-PGM
00411          IF PI-RETURN-TO-PROGRAM  NOT = THIS-PGM
00412              MOVE PI-SAVED-PROGRAM-5    TO  PI-SAVED-PROGRAM-6
00413              MOVE PI-SAVED-PROGRAM-4    TO  PI-SAVED-PROGRAM-5
00414              MOVE PI-SAVED-PROGRAM-3    TO  PI-SAVED-PROGRAM-4
00415              MOVE PI-SAVED-PROGRAM-2    TO  PI-SAVED-PROGRAM-3
00416              MOVE PI-SAVED-PROGRAM-1    TO  PI-SAVED-PROGRAM-2
00417              MOVE PI-RETURN-TO-PROGRAM  TO  PI-SAVED-PROGRAM-1
00418              MOVE PI-CALLING-PROGRAM    TO  PI-RETURN-TO-PROGRAM
00419              MOVE THIS-PGM              TO  PI-CALLING-PROGRAM
00420              MOVE LOW-VALUES            TO  PI-PROGRAM-WORK-AREA
00421              MOVE ZEROS                 TO  PI-TOTAL-LINES
00422                                             PI-LINE-SELECTED
00423              MOVE SPACE                 TO  PI-RECORD-ADDED-SW
00424              MOVE 1                     TO  PI-PAGE-NUMBER
00425          ELSE
00426              MOVE PI-RETURN-TO-PROGRAM  TO  PI-CALLING-PROGRAM
00427              MOVE PI-SAVED-PROGRAM-1    TO  PI-RETURN-TO-PROGRAM
00428              MOVE PI-SAVED-PROGRAM-2    TO  PI-SAVED-PROGRAM-1
00429              MOVE PI-SAVED-PROGRAM-3    TO  PI-SAVED-PROGRAM-2
00430              MOVE PI-SAVED-PROGRAM-4    TO  PI-SAVED-PROGRAM-3
00431              MOVE PI-SAVED-PROGRAM-5    TO  PI-SAVED-PROGRAM-4
00432              MOVE PI-SAVED-PROGRAM-6    TO  PI-SAVED-PROGRAM-5
00433              MOVE SPACES                TO  PI-SAVED-PROGRAM-6.
00434
00435 **************************************************************
00436 *               CLIENT "DMD" CUSTOM CODE
00437 * CHECK IF CONTROL PASSED FROM EL608 PROGRAM
00438 * START BROWSE BEGINNING WITH KEY PASSED BY EL608
00439 *              CSO CR# 6475
00440      IF EIBTRNID  =  EL608-TRANS-ID
00441          MOVE 1                     TO RETURN-FROM-SW
00442          MOVE 'Y'                   TO FROM-EL608-SW
00443          MOVE TRANS-ID              TO EIBTRNID
00444          MOVE XCTL-EL601            TO PI-RETURN-TO-PROGRAM
00445          MOVE PI-PREV-VG-ACCOUNT    TO PI-ACCT-CCGSA-KEY
00446                                        KEY-SAVE
00447          MOVE LOW-VALUES            TO PI-ACCT-EXP-DT
00448                                        PI-ACCT-REST-OF-EXP
00449                                        PI-DATE-RANGE-TABLE
00450          EXEC CICS  HANDLE CONDITION
00451              NOTFND   (5340-END)
00452              ENDFILE  (5330-NOT-FOUND)
00453          END-EXEC
00454          MOVE 'Y'                   TO FIRST-TIME-SW
00455          MOVE ZEROS                 TO PI-TOTAL-LINES
00456          SET T-INDEX
00457              TS-INDEX  TO  +1
00458          MOVE ACCT-FILE-ID          TO PI-ACCT-ID
00459          MOVE 1                     TO PI-PAGE-NUMBER
00460          MOVE 'S'                   TO PI-MAINT
00461          MOVE DFHPF1                TO EIBAID
00462          GO TO 1002-SHOW-NEW-RECORDS.
00463 *    END OF CLIENT "DMD" CUSTOM CODE
00464 **************************************************************
00465
00466      IF RETURNED-FROM = XCTL-6501 OR XCTL-6502 OR XCTL-6503 OR
00467                         XCTL-6504 OR XCTL-6505 OR XCTL-6506 OR
00468                         XCTL-6508
00469          MOVE 1                     TO  RETURN-FROM-SW
00470          MOVE PI-PREV-VG-ACCOUNT    TO  PI-ACCT-CCGSA-KEY
00471          MOVE PI-ACCT-CCGSA-KEY     TO  KEY-SAVE
00472          MOVE LOW-VALUES            TO  PI-ACCT-EXP-DT
00473          GO TO 1002-SHOW-NEW-RECORDS.
00474
00475  EJECT
00476      IF EIBTRNID  NOT =  TRANS-ID
00477          IF EIBTRNID  NOT = EL640-TRANS-ID  AND
00478             EIBTRNID  NOT = EL658-TRANS-ID  AND
00479             EIBTRNID  NOT = EL6311-TRANS-ID AND
020816            EIBTRNID  NOT = VP6311-TRANS-ID AND
CIDMOD            EIBTRNID  NOT = EL1273-TRANS-ID AND
CIDMOD            EIBTRNID  NOT = EL652-TRANS-ID  AND
100703            EIBTRNID  NOT = EL6523-TRANS-ID AND
00480             EIBTRNID  NOT = EL6311NC-TRANS-ID AND
00481             EIBTRNID  NOT = EL130-TRANS-ID  AND
00482             EIBTRNID  NOT = EL6592-TRANS-ID AND
00483             EIBTRNID  NOT = EL6509-TRANS-ID
110706            AND EIBTRNID NOT = EL6503-TRANS-ID
00484              GO TO 8100-SEND-INITIAL-MAP
00485          ELSE
00486              IF PI-CR-CONTROL-IN-PROGRESS  = SPACES
00487                  GO TO 8100-SEND-INITIAL-MAP
00488              ELSE
00489                  MOVE 'S'             TO  MAINTI
00490                  MOVE PI-CR-CARRIER   TO  ACARIERI
00491                  MOVE PI-CR-GROUPING  TO  AGROUPI
00492                  MOVE PI-CR-STATE     TO  ASTATEI
00493                  MOVE PI-CR-ACCOUNT   TO  AACCTI
00494                  MOVE +1              TO  ACARIERL
00495                                           MAINTL
00496                  MOVE +6              TO  AGROUPL
00497                  MOVE +2              TO  ASTATEL
00498                  MOVE +10             TO  AACCTL
00499                  GO TO 0330-EDIT-DATA.
00500
00501      EXEC CICS  HANDLE CONDITION
00502          PGMIDERR  (9600-PGMID-ERROR)
00503          ERROR     (9990-ABEND)
00504      END-EXEC.
00505
00506      IF EIBAID  = DFHCLEAR
00507          GO TO 9400-CLEAR.
00508
00509      IF PI-PROCESSOR-ID  =  'LGXX'
00510          GO TO 0200-RECEIVE.
00511
00512      EXEC CICS  READQ TS
00513          QUEUE   (PI-SECURITY-TEMP-STORE-ID)
00514          INTO    (SECURITY-CONTROL)
00515          LENGTH  (SC-COMM-LENGTH)
00516          ITEM    (SC-ITEM)
00517      END-EXEC.
00518
00519      MOVE SC-CREDIT-DISPLAY (04)  TO  PI-DISPLAY-CAP.
00520      MOVE SC-CREDIT-UPDATE  (04)  TO  PI-MODIFY-CAP.
00521
00522      IF NOT DISPLAY-CAP
00523          MOVE 'READ'             TO  SM-READ
00524          PERFORM 9995-SECURITY-VIOLATION  THRU  9995-EXIT
00525          MOVE ER-0070            TO  EMI-ERROR
00526          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
00527          GO TO 8100-SEND-INITIAL-MAP.
00528  EJECT
00529  0200-RECEIVE.
00530      IF EIBAID  = DFHPA1  OR  DFHPA2  OR  DFHPA3
00531          MOVE ER-0008            TO  EMI-ERROR
00532          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
00533          MOVE -1                 TO  MAINTL
00534          GO TO 8200-SEND-DATAONLY.
00535
00536      EXEC CICS  RECEIVE
00537          MAP     (MAP-NAME)
00538          MAPSET  (MAPSET-NAME)
00539          INTO    (EL650AI)
00540      END-EXEC.
00541
00542      IF PFENTERL  = ZERO
00543          GO TO 0300-CHECK-PFKEYS.
00544
00545      IF EIBAID  NOT = DFHENTER
00546          MOVE ER-0004            TO  EMI-ERROR
00547          GO TO 0320-INPUT-ERROR.
00548
00549      IF (PFENTERI NUMERIC)
00550        AND  (PFENTERI GREATER ZERO AND LESS 25)
00551          MOVE PF-VALUES (PFENTERI)  TO  EIBAID
00552      ELSE
00553          MOVE ER-0029               TO  EMI-ERROR
00554          GO TO 0320-INPUT-ERROR.
00555
00556  0300-CHECK-PFKEYS.
00557      IF EIBAID  = DFHPF23
00558          GO TO 8810-PF23.
00559
00560      IF EIBAID  = DFHPF24
00561          GO TO 9200-RETURN-MAIN-MENU.
00562
00563      IF EIBAID  = DFHPF12
00564          GO TO 9500-PF12.
00565
00566      IF MAINTL  NOT = ZERO   AND
00567         MAINTI  NOT = SPACE  AND
00568         EIBAID  NOT = DFHENTER
00569           MOVE ER-0050            TO  EMI-ERROR
00570           GO TO 0320-INPUT-ERROR.
00571
pemtst     if (maintl not = zeros)
pemtst        and (mainti not = 'D')
pemtst        move spaces              to pi-el650-del-sw
pemtst     end-if

00572      IF EIBAID  = DFHPF1
00573          GO TO 5300-FIND-NEXT-ACCOUNT.
00574
00575      IF EIBAID  = DFHPF2
00576          GO TO 5400-FIND-PREV-ACCOUNT.
00577
00578      IF EIBAID  = DFHPF3
00579          GO TO 5100-FIND-NEXT-RANGE.
00580
00581      IF EIBAID  = DFHPF4
00582          GO TO 5200-FIND-PREV-RANGE.
00583
00584      IF EIBAID  = DFHPF5
00585          GO TO 8900-PF5.
00586
110706     IF EIBAID  = DFHPF6
110706        GO TO 8910-PF6
110706     END-IF

00587      IF EIBAID  = DFHENTER
00588          GO TO 0330-EDIT-DATA.
00589
00590      MOVE ER-0029                TO  EMI-ERROR.
00591
00592  0320-INPUT-ERROR.
00593      PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
00594
00595      MOVE AL-UNBON               TO  PFENTERA.
00596
00597      IF PFENTERL  = ZERO
00598          MOVE -1                 TO  MAINTL
00599      ELSE
00600          MOVE -1                 TO  PFENTERL.
00601
00602      GO TO 8200-SEND-DATAONLY.
00603  EJECT
00604  0330-EDIT-DATA.
00605      MOVE EIBDATE                TO  DC-JULIAN-YYDDD.
00606      MOVE '5'                    TO  DC-OPTION-CODE.
00607
00608      PERFORM 9700-DATE-CONVERSION.
00609
00610      MOVE DC-BIN-DATE-1          TO  BIN-CURRENT-SAVE.
00611      MOVE DC-GREG-DATE-1-YMD     TO  CURRENT-SAVE.
00612
00613      IF ACARIERL GREATER ZEROS
00614          IF PI-CARRIER-SECURITY GREATER SPACES
00615              IF ACARIERI = PI-CARRIER-SECURITY
00616                  NEXT SENTENCE
00617              ELSE
00618                  MOVE -1         TO  ACARIERL
00619                  MOVE ER-2370    TO  EMI-ERROR
00620                  PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
00621                  MOVE AL-UABON   TO  ACARIERA
00622                  GO TO 8200-SEND-DATAONLY.
00623
00624      IF MAINTI  = 'S'
00625        IF LINEL = ZEROS
00626            MOVE 1 TO PI-PAGE-NUMBER
00627            GO TO 1000-SHOW-ACCOUNT
00628        ELSE
00629            GO TO 1000-SHOW-ACCOUNT.
00630
00631      PERFORM 6800-COMPANY-REC-READ  THRU  6899-EXIT.
00632
00633      IF EMI-ERROR  NOT = ZEROS
00634          MOVE -1                 TO  MAINTL
00635          GO TO 8200-SEND-DATAONLY.
00636
00637      IF NOT  MODIFY-CAP
00638          MOVE 'UPDATE'           TO  SM-READ
00639          PERFORM 9995-SECURITY-VIOLATION  THRU  9995-EXIT
00640          MOVE ER-0070            TO  EMI-ERROR
00641          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
00642          MOVE AL-UANON           TO  ACARIERA  AGROUPA
00643                                      ASTATEA   AACCTA
00644          GO TO 8100-SEND-INITIAL-MAP.
00645
00646      IF MAINTI  = 'A'
00647          GO TO 1500-ADD-ACCOUNT.
00648
00649      IF MAINTI  = 'C'
00650          IF PI-SV-MAINT IS EQUAL TO 'L'
00651             MOVE -1              TO  MAINTL
00652             MOVE AL-UABON        TO  MAINTA
00653             MOVE ER-0899         TO  EMI-ERROR
00654             PERFORM 9900-ERROR-FORMAT THRU 9900-EXIT
00655             GO TO 8200-SEND-DATAONLY
00656          ELSE
00657             GO TO 2000-CHANGE-ACCOUNT.
00658
00659      IF MAINTI  = 'D'
00660          GO TO 2500-DELETE-ACCOUNT.
00661
00662      IF MAINTI  = 'X'
00663          GO TO 3000-CHANGE-EXPIRATION.
00664
00665      IF MAINTI  = 'E'
00666          GO TO 3500-CHANGE-EFFECTIVE.
00667
00668      IF MAINTI  = 'L'
00669          MOVE 'L'             TO  PI-SV-MAINT
00670          GO TO 4000-CHANGE-ALL.
00671
00672      IF MAINTI  = 'K'
00673        IF LINEL GREATER ZERO
00674            GO TO 4900-CREATE-NEW-ACCOUNT
00675         ELSE
00676            GO TO 4500-CREATE-NEW-ACCOUNT.
00677
00678      IF PI-COMPANY-ID  =  'LGX' OR 'MIC' OR 'MCC'
00679          IF MAINTI  = 'T'
00680              GO TO 4500-CREATE-NEW-ACCOUNT.
00681
00682      IF MAINTI  = 'N'
00683          GO TO 5000-CREATE-NEW-RANGE.
00684
00685      MOVE ER-0023                TO  EMI-ERROR.
00686
00687      PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
00688
00689      MOVE -1                     TO  MAINTL.
00690      MOVE AL-UABON               TO  MAINTA.
00691
00692      GO TO 8200-SEND-DATAONLY.
00693  EJECT
00694  1000-SHOW-ACCOUNT.
00695      PERFORM 6100-BUILD-KEY  THRU  6199-EXIT.
00696
00697      IF LINEL GREATER ZEROS   AND
00698         PI-TOTAL-LINES  NOT =  ZEROS
00699          NEXT SENTENCE
00700      ELSE
00701          GO TO 1002-SHOW-NEW-RECORDS.
00702
00703      PERFORM 6000-EDIT-LINE  THRU  6000-LIN-EXIT.
00704
00705      IF EMI-ERROR  NOT = ZEROS
00706          GO TO 8200-SEND-DATAONLY.
00707
00708      SET T-INDEX                 TO  LINEI.
00709
00710      IF PI-2ND-PAGE
00711          SET T-INDEX  UP  BY  8
00712      ELSE
00713      IF PI-3RD-PAGE
00714          SET T-INDEX  UP  BY  16
00715      ELSE
00716      IF PI-LST-PAGE
00717          SET T-INDEX  UP  BY  24.
00718
00719      MOVE PI-BIN-EXP-DT (T-INDEX)  TO  PI-ACCT-EXP-DT.
00720      MOVE LOW-VALUES               TO  PI-ACCT-REST-OF-EXP.
00721
00722      IF PI-PREV-ACCOUNT  =  PI-ACCT-CCGSA-KEY
00723          MOVE MAINTI             TO  PI-MAINT
00724          MOVE XCTL-6501          TO  PGM-NAME
00725          GO TO 9300-XCTL
00726      ELSE
00727          IF PI-PREV-VG-ACCOUNT = PI-ACCT-CCGSA-KEY
00728              MOVE MAINTI         TO  PI-MAINT
00729              MOVE XCTL-6501      TO  PGM-NAME
00730              PERFORM 7400-READ-TEMP-STORAGE  THRU  7400-EXIT
00731              SET TS-INDEX        TO  T-INDEX
00732              MOVE TS-ACCT-KEY (TS-INDEX)
00733                                  TO  PI-ACCT-CCGSA-KEY
00734              GO TO 9300-XCTL.
00735
00736  1002-SHOW-NEW-RECORDS.
00737      EXEC CICS  HANDLE CONDITION
00738           NOTFND   (1010-NOT-FOUND)
00739           ENDFILE  (1010-NOT-FOUND)
00740      END-EXEC.
00741
00742      SET T-INDEX
00743         TS-INDEX  TO  +1.
00744
00745      MOVE PI-ACCT-CCGSA-KEY      TO  KEY-SAVE.
00746
00747      MOVE LOW-VALUES             TO  TEMP-STORAGE-RECORD.
00748
00749      MOVE ZEROS                  TO  PI-TOTAL-LINES.
00750
00751      PERFORM 7100-START-BROWSE  THRU  7100-EXIT.
00752
00753      MOVE 'Y'                    TO  BROWSE-STARTED-SW.
00754
00755  1005-READ-ACCT.
00756      PERFORM 6310-READNEXT  THRU  6310-EXIT.
00757
00758      IF K-WITH-LINE-NO
00759        IF SV-CARRIER   NOT = AM-CARRIER  OR
00760           SV-GROUPING  NOT = AM-GROUPING OR
00761           SV-STATE     NOT = AM-STATE    OR
00762           SV-ACCOUNT   NOT = AM-ACCOUNT
00763             GO TO 1010-NOT-FOUND.
00764
00765      IF SV-CO  NOT = AM-COMPANY-CD
00766          GO TO 1010-NOT-FOUND.
00767
00768      IF SV-ACCOUNT  NOT =  AM-ACCOUNT
00769          GO TO 1010-NOT-FOUND.
00770
00771      IF SV-STATE  NOT = AM-STATE
00772        AND  (ST-ACCNT-CNTL
00773        OR  CARR-ST-ACCNT-CNTL
00774        OR  CARR-GROUP-ST-ACCNT-CNTL)
00775          GO TO 1010-NOT-FOUND.
00776
00777      IF SV-CARRIER  NOT =  AM-CARRIER
00778        AND  (CARR-ACCNT-CNTL
00779        OR  CARR-ST-ACCNT-CNTL
00780        OR  CARR-GROUP-ST-ACCNT-CNTL)
00781          GO TO 1010-NOT-FOUND.
00782
00783      IF SV-GROUPING  NOT = AM-GROUPING
00784        AND  CARR-GROUP-ST-ACCNT-CNTL
00785          GO TO 1010-NOT-FOUND.
00786
00787      IF AM-TRNTO-DTE  =  SPACES
00788          MOVE LOW-VALUES         TO  AM-TRNTO-DTE.
00789
00790      IF AM-TRNFROM-DTE  = SPACES
00791          MOVE LOW-VALUES         TO  AM-TRNFROM-DTE.
00792
00793      IF T-INDEX  = 1
00794          IF PI-COMPANY-ID  =  'LGX' OR 'MIC' OR 'MCC'
00795              MOVE AM-TRANSFERRED-TO  TO  TRNTOO
00796              MOVE AM-TRNTO-DTE       TO  DC-BIN-DATE-1
00797              MOVE SPACE              TO  DC-OPTION-CODE
00798              PERFORM 9700-DATE-CONVERSION
00799              IF DATE-CONVERSION-ERROR
00800                  MOVE SPACES         TO  TRNTODTO
00801              ELSE
00802                  MOVE DC-GREG-DATE-1-EDIT
00803                                      TO  TRNTODTO.
00804
00805      IF T-INDEX  =  1
00806          IF PI-COMPANY-ID  =  'LGX' OR 'MIC' OR 'MCC'
00807              MOVE AM-TRANSFERRED-FROM  TO  TRNFROMO
00808              MOVE AM-TRNFROM-DTE       TO  DC-BIN-DATE-1
00809              MOVE SPACE                TO  DC-OPTION-CODE
00810              PERFORM 9700-DATE-CONVERSION
00811              IF DATE-CONVERSION-ERROR
00812                  MOVE SPACES           TO  TRNFRDTO
00813              ELSE
00814                  MOVE DC-GREG-DATE-1-EDIT
00815                                        TO  TRNFRDTO.
00816
00817      IF T-INDEX  = 1
00818          MOVE AM-CONTROL-PRIMARY  TO  PI-PREV-ACCOUNT
00819          MOVE PI-ACCT-CCGSA-KEY   TO  PI-PREV-VG-ACCOUNT
00820          MOVE AM-NAME             TO  NAMEO
00821                                       PI-ACCNAME
00822          MOVE SPACE               TO  NAMEFLGO
00823                                       PI-NAMEFLG.
00824
00825      IF AM-NAME  NOT = NAMEO
00826          MOVE '*'                TO  NAMEFLGO
00827                                      PI-NAMEFLG.
00828
00829      PERFORM 1020-MOVE-TO-TABLE  THRU  1020-EXIT.
00830
00831      GO TO 1005-READ-ACCT.
00832
00833  1010-NOT-FOUND.
00834      IF BROWSE-STARTED
00835          EXEC CICS  ENDBR
00836              DATASET  (PI-ACCT-ID)
00837          END-EXEC.
00838
00839      IF T-INDEX  NOT = 1
00840          PERFORM 7200-WRITE-TEMP-STORAGE  THRU  7200-EXIT
00841          GO TO 7000-BUILD-OUTPUT-MAP.
00842
00843      MOVE ER-0226                TO  EMI-ERROR.
00844
00845      PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
00846
00847      MOVE -1                     TO  ACARIERL.
00848      MOVE AL-UABON               TO  ACARIERA
00849                                      AGROUPA
00850                                      ASTATEA
00851                                      AACCTA.
00852
00853      IF FROM-EL608-SW = 'Y'
00854          MOVE SV-CARRIER     TO  ACARIERO
00855          MOVE SV-GROUPING    TO  AGROUPO
00856          MOVE SV-STATE       TO  ASTATEO
00857          MOVE SV-ACCOUNT     TO  AACCTO
00858          MOVE 'N'            TO  FROM-EL608-SW.
00859
00860      IF PI-MAINT = 'A'
00861          IF PI-RECORD-NOT-CREATED
00862              MOVE SV-CARRIER     TO  ACARIERO
00863              MOVE SV-GROUPING    TO  AGROUPO
00864              MOVE SV-STATE       TO  ASTATEO
00865              MOVE SV-ACCOUNT     TO  AACCTO
00866              MOVE ER-2151        TO  EMI-ERROR
00867              PERFORM 9900-ERROR-FORMAT THRU 9900-EXIT
00868              MOVE 'S'            TO  PI-MAINT.
00869
00870      GO TO 8100-SEND-INITIAL-MAP.
00871  EJECT
00872  1020-MOVE-TO-TABLE.
00873      MOVE AM-EXPIRATION-DT       TO  PI-BIN-EXP-DT   (T-INDEX).
00874      MOVE AM-EFFECTIVE-DT        TO  PI-BIN-EFF-DT   (T-INDEX).
00875      MOVE AM-LAST-MAINT-DT       TO  PI-BIN-MAINT-DT (T-INDEX).
00876      MOVE AM-HI-CERT-DATE        TO  DC-GREG-DATE-1-YMD.
00877      MOVE '3'                    TO  DC-OPTION-CODE.
00878
00879      PERFORM 9700-DATE-CONVERSION.
00880
00881      IF DATE-CONVERSION-ERROR
00882          MOVE LOW-VALUES         TO  PI-BIN-HI-CERT (T-INDEX)
00883      ELSE
00884          MOVE DC-BIN-DATE-1      TO  PI-BIN-HI-CERT (T-INDEX).
00885
00886      MOVE AM-LO-CERT-DATE        TO  DC-GREG-DATE-1-YMD.
00887      MOVE '3'                    TO  DC-OPTION-CODE.
00888
00889      PERFORM 9700-DATE-CONVERSION.
00890
00891      IF DATE-CONVERSION-ERROR
00892          MOVE LOW-VALUES         TO  PI-BIN-LO-CERT (T-INDEX)
00893      ELSE
00894          MOVE DC-BIN-DATE-1      TO  PI-BIN-LO-CERT (T-INDEX).
00895
00896      MOVE AM-AR-HI-CERT-DATE     TO  PI-BIN-AR-HI-CERT (T-INDEX).
00897      MOVE AM-CONTROL-PRIMARY     TO  TS-ACCT-KEY (TS-INDEX).
00898
00899      ADD 1                       TO  PI-TOTAL-LINES.
00900
00901      SET T-INDEX
00902         TS-INDEX  UP  BY  +1.
00903
00904      IF T-INDEX  = +33
00905          GO TO 7000-BUILD-OUTPUT-MAP.
00906
00907
00908  1020-EXIT.
00909      EXIT.
00910  EJECT
00911  1500-ADD-ACCOUNT.
00912      PERFORM 6100-BUILD-KEY             THRU  6199-EXIT.
00913
00914      MOVE PI-ACCT-CCGSA-KEY      TO  NEW-KEY-SAVE.
00915
00916      MOVE LOW-VALUES             TO  PI-ACCT-EXP-DT
00917                                      PI-ACCT-REST-OF-EXP.
00918
00919      PERFORM 6500-READ-ACCT  THRU  6599-EXIT.
00920
00921      IF REC-FOUND
00922          MOVE -1                 TO  ACARIERL
00923          MOVE ER-0561            TO  EMI-ERROR
00924          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
00925          GO TO 8200-SEND-DATAONLY.
00926
00927      MOVE NEW-KEY-SAVE           TO PI-ACCT-CCGSA-KEY.
00928
00929      PERFORM 6000-EDIT-EFFECTIVE-DATE   THRU  6000-EFF-EXIT.
00930
00931      PERFORM 6000-EDIT-EXPIRATION-DATE  THRU  6000-EXP-EXIT.
00932
00933      PERFORM 6700-VERIFY-STATE-CARRIER  THRU  6799-EXIT.
00934
00935      IF BIN-EXPCHG-SAVE NOT GREATER BIN-EFFCHG-SAVE
00936          MOVE -1                 TO  EXPCHGL
00937          MOVE ER-1228            TO  EMI-ERROR
00938          MOVE AL-UABON           TO  EXPCHGA
00939          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
00940
00941      IF NOT EMI-NO-ERRORS
00942          GO TO 8200-SEND-DATAONLY.
00943
00944      PERFORM 4100-BUILD-VERIFICATION  THRU  4199-EXIT.
00945
00946      IF WS-ACCT2-FOUND  = 'Y'
111513         PERFORM 4200-CHECK-VALIDITY  THRU  4200-EXIT
00948              VARYING  WS-INDEX  FROM  +1  BY  +1
00949                 UNTIL WS-INDEX GREATER +32.
00950
00951      IF NOT EMI-NO-ERRORS
00952          GO TO 8200-SEND-DATAONLY.
00953
00954      MOVE PI-ACCT-CCGSA-KEY      TO  NEW-KEY-SAVE.
00955
00956      MOVE NEW-KEY-SAVE           TO  PI-ACCT-CCGSA-KEY
00957                                      PI-PREV-ACCOUNT
00958                                      PI-PREV-VG-ACCOUNT.
00959      MOVE LOW-VALUES             TO  PI-DATE-RANGE-TABLE
00960                                      PI-ACCT-REST-OF-EXP.
00961      MOVE BIN-EXPCHG-SAVE        TO  PI-ACCT-EXP-DT
00962                                      PI-BIN-EXP-DT (1)
00963      MOVE BIN-EFFCHG-SAVE        TO  PI-BIN-EFF-DT (1).
00964      MOVE 1                      TO  PI-TOTAL-LINES
00965                                      PI-PAGE-NUMBER
00966                                      PI-LINE-SELECTED.
00967      MOVE SPACE                  TO  PI-RECORD-ADDED-SW.
00968      MOVE MAINTI                 TO  PI-MAINT.
00969      MOVE XCTL-6501              TO  PGM-NAME.
00970
00971      GO TO 9300-XCTL.
00972  EJECT
00973  2000-CHANGE-ACCOUNT.
00974      PERFORM 6100-BUILD-KEY  THRU  6199-EXIT.
00975
00976      PERFORM 6200-CHECK-KEY  THRU  6200-EXIT.
00977
00978      PERFORM 6000-EDIT-LINE  THRU  6000-LIN-EXIT.
00979
00980      IF NOT EMI-NO-ERRORS
00981          GO TO 8200-SEND-DATAONLY.
00982
00983      SET T-INDEX TO LINEI.
00984
00985      IF PI-2ND-PAGE
00986          SET T-INDEX  UP  BY  8
00987      ELSE
00988      IF PI-3RD-PAGE
00989          SET T-INDEX  UP  BY  16
00990      ELSE
00991      IF PI-LST-PAGE
00992          SET T-INDEX  UP  BY  24.
00993
00994      MOVE PI-BIN-EXP-DT (T-INDEX)  TO  PI-ACCT-EXP-DT.
00995      MOVE LOW-VALUES               TO  PI-ACCT-REST-OF-EXP.
00996      MOVE MAINTI                   TO  PI-MAINT.
00997      MOVE XCTL-6501                TO  PGM-NAME.
00998
00999      GO TO 9300-XCTL.
01000  EJECT
01001  2500-DELETE-ACCOUNT.
01002      PERFORM 6100-BUILD-KEY  THRU  6199-EXIT.
01003
01004      PERFORM 6200-CHECK-KEY  THRU  6200-EXIT.
01005
01006      PERFORM 6000-EDIT-LINE  THRU  6000-LIN-EXIT.
01007
01008      IF NOT EMI-NO-ERRORS
01009          GO TO 8200-SEND-DATAONLY.
01010
01011      SET T-INDEX                 TO  LINEI.
01012
01013      IF PI-2ND-PAGE
01014          SET T-INDEX  UP  BY  8
01015      ELSE
01016      IF PI-3RD-PAGE
01017          SET T-INDEX  UP  BY  16
01018      ELSE
01019      IF PI-LST-PAGE
01020          SET T-INDEX  UP  BY  24.
01021
01022      MOVE PI-BIN-EXP-DT (T-INDEX)  TO  PI-ACCT-EXP-DT
01023                                        BIN-MAX-DATE.
01024      MOVE LOW-VALUES               TO  PI-ACCT-REST-OF-EXP.
01025      MOVE PI-BIN-EFF-DT (T-INDEX)  TO  BIN-MIN-DATE.
01026      MOVE '1'                      TO  FROM-WHERE-SW.
01027
01028      PERFORM 2550-CHECK-HI-LO-DATES  THRU  2550-EXIT
01029        VARYING  T-INDEX  FROM  +1  BY  +1
01030          UNTIL  T-INDEX GREATER PI-TOTAL-LINES.
01031
01032      MOVE SPACE                  TO  FROM-WHERE-SW.
01033
01034      IF NOT EMI-NO-ERRORS
01035          GO TO 8200-SEND-DATAONLY.
01036
01037      SET T-INDEX TO LINEI.
01038
01039      IF PI-2ND-PAGE
01040          SET T-INDEX  UP  BY  8
01041      ELSE
01042      IF PI-3RD-PAGE
01043          SET T-INDEX  UP  BY  16
01044      ELSE
01045      IF PI-LST-PAGE
01046          SET T-INDEX  UP  BY  24.
01047
01048      EXEC CICS  READ
01049          UPDATE
01050          DATASET  (PI-ACCT-ID)
01051          SET      (ADDRESS OF ACCOUNT-MASTER)
01052          RIDFLD   (PI-ACCT-KEY)
01053      END-EXEC.
01054
01055      IF PI-BIN-LO-CERT (T-INDEX)  NOT =  LOW-VALUES  OR
01056         PI-BIN-HI-CERT (T-INDEX)  NOT =  LOW-VALUES
01057          MOVE ER-2070            TO  EMI-ERROR
01058          MOVE -1                 TO  MAINTL
01059          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
01060          GO TO 8200-SEND-DATAONLY.
01061
01062      IF (PI-COMPANY-ID IS EQUAL TO 'AIG' OR 'AUK')
01063          MOVE 'N'                TO  PENDING-BUSINESS-SW
01064      ELSE
01065          PERFORM 2560-CHECK-PENDING-BUSINESS  THRU  2569-EXIT.
01066
01067      IF THERE-IS-PENDING-BUSINESS
01068          MOVE ER-1612            TO  EMI-ERROR
01069          MOVE -1                 TO  MAINTL
01070          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
01071          GO TO 8200-SEND-DATAONLY.
01072
01073      MOVE AM-CONTROL-PRIMARY     TO  B4-ACCT-KEY.
01074      MOVE AM-EFFECTIVE-DT        TO  B4-ACCT-EFF-DT.
01075
pemtst     if pi-el650-del-sw = 'Y'
pemtst        continue
pemtst     else
pemtst        move 'Y'                 to pi-el650-del-sw
pemtst        move er-1299             to emi-error
pemtst        perform 9900-error-format thru 9900-exit
pemtst        MOVE 1 TO PI-PAGE-NUMBER
pemtst        move spaces              to mainto
pemtst        MOVE -1                  TO MAINTL
pemtst        GO TO 1000-SHOW-ACCOUNT
01108      end-if

01076      PERFORM 5600-CHG-AXRF-RECORDS  THRU  5699-EXIT
01077
01078 ****** DO NOT DELETE PLAN MASTER RECORDS IF MORE ACCT DATE RANGES
01079      IF (PI-COMPANY-ID IS EQUAL TO 'AIG' OR 'AUK')
01080          NEXT SENTENCE
01081      ELSE
01082          IF PI-TOTAL-LINES  = 1
01083              PERFORM 4800-DELETE-PLAN-REC   THRU  4899-EXIT
               end-if
           end-if
01084
01085      MOVE 'D'                    TO  JP-RECORD-TYPE
01086      MOVE ACCOUNT-MASTER         TO  JP-RECORD-AREA
01087
01088      EXEC CICS  DELETE
01089          DATASET  (PI-ACCT-ID)
01090      END-EXEC
01091
01092      PERFORM 8400-LOG-JOURNAL-RECORD
01093
01094      PERFORM 6600-UPDATE-MAINT-DT  THRU  6699-EXIT
01095
01096      MOVE LOW-VALUES             TO  PI-ACCT-EXP-DT
01097                                      PI-ACCT-REST-OF-EXP
01098                                      LINEO
01099      MOVE ER-0000                TO  EMI-ERROR
01100
01101      PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
01102
01103      IF PI-TOTAL-LINES  = 1
01104          MOVE LOW-VALUES         TO  PI-DATE-RANGE-TABLE
01105          MOVE ZEROS              TO  PI-TOTAL-LINES
01106          MOVE 1                  TO  PI-PAGE-NUMBER
01107          GO TO 7000-BUILD-OUTPUT-MAP
pemtst     end-if

01109      GO TO 1002-SHOW-NEW-RECORDS.
01110  EJECT
01111  2550-CHECK-HI-LO-DATES.
01112      IF FROM-CHANGE
01113          GO TO 2550-CHECK-CHANGE.
01114
01115      IF PI-BIN-LO-CERT (T-INDEX)  =  LOW-VALUES  AND
01116         PI-BIN-HI-CERT (T-INDEX)  =  LOW-VALUES
01117          GO TO 2550-CHECK-AR-DELETE.
01118
01119      IF ((PI-BIN-HI-CERT (T-INDEX) NOT LESS BIN-MIN-DATE AND
01120           PI-BIN-HI-CERT (T-INDEX) LESS BIN-MAX-DATE)
01121        OR
01122          (PI-BIN-LO-CERT (T-INDEX) NOT LESS BIN-MIN-DATE AND
01123           PI-BIN-LO-CERT (T-INDEX) LESS BIN-MAX-DATE))
01124        AND
01125             (PI-PROCESSOR-ID  NOT =  'LGXX')
01126          MOVE ER-2087            TO  EMI-ERROR
01127          MOVE -1                 TO  MAINTL
01128          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
01129
01130  2550-CHECK-AR-DELETE.
01131      IF NOT PI-AR-PROCESSING
01132          GO TO 2550-EXIT.
01133
01134      IF T-INDEX NOT = LINEI
01135          GO TO 2550-EXIT.
01136
01137      IF PI-BIN-AR-HI-CERT (T-INDEX) = LOW-VALUES OR SPACES
01138         GO TO 2550-EXIT.
01139
01140      MOVE ER-3155                TO  EMI-ERROR
01141      MOVE -1                     TO  MAINTL
01142      PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
01143
01144  2550-CHECK-CHANGE.
01145      IF PI-BIN-HI-CERT (T-INDEX)  = LOW-VALUES OR SPACES
01146          GO TO 2550-CHECK-AR-CHANGE.
01147
01148      IF PI-BIN-HI-CERT (T-INDEX) NOT LESS BIN-MAX-DATE
01149          MOVE ER-2113            TO  EMI-ERROR
01150          MOVE -1                 TO  MAINTL
01151          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
01152
01153  2550-CHECK-AR-CHANGE.
01154       IF NOT PI-AR-PROCESSING
01155           GO TO 2550-EXIT.
01156
01157       IF PI-BIN-AR-HI-CERT (T-INDEX) = LOW-VALUES
01158           GO TO 2550-EXIT.
01159
01160       IF PI-BIN-AR-HI-CERT (T-INDEX) NOT LESS BIN-MAX-DATE
01161          MOVE ER-3155            TO  EMI-ERROR
01162          MOVE -1                 TO  MAINTL
01163          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
01164
01165  2550-EXIT.
01166      EXIT.
01167  EJECT
01168  2560-CHECK-PENDING-BUSINESS.
01169      MOVE 'N'                    TO  PENDING-BUSINESS-SW.
01170      MOVE LOW-VALUES             TO  ERPNDB-KEY
01171                                      PI-ERPNDB-ALT-KEY.
01172      MOVE AM-COMPANY-CD-A1       TO  PNDB-COMPANY-CD-A1
01173                                      PI-PB-COMPANY-CD-A1.
01174      MOVE AM-VG-CARRIER          TO  PNDB-CARRIER
01175                                      PI-PB-CARRIER.
01176      MOVE AM-VG-GROUPING         TO  PNDB-GROUPING
01177                                      PI-PB-GROUPING.
01178      MOVE AM-VG-STATE            TO  PNDB-STATE
01179                                      PI-PB-STATE.
01180      MOVE AM-VG-ACCOUNT          TO  PNDB-ACCOUNT
01181                                      PI-PB-ACCOUNT.
01182
01183      EXEC CICS  HANDLE CONDITION
01184           NOTFND   (2568-NO-RECORDS)
01185           ENDFILE  (2568-NO-RECORDS)
01186      END-EXEC.
01187
01188      EXEC CICS  STARTBR
01189          DATASET  (PEND2-FILE-ID)
01190          RIDFLD   (ERPNDB-KEY)
01191      END-EXEC.
01192
01193      MOVE 'Y'                    TO  PENDING-BROWSE-SW.
01194
01195  2562-READ-NEXT-PENDING.
01196      EXEC CICS  READNEXT
01197          DATASET  (PEND2-FILE-ID)
01198          RIDFLD   (ERPNDB-KEY)
01199          SET      (ADDRESS OF PENDING-BUSINESS)
01200      END-EXEC.
01201
01202      IF PB-COMPANY-CD-A1 = PI-PB-COMPANY-CD-A1 AND
01203         PB-CARRIER       = PI-PB-CARRIER       AND
01204         PB-GROUPING      = PI-PB-GROUPING      AND
01205         PB-STATE         = PI-PB-STATE         AND
01206         PB-ACCOUNT       = PI-PB-ACCOUNT
01207             NEXT SENTENCE
01208           ELSE
01209             GO TO 2568-NO-RECORDS.
01210
01211      IF PB-ISSUE
01212        OR  PB-CANCELLATION
01213          NEXT SENTENCE
01214      ELSE
01215          GO TO 2562-READ-NEXT-PENDING.
01216
01217      IF PB-CERT-EFF-DT  IS EQUAL TO  AM-EFFECTIVE-DT
01218          MOVE 'Y'                TO  PENDING-BUSINESS-SW
01219          GO TO 2568-NO-RECORDS.
01220
01221      IF PB-CERT-EFF-DT  IS GREATER THAN  AM-EFFECTIVE-DT
01222        AND PB-CERT-EFF-DT  IS LESS THAN  AM-EXPIRATION-DT
01223          MOVE 'Y'                TO  PENDING-BUSINESS-SW
01224          GO TO 2568-NO-RECORDS.
01225
01226      GO TO 2562-READ-NEXT-PENDING.
01227
01228  2568-NO-RECORDS.
01229      IF PENDING-BROWSE-STARTED
01230          MOVE ' '                TO  PENDING-BROWSE-SW
01231         EXEC CICS ENDBR
01232             DATASET  (PEND2-FILE-ID)
01233         END-EXEC.
01234
01235  2569-EXIT.
01236      EXIT.
01237  EJECT
01238  3000-CHANGE-EXPIRATION.
01239      PERFORM 6100-BUILD-KEY             THRU  6199-EXIT.
01240      PERFORM 6200-CHECK-KEY             THRU  6200-EXIT.
01241      PERFORM 6000-EDIT-EXPIRATION-DATE  THRU  6000-EXP-EXIT.
01242      PERFORM 4100-BUILD-VERIFICATION    THRU  4199-EXIT.
01243
01244      SET T-INDEX  TO  PI-TOTAL-LINES.
01245
01246      PERFORM VARYING WS-INDEX FROM +1 BY +1
01247              UNTIL  PI-BIN-EXP-DT (T-INDEX)
01248                         =  WS-ACCT-EXP-DT (WS-INDEX)
01249                  OR  WS-INDEX GREATER +32
01250      END-PERFORM.
01251
01252      IF PI-BIN-EXP-DT (T-INDEX) = WS-ACCT-EXP-DT (WS-INDEX)
01253          MOVE BIN-EXPCHG-SAVE    TO  WS-ACCT-EXP-DT (WS-INDEX)
01254          PERFORM 4300-VERIFY-INTEGRITY THRU 4399-EXIT
01255              VARYING  WS-INDEX  FROM  +1  BY  +1
01256                UNTIL  WS-INDEX GREATER +32
01257      ELSE
01258          MOVE ER-0453            TO  EMI-ERROR
01259          MOVE -1                 TO  MAINTL
01260          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
01261
01262      IF NOT EMI-NO-ERRORS
01263          GO TO 8200-SEND-DATAONLY.
01264
01265      MOVE BIN-EXPCHG-SAVE        TO  BIN-MAX-DATE.
01266      MOVE '2'                    TO  FROM-WHERE-SW.
01267
01268      PERFORM 2550-CHECK-HI-LO-DATES  THRU  2550-EXIT
01269        VARYING  T-INDEX  FROM  +1  BY  +1
01270          UNTIL  T-INDEX GREATER PI-TOTAL-LINES.
01271
01272      MOVE SPACE                  TO  FROM-WHERE-SW.
01273
01274      IF NOT EMI-NO-ERRORS
01275          GO TO 8200-SEND-DATAONLY.
01276
01277      SET T-INDEX  TO  PI-TOTAL-LINES.
01278      PERFORM 6010-DATE-CHECK  THRU  6010-EXIT.
01279
01280      IF NOT EMI-NO-ERRORS
01281          GO TO 8200-SEND-DATAONLY.
01282
01283      MOVE PI-BIN-EXP-DT (T-INDEX)  TO  PI-ACCT-EXP-DT.
01284      MOVE LOW-VALUES               TO  PI-ACCT-REST-OF-EXP.
01285      MOVE PI-ACCT-KEY              TO  B4-ACCT-KEY.
01286      MOVE PI-BIN-EFF-DT (T-INDEX)  TO  B4-ACCT-EFF-DT.
01287
01288      PERFORM 3100-DELETE-DATE-RANGE  THRU  3199-EXIT.
01289
01290      MOVE BIN-EXPCHG-SAVE        TO  AM-EXPIRATION-DT
01291                                      AM-VG-EXPIRATION-DT
01292                                      PI-BIN-EXP-DT (T-INDEX).
01293      MOVE BIN-CURRENT-SAVE       TO  AM-LAST-MAINT-DT
01294                                      PI-BIN-MAINT-DT (T-INDEX).
01295      MOVE AM-CONTROL-PRIMARY     TO  AF-ACCT-KEY.
01296      MOVE AM-EFFECTIVE-DT        TO  AF-ACCT-EFF-DT.
01297
01298      PERFORM 5600-CHG-AXRF-RECORDS  THRU  5699-EXIT.
01299
01300      PERFORM 3200-ADD-DATE-RANGE    THRU  3299-EXIT.
01301
01302      PERFORM 6600-UPDATE-MAINT-DT   THRU  6699-EXIT.
01303
CIDMOD     MOVE SPACES                 TO  EXPCHGI.
CIDMOD     MOVE AL-UANOF               TO  EXPCHGA.
CIDMOD     MOVE ER-0000                TO  EMI-ERROR.
01305
01306      PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
01307
01308      GO TO 7000-BUILD-OUTPUT-MAP.
01309
01310  EJECT
01311  3100-DELETE-DATE-RANGE.
01312      EXEC CICS GETMAIN
01313          LENGTH   (ACCT-REC-LEN)
01314          SET      (ADDRESS OF ACCOUNT-MASTER)
01315          INITIMG  (GETMAIN-SPACE)
01316      END-EXEC.
01317
01318      EXEC CICS  HANDLE CONDITION
01319          NOTFND   (3175-CONTINUE)
01320          ENDFILE  (3175-CONTINUE)
01321      END-EXEC.
01322
01323
01324      EXEC CICS  READ
01325          UPDATE
01326          DATASET  (PI-ACCT-ID)
01327          INTO     (ACCOUNT-MASTER)
01328          RIDFLD   (PI-ACCT-KEY)
01329      END-EXEC.
01330
01331      MOVE 'D'                    TO  JP-RECORD-TYPE.
01332      MOVE ACCOUNT-MASTER         TO  JP-RECORD-AREA.
01333      MOVE ACCT-FILE-ID           TO  FILE-ID.
01334
01335      EXEC CICS  DELETE
01336          DATASET  (PI-ACCT-ID)
01337      END-EXEC.
01338
01339      PERFORM 8400-LOG-JOURNAL-RECORD.
01340
01341      GO TO 3199-EXIT.
01342
01343  3175-CONTINUE.
01344      IF BROWSE-STARTED
01345          PERFORM 6320-ENDBR THRU 6320-EXIT.
01346
01347      MOVE ER-0226                TO  EMI-ERROR.
01348
01349      PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
01350
01351      MOVE -1                     TO  MAINTL.
01352
01353      GO TO 8200-SEND-DATAONLY.
01354
01355  3199-EXIT.
01356      EXIT.
01357
01358  3200-ADD-DATE-RANGE.
01359      MOVE 'A'                    TO  JP-RECORD-TYPE.
01360      MOVE ACCOUNT-MASTER         TO  JP-RECORD-AREA.
01361
01362      EXEC CICS  WRITE
01363          FROM     (ACCOUNT-MASTER)
01364          RIDFLD   (AM-CONTROL-PRIMARY)
01365          DATASET  (PI-ACCT-ID)
01366      END-EXEC.
01367
01368      PERFORM 8400-LOG-JOURNAL-RECORD.
01369
01370  3299-EXIT.
01371      EXIT.
01372  EJECT
01373  3500-CHANGE-EFFECTIVE.
01374      PERFORM 6100-BUILD-KEY  THRU  6199-EXIT.
01375
01376      PERFORM 6200-CHECK-KEY  THRU  6200-EXIT.
01377
01378      PERFORM 6000-EDIT-EFFECTIVE-DATE  THRU  6000-EFF-EXIT.
01379
01380      IF NOT EMI-NO-ERRORS
01381          GO TO 8200-SEND-DATAONLY.
01382
01383      PERFORM 4100-BUILD-VERIFICATION  THRU  4199-EXIT.
01384
01385      SET T-INDEX                 TO  +1.
01386
01387      PERFORM VARYING  WS-INDEX  FROM  +1  BY  +1
01388              UNTIL  PI-BIN-EFF-DT (T-INDEX)
01389                         =  WS-ACCT-EFF-DT (WS-INDEX)
01390                  OR  WS-INDEX GREATER +32
01391      END-PERFORM.
01392
01393      IF PI-BIN-EFF-DT (T-INDEX)
01394                  = WS-ACCT-EFF-DT (WS-INDEX)
01395          MOVE BIN-EFFCHG-SAVE    TO  WS-ACCT-EFF-DT (WS-INDEX)
01396          PERFORM 4300-VERIFY-INTEGRITY THRU 4399-EXIT
01397              VARYING  WS-INDEX  FROM  +1  BY  +1
01398                UNTIL  WS-INDEX GREATER +32
01399      ELSE
01400          MOVE ER-0453            TO  EMI-ERROR
01401          MOVE -1                 TO  MAINTL
01402          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
01403
01404      IF NOT EMI-NO-ERRORS
01405          GO TO 8200-SEND-DATAONLY.
01406
01407      SET T-INDEX TO  1.
01408
01409      PERFORM 6010-DATE-CHECK  THRU  6010-EXIT.
01410
01411      IF NOT EMI-NO-ERRORS
01412          GO TO 8200-SEND-DATAONLY.
01413
01414      MOVE PI-BIN-EXP-DT (T-INDEX)  TO  PI-ACCT-EXP-DT.
01415      MOVE LOW-VALUES               TO  PI-ACCT-REST-OF-EXP.
01416      MOVE PI-ACCT-KEY              TO  B4-ACCT-KEY.
01417      MOVE PI-BIN-EFF-DT (T-INDEX)  TO  B4-ACCT-EFF-DT.
01418
01419      EXEC CICS  READ
01420          UPDATE
01421          DATASET  (PI-ACCT-ID)
01422          SET      (ADDRESS OF ACCOUNT-MASTER)
01423          RIDFLD   (PI-ACCT-KEY)
01424      END-EXEC.
01425
01426      MOVE 'B'                    TO  JP-RECORD-TYPE.
01427      MOVE ACCT-FILE-ID           TO  FILE-ID.
01428      MOVE ACCOUNT-MASTER         TO  JP-RECORD-AREA.
01429
01430      PERFORM 8400-LOG-JOURNAL-RECORD.
01431
01432      MOVE BIN-EFFCHG-SAVE        TO  AM-EFFECTIVE-DT
01433                                      PI-BIN-EFF-DT (T-INDEX).
01434      MOVE EFFCHG-SAVE            TO  AM-EFFECT-DT.
01435      MOVE BIN-CURRENT-SAVE       TO  AM-LAST-MAINT-DT
01436                                      PI-BIN-MAINT-DT (T-INDEX).
01437      MOVE AM-CONTROL-PRIMARY     TO  AF-ACCT-KEY.
01438      MOVE AM-EFFECTIVE-DT        TO  AF-ACCT-EFF-DT.
01439      MOVE 'C'                    TO  JP-RECORD-TYPE.
01440      MOVE ACCOUNT-MASTER         TO  JP-RECORD-AREA.
01441
01442      PERFORM 5600-CHG-AXRF-RECORDS  THRU  5699-EXIT.
01443
01444      EXEC CICS  REWRITE
01445          FROM     (ACCOUNT-MASTER)
01446          DATASET  (PI-ACCT-ID)
01447      END-EXEC.
01448
01449      PERFORM 8400-LOG-JOURNAL-RECORD.
01450
01451      PERFORM 6600-UPDATE-MAINT-DT  THRU  6699-EXIT.
01452
CIDMOD     MOVE SPACES                 TO  EFFCHGO.
CIDMOD
01453      MOVE ZERO                   TO  EFFCHGL
01454                                      EXPCHGL.
CIDMOD
CIDMOD     MOVE AL-UANOF               TO  EFFCHGA.
CIDMOD
01455      MOVE ER-0000                TO  EMI-ERROR.
01456
01457      PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
01458
01459      GO TO 7000-BUILD-OUTPUT-MAP.
01460  EJECT
01461  4000-CHANGE-ALL.
01462      PERFORM 6100-BUILD-KEY  THRU  6199-EXIT.
01463
01464      PERFORM 6200-CHECK-KEY  THRU  6200-EXIT.
01465
01466      IF NOT EMI-NO-ERRORS
01467          GO TO 8200-SEND-DATAONLY.
01468
01469      MOVE 1                      TO  PI-LINE-SELECTED.
01470      MOVE MAINTI                 TO  PI-MAINT.
01471      MOVE XCTL-6501              TO  PGM-NAME.
01472
01473      GO TO 9300-XCTL.
01474  EJECT
01475  4100-BUILD-VERIFICATION.
01476      MOVE SPACES                 TO  BROWSE-STARTED-SW.
01477
01478      SET WS-INDEX  TO  +1.
01479
01480      MOVE 'N'                    TO  WS-ACCT2-FOUND.
01481      MOVE PI-ACCT-KEY            TO  WS-ACCT-VERIFY-KEY.
01482      MOVE LOW-VALUES             TO  WS-ACCT-VER-EXP-DT
01483                                      WS-VERIFY-AREA.
01484
01485      IF ST-ACCNT-CNTL  OR
01486         ACCNT-CNTL
01487           MOVE SPACES             TO  WS-ACCT-VER-CARR.
01488
01489      IF ST-ACCNT-CNTL      OR
01490         CARR-ST-ACCNT-CNTL OR
01491         ACCNT-CNTL         OR
01492         CARR-ACCNT-CNTL
01493           MOVE SPACES             TO  WS-ACCT-VER-GROUP.
01494
01495      IF ACCNT-CNTL OR
01496         CARR-ACCNT-CNTL
01497           MOVE SPACES             TO  WS-ACCT-VER-STATE.
01498
01499      MOVE WS-ACCT-VERIFY-KEY     TO  WS-SAVE-VERIFY-KEY.
01500
01501      EXEC CICS  HANDLE CONDITION
01502          NOTFND   (4175-CONTINUE)
01503          ENDFILE  (4175-CONTINUE)
01504      END-EXEC.
01505
01506      EXEC CICS  STARTBR
01507          DATASET  (ACCT2-FILE-ID)
01508          RIDFLD   (WS-ACCT-VERIFY-KEY)
01509          GTEQ
01510      END-EXEC.
01511
01512      MOVE 'Y'                    TO  BROWSE-STARTED-SW.
01513
01514  4150-READ-NEXT.
01515      EXEC CICS  READNEXT
01516          DATASET  (ACCT2-FILE-ID)
01517          RIDFLD   (WS-ACCT-VERIFY-KEY)
01518          SET      (ADDRESS OF ACCOUNT-MASTER)
01519      END-EXEC.
01520
01521      MOVE AM-CONTROL-BY-VAR-GRP  TO  WS-TEST-KEY.
01522
01523      IF WS-TEST-KEY  NOT = WS-SAVE-VERIFY-KEY
01524          GO TO 4175-CONTINUE.
01525
01526      MOVE 'Y'                    TO  WS-ACCT2-FOUND.
01527      MOVE AM-CONTROL-PRIMARY     TO  WS-ACCT-KEY (WS-INDEX).
01528      MOVE AM-VG-EXPIRATION-DT    TO  WS-ACCT-EXP-DT (WS-INDEX).
01529      MOVE AM-EFFECTIVE-DT        TO  WS-ACCT-EFF-DT (WS-INDEX).
01530      MOVE AM-HI-CERT-DATE        TO  DC-GREG-DATE-1-YMD.
01531      MOVE '3'                    TO  DC-OPTION-CODE.
01532
01533      PERFORM 9700-DATE-CONVERSION.
01534
01535      IF DATE-CONVERSION-ERROR
01536          MOVE LOW-VALUES         TO  WS-ACCT-HI-CERT (WS-INDEX)
01537      ELSE
01538          MOVE DC-BIN-DATE-1      TO  WS-ACCT-HI-CERT (WS-INDEX).
01539
01540      MOVE AM-LO-CERT-DATE        TO  DC-GREG-DATE-1-YMD.
01541      MOVE '3'                    TO  DC-OPTION-CODE.
01542
01543      PERFORM 9700-DATE-CONVERSION.
01544
01545      IF DATE-CONVERSION-ERROR
01546          MOVE LOW-VALUES         TO  WS-ACCT-LO-CERT (WS-INDEX)
01547      ELSE
01548          MOVE DC-BIN-DATE-1      TO  WS-ACCT-LO-CERT (WS-INDEX).
01549
01550      SET WS-INDEX  UP  BY  +1.
01551
01552      GO TO 4150-READ-NEXT.
01553
01554  4175-CONTINUE.
01555      IF BROWSE-STARTED
01556          EXEC CICS  ENDBR
01557              DATASET  (ACCT2-FILE-ID)
01558          END-EXEC
01559          MOVE SPACES             TO  BROWSE-STARTED-SW.
01560
01561  4199-EXIT.
01562      EXIT.
01563  EJECT
01564  4200-CHECK-VALIDITY.
01565      IF (BIN-EXPCHG-SAVE GREATER
01566                     WS-ACCT-EFF-DT (WS-INDEX))
01567        AND  (BIN-EXPCHG-SAVE NOT GREATER
01568                     WS-ACCT-EXP-DT (WS-INDEX))
01569          MOVE ER-2051            TO  EMI-ERROR
01570          MOVE -1                 TO  EXPCHGL
01571          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
01572
01573      IF BIN-EFFCHG-SAVE NOT LESS WS-ACCT-EFF-DT (WS-INDEX) AND
01574         BIN-EFFCHG-SAVE LESS     WS-ACCT-EXP-DT (WS-INDEX)
01575           MOVE ER-2051            TO  EMI-ERROR
01576           MOVE -1                 TO  EFFCHGL
01577           PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
01578
111513 4200-EXIT.
01580      EXIT.

111513 4220-DISPLAY-VERI-TABLE.
111513
111513     perform varying d1 from +1 by +1 until
111513        (d1 > +32)
111513        or (ws-acct-eff-dt (d1) = low-values)
111513        move ws-acct-eff-dt (d1) to dc-bin-date-1
111513        move ' '                 to dc-option-code
111513        perform 9700-date-conversion
111513        move dc-greg-date-1-edit to ws-display-eff-date
111513        
111513        move ws-acct-exp-dt (d1) to dc-bin-date-1
111513        move ' '                 to dc-option-code
111513        perform 9700-date-conversion
111513        move dc-greg-date-1-edit to ws-display-exp-date
111513        
111513        display ' date range ' d1 ' ' ws-display-eff-date
111513                                  ' ' ws-display-exp-date
111513     end-perform
111513
111513     .
111513 4220-EXIT.
           EXIT.
01581
01582  4300-VERIFY-INTEGRITY.

111513     set d1 to ws-index

01583      IF WS-ACCT-EFF-DT (WS-INDEX + 1)  = LOW-VALUES
01584          SET WS-INDEX  TO  +32
01585          GO TO 4399-EXIT.
01586
01587      IF ((WS-ACCT-EXP-DT (WS-INDEX)  =  BIN-EXPCHG-SAVE)  OR
01588         (WS-ACCT-EFF-DT (WS-INDEX + 1)  =  BIN-EFFCHG-SAVE))
111513                  AND
111513        (WS-ACCT-EXP-DT (WS-INDEX) > WS-ACCT-EFF-DT (WS-INDEX))
01589          SET WS-INDEX  TO  +33
01590          GO TO 4399-EXIT.
01591
01592      IF (WS-ACCT-EXP-DT (WS-INDEX) NOT GREATER
01593                     WS-ACCT-EFF-DT (WS-INDEX + 1))
01594        AND  (WS-ACCT-EXP-DT (WS-INDEX) GREATER
01595                     WS-ACCT-EFF-DT (WS-INDEX))
01596          GO TO 4399-EXIT.
01597
01598      MOVE ER-2051                TO  EMI-ERROR.
01599      MOVE -1                     TO  EXPCHGL.
01600
01601      PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
01602
01603  4399-EXIT.
01604      EXIT.
01605  EJECT
01606  4500-CREATE-NEW-ACCOUNT.
01607      PERFORM 6100-BUILD-KEY            THRU 6199-EXIT.
01608
01609      PERFORM 6700-VERIFY-STATE-CARRIER THRU 6799-EXIT.
01610
01611      IF MAINTI = 'T'
01612          PERFORM 6000-EDIT-EFFECTIVE-DATE THRU 6000-EFF-EXIT.
01613
01614      PERFORM 4100-BUILD-VERIFICATION THRU 4199-EXIT.
01615
01616      IF WS-ACCT2-FOUND = 'Y'
01617          MOVE ER-0561            TO  EMI-ERROR
01618          MOVE -1                 TO  MAINTL
01619          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
01620
01621      MOVE SPACES                 TO  GETMAIN-SW.
01622
01623      IF NOT EMI-NO-ERRORS
01624          GO TO 8200-SEND-DATAONLY.
01625
01626      IF PI-PREV-ACCOUNT  =  LOW-VALUES
01627          MOVE ER-0453            TO  EMI-ERROR
01628          MOVE -1                 TO  MAINTL
01629          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
01630          GO TO 8200-SEND-DATAONLY.
01631
01632      MOVE PI-ACCT-CCGSA-KEY      TO  NEW-KEY-SAVE.
01633
01634      MOVE LOW-VALUES             TO  PI-ACCT-EXP-DT
01635                                      PI-ACCT-REST-OF-EXP.
01636
01637      PERFORM 6500-READ-ACCT  THRU  6599-EXIT.
01638
01639      IF REC-FOUND
01640          MOVE -1                 TO  ACARIERL
01641          MOVE ER-0561            TO  EMI-ERROR
01642          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
01643          GO TO 8200-SEND-DATAONLY.
01644
01645      MOVE PI-PREV-ACCOUNT        TO  PI-ACCT-CCGSA-KEY.
01646      MOVE LOW-VALUES             TO  PI-ACCT-EXP-DT
01647                                      PI-ACCT-REST-OF-EXP.
01648
01649      EXEC CICS  HANDLE CONDITION
01650          NOTFND   (4530-NOT-FOUND)
01651          ENDFILE  (4530-NOT-FOUND)
01652      END-EXEC.
01653
01654      PERFORM 6300-STARTBR  THRU  6300-EXIT.
01655
01656  4510-READ-NEXT.
01657      PERFORM 6310-READNEXT  THRU  6310-EXIT.
01658
01659      IF PI-ACCT-CCGSA-KEY  NOT = PI-PREV-ACCOUNT
01660          GO TO 4540-END-COPY.
01661
01662      MOVE AM-EXPIRATION-DT       TO  WS-EXP-SAVE.
01663      MOVE NEW-KEY-SAVE           TO  AM-CONTROL-PRIMARY.
01664      MOVE LOW-VALUES             TO  AM-CNTRL-B.
01665      MOVE WS-EXP-SAVE            TO  AM-EXPIRATION-DT.
01666
01667      PERFORM 6400-BUILD-VG-KEY  THRU  6499-EXIT.
01668
01669      IF MAINTI  = 'T'
01670          MOVE '2'                TO  AM-STATUS
01671          MOVE AM-ACCOUNT         TO  AM-AGT (1)
01672          MOVE PI-ACCT-CARRIER    TO  AM-TRNFROM-CARRIER
01673          MOVE PI-ACCT-GROUPING   TO  AM-TRNFROM-GROUPING
01674          MOVE PI-ACCT-STATE      TO  AM-TRNFROM-STATE
01675          MOVE PI-ACCT-ACCOUNT    TO  AM-TRNFROM-ACCOUNT
01676          MOVE BIN-EFFCHG-SAVE    TO  AM-TRNFROM-DTE
01677      ELSE
01678          MOVE LOW-VALUES         TO  AM-AR-HI-CERT-DATE
01679          MOVE ZEROS              TO  AM-HI-CERT-DATE
01680                                      AM-LO-CERT-DATE
01681          MOVE SPACES             TO  AM-1ST-PROD-DATE
01682                                      AM-PREV-DATES
121207                                     AM-ORIG-DEALER-NO
121207     END-IF                                           
01683
01684      MOVE SPACES                 TO  AM-CERTS-PURGED-DATE.
01685      MOVE BIN-CURRENT-SAVE       TO  AM-LAST-MAINT-DT.
01686      MOVE PI-PROCESSOR-ID        TO  AM-LAST-MAINT-USER.
01687      MOVE EIBTIME                TO  AM-LAST-MAINT-HHMMSS.
01688
01689      PERFORM 5500-ADD-NEW-AXRF-RECORD  THRU  5599-EXIT.
01690
01691      MOVE ACCOUNT-MASTER         TO  JP-RECORD-AREA.
01692      MOVE 'A'                    TO  JP-RECORD-TYPE.
01693
01694      PERFORM 6320-ENDBR THRU 6320-EXIT.
01695
01696      EXEC CICS GETMAIN
01697          LENGTH   (ACCT-REC-LEN)
01698          SET      (ADDRESS OF ACCOUNT-MASTER)
01699          INITIMG  (GETMAIN-SPACE)
01700      END-EXEC.
01701
01702      MOVE JP-RECORD-AREA         TO  ACCOUNT-MASTER.
01703
01704      EXEC CICS  WRITE
01705          DATASET  (PI-ACCT-ID)
01706          RIDFLD   (AM-CONTROL-PRIMARY)
01707          FROM     (ACCOUNT-MASTER)
01708      END-EXEC.
01709
01710      MOVE PI-ACCT-ID             TO  FILE-ID.
01711
01712      PERFORM 8400-LOG-JOURNAL-RECORD.
01713
01714      PERFORM 6300-STARTBR  THRU 6300-EXIT.
01715      PERFORM 6310-READNEXT THRU 6310-EXIT.
01716
01717      GO TO 4510-READ-NEXT.
01718
01719  4530-NOT-FOUND.
01720      IF BROWSE-STARTED
01721          PERFORM 6320-ENDBR THRU 6320-EXIT.
01722
01723      MOVE ER-0226                TO  EMI-ERROR.
01724
01725      PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
01726
01727      MOVE -1                     TO  MAINTL.
01728
01729      GO TO 8200-SEND-DATAONLY.
01730
01731  4540-END-COPY.
01732      IF MAINTI = 'K'
01733          IF (PI-COMPANY-ID IS EQUAL TO 'AIG' OR 'AUK')
01734              NEXT SENTENCE
01735          ELSE
01736              PERFORM 4600-ADD-ERPLAN-RECORD THRU 4699-EXIT.
01737
01738      IF BROWSE-STARTED
01739          PERFORM 6320-ENDBR THRU 6320-EXIT.
01740
01741      IF MAINTI  NOT = 'T'
01742          GO TO 4590-SHOW-NEW-ACCOUNT.
01743
01744      MOVE PI-PREV-ACCOUNT        TO  PI-ACCT-CCGSA-KEY.
01745      MOVE LOW-VALUES             TO  PI-ACCT-EXP-DT
01746                                      PI-ACCT-REST-OF-EXP.
01747
01748      PERFORM 6300-STARTBR  THRU  6300-EXIT.
01749
01750  4550-READ-NEXT.
01751      PERFORM 6310-READNEXT  THRU  6310-EXIT.
01752
01753      IF PI-ACCT-CCGSA-KEY  NOT =  PI-PREV-ACCOUNT
01754          GO TO 4570-END-REWRITE.
01755
01756      EXEC CICS  READ
01757          UPDATE
01758          DATASET  (PI-ACCT-ID)
01759          SET      (ADDRESS OF ACCOUNT-MASTER)
01760          RIDFLD   (PI-ACCT-KEY)
01761      END-EXEC.
01762
01763      MOVE 'B'                    TO  JP-RECORD-TYPE.
01764      MOVE ACCT-FILE-ID           TO  FILE-ID.
01765      MOVE ACCOUNT-MASTER         TO  JP-RECORD-AREA.
01766
01767      PERFORM 8400-LOG-JOURNAL-RECORD.
01768
01769      MOVE NEW-KEY-CNTRL          TO  AM-TRANSFERRED-TO.
01770      MOVE BIN-EFFCHG-SAVE        TO  AM-TRNTO-DTE.
01771      MOVE BIN-CURRENT-SAVE       TO  AM-LAST-MAINT-DT.
01772      MOVE PI-PROCESSOR-ID        TO  AM-LAST-MAINT-USER.
01773      MOVE EIBTIME                TO  AM-LAST-MAINT-HHMMSS.
01774      MOVE ACCOUNT-MASTER         TO  JP-RECORD-AREA.
01775      MOVE 'C'                    TO  JP-RECORD-TYPE.
01776
01777      EXEC CICS  REWRITE
01778          DATASET  (PI-ACCT-ID)
01779          FROM     (ACCOUNT-MASTER)
01780      END-EXEC.
01781
01782      MOVE PI-ACCT-ID             TO  FILE-ID.
01783
01784      PERFORM 8400-LOG-JOURNAL-RECORD.
01785
01786      GO TO 4550-READ-NEXT.
01787
01788  4570-END-REWRITE.
01789      IF BROWSE-STARTED
01790          PERFORM 6320-ENDBR  THRU  6320-EXIT.
01791
01792      MOVE SPACES                 TO  MAINTI
01793                                      EFFCHGI.
01794      MOVE AL-UANOF               TO  EFFCHGA.
01795
01796  4590-SHOW-NEW-ACCOUNT.
01797      PERFORM 6600-UPDATE-MAINT-DT  THRU  6699-EXIT.
01798
01799      MOVE NEW-KEY-SAVE           TO  PI-PREV-ACCOUNT
01800                                      PI-ACCT-CCGSA-KEY.
01801
01802      MOVE -1                     TO  MAINTL.
01803      MOVE ER-0000                TO  EMI-ERROR.
01804
01805      PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
01806
01807      MOVE LOW-VALUES             TO  PI-ACCT-EXP-DT.
01808
01809      GO TO 1002-SHOW-NEW-RECORDS.
01810
01811  EJECT
01812  4600-ADD-ERPLAN-RECORD.
01813      EXEC CICS  HANDLE CONDITION
01814          NOTFND   (4699-EXIT)
01815          ENDFILE  (4689-EOF)
01816      END-EXEC.
01817
01818      MOVE LOW-VALUES            TO WS-PLAN-KEY.
01819      MOVE PI-PREV-ACCOUNT       TO EK-CCGSA-KEY.
01820
01821      PERFORM 4700-STARTBR-PLAN THRU 4700-EXIT.
01822
01823  4610-READ-NEXT.
01824      PERFORM 4710-READNEXT-PLAN THRU 4710-EXIT.
01825
01826      IF EK-CCGSA-KEY NOT = PI-PREV-ACCOUNT
01827          GO TO 4689-EOF.
01828
01829      MOVE NEW-KEY-SAVE           TO  PL-CONTROL-PRIMARY.
01830
01831      MOVE EK-BTYPE               TO  PL-BENEFIT-TYPE.
01832      MOVE EK-BCODE               TO  PL-BENEFIT-CODE.
01833      MOVE EK-REVISION            TO  PL-REVISION-NO.
01834
01835      MOVE PL-CONTROL-PRIMARY     TO  PL-CONTROL-BY-VAR-GRP.
01836
01837      MOVE PLAN-MASTER            TO  JP-RECORD-AREA.
01838
01839      EXEC CICS  ENDBR
01840          DATASET  (PLAN-FILE-ID)
01841      END-EXEC.
01842
01843      EXEC CICS GETMAIN
01844          LENGTH   (PLAN-REC-LEN)
01845          SET      (ADDRESS OF PLAN-MASTER)
01846          INITIMG  (GETMAIN-SPACE)
01847      END-EXEC.
01848
01849      MOVE JP-RECORD-AREA         TO PLAN-MASTER.
01850
01851      EXEC CICS  WRITE
01852          DATASET  (PLAN-FILE-ID)
01853          RIDFLD   (PL-CONTROL-PRIMARY)
01854          FROM     (PLAN-MASTER)
01855      END-EXEC.
01856
01857      PERFORM 4700-STARTBR-PLAN  THRU 4700-EXIT.
01858      PERFORM 4710-READNEXT-PLAN THRU 4710-EXIT.
01859
01860      GO TO 4610-READ-NEXT.
01861
01862  4689-EOF.
01863      EXEC CICS  ENDBR
01864          DATASET  (PLAN-FILE-ID)
01865      END-EXEC.
01866
01867  4699-EXIT.
01868       EXIT.
01869
01870  4700-STARTBR-PLAN.
01871      EXEC CICS  STARTBR
01872          DATASET  (PLAN-FILE-ID)
01873          RIDFLD   (WS-PLAN-KEY)
01874      END-EXEC.
01875
01876  4700-EXIT.
01877       EXIT.
01878
01879  4710-READNEXT-PLAN.
01880      EXEC CICS  READNEXT
01881          DATASET  (PLAN-FILE-ID)
01882          SET      (ADDRESS OF PLAN-MASTER)
01883          RIDFLD   (WS-PLAN-KEY)
01884      END-EXEC.
01885
01886  4710-EXIT.
01887       EXIT.
01888
01889  EJECT
01890  4800-DELETE-PLAN-REC.
01891      EXEC CICS  HANDLE CONDITION
01892          NOTFND   (4899-EXIT)
01893          ENDFILE  (4889-EOF)
01894      END-EXEC.
01895
01896      MOVE LOW-VALUES            TO WS-PLAN-KEY.
01897      MOVE AM-CONTROL-PRIMARY    TO EK-CCGSA-KEY
01898                                    PI-PREV-ACCOUNT.
01899
01900  4805-STARTBR.
01901      PERFORM 4700-STARTBR-PLAN THRU 4700-EXIT.
01902
01903  4810-READ-NEXT.
01904      PERFORM 4710-READNEXT-PLAN THRU 4710-EXIT.
01905
01906      IF EK-CCGSA-KEY NOT = PI-PREV-ACCOUNT
01907          GO TO 4889-EOF.
01908
01909      EXEC CICS  ENDBR
01910          DATASET  (PLAN-FILE-ID)
01911      END-EXEC.
01912
01913      EXEC CICS  READ
01914          UPDATE
01915          DATASET  (PLAN-FILE-ID)
01916          SET      (ADDRESS OF PLAN-MASTER)
01917          RIDFLD   (WS-PLAN-KEY)
01918      END-EXEC.
01919
01920      EXEC CICS  DELETE
01921          DATASET  (PLAN-FILE-ID)
01922      END-EXEC.
01923
01924      GO TO 4805-STARTBR.
01925
01926  4889-EOF.
01927      EXEC CICS  ENDBR
01928          DATASET  (PLAN-FILE-ID)
01929      END-EXEC.
01930
01931  4899-EXIT.
01932       EXIT.
01933  EJECT
01934  4900-CREATE-NEW-ACCOUNT.
01935      PERFORM 6100-BUILD-KEY             THRU  6199-EXIT.
01936
01937      SET T-INDEX                 TO  LINEI.
01938
01939      IF PI-2ND-PAGE
01940          SET T-INDEX  UP  BY  8
01941      ELSE
01942      IF PI-3RD-PAGE
01943          SET T-INDEX  UP  BY  16
01944      ELSE
01945      IF PI-LST-PAGE
01946          SET T-INDEX  UP  BY  24.
01947
01948      IF EFFCHGL NOT =  ZERO
01949          PERFORM 6000-EDIT-EFFECTIVE-DATE   THRU  6000-EFF-EXIT
01950        ELSE
01951          MOVE PI-BIN-EFF-DT (T-INDEX)  TO  BIN-EFFCHG-SAVE.
01952
01953      IF EXPCHGL NOT =  ZERO
01954          PERFORM 6000-EDIT-EXPIRATION-DATE  THRU  6000-EXP-EXIT
01955        ELSE
01956          MOVE PI-BIN-EXP-DT (T-INDEX)  TO  BIN-EXPCHG-SAVE.
01957
01958      PERFORM 6700-VERIFY-STATE-CARRIER  THRU  6799-EXIT.
01959
01960      IF BIN-EXPCHG-SAVE NOT GREATER BIN-EFFCHG-SAVE
01961          MOVE -1                 TO  EXPCHGL
01962          MOVE ER-1228            TO  EMI-ERROR
01963          MOVE AL-UABON           TO  EXPCHGA
01964          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
01965
01966      PERFORM 6000-EDIT-LINE  THRU  6000-LIN-EXIT.
01967
01968      IF EMI-ERROR  NOT = ZEROS
01969          GO TO 8200-SEND-DATAONLY.
01970
01971      MOVE SPACES                 TO  GETMAIN-SW.
01972
01973      IF NOT EMI-NO-ERRORS
01974          GO TO 8200-SEND-DATAONLY.
01975
01976      IF PI-PREV-ACCOUNT  =  LOW-VALUES
01977          MOVE ER-0453            TO  EMI-ERROR
01978          MOVE -1                 TO  MAINTL
01979          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
01980          GO TO 8200-SEND-DATAONLY.
01981
01982      MOVE PI-ACCT-CCGSA-KEY      TO  NEW-KEY-SAVE.
01983
01984      PERFORM 4100-BUILD-VERIFICATION  THRU  4199-EXIT.
01985
111513     PERFORM 4200-CHECK-VALIDITY  THRU  4200-EXIT
01987          VARYING  WS-INDEX  FROM  +1  BY  +1
01988             UNTIL WS-INDEX GREATER +32.
01989
01990      IF NOT EMI-NO-ERRORS
01991          GO TO 8200-SEND-DATAONLY.
01992
01993      MOVE PI-PREV-ACCOUNT          TO  PI-ACCT-CCGSA-KEY.
01994      MOVE PI-BIN-EXP-DT (T-INDEX)  TO  PI-ACCT-EXP-DT.
01995      MOVE LOW-VALUES               TO  PI-ACCT-REST-OF-EXP.
01996
01997      PERFORM 6500-READ-ACCT     THRU  6599-EXIT.
01998
01999      MOVE LOW-VALUES             TO  AM-AR-HI-CERT-DATE
02000      MOVE ZEROS                  TO  AM-HI-CERT-DATE
02001                                      AM-LO-CERT-DATE
           MOVE ZEROS                  TO  AM-DCC-MAX-MARKETING-FEE
02002      MOVE SPACES                 TO  AM-1ST-PROD-DATE
02003                                      AM-PREV-DATES
121207                                     AM-ORIG-DEALER-NO
02004
02005      MOVE SPACES                 TO  AM-CERTS-PURGED-DATE.
02006      MOVE BIN-CURRENT-SAVE       TO  AM-LAST-MAINT-DT.
02007      MOVE PI-PROCESSOR-ID        TO  AM-LAST-MAINT-USER.
02008      MOVE EIBTIME                TO  AM-LAST-MAINT-HHMMSS.
02009
02010      PERFORM 5500-ADD-NEW-AXRF-RECORD  THRU  5599-EXIT.
02011
02012      MOVE ACCOUNT-MASTER         TO  JP-RECORD-AREA.
02013      MOVE 'A'                    TO  JP-RECORD-TYPE.
02014
02015      EXEC CICS GETMAIN
02016          LENGTH   (ACCT-REC-LEN)
02017          SET      (ADDRESS OF ACCOUNT-MASTER)
02018          INITIMG  (GETMAIN-SPACE)
02019      END-EXEC.
02020
02021      MOVE JP-RECORD-AREA         TO  ACCOUNT-MASTER.
02022      MOVE NEW-KEY-SAVE           TO  AM-CONTROL-PRIMARY.
02023      MOVE LOW-VALUES             TO  AM-CNTRL-B.
02024      MOVE BIN-EFFCHG-SAVE        TO  AM-EFFECTIVE-DT.
02025      MOVE BIN-EXPCHG-SAVE        TO  AM-EXPIRATION-DT
02026                                      AM-VG-EXPIRATION-DT.
02027
02028      PERFORM 6400-BUILD-VG-KEY  THRU  6499-EXIT.
02029
02030      EXEC CICS  HANDLE CONDITION
02031          DUPREC   (4580-DUPREC)
02032          DUPKEY   (4580-DUPREC)
02033      END-EXEC.
02034
02035      EXEC CICS  WRITE
02036          DATASET  (PI-ACCT-ID)
02037          RIDFLD   (AM-CONTROL-PRIMARY)
02038          FROM     (ACCOUNT-MASTER)
02039      END-EXEC.
02040
02041      MOVE PI-ACCT-ID             TO  FILE-ID.
02042
02043      PERFORM 8400-LOG-JOURNAL-RECORD.
02044
02045      IF (PI-COMPANY-ID IS EQUAL TO 'AIG' OR 'AUK')
02046          NEXT SENTENCE
02047      ELSE
02048          PERFORM 4950-ADD-ERPLAN-RECORD THRU 4959-EXIT
02049          PERFORM 4960-ADD-ERCOMP-RECORD THRU 4969-EXIT.
02050
02051  4580-DUPREC.
02052      MOVE 1                  TO K-WITH-LINE-USED.
02053      MOVE LOW-VALUES         TO EL650AI.
02054
02055      GO TO 4590-SHOW-NEW-ACCOUNT.
02056
02057  EJECT
02058  4950-ADD-ERPLAN-RECORD.
02059      EXEC CICS GETMAIN
02060          LENGTH   (PLAN-REC-LEN)
02061          SET      (ADDRESS OF PLAN-MASTER)
02062          INITIMG  (GETMAIN-SPACE)
02063      END-EXEC.
02064
02065      EXEC CICS  HANDLE CONDITION
02066          DUPREC   (4959-EXIT)
02067          DUPKEY   (4959-EXIT)
02068      END-EXEC.
02069
02070      MOVE NEW-KEY-SAVE           TO  PL-CONTROL-PRIMARY.
02071
02072      MOVE EK-BTYPE               TO  PL-BENEFIT-TYPE.
02073      MOVE EK-BCODE               TO  PL-BENEFIT-CODE.
02074      MOVE EK-REVISION            TO  PL-REVISION-NO.
02075
02076      MOVE PL-CONTROL-PRIMARY     TO  PL-CONTROL-BY-VAR-GRP.
02077
02078      EXEC CICS  WRITE
02079          DATASET  (PLAN-FILE-ID)
02080          RIDFLD   (PL-CONTROL-PRIMARY)
02081          FROM     (PLAN-MASTER)
02082      END-EXEC.
02083
02084  4959-EXIT.
02085      EXIT.
02086
02087  EJECT
02088  4960-ADD-ERCOMP-RECORD.
02089      EXEC CICS GETMAIN
02090           LENGTH   (COMP-REC-LEN)
02091           SET      (ADDRESS OF COMPENSATION-MASTER)
02092           INITIMG  (GETMAIN-SPACE)
02093      END-EXEC.
02094
02095      MOVE SPACES                 TO COMPENSATION-MASTER.
02096      MOVE ZEROS TO CO-BAL-FWD      CO-CUR-COM   CO-CUR-CHG
02097                    CO-CUR-PMT      CO-END-BAL   CO-CUR
02098                    CO-OV30         CO-OV60      CO-OV90
02099                    CO-YTD-COM      CO-YTD-OV    CO-CUR-OVR-UNDR
02100                    CO-YTD-OVR-UNDR CO-CUR-FICA CO-YTD-FICA
02101                    CO-LF-CLM-AMT   CO-AH-CLM-AMT
02102                    CO-CURRENT-BAL-FWD CO-CURRENT-CUR-COM
02103                    CO-CURRENT-CUR-CHG CO-CURRENT-END-BAL
02104                    CO-CURRENT-CUR     CO-CURRENT-OV30
02105                    CO-CURRENT-OV60    CO-CURRENT-OV90
02106                    CO-CURRENT-CUR-PMT
02107                    CO-CURRENT-YTD-COM CO-CURRENT-YTD-OV
02108                    CO-ACT-YEAR        CO-ACT-MONTH
02109                    CO-ACT-DAY         CO-LAST-STMT-YEAR
02110                    CO-LAST-STMT-MONTH CO-LAST-STMT-DAY
02111                    CO-CURRENT-LAST-STMT-YEAR
02112                    CO-CURRENT-LAST-STMT-MONTH
02113                    CO-CURRENT-LAST-STMT-DAY
02114                    CO-YTD-PAID-COM
080612                   CO-YTD-PAID-OV co-ov120 co-current-ov120

02117      MOVE BIN-CURRENT-SAVE       TO CO-LAST-MAINT-DT.
02118      MOVE PI-PROCESSOR-ID        TO CO-LAST-MAINT-USER.
02119      MOVE EIBTIME                TO CO-LAST-MAINT-HHMMSS.
02120      MOVE PI-CR-MONTH-END-DT     TO CO-ROLADEX-PRINT-DT.
02121      MOVE PI-COMPANY-CD          TO CO-COMPANY-CD.
02122
02123      IF PI-ZERO-CARRIER OR PI-ZERO-CAR-GROUP
02124          MOVE ZEROS              TO CO-CARRIER
02125      ELSE
02126          MOVE AM-CARRIER         TO CO-CARRIER.
02127
02128      IF PI-ZERO-GROUPING OR PI-ZERO-CAR-GROUP
02129          MOVE ZEROS              TO CO-GROUPING
02130      ELSE
02131          MOVE AM-GROUPING        TO CO-GROUPING.
02132
02133      MOVE 'A'                    TO CO-TYPE.
02134      MOVE AM-NAME                TO CO-ACCT-NAME.
02135      MOVE AM-PERSON              TO CO-MAIL-NAME.
02136      MOVE AM-ADDRS               TO CO-ADDR-1.
02137 *    MOVE AM-CITY                TO CO-ADDR-3.
           MOVE AM-ADDR-CITY           TO CO-ADDR-CITY
           MOVE AM-ADDR-STATE          TO CO-ADDR-STATE
02138      MOVE AM-ZIP                 TO CO-ZIP.
02139      MOVE AM-CSR-CODE            TO CO-CSR-CODE.
02140      MOVE AM-TEL-NO              TO CO-TELEPHONE.
02141      MOVE AM-ID-NO               TO CO-SOC-SEC.
02142
02143      IF AM-REMIT-TO NOT = ZERO
02144          MOVE AM-AGT (AM-REMIT-TO) TO CO-RESP-NO
02145        ELSE
02146          MOVE AM-ACCOUNT           TO CO-RESP-NO.
02147
02148      MOVE +0                      TO AGT-SUB.
02149
02150      PERFORM 4970-FIND-C-AGT THRU 4970-EXIT.
02151
02152      IF VALID-AGT-SW = 'Y'
02153          NEXT SENTENCE
02154      ELSE
02155          GO TO 4969-EXIT.
02156
02157      IF CO-RESP-NO = CO-ACCOUNT
02158          MOVE 'Y'                  TO CO-BALANCE-CONTROL
02159      ELSE
02160          MOVE 'N'                  TO CO-BALANCE-CONTROL.
02161
02162      IF PI-AR-PROCESSING
02163         MOVE 'G'                   TO CO-AR-REPORTING
02164         MOVE '1'                   TO CO-AR-BAL-LEVEL
02165         MOVE 'Y'                   TO CO-AR-PULL-CHECK
02166         MOVE 'Y'                   TO CO-AR-NORMAL-PRINT.
02167
02168      IF PI-COMPANY-ID = 'CRI' OR 'HER' OR 'HSL'
02169         MOVE 'N'                   TO CO-AR-REPORTING.
02170
02171      IF PI-COMPANY-ID = 'HER' OR 'HSL'
02172         MOVE '999999'              TO CO-AR-SUMMARY-CODE.
02173
02174      PERFORM 4980-ADD-COMPENSATION-NAME THRU 4980-EXIT.
02175
02176      EXEC CICS HANDLE CONDITION
02177           DUPREC   (4969-EXIT)
02178           DUPKEY   (4969-EXIT)
02179      END-EXEC.
02180
02181      EXEC CICS WRITE
02182          FROM      (COMPENSATION-MASTER)
02183          RIDFLD    (CO-CONTROL-PRIMARY)
02184          DATASET   (COMP-FILE-ID)
02185      END-EXEC.
02186
02187  4969-EXIT.
02188      EXIT.
02189
02190  4970-FIND-C-AGT.
02191      ADD 1 TO  AGT-SUB.
02192      IF AGT-SUB GREATER 10
02193          MOVE 'N'                TO  VALID-AGT-SW
02194          GO TO 4970-EXIT.
02195
052814     IF (AM-COM-TYP (AGT-SUB) = 'C' OR 'D' OR 'F')
02197          MOVE AM-AGT (AGT-SUB)   TO  CO-ACCOUNT
02198          MOVE 'Y'                TO  VALID-AGT-SW
02199          GO TO 4970-EXIT
02200      ELSE
02201          GO TO 4970-FIND-C-AGT.
02202
02203  4970-EXIT.
02204      EXIT.
02205
02206  4980-ADD-COMPENSATION-NAME.
02207      EXEC CICS HANDLE CONDITION
02208           DUPREC   (4980-EXIT)
02209      END-EXEC.
02210
02211      EXEC CICS GETMAIN
02212           LENGTH   (NAME-REC-LEN)
02213           SET      (ADDRESS OF NAME-LOOKUP-MASTER)
02214           INITIMG  (GETMAIN-SPACE)
02215      END-EXEC.
02216
02217      MOVE SPACES                 TO  NAME-LOOKUP-MASTER.
02218      MOVE BIN-CURRENT-SAVE       TO  NL-LAST-MAINT-DT.
02219      MOVE PI-PROCESSOR-ID        TO  NL-LAST-MAINT-USER.
02220      MOVE EIBTIME                TO  NL-LAST-MAINT-HHMMSS.
02221      MOVE PI-COMPANY-CD          TO  NL-COMPANY-CD.
02222      MOVE CO-ACCT-NAME           TO  NL-NAME.
02223      MOVE CO-ADDR-CITY           TO  NL-CITY
           MOVE CO-ADDR-STATE          TO  NL-ST
02224      MOVE 'C'                    TO  NL-RECORD-TYPE.
02225      MOVE PI-COMPANY-CD          TO  NL-CO-COMPANY-CD.
02226      MOVE CO-CARRIER             TO  NL-CO-CARRIER.
02227      MOVE CO-GROUPING            TO  NL-CO-GROUPING.
02228      MOVE CO-RESP-NO             TO  NL-CO-RESP-NO.
02229      MOVE CO-ACCOUNT             TO  NL-CO-ACCOUNT.
02230      MOVE CO-TYPE                TO  NL-CO-TYPE.
02231
02232      EXEC CICS WRITE
02233          FROM      (NAME-LOOKUP-MASTER)
02234          RIDFLD    (NL-CONTROL-PRIMARY)
02235          DATASET   (NAME-FILE-ID)
02236      END-EXEC.
02237
02238  4980-EXIT.
02239      EXIT.
02240  EJECT
02241  5000-CREATE-NEW-RANGE.
02242
02243      PERFORM 6100-BUILD-KEY  THRU  6199-EXIT.
02244      PERFORM 6200-CHECK-KEY  THRU  6200-EXIT.
02245
02246      PERFORM 6000-EDIT-EFFECTIVE-DATE  THRU 6000-EFF-EXIT.
02247      PERFORM 6000-EDIT-EXPIRATION-DATE THRU 6000-EXP-EXIT.
02248
02249      SET T-INDEX TO PI-TOTAL-LINES.
02250
02251      PERFORM 600A-CHECK-HI-LO-DATES    THRU 600A-EXIT
02252         VARYING T-INDEX FROM T-INDEX BY +1
02253          UNTIL  T-INDEX GREATER PI-TOTAL-LINES.
02254
02255      IF NOT EMI-NO-ERRORS
02256          GO TO 8200-SEND-DATAONLY.
02257
02258      MOVE PI-BIN-EXP-DT (T-INDEX)  TO  PI-ACCT-EXP-DT.
02259      MOVE LOW-VALUES               TO  PI-ACCT-REST-OF-EXP.
02260
02261      IF BIN-EFFCHG-SAVE NOT LESS BIN-EXPCHG-SAVE
02262          MOVE -1                 TO  EFFCHGL EXPCHGL
02263          MOVE AL-UANON           TO  EFFCHGA EXPCHGA
02264          MOVE ER-2053            TO  EMI-ERROR
02265          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
02266
02267      IF NOT EMI-NO-ERRORS
02268          GO TO 8200-SEND-DATAONLY.
02269
02270 ***************************************************************
02271 *    NO EFFECTIVE DATE ENTERED, WILL USE CURRENT EXPIRATION   *
02272 *    DATE IF WITHIN LIMITS                                    *
02273 ***************************************************************
02274
02275      IF EFFCHGL = ZERO
02276          SET T-INDEX             TO  PI-TOTAL-LINES
02277          IF PI-BIN-EXP-DT (T-INDEX)  =  HIGH-VALUES
02278              MOVE ER-2060        TO  EMI-ERROR
02279              MOVE -1             TO  EFFCHGL
02280              MOVE AL-UABON       TO  EFFCHGA
02281              PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
02282              GO TO 8200-SEND-DATAONLY
02283          ELSE
02284              IF PI-BIN-EXP-DT (T-INDEX) NOT LESS
02285                     BIN-EXPCHG-SAVE
02286                  MOVE ER-2061    TO  EMI-ERROR
02287                  MOVE -1         TO  EXPCHGL
02288                  MOVE AL-UABON   TO  EXPCHGA
02289                  PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
02290                  GO TO 8200-SEND-DATAONLY.
02291
02292      PERFORM 4100-BUILD-VERIFICATION  THRU  4199-EXIT.
02293
111513*    display ' VERI TABLE BEFORE '
111513*    perform 4220-DISPLAY-VERI-TABLE
111513*                                thru 4220-exit

02294      IF EFFCHGL = ZERO
02295          MOVE PI-BIN-EXP-DT (T-INDEX)
02296                                  TO WS-ACCT-EFF-DT (WS-INDEX)
02297          MOVE BIN-EXPCHG-SAVE    TO WS-ACCT-EXP-DT (WS-INDEX)
02298      ELSE
02299          MOVE BIN-EFFCHG-SAVE    TO WS-ACCT-EFF-DT (WS-INDEX)
02300                                     WS-ACCT-EXP-DT (WS-INDEX - 1)
02301          MOVE BIN-EXPCHG-SAVE    TO WS-ACCT-EXP-DT (WS-INDEX).
02302
111513*    display ' VERI TABLE AFTER  '
111513*    perform 4220-DISPLAY-VERI-TABLE
111513*                                thru 4220-exit

02303      PERFORM 4300-VERIFY-INTEGRITY  THRU  4399-EXIT
02304          VARYING  WS-INDEX  FROM  +1  BY  +1
02305            UNTIL  WS-INDEX  GREATER +32.
02306
02307      IF NOT EMI-NO-ERRORS
02308          GO TO 8200-SEND-DATAONLY.
02309
02310      IF EFFCHGL  =  ZEROS
02311          MOVE PI-BIN-EXP-DT (T-INDEX)
02312                                   TO  PI-ACCT-EXP-DT
02313                                       BIN-EFFCHG-SAVE
02314                                       DC-BIN-DATE-1
02315          MOVE SPACE               TO  DC-OPTION-CODE
02316          PERFORM 9700-DATE-CONVERSION
02317 ***  Y2K PROJ 7744
02318          MOVE DC-GREG-DATE-1-YMD  TO  WS-EFFCHG-SAVE
02319          MOVE DC-ALPHA-CENTURY    TO  WS-EFFCHG-SAVE(4:2)
02320          MOVE WS-EFFCHG-SAVE      TO  EFFCHG-SAVE
02321
02322          PERFORM 5020-READ-PREV-REC
02323          PERFORM 5050-CREATE-NEW-REC  THRU  5050-EXIT
02324          GO TO 5005-EXIT
02325      END-IF.
02326 ***  Y2K PROJ 7744
02327
02328      IF NOT EMI-NO-ERRORS
02329          GO TO 8200-SEND-DATAONLY.
02330
02331      SET T-INDEX  TO  PI-TOTAL-LINES.
02332
CIDMOD*    IF (EFFCHGL GREATER THAN +0) AND
02334 *       (EXPCHGL GREATER THAN +0)
02335 *       IF (PI-BIN-EXP-DT (T-INDEX) EQUAL HIGH-VALUES) AND
02336 *          (BIN-EXPCHG-SAVE EQUAL HIGH-VALUES)
02337 *          NEXT SENTENCE
02338 *       ELSE
02339 *          GO TO 5006-FIND-EXP-DATE.
02340 *
02341
02342
02343 ***************************************************************
02344 *    EFFECTIVE DATE ENTERED, WILL REPLACE CURRENT EXPIRATION  *
02345 *    DATE IF ALL NINES AND CREATE NEW RANGE                   *
02346 ***************************************************************
02347      SET T-INDEX  TO  PI-TOTAL-LINES.
02348
02349      IF BROWSE-STARTED-SW EQUAL 'Y'
02350         PERFORM 6320-ENDBR THRU 6320-EXIT.
02351
02352      IF PI-BIN-EXP-DT (T-INDEX)  = HIGH-VALUES
02353          IF PI-BIN-EFF-DT (T-INDEX) NOT LESS BIN-EFFCHG-SAVE
02354              MOVE ER-2063             TO  EMI-ERROR
02355              MOVE -1                  TO  EFFCHGL
02356              MOVE AL-UABON            TO  EFFCHGA
02357              PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
02358              GO TO 8200-SEND-DATAONLY
02359          ELSE
02360              MOVE PI-BIN-EXP-DT (T-INDEX)  TO  PI-ACCT-EXP-DT
02361              MOVE LOW-VALUES              TO  PI-ACCT-REST-OF-EXP
02362              MOVE PI-ACCT-KEY         TO  B4-ACCT-KEY
02363              MOVE PI-BIN-EFF-DT (T-INDEX)
02364                                       TO  B4-ACCT-EFF-DT
02365              PERFORM 3100-DELETE-DATE-RANGE  THRU  3199-EXIT
02366              MOVE BIN-EFFCHG-SAVE     TO  AM-EXPIRATION-DT
02367                                           AM-VG-EXPIRATION-DT
02368                                           PI-BIN-EXP-DT
02369                                                     (T-INDEX)
02370              MOVE AM-CONTROL-PRIMARY  TO  AF-ACCT-KEY
02371              MOVE AM-EFFECTIVE-DT     TO  AF-ACCT-EFF-DT
02372              PERFORM 5600-CHG-AXRF-RECORDS  THRU  5699-EXIT
02373              PERFORM 3200-ADD-DATE-RANGE    THRU  3299-EXIT
02374              PERFORM 5050-CREATE-NEW-REC    THRU  5050-EXIT
02375              GO TO 5005-EXIT.
02376
02377 ***************************************************************
02378 *    EFFECTIVE DATE ENTERED, CHECK TO ENSURE EFFECTIVE        *
02379 *    DATE GREATER PREVIOUS EXPIRATION DATE                    *
02380 ***************************************************************
02381      SET T-INDEX  TO  PI-TOTAL-LINES.
02382
02383      MOVE PI-BIN-EXP-DT (T-INDEX)  TO  PI-ACCT-EXP-DT.
02384      MOVE LOW-VALUES               TO  PI-ACCT-REST-OF-EXP.
02385
02386      IF BIN-EFFCHG-SAVE LESS PI-BIN-EXP-DT (T-INDEX)
02387          PERFORM 5010-CHECK-VALID-NEW-RANGE  THRU  5010-EXIT
02388      ELSE
02389          PERFORM 5020-READ-PREV-REC
02390          PERFORM 5050-CREATE-NEW-REC  THRU  5050-EXIT.
02391
02392  5005-EXIT.
02393      MOVE SPACES                 TO  EFFCHGO
02394                                      EXPCHGI.
02395      MOVE AL-UANOF               TO  EFFCHGA
02396                                      EXPCHGA.
02397      MOVE ER-0000                TO  EMI-ERROR.
02398
02399      PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
02400
02401      GO TO 7000-BUILD-OUTPUT-MAP.
02402
02403      EJECT
02404  5006-FIND-EXP-DATE.
02405      MOVE LOW-VALUES             TO  PI-ACCT-EXP-DT
02406                                      PI-ACCT-REST-OF-EXP.
02407      PERFORM 6300-STARTBR  THRU  6300-EXIT.
02408
02409      PERFORM 6310-READNEXT  THRU  6310-EXIT
02410          VARYING  T-INDEX  FROM  +1  BY  +1
02411            UNTIL  PI-BIN-EXP-DT (T-INDEX)  > BIN-EFFCHG-SAVE.
02412
02413      IF BIN-EFFCHG-SAVE > PI-BIN-EXP-DT (T-INDEX)
02414         MOVE ER-1220             TO  EMI-ERROR
02415         MOVE -1                  TO  EFFCHGL
02416         MOVE AL-UABON            TO  EFFCHGA
02417         PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
02418         GO TO         8200-SEND-DATAONLY.
02419
02420      IF (BIN-EFFCHG-SAVE > PI-BIN-EFF-DT (T-INDEX)) AND
02421         (BIN-EXPCHG-SAVE < PI-BIN-EXP-DT (T-INDEX))
02422         PERFORM 5007-CREATE-SUB-DATE-RANGES THRU 5007-EXIT
02423         MOVE 1 TO PI-PAGE-NUMBER
02424         GO TO 1000-SHOW-ACCOUNT
02425      ELSE
02426      IF (BIN-EFFCHG-SAVE = PI-BIN-EFF-DT (T-INDEX)) AND
02427         (BIN-EXPCHG-SAVE < PI-BIN-EXP-DT (T-INDEX))
02428         PERFORM 5008-CREATE-SUB-DATE-RANGES THRU 5008-EXIT
02429         MOVE 1 TO PI-PAGE-NUMBER
02430         GO TO 1000-SHOW-ACCOUNT
02431      ELSE
02432         MOVE ER-2051             TO  EMI-ERROR
02433         MOVE -1                  TO  EFFCHGL
02434         MOVE AL-UABON            TO  EFFCHGA
02435         PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
02436         GO TO 8200-SEND-DATAONLY.
02437
02438  5006-EXIT. EXIT.
02439      EJECT
02440  5007-CREATE-SUB-DATE-RANGES.
02441
02442      IF BROWSE-STARTED-SW EQUAL 'Y'
02443         PERFORM 6320-ENDBR THRU 6320-EXIT.
02444
02445      IF NOT SYSTEM-MODIFY-CAP
02446          MOVE 'UPDATE'           TO  SM-READ
02447          PERFORM 9995-SECURITY-VIOLATION  THRU  9995-EXIT
02448          MOVE ER-0070            TO  EMI-ERROR
02449          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
02450          GO TO 8100-SEND-INITIAL-MAP.
02451
02452      EXEC CICS GETMAIN
02453          LENGTH   (ACCT-REC-LEN)
02454          SET      (ADDRESS OF ACCOUNT-MASTER)
02455          INITIMG  (GETMAIN-SPACE)
02456      END-EXEC.
02457
02458      EXEC CICS  HANDLE CONDITION
02459          ENDFILE  (5007-EXIT)
02460          NOTFND   (5007-EXIT)
02461      END-EXEC.
02462
02463      MOVE PI-BIN-EXP-DT (T-INDEX)  TO  PI-ACCT-EXP-DT.
02464      MOVE LOW-VALUES               TO  PI-ACCT-REST-OF-EXP.
02465
02466      EXEC CICS  READ
02467          UPDATE
02468          DATASET  (PI-ACCT-ID)
02469          SET      (ADDRESS OF ACCOUNT-MASTER)
02470          RIDFLD   (PI-ACCT-KEY)
02471      END-EXEC.
02472
02473      MOVE AM-CONTROL-PRIMARY     TO  SAVE-ACCT-KEY.
02474      MOVE AM-EXPIRATION-DT       TO  SAVE-CURR-EXP-DT.
02475      MOVE AM-EFFECTIVE-DT        TO  SAVE-CURR-EFF-DT.
02476      MOVE AM-CONTROL-PRIMARY     TO  AF-ACCT-KEY.
02477      MOVE AM-EXPIRATION-DT       TO  AF-ACCT-EFF-DT.
02478      MOVE BIN-CURRENT-SAVE       TO  AM-LAST-MAINT-DT
02479      MOVE 'C'                    TO  JP-RECORD-TYPE.
02480      MOVE ACCOUNT-MASTER         TO  JP-RECORD-AREA.
02481      PERFORM 5600-CHG-AXRF-RECORDS  THRU  5699-EXIT.
02482
02483      MOVE BIN-EXPCHG-SAVE     TO  AM-EFFECTIVE-DT
02484                                   PI-BIN-EFF-DT (T-INDEX).
02485      EXEC CICS  REWRITE
02486          FROM     (ACCOUNT-MASTER)
02487          DATASET  (PI-ACCT-ID)
02488      END-EXEC.
02489
02490      EXEC CICS SYNCPOINT
02491      END-EXEC.
02492
02493      PERFORM 8400-LOG-JOURNAL-RECORD.
02494      PERFORM 6600-UPDATE-MAINT-DT  THRU  6699-EXIT.
02495      MOVE SAVE-ACCT-KEY       TO  AM-CONTROL-PRIMARY.
02496      MOVE LOW-VALUES          TO  AM-CNTRL-B.
02497      MOVE SAVE-CURR-EFF-DT    TO  PI-BIN-EFF-DT (T-INDEX)
02498                                   AM-EFFECTIVE-DT.
02499      MOVE BIN-EFFCHG-SAVE     TO  PI-BIN-EXP-DT (T-INDEX)
02500                                   AM-EXPIRATION-DT
02501                                   AM-VG-EXPIRATION-DT.
02502      MOVE BIN-CURRENT-SAVE    TO  AM-LAST-MAINT-DT
02503                                   PI-BIN-MAINT-DT (T-INDEX).
02504      MOVE AM-CONTROL-PRIMARY  TO  AF-ACCT-KEY.
02505      MOVE AM-EFFECTIVE-DT     TO  AF-ACCT-EFF-DT.
02506      MOVE LOW-VALUES          TO  AM-AR-HI-CERT-DATE
02507      MOVE SPACES              TO  AM-1ST-PROD-DATE
02508                                   AM-PREV-DATES.
02509      PERFORM 5600-CHG-AXRF-RECORDS  THRU  5699-EXIT.
02510
02511      EXEC CICS  HANDLE CONDITION
02512          ENDFILE  (5007-EXIT)
02513          NOTFND   (5007-EXIT)
02514          DUPREC   (5007-EXIT)
02515          ILLOGIC  (5007-EXIT)
02516          INVREQ   (5007-EXIT)
02517      END-EXEC.
02518
02519      MOVE 'AM'                TO AM-RECORD-ID.
02520
02521      PERFORM 3200-ADD-DATE-RANGE    THRU  3299-EXIT.
02522      PERFORM 8400-LOG-JOURNAL-RECORD.
02523      PERFORM 6600-UPDATE-MAINT-DT   THRU  6699-EXIT.
02524
02525      MOVE SAVE-ACCT-KEY       TO  AM-CONTROL-PRIMARY.
02526      MOVE BIN-EFFCHG-SAVE     TO  AM-EFFECTIVE-DT
02527                                   PI-BIN-EFF-DT (T-INDEX).
02528      MOVE BIN-EXPCHG-SAVE     TO  PI-BIN-EXP-DT (T-INDEX)
02529                                   AM-EXPIRATION-DT
02530                                   AM-VG-EXPIRATION-DT.
02531      MOVE BIN-CURRENT-SAVE    TO  AM-LAST-MAINT-DT
02532                                   PI-BIN-MAINT-DT (T-INDEX).
02533      MOVE LOW-VALUES          TO  AM-AR-HI-CERT-DATE
02534      MOVE SPACES              TO  AM-1ST-PROD-DATE
02535                                   AM-PREV-DATES.
02536      MOVE AM-CONTROL-PRIMARY  TO  AF-ACCT-KEY.
02537      MOVE AM-EFFECTIVE-DT     TO  AF-ACCT-EFF-DT.
02538      PERFORM 5600-CHG-AXRF-RECORDS  THRU  5699-EXIT.
02539      PERFORM 3200-ADD-DATE-RANGE    THRU  3299-EXIT.
02540      PERFORM 8400-LOG-JOURNAL-RECORD.
02541      PERFORM 6600-UPDATE-MAINT-DT   THRU  6699-EXIT.
02542
02543      MOVE ZERO                TO EFFCHGL
02544                                  EXPCHGL.
02545
02546      EXEC CICS  HANDLE CONDITION
02547           NOTFND   (5015-NOT-FOUND)
02548           ENDFILE  (5015-NOT-FOUND)
02549      END-EXEC.
02550
02551  5007-EXIT. EXIT.
02552  EJECT
02553  5008-CREATE-SUB-DATE-RANGES.
02554
02555      IF BROWSE-STARTED-SW EQUAL 'Y'
02556         PERFORM 6320-ENDBR THRU 6320-EXIT.
02557
02558      IF NOT SYSTEM-MODIFY-CAP
02559          MOVE 'UPDATE'           TO  SM-READ
02560          PERFORM 9995-SECURITY-VIOLATION  THRU  9995-EXIT
02561          MOVE ER-0070            TO  EMI-ERROR
02562          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
02563          GO TO 8100-SEND-INITIAL-MAP.
02564
02565      MOVE PI-BIN-EXP-DT (T-INDEX)  TO  PI-ACCT-EXP-DT.
02566      MOVE LOW-VALUES               TO  PI-ACCT-REST-OF-EXP.
02567      MOVE PI-ACCT-KEY              TO  B4-ACCT-KEY.
02568      MOVE PI-BIN-EFF-DT (T-INDEX)  TO  B4-ACCT-EFF-DT.
02569
02570      PERFORM 3100-DELETE-DATE-RANGE  THRU  3199-EXIT.
02571
02572      MOVE BIN-EXPCHG-SAVE        TO  AM-EXPIRATION-DT
02573                                      AM-VG-EXPIRATION-DT
02574                                      PI-BIN-EXP-DT (T-INDEX).
02575      MOVE BIN-CURRENT-SAVE       TO  AM-LAST-MAINT-DT
02576                                      PI-BIN-MAINT-DT (T-INDEX).
02577      MOVE AM-CONTROL-PRIMARY     TO  AF-ACCT-KEY.
02578      MOVE AM-EFFECTIVE-DT        TO  AF-ACCT-EFF-DT.
02579
02580      PERFORM 5600-CHG-AXRF-RECORDS  THRU  5699-EXIT.
02581
02582      PERFORM 3200-ADD-DATE-RANGE    THRU  3299-EXIT.
02583
02584      PERFORM 6600-UPDATE-MAINT-DT   THRU  6699-EXIT.
02585
02586      IF T-INDEX = PI-TOTAL-LINES
02587         MOVE ZERO             TO EFFCHGL
02588                                  EXPCHGL
02589         GO TO 5008-EXIT.
02590
02591      MOVE BIN-EXPCHG-SAVE        TO  PI-ACCT-EXP-DT
02592      MOVE LOW-VALUES             TO  PI-ACCT-REST-OF-EXP.
02593      PERFORM 6300-STARTBR  THRU  6300-EXIT.
02594
02595      PERFORM 6310-READNEXT  THRU  6310-EXIT.
02596
02597      PERFORM 6310-READNEXT  THRU  6310-EXIT
02598          VARYING  T-INDEX  FROM T-INDEX  BY  +1
02599            UNTIL  PI-BIN-EXP-DT (T-INDEX)  > BIN-EXPCHG-SAVE.
02600
02601      IF BROWSE-STARTED-SW EQUAL 'Y'
02602         PERFORM 6320-ENDBR THRU 6320-EXIT.
02603
02604      EXEC CICS  READ
02605          UPDATE
02606          DATASET  (PI-ACCT-ID)
02607          SET      (ADDRESS OF ACCOUNT-MASTER)
02608          RIDFLD   (PI-ACCT-KEY)
02609      END-EXEC.
02610
02611      MOVE 'B'                    TO  JP-RECORD-TYPE.
02612      MOVE ACCT-FILE-ID           TO  FILE-ID.
02613      MOVE ACCOUNT-MASTER         TO  JP-RECORD-AREA.
02614
02615      PERFORM 8400-LOG-JOURNAL-RECORD.
02616
02617      MOVE BIN-EXPCHG-SAVE        TO  AM-EFFECTIVE-DT
02618                                      PI-BIN-EFF-DT (T-INDEX).
02619      MOVE EXPCHG-SAVE            TO  AM-EFFECT-DT.
02620      MOVE BIN-CURRENT-SAVE       TO  AM-LAST-MAINT-DT
02621                                      PI-BIN-MAINT-DT (T-INDEX).
02622      MOVE AM-CONTROL-PRIMARY     TO  AF-ACCT-KEY.
02623      MOVE AM-EFFECTIVE-DT        TO  AF-ACCT-EFF-DT.
02624      MOVE 'C'                    TO  JP-RECORD-TYPE.
02625      MOVE ACCOUNT-MASTER         TO  JP-RECORD-AREA.
02626
02627      PERFORM 5600-CHG-AXRF-RECORDS  THRU  5699-EXIT.
02628
02629      EXEC CICS  REWRITE
02630          FROM     (ACCOUNT-MASTER)
02631          DATASET  (PI-ACCT-ID)
02632      END-EXEC.
02633
02634      PERFORM 8400-LOG-JOURNAL-RECORD.
02635
02636      PERFORM 6600-UPDATE-MAINT-DT  THRU  6699-EXIT.
02637
02638
02639
02640      MOVE ZERO                TO EFFCHGL
02641                                  EXPCHGL.
02642
02643      EXEC CICS  HANDLE CONDITION
02644           NOTFND   (5015-NOT-FOUND)
02645           ENDFILE  (5015-NOT-FOUND)
02646      END-EXEC.
02647
02648  5008-EXIT. EXIT.
02649  EJECT
02650  5010-CHECK-VALID-NEW-RANGE.
02651
02652 ***************************************************************
02653 *    THIS PARAGRAPH ALLOWS YOU TO EXPIRE A RANGE PRIOR TO HI  *
02654 *    CERT DATE AS LONG AS A NEW RANGE IS BEING CREATED        *
02655 ***************************************************************
02656
02657      IF PI-AR-PROCESSING
02658         GO TO 5010-CHECK-AR-VALID-NEW-RANGE.
02659
02660      IF BIN-EXPCHG-SAVE GREATER PI-BIN-EXP-DT (T-INDEX)  AND
02661                         GREATER BIN-EFFCHG-SAVE          AND
02662                         GREATER PI-BIN-HI-CERT (T-INDEX)
02663          MOVE PI-BIN-EXP-DT (T-INDEX)
02664                                   TO  PI-ACCT-EXP-DT
02665          MOVE LOW-VALUES          TO  PI-ACCT-REST-OF-EXP
02666          MOVE PI-ACCT-KEY         TO  B4-ACCT-KEY
02667          MOVE PI-BIN-EFF-DT (T-INDEX)
02668                                   TO  B4-ACCT-EFF-DT
02669          PERFORM 3100-DELETE-DATE-RANGE  THRU  3199-EXIT
02670          MOVE BIN-EFFCHG-SAVE     TO  AM-EXPIRATION-DT
02671                                       AM-VG-EXPIRATION-DT
02672                                       PI-BIN-EXP-DT (T-INDEX)
02673          MOVE BIN-CURRENT-SAVE    TO  PI-BIN-MAINT-DT (T-INDEX)
02674          MOVE AM-CONTROL-PRIMARY  TO  AF-ACCT-KEY
02675          MOVE AM-EFFECTIVE-DT     TO  AF-ACCT-EFF-DT
02676          PERFORM 5600-CHG-AXRF-RECORDS  THRU  5699-EXIT
02677          PERFORM 3200-ADD-DATE-RANGE    THRU  3299-EXIT
02678          PERFORM 5050-CREATE-NEW-REC    THRU  5050-EXIT
02679      ELSE
02680          MOVE ER-2051             TO  EMI-ERROR
02681          MOVE -1                  TO  EFFCHGL
02682          MOVE AL-UABON            TO  EFFCHGA  EXPCHGA
02683          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
02684          GO TO 8200-SEND-DATAONLY.
02685
02686      GO TO 5010-EXIT.
02687
02688  5010-CHECK-AR-VALID-NEW-RANGE.
02689
02690 ***************************************************************
02691 *    THIS PARAGRAPH ALLOWS YOU TO EXPIRE A RANGE PRIOR TO HI  *
02692 *    A/R CERT DATE AS LONG AS A NEW RANGE IS BEING CREATED    *
02693 ***************************************************************
02694
02695      IF BIN-EXPCHG-SAVE GREATER PI-BIN-EXP-DT (T-INDEX)  AND
02696                         GREATER BIN-EFFCHG-SAVE          AND
02697                         GREATER PI-BIN-AR-HI-CERT (T-INDEX)
02698          MOVE PI-BIN-EXP-DT (T-INDEX)
02699                                   TO  PI-ACCT-EXP-DT
02700          MOVE LOW-VALUES          TO  PI-ACCT-REST-OF-EXP
02701          MOVE PI-ACCT-KEY         TO  B4-ACCT-KEY
02702          MOVE PI-BIN-EFF-DT (T-INDEX)
02703                                   TO  B4-ACCT-EFF-DT
02704          PERFORM 3100-DELETE-DATE-RANGE  THRU  3199-EXIT
02705          MOVE BIN-EFFCHG-SAVE     TO  AM-EXPIRATION-DT
02706                                       AM-VG-EXPIRATION-DT
02707                                       PI-BIN-EXP-DT (T-INDEX)
02708          MOVE BIN-CURRENT-SAVE    TO  PI-BIN-MAINT-DT (T-INDEX)
02709          MOVE AM-CONTROL-PRIMARY  TO  AF-ACCT-KEY
02710          MOVE AM-EFFECTIVE-DT     TO  AF-ACCT-EFF-DT
02711          PERFORM 5600-CHG-AXRF-RECORDS  THRU  5699-EXIT
02712          PERFORM 3200-ADD-DATE-RANGE    THRU  3299-EXIT
02713          PERFORM 5050-CREATE-NEW-REC    THRU  5050-EXIT
02714      ELSE
02715          MOVE ER-2051             TO  EMI-ERROR
02716          MOVE -1                  TO  EFFCHGL
02717          MOVE AL-UABON            TO  EFFCHGA  EXPCHGA
02718          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
02719          GO TO 8200-SEND-DATAONLY.
02720
02721  5010-EXIT.
02722      EXIT.
02723
02724  EJECT
02725  5015-NOT-FOUND.
02726      MOVE ER-0226                TO  EMI-ERROR.
02727      PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
02728      MOVE -1                 TO  MAINTL.
02729      GO TO 8200-SEND-DATAONLY.
02730
02731  5015-EXIT.
02732      EXIT.
02733  EJECT
02734  5020-READ-PREV-REC.
02735      EXEC CICS  READ
02736          DATASET  (PI-ACCT-ID)
02737          SET      (ADDRESS OF ACCOUNT-MASTER)
02738          RIDFLD   (PI-ACCT-KEY)
02739      END-EXEC.
02740
02741  5050-CREATE-NEW-REC.
02742      MOVE BIN-EFFCHG-SAVE        TO  AM-EFFECTIVE-DT.
02743      MOVE BIN-EXPCHG-SAVE        TO  AM-EXPIRATION-DT
02744                                      AM-VG-EXPIRATION-DT.
02745      MOVE EFFCHG-SAVE            TO  AM-EFFECT-DT.
02746      MOVE SPACES                 TO  AM-CERTS-PURGED-DATE
02747                                      AM-1ST-PROD-DATE
02748                                      AM-PREV-DATES
02749                                      AM-BILLING-STATUS.
02750      MOVE ZEROS                  TO  AM-HI-CERT-DATE
02751                                      AM-LO-CERT-DATE.
02752
02753      IF AM-AR-HI-CERT-DATE NOT LESS THAN AM-EFFECTIVE-DT
02754         NEXT SENTENCE
02755      ELSE
02756         MOVE LOW-VALUES          TO  AM-AR-HI-CERT-DATE.
02757
02758      MOVE BIN-CURRENT-SAVE       TO  AM-LAST-MAINT-DT.
02759      MOVE PI-PROCESSOR-ID        TO  AM-LAST-MAINT-USER.
02760      MOVE EIBTIME                TO  AM-LAST-MAINT-HHMMSS.
02761
02762      SET T-INDEX  UP  BY  1.
02763      IF T-INDEX  GREATER 32
02764          MOVE ER-2062            TO  EMI-ERROR
02765          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
02766          MOVE -1                 TO  MAINTL
02767          GO TO 8200-SEND-DATAONLY.
02768
02769      ADD 1                       TO  PI-TOTAL-LINES.
02770
02771      MOVE LOW-VALUES             TO  PI-TABLE-ENT    (T-INDEX).
02772      MOVE BIN-EFFCHG-SAVE        TO  PI-BIN-EFF-DT   (T-INDEX).
02773      MOVE BIN-EXPCHG-SAVE        TO  PI-BIN-EXP-DT   (T-INDEX).
02774      MOVE AM-AR-HI-CERT-DATE     TO  PI-BIN-AR-HI-CERT (T-INDEX).
02775      MOVE BIN-CURRENT-SAVE       TO  PI-BIN-MAINT-DT (T-INDEX).
02776
02777      PERFORM 5500-ADD-NEW-AXRF-RECORD  THRU  5599-EXIT.
02778
02779      MOVE 'A'                    TO  JP-RECORD-TYPE.
02780      MOVE ACCOUNT-MASTER         TO  JP-RECORD-AREA.
02781      MOVE ACCT-FILE-ID           TO  FILE-ID.
02782
02783      EXEC CICS  WRITE
02784          RIDFLD   (AM-CONTROL-PRIMARY)
02785          FROM     (ACCOUNT-MASTER)
02786          DATASET  (PI-ACCT-ID)
02787      END-EXEC.
02788
02789      PERFORM 8400-LOG-JOURNAL-RECORD.
02790
02791      PERFORM 6600-UPDATE-MAINT-DT  THRU  6699-EXIT.
02792
02793  5050-EXIT.
02794      EXIT.
02795  EJECT
02796  5100-FIND-NEXT-RANGE.
02797      MOVE PI-PREV-VG-ACCOUNT     TO  KEY-SAVE.
02798
02799      IF PI-TOTAL-LINES  = ZERO
02800          GO TO 5210-NO-DATA.
02801
02802      IF PI-LST-PAGE
02803        OR (PI-FST-PAGE AND PI-TOTAL-LINES NOT GREATER 8)
02804        OR (PI-2ND-PAGE AND PI-TOTAL-LINES NOT GREATER 16)
02805        OR (PI-3RD-PAGE AND PI-TOTAL-LINES NOT GREATER 24)
02806          MOVE ER-2065            TO  EMI-ERROR
02807          MOVE -1                 TO  MAINTL
02808          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
02809          GO TO 8200-SEND-DATAONLY.
02810
02811      IF PI-FST-PAGE
02812          MOVE 2                  TO  PI-PAGE-NUMBER
02813      ELSE
02814      IF PI-2ND-PAGE
02815          MOVE 3                  TO  PI-PAGE-NUMBER
02816      ELSE
02817      IF PI-3RD-PAGE
02818          MOVE 4                  TO  PI-PAGE-NUMBER.
02819
02820      GO TO 7000-BUILD-OUTPUT-MAP.
02821
02822  5200-FIND-PREV-RANGE.
02823      MOVE PI-PREV-VG-ACCOUNT     TO  KEY-SAVE.
02824
02825      IF PI-TOTAL-LINES  =  ZERO
02826          GO TO 5210-NO-DATA.
02827
02828      IF PI-FST-PAGE
02829          MOVE ER-2066            TO  EMI-ERROR
02830          MOVE -1                 TO  MAINTL
02831          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
02832          GO TO 8200-SEND-DATAONLY.
02833
02834      IF PI-2ND-PAGE
02835          MOVE 1                  TO  PI-PAGE-NUMBER
02836      ELSE
02837      IF PI-3RD-PAGE
02838          MOVE 2                  TO  PI-PAGE-NUMBER
02839      ELSE
02840      IF PI-LST-PAGE
02841          MOVE 3                  TO  PI-PAGE-NUMBER.
02842
02843      GO TO 7000-BUILD-OUTPUT-MAP.
02844
02845  5210-NO-DATA.
02846      MOVE ER-2069                TO  EMI-ERROR.
02847      MOVE -1                     TO  MAINTL.
02848
02849      PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
02850
02851      GO TO 8200-SEND-DATAONLY.
02852  EJECT
02853  5300-FIND-NEXT-ACCOUNT.
02854      MOVE PI-PREV-ACCOUNT        TO  PI-ACCT-CCGSA-KEY.
02855      MOVE 'Y'                    TO  FIRST-TIME-SW.
02856
02857      IF PI-PREV-ACCOUNT  = LOW-VALUES
02858          MOVE PI-COMPANY-CD      TO  PI-ACCT-CO
02859      ELSE
02860          PERFORM 6100-BUILD-KEY  THRU  6199-EXIT
02861          MOVE HIGH-VALUES        TO  PI-ACCT-EXP-DT
02862          MOVE LOW-VALUES         TO  PI-ACCT-REST-OF-EXP.
02863
02864      IF NOT EMI-NO-ERRORS
02865          GO TO 8200-SEND-DATAONLY.
02866
02867      EXEC CICS  HANDLE CONDITION
02868          NOTFND   (5340-END)
02869          ENDFILE  (5330-NOT-FOUND)
02870      END-EXEC.
02871
02872  5305-START-BR.
02873      MOVE ZEROS                  TO  PI-TOTAL-LINES.
02874
02875      SET T-INDEX
02876         TS-INDEX  TO  +1.
02877
02878      PERFORM 6300-STARTBR  THRU  6300-EXIT.
02879
02880  5310-READ-NEXT.
02881      PERFORM 6310-READNEXT  THRU  6310-EXIT.
02882
02883      IF PI-ACCT-CO  NOT = PI-COMPANY-CD
02884          GO TO 5330-NOT-FOUND.
02885
02886      IF PI-CARRIER-SECURITY GREATER SPACES
02887          IF PI-ACCT-CARRIER  =  PI-CARRIER-SECURITY
02888              NEXT SENTENCE
02889          ELSE
02890              GO TO 5310-READ-NEXT.
02891
02892      IF FIRST-TIME
02893          IF PI-ACCT-CCGSA-KEY  NOT = KEY-SAVE
02894              MOVE SPACES             TO  FIRST-TIME-SW
02895              MOVE PI-ACCT-CCGSA-KEY  TO  KEY-SAVE
02896                                          PI-PREV-VG-ACCOUNT
02897          ELSE
02898              GO TO 5310-READ-NEXT.
02899
02900      IF PI-PREV-ACCOUNT  =  LOW-VALUES
02901          IF PI-COMPANY-CD  = PI-ACCT-CO
02902              GO TO 5320-DISPLAY-RECORD
02903          ELSE
02904              MOVE ER-0066        TO  EMI-ERROR
02905              PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
02906              MOVE -1             TO  MAINTL
02907              GO TO 8200-SEND-DATAONLY.
02908
02909      IF PI-ACCT-CCGSA-KEY  = KEY-SAVE
02910          GO TO 5320-DISPLAY-RECORD
02911      ELSE
02912          MOVE 1                  TO  PI-PAGE-NUMBER
02913          GO TO 7000-BUILD-OUTPUT-MAP.
02914
02915  5320-DISPLAY-RECORD.
02916      IF T-INDEX  = 1
02917          MOVE LOW-VALUES         TO  PI-DATE-RANGE-TABLE
02918          MOVE SPACE              TO  NAMEFLGO
02919                                      PI-NAMEFLG.
02920
02921      IF AM-TRNTO-DTE  =  SPACES
02922          MOVE LOW-VALUES         TO  AM-TRNTO-DTE.
02923
02924      IF AM-TRNFROM-DTE  = SPACES
02925          MOVE LOW-VALUES         TO  AM-TRNFROM-DTE.
02926
02927      IF T-INDEX  = 1
02928          IF PI-COMPANY-ID  = 'LGX'  OR
02929                              'MIC'  OR  'MCC'
02930              MOVE AM-TRANSFERRED-TO  TO  TRNTOO
02931              MOVE AM-TRNTO-DTE       TO  DC-BIN-DATE-1
02932              MOVE SPACE              TO  DC-OPTION-CODE
02933              PERFORM 9700-DATE-CONVERSION
02934              IF DATE-CONVERSION-ERROR
02935                  MOVE SPACES         TO  TRNTODTO
02936              ELSE
02937                  MOVE DC-GREG-DATE-1-EDIT
02938                                      TO  TRNTODTO.
02939
02940      IF T-INDEX  = 1
02941          IF PI-COMPANY-ID  =  'LGX'  OR
02942                               'MIC'  OR  'MCC'
02943              MOVE AM-TRANSFERRED-FROM  TO  TRNFROMO
02944              MOVE AM-TRNFROM-DTE       TO  DC-BIN-DATE-1
02945              MOVE SPACE                TO  DC-OPTION-CODE
02946              PERFORM 9700-DATE-CONVERSION
02947              IF DATE-CONVERSION-ERROR
02948                  MOVE SPACES           TO  TRNFRDTO
02949              ELSE
02950                  MOVE DC-GREG-DATE-1-EDIT
02951                                        TO  TRNFRDTO.
02952
02953      IF NAMEO  NOT = LOW-VALUES
02954          IF AM-NAME  NOT = NAMEO
02955              MOVE '*'            TO  NAMEFLGO
02956                                      PI-NAMEFLG.
02957
02958      MOVE AM-CONTROL-PRIMARY     TO  PI-PREV-ACCOUNT.
02959
02960      IF T-INDEX  = 1
02961          MOVE AM-NAME                TO  NAMEO
02962                                          PI-ACCNAME
02963         ELSE
02964          MOVE PI-ACCNAME             TO  NAMEO.
02965
02966      PERFORM 1020-MOVE-TO-TABLE  THRU  1020-EXIT.
02967
02968      GO TO 5310-READ-NEXT.
02969
02970  5330-NOT-FOUND.
02971      IF BROWSE-STARTED
02972          PERFORM 6320-ENDBR  THRU  6320-EXIT
02973      ELSE
02974          GO TO 5340-END.
02975
02976      IF PI-ACCT-ACCOUNT  = LOW-VALUE
02977          MOVE ER-0472            TO  EMI-ERROR
02978          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
02979          MOVE -1                 TO  MAINTL
02980          GO TO 8200-SEND-DATAONLY.
02981
02982      IF PI-ACCT-CCGSA-KEY  =  PI-PREV-ACCOUNT
02983        AND  T-INDEX  NOT = 1
02984          MOVE 1                  TO  PI-PAGE-NUMBER
02985          GO TO 7000-BUILD-OUTPUT-MAP
02986      ELSE
02987          IF T-INDEX  NOT = 1
02988              MOVE ER-0066        TO  EMI-ERROR
02989              PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
02990              MOVE 1              TO  PI-PAGE-NUMBER
02991              GO TO 7000-BUILD-OUTPUT-MAP.
02992
02993  5340-END.
02994      MOVE ER-0066                TO  EMI-ERROR.
02995
02996      PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
02997
02998      MOVE -1                     TO  MAINTL.
02999
03000      GO TO 8200-SEND-DATAONLY.
03001  EJECT
03002  5400-FIND-PREV-ACCOUNT.
03003      IF PI-PREV-ACCOUNT  = LOW-VALUES
03004          GO TO 5430-NOT-FOUND.
03005
03006      MOVE PI-PREV-ACCOUNT        TO  PI-ACCT-CCGSA-KEY.
03007      MOVE PI-BIN-EXP-DT (1)      TO  PI-ACCT-EXP-DT.
03008      MOVE LOW-VALUES             TO  PI-ACCT-REST-OF-EXP.
03009
03010      EXEC CICS  HANDLE CONDITION
03011          NOTFND   (5440-NOT-FOUND)
03012          ENDFILE  (5430-NOT-FOUND)
03013      END-EXEC.
03014
03015      EXEC CICS  STARTBR
03016          DATASET  (PI-ACCT-ID)
03017          RIDFLD   (PI-ACCT-KEY)
03018      END-EXEC.
03019
03020      MOVE 'Y'                    TO  BROWSE-STARTED-SW
03021                                      FIRST-TIME-SW.
03022  5410-READ-PREV.
03023      EXEC CICS  READPREV
03024          DATASET  (PI-ACCT-ID)
03025          SET      (ADDRESS OF ACCOUNT-MASTER)
03026          RIDFLD   (PI-ACCT-KEY)
03027      END-EXEC.
03028
03029      IF FIRST-TIME
03030          MOVE SPACE              TO  FIRST-TIME-SW
03031          GO TO 5410-READ-PREV.
03032
03033      IF PI-ACCT-CO  NOT = PI-COMPANY-CD
03034          GO TO 5430-NOT-FOUND.
03035
03036      IF PI-CARRIER-SECURITY GREATER SPACES
03037          IF PI-ACCT-CARRIER  = PI-CARRIER-SECURITY
03038              NEXT SENTENCE
03039          ELSE
03040              GO TO 5410-READ-PREV.
03041
03042      MOVE AM-CONTROL-PRIMARY     TO  PI-PREV-ACCOUNT.
03043      MOVE PI-ACCT-CCGSA-KEY      TO  KEY-SAVE
03044                                      PI-PREV-VG-ACCOUNT.
03045      MOVE LOW-VALUES             TO  PI-ACCT-EXP-DT
03046                                      PI-ACCT-REST-OF-EXP
03047                                      PI-DATE-RANGE-TABLE.
03048
03049      MOVE SPACE                  TO  BROWSE-STARTED-SW.
03050
03051      EXEC CICS  ENDBR
03052          DATASET  (PI-ACCT-ID)
03053      END-EXEC.
03054
03055      GO TO 5305-START-BR.
03056
03057  5430-NOT-FOUND.
03058      IF BROWSE-STARTED
03059          MOVE SPACE              TO  BROWSE-STARTED-SW
03060          EXEC CICS  ENDBR
03061              DATASET  (PI-ACCT-ID)
03062          END-EXEC.
03063
03064      MOVE ER-0067                TO  EMI-ERROR.
03065
03066      PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
03067
03068      MOVE -1                     TO  MAINTL.
03069
03070      IF PI-PREV-ACCOUNT  = LOW-VALUES
03071          GO TO 5300-FIND-NEXT-ACCOUNT.
03072
03073      GO TO 8200-SEND-DATAONLY.
03074
03075  5440-NOT-FOUND.
03076      MOVE PI-PREV-ACCOUNT        TO  PI-ACCT-CCGSA-KEY.
03077
03078      PERFORM 6310-READNEXT THRU 6310-EXIT.
03079
03080      GO TO 5410-READ-PREV.
03081  EJECT
03082  5500-ADD-NEW-AXRF-RECORD.
03083      IF NOT PI-GA-BILLING
03084          GO TO 5599-EXIT.
03085
03086      MOVE +1                     TO  AXRF-SUB.
03087
03088 *    IF MAINTI  = 'K'
03089 *        IF GETMAIN-SW  = 'X'
03090 *            GO TO 5510-BUILD-AXRF-KEY.
03091
03092      MOVE ERGXRF-MAX-LEN         TO  ERGXRF-REC-LEN.
03093
03094 *    EXEC CICS  GETMAIN
03095 *        LENGTH   (ERGXRF-REC-LEN)
03096 *        SET      (ADDRESS OF AGENT-CROSS-REFERENCE)
03097 *        INITIMG  (GETMAIN-SPACE)
03098 *    END-EXEC.

pemcp *    display ' beginning of CP displays  '
pemcp *    display ' ergxrf-rec-len  ==   '  ergxrf-rec-len
pemcp *    display ' address         ==   '
pemcp *              address of agent-cross-reference
pemcp *    display ' contents        ==   '  agent-cross-reference (1:1)
pemcp *    display ' end       of CP displays  '

03099
03100 *    MOVE 'X'                    TO  GETMAIN-SW.
03101
03102  5510-BUILD-AXRF-KEY.
03103      MOVE ERGXRF-MAX-LEN         TO  ERGXRF-REC-LEN.
03104
03105      IF AXRF-SUB  =  +11
03106          GO TO 5599-EXIT.
03107
03108      IF AM-AGT (AXRF-SUB)  =  SPACES  OR ZEROS OR LOW-VALUES
03109          ADD +1 TO AXRF-SUB
03110          GO TO 5510-BUILD-AXRF-KEY.
03111
052814     IF (AM-COM-TYP (AXRF-SUB)  NOT = 'O' AND 'P' AND 'S'
011410         AND 'G' AND 'B' AND 'I' AND 'L' AND 'K' AND 'A')
03114          ADD +1 TO AXRF-SUB
03115          GO TO 5510-BUILD-AXRF-KEY.
03116
03117      MOVE PI-COMPANY-CD          TO  WS-AXRF-COMPANY-CD.
03118
03119      IF PI-ZERO-CARRIER  OR
03120         PI-ZERO-CAR-GROUP
03121          MOVE ZERO               TO  WS-AXRF-CARRIER
03122      ELSE
03123          MOVE AM-CARRIER         TO  WS-AXRF-CARRIER.
03124
03125      IF PI-ZERO-GROUPING  OR
03126         PI-ZERO-CAR-GROUP
03127          MOVE ZEROS              TO  WS-AXRF-GROUPING
03128      ELSE
03129          MOVE AM-GROUPING        TO  WS-AXRF-GROUPING.
03130
03131      MOVE AM-AGT (AXRF-SUB)      TO  WS-AXRF-AGENT-NO.
03132
03133      EXEC CICS  HANDLE CONDITION
03134          ENDFILE  (5525-SKIP-REWRITE)
03135          NOTFND   (5525-SKIP-REWRITE)
03136      END-EXEC.
03137
03138      EXEC CICS  READ
03139          DATASET  (ERGXRF-FILE-ID)
03140          LENGTH   (ERGXRF-REC-LEN)
03141          INTO     (AGENT-CROSS-REFERENCE)
03142          RIDFLD   (WS-AXRF-KEY)
03143          EQUAL
03144          UPDATE
03145      END-EXEC.
03146
03147      MOVE GX-AGENT-POINTER-CNT   TO  GX-AGENT-POINTER-CNT.
03148      MOVE +1                     TO  GX-AGENT-SUB.
03149
03150      IF GX-AGENT-POINTER-CNT = +1006
03151          GO TO 5525-SKIP-REWRITE.
03152
03153
03154  5520-FIND-REC-LOCATION.
03155      MOVE AM-COMPANY-CD          TO  GX-AM-COMPANY-CD.
03156      MOVE GX-AGENT-POINTER (GX-AGENT-SUB)
03157                                  TO  GX-REST-OF-KEY.
03158      MOVE LOW-VALUES             TO  GX-AM-CNTL-FILLER.
03159
03160      IF GX-AM-CONTROL-PRIMARY GREATER AM-CONTROL-PRIMARY  OR
03161         GX-AGENT-SUB          GREATER GX-AGENT-POINTER-CNT
03162          NEXT SENTENCE
03163      ELSE
03164          ADD +1 TO GX-AGENT-SUB
03165          GO TO 5520-FIND-REC-LOCATION.
03166
03167      MOVE GX-AGENT-POINTER-CNT   TO  COMM-WORK-SUB
03168                                      COMM-WORK-SUB2.
03169
03170      ADD +1 TO COMM-WORK-SUB2.
03171
03172      IF GX-AGENT-SUB GREATER GX-AGENT-POINTER-CNT
03173          NEXT SENTENCE
03174       ELSE
03175          PERFORM 5530-BUMP-AXRF-REC
03176              VARYING  COMM-WORK-SUB  FROM  COMM-WORK-SUB  BY  -1
03177                UNTIL  COMM-WORK-SUB2 = GX-AGENT-SUB.
03178
03179      ADD ERGXRF-INC-LEN TO  ERGXRF-REC-LEN.
03180      ADD  +1 TO  GX-AGENT-POINTER-CNT.
03181
03182      MOVE AM-CARRIER        TO  GX-AM-CARRIER   (COMM-WORK-SUB2).
03183      MOVE AM-GROUPING       TO  GX-AM-GROUPING  (COMM-WORK-SUB2).
03184      MOVE AM-STATE          TO  GX-AM-STATE     (COMM-WORK-SUB2).
03185      MOVE AM-ACCOUNT        TO  GX-AM-ACCOUNT   (COMM-WORK-SUB2).
03186      MOVE AM-EXPIRATION-DT  TO  GX-AM-EXPIRATION-DT
03187                                                 (COMM-WORK-SUB2).
03188      MOVE AM-EFFECTIVE-DT   TO  GX-AM-EFF-DT    (COMM-WORK-SUB2).
03189      MOVE AXRF-SUB          TO  GX-AM-LEVEL-NO  (COMM-WORK-SUB2).
03190      MOVE LOW-VALUES        TO  GX-LAST-BILL-DT (COMM-WORK-SUB2).
03191      MOVE BIN-CURRENT-SAVE  TO  GX-LAST-MAINT-DT.
03192      MOVE EIBTIME           TO  GX-LAST-MAINT-HHMMSS.
03193      MOVE PI-PROCESSOR-ID   TO  GX-LAST-MAINT-USER.
pemgm      compute ergxrf-rec-len = (gx-agent-pointer-cnt * +31) + +78
pemgm      if ergxrf-rec-len > +31264
pemgm         move +31264          to ergxrf-rec-len
pemgm         move +1006           to gx-agent-pointer-cnt
pemgm      end-if
03194
03195      EXEC CICS  REWRITE
03196          FROM     (AGENT-CROSS-REFERENCE)
03197          LENGTH   (ERGXRF-REC-LEN)
03198          DATASET  (ERGXRF-FILE-ID)
03199      END-EXEC.
03200
03201  5525-SKIP-REWRITE.
03202
03203      ADD +1 TO AXRF-SUB.
03204
03205      GO TO 5510-BUILD-AXRF-KEY.
03206
03207  5530-BUMP-AXRF-REC.
03208      MOVE GX-AGENT-POINTER (COMM-WORK-SUB)
03209                           TO  GX-AGENT-POINTER (COMM-WORK-SUB2).
03210
03211      SUBTRACT +1  FROM  COMM-WORK-SUB2.
03212
03213  5599-EXIT.
03214      EXIT.
03215  EJECT
03216  5600-CHG-AXRF-RECORDS.
03217      IF NOT PI-GA-BILLING
03218          GO TO 5699-EXIT.
03219
03220      MOVE +1                     TO  AXRF-SUB.
03221      MOVE ERGXRF-MAX-LEN         TO  ERGXRF-REC-LEN.
03222
03223 *    IF GETMAIN-SW  = 'X'
03224 *        GO TO 5610-BUILD-AXRF-KEY.
03225
03226 *    EXEC CICS  GETMAIN
03227 *        LENGTH   (ERGXRF-REC-LEN)
03228 *        SET      (ADDRESS OF AGENT-CROSS-REFERENCE)
03229 *        INITIMG  (GETMAIN-SPACE)
03230 *    END-EXEC.
      *
pemcp *    display ' beginning of CP displays  '
pemcp *    display ' ergxrf-rec-len  ==   '  ergxrf-rec-len
pemcp *    display ' address         ==   '
pemcp *              address of agent-cross-reference
pemcp *    display ' contents        ==   '  agent-cross-reference (1:1)
pemcp *    display ' end       of CP displays  '

03231
03232 *    MOVE 'X'                    TO  GETMAIN-SW.
03233
03234  5610-BUILD-AXRF-KEY.
03235      IF AXRF-SUB  = +11
03236          GO TO 5699-EXIT.
03237
03238      IF AM-AGT (AXRF-SUB)  = SPACES OR ZEROS OR LOW-VALUES
03239          ADD +1 TO AXRF-SUB
03240          GO TO 5610-BUILD-AXRF-KEY.
03241
052814     IF (AM-COM-TYP (AXRF-SUB) NOT = 'O' AND 'P' AND 'S'
011410         AND 'G' AND 'B' AND 'I' AND 'L' AND 'K' AND 'A')
03244          ADD +1 TO AXRF-SUB
03245          GO TO 5610-BUILD-AXRF-KEY.
03246
03247      MOVE ERGXRF-MAX-LEN         TO  ERGXRF-REC-LEN.
03248      MOVE PI-COMPANY-CD          TO  WS-AXRF-COMPANY-CD.
03249
03250      IF PI-ZERO-CARRIER OR
03251         PI-ZERO-CAR-GROUP
03252          MOVE ZERO               TO  WS-AXRF-CARRIER
03253      ELSE
03254          MOVE AM-CARRIER         TO  WS-AXRF-CARRIER.
03255
03256      IF PI-ZERO-GROUPING  OR
03257         PI-ZERO-CAR-GROUP
03258          MOVE ZEROS              TO  WS-AXRF-GROUPING
03259      ELSE
03260          MOVE AM-GROUPING        TO  WS-AXRF-GROUPING.
03261
03262      MOVE AM-AGT (AXRF-SUB)      TO  WS-AXRF-AGENT-NO.
03263
03264      EXEC CICS  HANDLE CONDITION
03265          ENDFILE  (5615-SKIP-REWRITE)
03266          NOTFND   (5615-SKIP-REWRITE)
03267      END-EXEC.
03268
03269      EXEC CICS  READ
03270          DATASET  (ERGXRF-FILE-ID)
03271          LENGTH   (ERGXRF-REC-LEN)
03272          INTO     (AGENT-CROSS-REFERENCE)
03273          RIDFLD   (WS-AXRF-KEY)
03274          EQUAL
03275          UPDATE
03276      END-EXEC.
03277
03278      MOVE GX-AGENT-POINTER-CNT   TO  GX-AGENT-POINTER-CNT.
03279
03280      PERFORM VARYING GX-AGENT-SUB FROM +1 BY +1
03281              UNTIL
03282        (GX-AGENT-SUB GREATER GX-AGENT-POINTER-CNT)
03283             OR
03284        (GX-AM-ACCOUNT       (GX-AGENT-SUB) = B4-ACCT-ACCOUNT  AND
03285         GX-AM-EXPIRATION-DT (GX-AGENT-SUB) = B4-ACCT-EXP-DT   AND
03286         GX-AM-EFF-DT        (GX-AGENT-SUB) = B4-ACCT-EFF-DT   AND
03287         GX-AM-CARRIER       (GX-AGENT-SUB) = B4-ACCT-CARRIER  AND
03288         GX-AM-GROUPING      (GX-AGENT-SUB) = B4-ACCT-GROUPING AND
03289         GX-AM-STATE         (GX-AGENT-SUB) = B4-ACCT-STATE)
03290      END-PERFORM.
03291
03292      IF MAINTI  = 'D'
03293         IF GX-AGENT-SUB NOT GREATER GX-AGENT-POINTER-CNT
03294             MOVE GX-AGENT-SUB    TO  COMM-WORK-SUB
03295             PERFORM 5630-BUMP-RECORDS
03296                 VARYING  GX-AGENT-SUB  FROM  GX-AGENT-SUB  BY  +1
03297                   UNTIL  GX-AGENT-SUB  = GX-AGENT-POINTER-CNT
03298             SUBTRACT ERGXRF-INC-LEN  FROM  ERGXRF-REC-LEN
03299             SUBTRACT +1              FROM  GX-AGENT-POINTER-CNT
03300             GO TO 5615-REWRITE-AXRF-RECORD.
03301
03302      IF GX-AGENT-SUB NOT GREATER GX-AGENT-POINTER-CNT
03303         MOVE AF-ACCT-EFF-DT  TO GX-AM-EFF-DT        (GX-AGENT-SUB)
03304         MOVE AF-ACCT-EXP-DT  TO GX-AM-EXPIRATION-DT (GX-AGENT-SUB)
03305         MOVE AXRF-SUB        TO GX-AM-LEVEL-NO      (GX-AGENT-SUB)
03306         GO TO 5615-REWRITE-AXRF-RECORD.
03307
03308      EXEC CICS  UNLOCK
03309          DATASET  (ERGXRF-FILE-ID)
03310      END-EXEC.
03311
03312      GO TO 5615-SKIP-REWRITE.
03313
03314  5615-REWRITE-AXRF-RECORD.
03315      IF MAINTI  = 'D'
03316        AND  GX-AGENT-POINTER-CNT  =  +0
03317          EXEC CICS  DELETE
03318              DATASET  (ERGXRF-FILE-ID)
03319          END-EXEC
03320      ELSE
03321          MOVE BIN-CURRENT-SAVE   TO  GX-LAST-MAINT-DT
03322          MOVE EIBTIME            TO  GX-LAST-MAINT-HHMMSS
03323          MOVE PI-PROCESSOR-ID    TO  GX-LAST-MAINT-USER
pemgm          compute ergxrf-rec-len =
pemgm               (gx-agent-pointer-cnt * +31) + +78
pemgm          if ergxrf-rec-len > +31264
pemgm             move +31264          to ergxrf-rec-len
pemgm             move +1006           to gx-agent-pointer-cnt
pemgm          end-if
03324          EXEC CICS  REWRITE
03325              FROM     (AGENT-CROSS-REFERENCE)
03326              LENGTH   (ERGXRF-REC-LEN)
03327              DATASET  (ERGXRF-FILE-ID)
03328          END-EXEC.
03329
03330  5615-SKIP-REWRITE.
03331      ADD +1  TO  AXRF-SUB.
03332
03333      GO TO 5610-BUILD-AXRF-KEY.
03334
03335  5630-BUMP-RECORDS.
03336      ADD +1  TO  COMM-WORK-SUB.
03337
03338      MOVE GX-AGENT-POINTER (COMM-WORK-SUB)
03339                             TO  GX-AGENT-POINTER (GX-AGENT-SUB).
03340
03341  5699-EXIT.
03342      EXIT.
03343  EJECT
03344  6000-EDIT-EFFECTIVE-DATE.
03345      IF EFFCHGL  NOT =  ZEROS
03346          MOVE EFFCHGI                  TO  WS-DT-DEEDIT-FIELD
03347          PERFORM 8600-DEEDIT  THRU 8600-EXIT
03348          MOVE WS-DEEDIT-FIELD-DATE-OUT TO  DC-GREG-DATE-1-MDY
03349          MOVE '4'                      TO  DC-OPTION-CODE
03350          PERFORM 9700-DATE-CONVERSION
03351          IF DATE-CONVERSION-ERROR
03352              MOVE ER-0348              TO  EMI-ERROR
03353              MOVE -1                   TO  EFFCHGL
03354              MOVE AL-UABON             TO  EFFCHGA
03355              PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
03356          ELSE
03357              MOVE DC-GREG-DATE-1-EDIT  TO  EFFCHGI
03358              MOVE AL-UANON             TO  EFFCHGA
03359 ***  Y2K PROJ 7744
03360              MOVE DC-GREG-DATE-1-YMD   TO  WS-EFFCHG-SAVE
03361              MOVE DC-ALPHA-CENTURY     TO  WS-EFFCHG-SAVE(4:2)
03362              MOVE WS-EFFCHG-SAVE       TO  EFFCHG-SAVE
03363              MOVE DC-BIN-DATE-1        TO  BIN-EFFCHG-SAVE
03364          END-IF
03365 ***  Y2K PROJ 7744
03366      ELSE
03367          IF MAINTI  NOT = 'N'
03368              MOVE ER-0216              TO  EMI-ERROR
03369              MOVE -1                   TO  EFFCHGL
03370              MOVE AL-UABON             TO  EFFCHGA
03371              PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
03372
03373  6000-EFF-EXIT.
03374      EXIT.
03375  EJECT
03376  6000-EDIT-EXPIRATION-DATE.
03377 ***  Y2K PROJ 7744
03378      IF EXPCHGL  NOT = ZEROS
03379          MOVE EXPCHGI                  TO  WS-DT-DEEDIT-FIELD
03380          PERFORM 8600-DEEDIT  THRU 8600-EXIT
03381          IF WS-DEEDIT-FIELD-DATE-OUT NOT LESS 999999
03382              MOVE WS-DEEDIT-FIELD-DATE-OUT TO  EXPCHGO
03383              INSPECT EXPCHGI CONVERTING SPACES TO '/'
03384              MOVE HIGH-VALUES              TO  BIN-EXPCHG-SAVE
03385              MOVE 99999999999              TO  EXPCHG-SAVE
03386          ELSE
03387              MOVE WS-DEEDIT-FIELD-DATE-OUT TO  DC-GREG-DATE-1-MDY
03388              MOVE '4'                      TO  DC-OPTION-CODE
03389              PERFORM 9700-DATE-CONVERSION
03390              IF DATE-CONVERSION-ERROR
03391                  MOVE ER-0454              TO  EMI-ERROR
03392                  MOVE -1                   TO  EXPCHGL
03393                  MOVE AL-UABON             TO  EXPCHGA
03394                  PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
03395              ELSE
03396                  MOVE DC-GREG-DATE-1-EDIT  TO  EXPCHGI
03397                  MOVE AL-UANON             TO  EXPCHGA
03398                  MOVE DC-GREG-DATE-1-YMD   TO  WS-EXPCHG-SAVE
03399                  MOVE DC-ALPHA-CENTURY     TO  WS-EXPCHG-SAVE(4:2)
03400                  MOVE WS-EXPCHG-SAVE       TO  EXPCHG-SAVE
03401                  MOVE DC-BIN-DATE-1        TO  BIN-EXPCHG-SAVE
03402              END-IF
03403          END-IF
03404      ELSE
03405          IF MAINTI  NOT =  'N'
03406              MOVE ER-2058                  TO  EMI-ERROR
03407              MOVE -1                       TO  EXPCHGL
03408              MOVE AL-UABON                 TO  EXPCHGA
03409              PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
03410          ELSE
03411              MOVE HIGH-VALUES              TO  BIN-EXPCHG-SAVE
03412              MOVE 99999999999              TO  EXPCHG-SAVE
03413          END-IF
03414      END-IF.
03415 ***  Y2K PROJ 7744
03416
03417  6000-EXP-EXIT.
03418      EXIT.
03419  EJECT
03420  6000-EDIT-LINE.
03421      IF LINEL  = ZEROS
03422          GO TO 6000-NO-LINE.
03423
03424      MOVE AL-UNNON               TO  LINEA.
03425
03426      IF LINEI NOT NUMERIC
03427          GO TO 6000-LINE-ERROR.
03428
03429      IF LINEI LESS 1 OR GREATER 8 OR GREATER PI-TOTAL-LINES
03430          GO TO 6000-LINE-ERROR.
03431
03432      MOVE PI-TOTAL-LINES         TO WS-PAGE-LINES.
03433
03434      IF PI-2ND-PAGE
03435          SUBTRACT 8 FROM WS-PAGE-LINES
03436      ELSE
03437      IF PI-3RD-PAGE
03438          SUBTRACT 16 FROM WS-PAGE-LINES
03439      ELSE
03440      IF PI-LST-PAGE
03441          SUBTRACT 24 FROM WS-PAGE-LINES.
03442
03443      IF LINEI GREATER WS-PAGE-LINES
03444          GO TO 6000-LINE-ERROR.
03445
03446      MOVE LINEI                  TO  PI-LINE-SELECTED.
03447
03448      GO TO 6000-LIN-EXIT.
03449
03450  6000-NO-LINE.
03451      IF PI-TOTAL-LINES  = 1
03452          MOVE 1                  TO  PI-LINE-SELECTED
03453                                      LINEI
03454          MOVE AL-UNNON           TO  LINEA
03455          GO TO 6000-LIN-EXIT.
03456
03457  6000-LINE-ERROR.
03458      MOVE ER-2064                TO  EMI-ERROR.
03459      MOVE -1                     TO  LINEL.
03460      MOVE AL-UABON               TO  LINEA.
03461
03462      PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
03463
03464  6000-LIN-EXIT.
03465      EXIT.
03466  EJECT
03467  600A-CHECK-HI-LO-DATES.
03468
03469      IF PI-BIN-HI-CERT (T-INDEX)  =  LOW-VALUES
03470          GO TO 600A-CHECK-AR-DATES.
03471
03472      IF BIN-EFFCHG-SAVE GREATER THAN PI-BIN-EFF-DT (T-INDEX)
03473      IF (PI-BIN-HI-CERT (T-INDEX) NOT LESS BIN-EXPCHG-SAVE)
03474          MOVE ER-2113            TO  EMI-ERROR
03475          MOVE -1                 TO  MAINTL
03476          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
03477
03478 *    IF (PI-BIN-HI-CERT (T-INDEX) NOT LESS BIN-EXPCHG-SAVE)
03479 *        MOVE ER-2113            TO  EMI-ERROR
03480 *        MOVE -1                 TO  MAINTL
03481 *        PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
03482
03483  600A-CHECK-AR-DATES.
03484      IF NOT PI-AR-PROCESSING
03485          GO TO 600A-EXIT.
03486
03487      IF PI-BIN-AR-HI-CERT (T-INDEX) = LOW-VALUES OR SPACES
03488         GO TO 600A-EXIT.
03489
03490      IF (PI-BIN-AR-HI-CERT (T-INDEX) NOT LESS BIN-EXPCHG-SAVE)
03491          MOVE ER-2113            TO  EMI-ERROR
03492          MOVE -1                 TO  MAINTL
03493          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
03494
03495  600A-EXIT.
03496      EXIT.
03497  EJECT
03498  6010-DATE-CHECK.
03499      IF MAINTI  = 'E'
03500          GO TO 6010-CHECK-EFF.
03501
03502      IF PI-BIN-HI-CERT (T-INDEX)  NOT = LOW-VALUES
03503          IF BIN-EXPCHG-SAVE NOT GREATER PI-BIN-HI-CERT (T-INDEX)
03504              MOVE ER-2072        TO  EMI-ERROR
03505              MOVE -1             TO  EXPCHGL
03506              MOVE AL-UABON       TO  EXPCHGA
03507              PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
03508
03509      IF BIN-EXPCHG-SAVE NOT GREATER PI-BIN-EFF-DT (T-INDEX)
03510          MOVE ER-2074            TO  EMI-ERROR
03511          MOVE -1                 TO  EXPCHGL
03512          MOVE AL-UABON           TO  EXPCHGA
03513          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
03514
03515      GO TO 6010-EXIT.
03516
03517  6010-CHECK-EFF.
03518      IF PI-BIN-LO-CERT (T-INDEX)  NOT =  LOW-VALUES
03519         IF (BIN-EFFCHG-SAVE >= PI-BIN-LO-CERT (T-INDEX))
                 AND (PI-PROCESSOR-ID NOT = 'PEMA')
03520            MOVE ER-2073          TO EMI-ERROR
03521            MOVE -1               TO EFFCHGL
03522            MOVE AL-UABON         TO EFFCHGA
03523            PERFORM 9900-ERROR-FORMAT
                                       THRU 9900-EXIT
              END-IF
           END-IF
03524
03525      IF BIN-EFFCHG-SAVE NOT LESS PI-BIN-EXP-DT (T-INDEX)
03526          MOVE ER-2075            TO  EMI-ERROR
03527          MOVE -1                 TO  EFFCHGL
03528          MOVE AL-UABON           TO  EFFCHGA
03529          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
03530
03531  6010-EXIT.
03532      EXIT.
03533  EJECT
03534  6100-BUILD-KEY.
03535      MOVE LOW-VALUES             TO  PI-ACCT-KEY.
03536      MOVE PI-COMPANY-CD          TO  PI-ACCT-CO.
03537
03538      IF ACARIERI  =  SPACES  OR  LOW-VALUES
03539          IF MAINTI  = 'S'
03540            OR  FIRST-TIME
03541              MOVE SPACES         TO  PI-ACCT-CARRIER
03542          ELSE
03543              MOVE -1             TO  ACARIERL
03544              MOVE ER-2059        TO  EMI-ERROR
03545              PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
03546      ELSE
03547          MOVE AL-UANON           TO  ACARIERA
03548          MOVE ACARIERI           TO  PI-ACCT-CARRIER.
03549
03550      IF AGROUPI  =  SPACES  OR  LOW-VALUES
03551          IF MAINTI  = 'S'
03552            OR  FIRST-TIME
03553              MOVE SPACES         TO  PI-ACCT-GROUPING
03554          ELSE
03555              MOVE -1             TO  AGROUPL
03556              MOVE ER-2059        TO  EMI-ERROR
03557              PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
03558      ELSE
03559          IF AGROUPI  =  '0     '
03560              MOVE -1             TO  AGROUPL
03561              MOVE ER-0195        TO  EMI-ERROR
03562              PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
03563          ELSE
03564              MOVE AL-UANON       TO  AGROUPA
03565              MOVE AGROUPI        TO  PI-ACCT-GROUPING.
03566
03567      IF ASTATEI  = SPACES  OR  LOW-VALUES
03568          IF MAINTI  =  'S'
03569            OR  FIRST-TIME
03570              MOVE SPACES         TO  PI-ACCT-STATE
03571          ELSE
03572              MOVE -1             TO  ASTATEL
03573              MOVE ER-2059        TO  EMI-ERROR
03574              PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
03575      ELSE
03576          MOVE AL-UANON           TO  ASTATEA
03577          MOVE ASTATEI            TO  PI-ACCT-STATE.
03578
03579      IF AACCTI  = SPACES  OR  LOW-VALUES
03580         IF MAINTI  = 'S'
03581           OR  FIRST-TIME
03582             MOVE SPACES          TO  PI-ACCT-ACCOUNT
03583         ELSE
03584             MOVE -1              TO  AACCTL
03585             MOVE ER-2059         TO  EMI-ERROR
03586             PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
03587      ELSE
03588          MOVE AL-UANON           TO  AACCTA
03589          MOVE AACCTI             TO  PI-ACCT-ACCOUNT.
03590
03591      IF PI-ACCT-CARRIER  = SPACES
03592        AND  (CARR-ACCNT-CNTL
03593        OR   CARR-ST-ACCNT-CNTL
03594        OR   CARR-GROUP-ST-ACCNT-CNTL)
03595          MOVE ER-0193            TO  EMI-ERROR
03596          MOVE -1                 TO  ACARIERL
03597          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
03598
03599      IF PI-ACCT-GROUPING  =  SPACES
03600        AND  CARR-GROUP-ST-ACCNT-CNTL
03601          MOVE ER-0195            TO  EMI-ERROR
03602          MOVE -1                 TO  AGROUPL
03603          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
03604
03605      IF PI-ACCT-STATE  = SPACES
03606        AND  (ST-ACCNT-CNTL
03607        OR   CARR-ST-ACCNT-CNTL
03608        OR   CARR-GROUP-ST-ACCNT-CNTL)
03609          MOVE ER-0144            TO  EMI-ERROR
03610          MOVE -1                 TO  ASTATEL
03611          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
03612
03613      IF MAINTI  =  'S'
03614        OR  FIRST-TIME
03615          NEXT SENTENCE
03616      ELSE
03617          MOVE PI-ACCT-CCGSA-KEY  TO  KEY-SAVE.
03618
03619      IF NOT EMI-NO-ERRORS
020816         IF EIBTRNID = EL6311-TRANS-ID OR VP6311-TRANS-ID
03621              GO TO 8100-SEND-INITIAL-MAP
03622            ELSE
03623              GO TO 8200-SEND-DATAONLY.
03624
03625      IF PI-ACCT-CARRIER   = SPACES  OR
03626         PI-ACCT-STATE     = SPACES  OR
03627         PI-ACCT-GROUPING  = SPACES
03628          MOVE ACCT2-FILE-ID      TO  PI-ACCT-ID
03629      ELSE
03630          MOVE ACCT-FILE-ID       TO  PI-ACCT-ID.
03631
03632      MOVE LOW-VALUES             TO  PI-ACCT-EXP-DT
03633                                      PI-ACCT-REST-OF-EXP.
03634
03635  6199-EXIT.
03636      EXIT.
03637
03638  6200-CHECK-KEY.
03639      IF PI-PREV-ACCOUNT  NOT = PI-ACCT-CCGSA-KEY
03640          MOVE ER-0453            TO  EMI-ERROR
03641          MOVE -1                 TO  MAINTL
03642          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
03643
03644  6200-EXIT.
03645      EXIT.
03646  EJECT
03647  6300-STARTBR.
03648      MOVE ACCT-FILE-ID           TO  PI-ACCT-ID.
03649
03650      EXEC CICS  STARTBR
03651          DATASET  (PI-ACCT-ID)
03652          RIDFLD   (PI-ACCT-KEY)
03653      END-EXEC.
03654
03655      MOVE 'Y'                    TO  BROWSE-STARTED-SW.
03656      MOVE PI-ACCT-CCGSA-KEY      TO  KEY-SAVE.
03657
03658  6300-EXIT.
03659      EXIT.
03660
03661  6310-READNEXT.
03662      EXEC CICS  READNEXT
03663          DATASET  (PI-ACCT-ID)
03664          SET      (ADDRESS OF ACCOUNT-MASTER)
03665          RIDFLD   (PI-ACCT-KEY)
03666      END-EXEC.
03667
03668  6310-EXIT.
03669      EXIT.
03670
03671  6320-ENDBR.
03672      MOVE SPACE                  TO  BROWSE-STARTED-SW.
03673
03674      EXEC CICS  ENDBR
03675          DATASET  (PI-ACCT-ID)
03676      END-EXEC.
03677
03678  6320-EXIT.
03679      EXIT.
03680  EJECT
03681  6400-BUILD-VG-KEY.
03682      MOVE AM-CONTROL-PRIMARY     TO  AM-CONTROL-BY-VAR-GRP.
03683
03684      IF ST-ACCNT-CNTL
03685          MOVE SPACES             TO  AM-VG-CARRIER
03686                                      AM-VG-GROUPING.
03687
03688      IF CARR-ST-ACCNT-CNTL
03689          MOVE SPACES             TO  AM-VG-GROUPING.
03690
03691      IF ACCNT-CNTL
03692          MOVE SPACES             TO  AM-VG-CARRIER
03693                                      AM-VG-STATE
03694                                      AM-VG-GROUPING.
03695
03696      IF CARR-ACCNT-CNTL
03697          MOVE SPACES             TO  AM-VG-STATE
03698                                      AM-VG-GROUPING.
03699
03700  6499-EXIT.
03701      EXIT.
03702  EJECT
03703  6500-READ-ACCT.
03704      EXEC CICS  HANDLE CONDITION
03705          NOTFND  (6599-EXIT)
03706      END-EXEC.
03707
03708      MOVE ZERO                   TO  REC-SW.
03709
03710      EXEC CICS  READ
03711          GTEQ
03712          DATASET  (ACCT-FILE-ID)
03713          SET      (ADDRESS OF ACCOUNT-MASTER)
03714          RIDFLD   (PI-ACCT-KEY)
03715      END-EXEC.
03716
03717      MOVE AM-CONTROL-PRIMARY     TO  PI-ACCT-KEY.
03718      MOVE LOW-VALUES             TO  PI-ACCT-REST-OF-EXP.
03719
03720      IF PI-ACCT-CCGSA-KEY  = NEW-KEY-SAVE
03721          MOVE 1                  TO  REC-SW.
03722
03723  6599-EXIT.
03724      EXIT.
03725  EJECT
03726  6600-UPDATE-MAINT-DT.
03727      MOVE SPACES                 TO  ELCNTL-KEY.
03728      MOVE PI-COMPANY-ID          TO  CNTL-COMP-ID.
03729      MOVE '1'                    TO  CNTL-REC-TYPE.
03730      MOVE +0                     TO  CNTL-SEQ-NO.
03731
03732      EXEC CICS  HANDLE CONDITION
03733          NOTFND  (6699-EXIT)
03734      END-EXEC.
03735
03736      EXEC CICS  READ
03737          UPDATE
03738          DATASET  (CNTL-FILE-ID)
03739          SET      (ADDRESS OF CONTROL-FILE)
03740          RIDFLD   (ELCNTL-KEY)
03741      END-EXEC.
03742
03743      MOVE CONTROL-FILE           TO  JP-RECORD-AREA.
03744      MOVE 'B'                    TO  JP-RECORD-TYPE.
03745      MOVE CNTL-FILE-ID           TO  FILE-ID.
03746
03747      PERFORM 8400-LOG-JOURNAL-RECORD.
03748
03749      MOVE BIN-CURRENT-SAVE       TO  CF-ACCOUNT-MSTR-MAINT-DT.
03750      MOVE CONTROL-FILE           TO  JP-RECORD-AREA.
03751      MOVE 'C'                    TO  JP-RECORD-TYPE.
03752      MOVE CNTL-FILE-ID           TO  FILE-ID.
03753
03754      EXEC CICS  REWRITE
03755          DATASET  (CNTL-FILE-ID)
03756          FROM     (CONTROL-FILE)
03757      END-EXEC.
03758
03759      PERFORM 8400-LOG-JOURNAL-RECORD.
03760
03761  6699-EXIT.
03762      EXIT.
03763  EJECT
03764  6700-VERIFY-STATE-CARRIER.
03765      IF PI-ACCT-CARRIER  = SPACES
03766          GO TO 6750-CHECK-STATE.
03767
03768      MOVE SPACES                 TO  ELCNTL-KEY.
03769      MOVE PI-COMPANY-ID          TO  CNTL-COMP-ID.
03770      MOVE '6'                    TO  CNTL-REC-TYPE.
03771      MOVE PI-ACCT-CARRIER        TO  CNTL-CARRIER
03772      MOVE +0                     TO  CNTL-SEQ-NO.
03773
03774      EXEC CICS  HANDLE CONDITION
03775          NOTFND  (6720-NO-CARRIER)
03776      END-EXEC.
03777
03778      EXEC CICS  READ
03779          DATASET  (CNTL-FILE-ID)
03780          SET      (ADDRESS OF CONTROL-FILE)
03781          RIDFLD   (ELCNTL-KEY)
03782      END-EXEC.
03783
03784      GO TO 6750-CHECK-STATE.
03785
03786  6720-NO-CARRIER.
03787      MOVE ER-0193                TO  EMI-ERROR.
03788      MOVE -1                     TO  ACARIERL.
03789      MOVE AL-UABON               TO  ACARIERA.
03790
03791      PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
03792
03793  6750-CHECK-STATE.
03794      IF PI-ACCT-STATE  =  SPACES
03795          GO TO 6799-EXIT.
03796
03797      MOVE SPACES                 TO  ELCNTL-KEY.
03798      MOVE PI-COMPANY-ID          TO  CNTL-COMP-ID.
03799      MOVE '3'                    TO  CNTL-REC-TYPE.
03800      MOVE PI-ACCT-STATE          TO  CNTL-STATE.
03801      MOVE +0                     TO  CNTL-SEQ-NO.
03802
03803      EXEC CICS  HANDLE CONDITION
03804          NOTFND  (6780-NO-STATE)
03805      END-EXEC.
03806
03807      EXEC CICS  READ
03808          DATASET  (CNTL-FILE-ID)
03809          SET      (ADDRESS OF CONTROL-FILE)
03810          RIDFLD   (ELCNTL-KEY)
03811      END-EXEC.
03812
03813      GO TO 6799-EXIT.
03814
03815  6780-NO-STATE.
03816      MOVE ER-0144                TO  EMI-ERROR.
03817      MOVE -1                     TO  ASTATEL.
03818      MOVE AL-UABON               TO  ASTATEA.
03819
03820      PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
03821
03822  6799-EXIT.
03823      EXIT.
03824  EJECT
03825  6800-COMPANY-REC-READ.
03826      MOVE SPACES                 TO  ELCNTL-KEY.
03827      MOVE PI-COMPANY-ID          TO  CNTL-COMP-ID.
03828      MOVE '1'                    TO  CNTL-REC-TYPE.
03829      MOVE +0                     TO  CNTL-SEQ-NO.
03830
03831      EXEC CICS  HANDLE CONDITION
03832          NOTFND  (6880-NO-COMP)
03833      END-EXEC.
03834
03835      EXEC CICS  READ
03836          DATASET  (CNTL-FILE-ID)
03837          SET      (ADDRESS OF CONTROL-FILE)
03838          RIDFLD   (ELCNTL-KEY)
03839      END-EXEC.
03840
03841      IF CF-ACCOUNT-MSTR-MAINT-DT  = LOW-VALUES
03842          MOVE ER-2572            TO  EMI-ERROR
03843          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
03844
03845      GO TO 6899-EXIT.
03846
03847  6880-NO-COMP.
03848      MOVE ER-0022                TO  EMI-ERROR.
03849
03850      PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
03851
03852  6899-EXIT.
03853      EXIT.
03854  EJECT
03855  7000-BUILD-OUTPUT-MAP.
03856      IF RETURNING-FROM-6501
03857          MOVE PI-PREV-VG-ACCOUNT  TO  KEY-SAVE.
03858
03859      IF PI-COMPANY-ID  = 'LGX'  OR  'MIC'  OR  'MCC'
03860          MOVE AL-SANOF           TO  TRNMSG1A
03861                                      TRNMSG2A
03862                                      TRNMSG3A
03863                                      TRNMSG4A.
03864
03865      MOVE AL-UANON               TO  ACARIERA
03866                                      AGROUPA
03867                                      ASTATEA
03868                                      AACCTA.
03869      MOVE SV-CARRIER             TO  ACARIERO.
03870      MOVE SV-GROUPING            TO  AGROUPO.
03871      MOVE SV-STATE               TO  ASTATEO.
03872      MOVE SV-ACCOUNT             TO  AACCTO.
03873
03874      SET M-INDEX  TO  1.
03875
03876      MOVE LOW-VALUES             TO  MAP-RECORD-AREA.
03877
03878      IF PI-MAINT  = 'A'
03879        AND  PI-RECORD-NOT-CREATED
03880          MOVE ER-2151            TO  EMI-ERROR
03881          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
03882          GO TO 7000-END.
03883
03884      IF PI-FST-PAGE
03885          SET T-INDEX   TO  1
03886      ELSE
03887      IF PI-2ND-PAGE
03888          SET T-INDEX   TO  9
03889      ELSE
03890      IF PI-3RD-PAGE
03891          SET T-INDEX   TO  17
03892      ELSE
03893          SET T-INDEX   TO  25.
03894
03895  7000-LOOP.
03896      IF PI-BIN-EXP-DT (T-INDEX)  NOT = HIGH-VALUES
03897          MOVE PI-BIN-EXP-DT (T-INDEX)
03898                                  TO  DC-BIN-DATE-1
03899          MOVE SPACE              TO  DC-OPTION-CODE
03900          PERFORM 9700-DATE-CONVERSION
03901          IF DATE-CONVERSION-ERROR
03902              MOVE SPACES         TO  MAP-EXP-DT (M-INDEX)
03903          ELSE
03904              MOVE DC-GREG-DATE-1-EDIT
03905                                  TO  MAP-EXP-DT (M-INDEX)
03906      ELSE
03907          MOVE 999999             TO  MAP-EXP-DT-ED (M-INDEX)
03908          INSPECT MAP-EXP-DT-ED (M-INDEX) CONVERTING SPACES TO '/'.
03909
03910      MOVE PI-BIN-EFF-DT (T-INDEX)  TO  DC-BIN-DATE-1.
03911      MOVE SPACE                    TO  DC-OPTION-CODE.
03912
03913      PERFORM 9700-DATE-CONVERSION.
03914
03915      IF DATE-CONVERSION-ERROR
03916          MOVE SPACES               TO  MAP-EFF-DT (M-INDEX)
03917      ELSE
03918          MOVE DC-GREG-DATE-1-EDIT  TO  MAP-EFF-DT (M-INDEX).
03919
03920      IF PI-BIN-MAINT-DT (T-INDEX)  = LOW-VALUES
03921          MOVE SPACES             TO  MAP-LST-MAINT-DT (M-INDEX)
03922      ELSE
03923          MOVE PI-BIN-MAINT-DT (T-INDEX)
03924                                  TO  DC-BIN-DATE-1
03925          MOVE SPACE              TO  DC-OPTION-CODE
03926          PERFORM 9700-DATE-CONVERSION
03927          IF DATE-CONVERSION-ERROR
03928              MOVE SPACES         TO  MAP-LST-MAINT-DT (M-INDEX)
03929          ELSE
03930              MOVE DC-GREG-DATE-1-EDIT
03931                                  TO  MAP-LST-MAINT-DT (M-INDEX).
03932
03933      IF PI-BIN-HI-CERT (T-INDEX)  =  LOW-VALUES
03934          MOVE SPACES             TO  MAP-HIGH-CERT (M-INDEX)
03935      ELSE
03936          MOVE PI-BIN-HI-CERT (T-INDEX)
03937                                  TO  DC-BIN-DATE-1
03938          MOVE SPACE              TO  DC-OPTION-CODE
03939          PERFORM 9700-DATE-CONVERSION
03940          IF DATE-CONVERSION-ERROR
03941              MOVE SPACES         TO  MAP-HIGH-CERT (M-INDEX)
03942          ELSE
03943              MOVE DC-GREG-DATE-1-EDIT
03944                                  TO  MAP-HIGH-CERT (M-INDEX).
03945
03946      IF PI-BIN-LO-CERT (T-INDEX)  = LOW-VALUES
03947          MOVE SPACES             TO  MAP-LOW-CERT (M-INDEX)
03948      ELSE
03949          MOVE PI-BIN-LO-CERT (T-INDEX)
03950                                  TO  DC-BIN-DATE-1
03951          MOVE SPACE              TO  DC-OPTION-CODE
03952          PERFORM 9700-DATE-CONVERSION
03953          IF DATE-CONVERSION-ERROR
03954              MOVE SPACES         TO  MAP-LOW-CERT (M-INDEX)
03955          ELSE
03956              MOVE DC-GREG-DATE-1-EDIT
03957                                  TO  MAP-LOW-CERT (M-INDEX).
03958
03959      IF PI-BIN-AR-HI-CERT (T-INDEX)  = LOW-VALUES OR SPACES
03960          MOVE SPACES             TO  MAP-AR-DATE (M-INDEX)
03961      ELSE
03962          MOVE PI-BIN-AR-HI-CERT (T-INDEX)
03963                                  TO  DC-BIN-DATE-1
03964          MOVE SPACE              TO  DC-OPTION-CODE
03965          PERFORM 9700-DATE-CONVERSION
03966          IF DATE-CONVERSION-ERROR
03967              MOVE SPACES         TO  MAP-AR-DATE (M-INDEX)
03968          ELSE
03969              MOVE DC-GREG-DATE-1-EDIT
03970                                  TO  MAP-AR-DATE (M-INDEX).
03971
03972      IF M-INDEX  =  8  OR
03973         T-INDEX  =  PI-TOTAL-LINES
03974          NEXT SENTENCE
03975      ELSE
03976          SET M-INDEX
03977              T-INDEX  UP  BY  1
03978          GO TO 7000-LOOP.
03979
03980      IF M-INDEX  =  8
03981        AND  T-INDEX  LESS THAN  PI-TOTAL-LINES
03982          MOVE ER-0668            TO  EMI-ERROR
03983          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
03984
03985      IF T-INDEX  =  PI-TOTAL-LINES
03986          MOVE ER-0630            TO  EMI-ERROR
03987          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT.
03988
03989  7000-END.
03990      MOVE -1                     TO  MAINTL.
03991      MOVE MAINTI                 TO  PI-MAINT.
03992
03993      IF MAINTI  = 'S'  OR  'D'
03994        OR  RETURNING-FROM-6501
03995          MOVE LOW-VALUES         TO  MAINTI
03996          MOVE PI-ACCNAME         TO  NAMEO
03997          MOVE PI-NAMEFLG         TO  NAMEFLGO
03998          GO TO 8100-SEND-INITIAL-MAP.
03999
04000      IF EIBAID  =  DFHPF1  OR  DFHPF2  OR
04001                    DFHPF3  OR  DFHPF4
04002          MOVE 'S'                TO  PI-MAINT
04003          MOVE PI-ACCNAME         TO  NAMEO
04004          MOVE PI-NAMEFLG         TO  NAMEFLGO
04005          GO TO 8100-SEND-INITIAL-MAP.
04006
04007      IF MAINTI  = 'N' OR 'E' OR 'X'
04008         MOVE SPACES                 TO  BIN-EXPCHG-SAVE
04009                                         BIN-EFFCHG-SAVE
04010         MOVE SPACES                 TO  EFFCHGI
04011                                         EXPCHGI
04012         MOVE AL-UANOF               TO  EFFCHGA
04013                                         EXPCHGA
04014         MOVE SPACES                 TO  PI-MAINT
04015         MOVE SPACES                 TO  MAINTI
04016                                         MAINTO
04017         MOVE AL-UANOF               TO  MAINTA.
04018
04019      IF K-WITH-LINE-NO
04020          MOVE ZERO         TO K-WITH-LINE-USED
04021          GO TO 8100-SEND-INITIAL-MAP.
04022
04023      GO TO 8200-SEND-DATAONLY.
04024  EJECT
04025  7100-START-BROWSE.
04026      EXEC CICS  STARTBR
04027          DATASET  (PI-ACCT-ID)
04028          RIDFLD   (PI-ACCT-KEY)
04029      END-EXEC.
04030
04031  7100-EXIT.
04032      EXIT.
04033  EJECT
04034  7200-WRITE-TEMP-STORAGE.
04035      PERFORM 7300-DELETE-TEMP-STORAGE  THRU  7300-EXIT.
04036
04037      EXEC CICS  HANDLE CONDITION
04038          ITEMERR  (7200-EXIT)
04039          QIDERR   (7200-EXIT)
04040      END-EXEC.
04041
04042      MOVE +0                     TO  TS-ITEM.
04043
04044      EXEC CICS  WRITEQ TS
04045          QUEUE   (TS-QUEUE)
04046          FROM    (TEMP-STORAGE-RECORD)
04047          LENGTH  (TS-LENGTH)
04048          ITEM    (TS-ITEM)
04049      END-EXEC.
04050
04051  7200-EXIT.
04052      EXIT.
04053
04054  7300-DELETE-TEMP-STORAGE.
04055      EXEC CICS  HANDLE CONDITION
04056          ITEMERR  (7300-EXIT)
04057          QIDERR   (7300-EXIT)
04058      END-EXEC.
04059
04060      EXEC CICS  DELETEQ TS
04061          QUEUE  (TS-QUEUE)
04062      END-EXEC.
04063
04064  7300-EXIT.
04065      EXIT.
04066
04067  7400-READ-TEMP-STORAGE.
04068      EXEC CICS  HANDLE CONDITION
04069          ITEMERR  (7400-EXIT)
04070          QIDERR   (7400-EXIT)
04071      END-EXEC.
04072
04073      MOVE +1                     TO  TS-ITEM.
04074
04075      EXEC CICS  READQ TS
04076          QUEUE   (TS-QUEUE)
04077          INTO    (TEMP-STORAGE-RECORD)
04078          LENGTH  (TS-LENGTH)
04079          ITEM    (TS-ITEM)
04080      END-EXEC.
04081
04082  7400-EXIT.
04083      EXIT.
04084
04085  8100-SEND-INITIAL-MAP.
04086      MOVE SAVE-DATE              TO  DATEO.
04087      MOVE EIBTIME                TO  TIME-IN.
04088      MOVE TIME-OUT               TO  TIMEO.
101101     MOVE PI-COMPANY-ID          TO  CMPNYIDO.
101101     MOVE PI-PROCESSOR-ID        TO  USERIDO.
04089      MOVE -1                     TO  MAINTL.
04090      MOVE AL-UABON               TO  MAINTA.
04091      MOVE EMI-MESSAGE-AREA (1)   TO  ERRMSG1O.
04092      MOVE EMI-MESSAGE-AREA (2)   TO  ERRMSG2O.
04093
04094      IF PI-AR-PROCESSING
04095         MOVE AL-PANOF            TO  ARHDGA.
04096
04097      EXEC CICS  SEND
04098          MAP     (MAP-NAME)
04099          MAPSET  (MAPSET-NAME)
04100          FROM    (EL650AO)
04101          ERASE
04102          CURSOR
04103      END-EXEC.
04104
04105      GO TO 9100-RETURN-TRAN.
04106
04107  8200-SEND-DATAONLY.
04108      MOVE SAVE-DATE              TO  DATEO.
04109      MOVE EIBTIME                TO  TIME-IN.
04110      MOVE TIME-OUT               TO  TIMEO.
101101     MOVE PI-COMPANY-ID          TO  CMPNYIDO.
101101     MOVE PI-PROCESSOR-ID        TO  USERIDO.
04111      MOVE EMI-MESSAGE-AREA (1)   TO  ERRMSG1O.
04112      MOVE EMI-MESSAGE-AREA (2)   TO  ERRMSG2O.
04113
04114      EXEC CICS  SEND
04115          MAP     (MAP-NAME)
04116          MAPSET  (MAPSET-NAME)
04117          FROM    (EL650AO)
04118          DATAONLY
04119          CURSOR
04120      END-EXEC.
04121
04122      GO TO 9100-RETURN-TRAN.
04123
04124  8300-SEND-TEXT.
04125      EXEC CICS  SEND TEXT
04126          FROM    (LOGOFF-TEXT)
04127          LENGTH  (LOGOFF-LENGTH)
04128          ERASE
04129          FREEKB
04130      END-EXEC.
04131
04132      EXEC CICS  RETURN
04133      END-EXEC.
04134
04135  8400-LOG-JOURNAL-RECORD.
CIDMOD*    MOVE PI-PROCESSOR-ID        TO  JP-USER-ID.
CIDMOD*    MOVE FILE-ID                TO  JP-FILE-ID.
CIDMOD*    MOVE THIS-PGM               TO  JP-PROGRAM-ID.
CIDMOD*
CIDMOD*    IF PI-JOURNAL-FILE-ID  NOT =  ZERO
CIDMOD*        EXEC CICS  JOURNAL
CIDMOD*            JFILEID  (PI-JOURNAL-FILE-ID)
CIDMOD*            JTYPEID  ('ER')
CIDMOD*            FROM     (JOURNAL-RECORD)
CIDMOD*            LENGTH   (2023)
CIDMOD*        END-EXEC.
04147
04148  8600-DEEDIT.
04149      EXEC CICS  BIF DEEDIT
04150          FIELD   (WS-DEEDIT-FIELD)
04151          LENGTH  (10)
04152      END-EXEC.
04153  8600-EXIT.
04154       EXIT.
04155  8800-UNAUTHORIZED-ACCESS.
04156      MOVE UNACCESS-MSG           TO  LOGOFF-MSG.
04157
04158      GO TO 8300-SEND-TEXT.
04159
04160  8810-PF23.
04161      MOVE EIBAID                 TO  PI-ENTRY-CD-1.
04162      MOVE XCTL-005               TO  PGM-NAME.
04163
04164      GO TO 9300-XCTL.
04165
04166  8900-PF5.
04167      IF PI-PREV-VG-ACCOUNT EQUAL LOW-VALUES OR SPACES
04168          MOVE ER-3785            TO  EMI-ERROR
04169          PERFORM 9900-ERROR-FORMAT  THRU  9900-EXIT
04170          MOVE -1                 TO  MAINTL
04171          GO TO 8200-SEND-DATAONLY.
04172
04173      MOVE PI-PREV-VG-ACCOUNT     TO  PI-ACCT-CCGSA-KEY.
04174
04175      MOVE XCTL-6509              TO  PGM-NAME.
04176
04177      GO TO 9300-XCTL.
04178
110706 8910-PF6.

110706     IF PI-PREV-VG-ACCOUNT EQUAL LOW-VALUES OR SPACES
110706        MOVE ER-3785             TO EMI-ERROR
110706        PERFORM 9900-ERROR-FORMAT
                                       THRU 9900-EXIT
110706        MOVE -1                  TO MAINTL
110706        GO TO 8200-SEND-DATAONLY
110706     END-IF

110706     MOVE PI-PREV-VG-ACCOUNT     TO PI-ACCT-CCGSA-KEY

110706     MOVE XCTL-6503              TO PGM-NAME

110706     GO TO 9300-XCTL
           .
04179  9100-RETURN-TRAN.
04180      MOVE EMI-ERROR-NUMBER (1)   TO  PI-LAST-ERROR-NO.
04181      MOVE SCREEN-NUMBER          TO  PI-CURRENT-SCREEN-NO.
04182      EXEC CICS  RETURN
04183          TRANSID   (TRANS-ID)
04184          COMMAREA  (PROGRAM-INTERFACE-BLOCK)
04185          LENGTH    (WS-COMM-LENGTH)
04186      END-EXEC.
04187
04188  9200-RETURN-MAIN-MENU.
04189      MOVE XCTL-126               TO  PGM-NAME.
04190
04191      GO TO 9300-XCTL.
04192
04193  9300-XCTL.
04194      EXEC CICS  XCTL
04195          PROGRAM   (PGM-NAME)
04196          COMMAREA  (PROGRAM-INTERFACE-BLOCK)
04197          LENGTH    (WS-COMM-LENGTH)
04198      END-EXEC.
04199
04200  9400-CLEAR.
04201      MOVE SPACES                 TO  PI-SV-MAINT.
04202      MOVE PI-RETURN-TO-PROGRAM   TO  PGM-NAME.
04203
04204      GO TO 9300-XCTL.
04205
04206  9500-PF12.
04207      MOVE XCTL-010               TO  PGM-NAME.
04208
04209      GO TO 9300-XCTL.
04210
04211  9600-PGMID-ERROR.
04212      EXEC CICS  HANDLE CONDITION
04213          PGMIDERR  (8300-SEND-TEXT)
04214      END-EXEC.
04215
04216      MOVE PGM-NAME               TO  PI-CALLING-PROGRAM.
04217      MOVE ' '                    TO  PI-ENTRY-CD-1.
04218      MOVE XCTL-005               TO  PGM-NAME.
04219      MOVE PGM-NAME               TO  LOGOFF-PGM.
04220      MOVE PGMIDERR-MSG           TO  LOGOFF-FILL.
04221
04222      GO TO 9300-XCTL.
04223
04224  9700-DATE-CONVERSION.
04225      MOVE LINK-ELDATCV           TO  PGM-NAME.
04226
04227      EXEC CICS  LINK
04228          PROGRAM   (PGM-NAME)
04229          COMMAREA  (DATE-CONVERSION-DATA)
04230          LENGTH    (DC-COMM-LENGTH)
04231      END-EXEC.
04232
04233  9900-ERROR-FORMAT.
04234      IF NOT EMI-ERRORS-COMPLETE
04235          MOVE LINK-001           TO  PGM-NAME
04236          EXEC CICS  LINK
04237              PROGRAM   (PGM-NAME)
04238              COMMAREA  (ERROR-MESSAGE-INTERFACE-BLOCK)
04239              LENGTH    (EMI-COMM-LENGTH)
04240          END-EXEC.
04241
04242  9900-EXIT.
04243      EXIT.
04244
04245  9990-ABEND.
04246      MOVE LINK-004               TO  PGM-NAME.
04247      MOVE DFHEIBLK               TO  EMI-LINE1.
04248
04249      EXEC CICS  LINK
04250          PROGRAM   (PGM-NAME)
04251          COMMAREA  (EMI-LINE1)
04252          LENGTH    (72)
04253      END-EXEC.
04254
04255      MOVE -1                     TO  MAINTL.
04256
04257      GO TO 8200-SEND-DATAONLY.
04258
04259  9995-SECURITY-VIOLATION.
04260                              COPY ELCSCTP.
04261
04262  9995-EXIT.
04263      EXIT.
