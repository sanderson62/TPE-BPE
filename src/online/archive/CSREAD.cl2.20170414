      *****************************************************************
      *                                                               *
      * Copyright (c) 2012 by Central States Health and Life          *
      * All rights reserved.                                          *
      *                                                               *
      *****************************************************************
       identification division.
       program-id. CSREAD.
      *
      *AUTHOR.    Pablo.
      *           Colleyville, TEXAS.

      ********************************************
      *   Coversheet read a specific form
      ********************************************
       environment division.

       data division.

       working-storage section.

      ************************************************
      * commarea passed to the business logic
      ************************************************
         
       01  read-commarea.
                                       copy CSREAD-COMMAREA.

      ************************************
      * fields used to read web data
      ************************************
      
       01  w-form-name       pic x(80).
       01  w-form-value      pic x(80).
       01  w-form-name-len   pic s9(8) comp.
       01  w-form-value-len  pic s9(8) comp.
       01  w-resp            pic s9(8) comp.
       01  w-doctoken        pic x(16).        
   
      *************************************
      * symbol list for DETAIL template 
      *************************************
      
       01 output-data.
          05 filler                pic x(5) value "FORM=".
          05 out-form-name         pic x(10).
          05 filler                pic x(7) value "&FORM1=".
          05 out-form1             pic x(10).
          05 filler                pic x(6) value "&FMTH=".
          05 out-form-mth          pic xxx.
          05 filler                pic x(6) value "&DESC=".
          05 out-desc              pic x(30).
          05 filler                pic x(7) value "&MESS1=".
          05 out-msgl1             pic x(75).
          05 filler                pic x(7) value "&MESS2=".
          05 out-msgl2             pic x(75).
          05 filler                pic x(7) value "&MESS3=".
          05 out-msgl3             pic x(75).
          05 filler                pic x(7) value "&MESS4=".
          05 out-msgl4             pic x(75).
          05 filler                pic x(7) value "&MESS5=".
          05 out-msgl5             pic x(75).
          05 filler                pic x(7) value "&MESS6=".
          05 out-msgl6             pic x(75).
          05 filler                pic x(7) value "&MESS7=".
          05 out-msgl7             pic x(75).
          05 filler                pic x(7) value "&MESS8=".
          05 out-msgl8             pic x(75).
          05 filler                pic x(7) value "&COMM1=".
          05 out-comm1             pic x(95).
          05 filler                pic x(7) value "&COMM2=".
          05 out-comm2             pic x(95).
          05 filler                pic x(5) value "&MSG=".
          05  out-message          pic x(50).

       procedure division.

      *********************
      * Receive web input
      *********************
      
           exec cics web startbr
              formfield
              resp   (w-resp) 
           end-exec
      	         
           perform 0200-read-form thru 0200-exit until
              w-resp not = dfhresp(normal)
      	             
           exec cics web
              endbr formfield
           end-exec
  
      ***********************************
      * Invoke the READ business logic
      ***********************************

          display ' form name ' bl-input-form-name

           exec cics link
              program  ('CSREADBL')
              commarea (read-commarea)
           end-exec

           move bl-output-message      to out-message
           move bl-output-form-name    to out-form-name
                                          out-form1
           move bl-output-form-month   to out-form-mth
           move bl-output-form-desc    to out-desc
           move bl-output-messages (1)  to out-msgl1
           move bl-output-messages (2)  to out-msgl2
           move bl-output-messages (3)  to out-msgl3
           move bl-output-messages (4)  to out-msgl4
           move bl-output-messages (5)  to out-msgl5
           move bl-output-messages (6)  to out-msgl6
           move bl-output-messages (7)  to out-msgl7
           move bl-output-messages (8)  to out-msgl8

           move bl-output-comment1     to out-comm1
           move bl-output-comment2     to out-comm2

           display ' out data ' output-data
           exec cics document create
              doctoken   (w-doctoken)
              template   ('CSDETAIL')
              symbollist (output-data)
              listlength (length of output-data)
           end-exec

           if bl-fail
              exec cics syncpoint rollback
              end-exec
           end-if
    
           exec cics web send
              doctoken(w-doctoken)
           end-exec

           exec cics return
           end-exec

      ******************************************************
      * Read the account number field of the incoming
      * form, moving its value to the corresponding
      * field of the commarea (the business logic input).
      ******************************************************
           .
       0200-read-form.

           move spaces                 to w-form-name
           move length of w-form-name  to w-form-name-len
	         move spaces                 to w-form-value
           move length of w-form-value to w-form-value-len

           exec cics web readnext 
              formfield   (w-form-name)
              namelength  (w-form-name-len)
              value       (w-form-value)
              valuelength (w-form-value-len)
              resp        (w-resp)
           end-exec

           evaluate w-resp
              when dfhresp(normal)
                 display ' read form normal '
                 evaluate w-form-name(1:w-form-name-len)
                    when 'form_nme'
                       if w-form-value-len not = 0
                          move w-form-value(1:w-form-value-len)
                                       to bl-input-form-name
                       else
                          move spaces  to bl-input-form-name
                       end-if
                 end-evaluate
              when other
                 continue
           end-evaluate

           .
       0200-exit.
           exit.
