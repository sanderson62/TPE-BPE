00001  IDENTIFICATION DIVISION.                                         03/27/97
00002                                                                   EL1522
00003  PROGRAM-ID.                 EL1522.                                 LV009
00004 *              PROGRAM CONVERTED BY                                  CL**7
00005 *              COBOL CONVERSION AID PO 5785-ABJ                      CL**7
00006 *              CONVERSION DATE 02/13/96 09:53:12.                    CL**7
00007 *                            VMOD=2.009.                             CL**9
00008 *                                                                    CL**8
00009 *AUTHOR.           LOGIC,INC.                                        CL**7
00010 *                  DALLAS,TEXAS.                                     CL**7
00011                                                                   EL1522
00012 *DATE-COMPILED.                                                      CL**7
00013                                                                   EL1522
00014 *SECURITY.   *****************************************************   CL**7
00015 *            *                                                   *   CL**7
00016 *            *   THIS PROGRAM IS THE PROPERTY OF LOGIC, INC.     *   CL**9
00017 *                                                                *   CL**7
00018 *            *   USE OF THIS PROGRAM BY OTHER THAN THE EMPLOYEES *   CL**9
00019 *            *   OF LOGIC, INC. IS EXPRESSLY PROHIBITED WITHOUT  *   CL**7
00020 *            *   THE PRIOR WRITTEN PERMISSION OF LOGIC INC.      *   CL**7
00021 *            *                                                   *   CL**7
00022 *            *****************************************************   CL**7
00023                                                                   EL1522
00024 *REMARKS. TRANSACTION EX57 - HARDCOPY PRINT PROGRAM.                 CL**2
00025 *        THIS PROGRAM IS USED TO PRINT LETTERS TO THE COMPANY        CL**2
00026 *        SPECIFIED PRINTER. IT IS ACTIVATED THROUGH THE START        CL**2
00027 *        COMMAND AND WILL HAVE THE INTERFACE BLOCK PASSED.           CL**2
00028 *        DATA IN THE INTERFACE BLOCK WILL INDICATE THE NUMBER OF     CL**2
00029 *        LINES TO PRINT AND THE NUMBER OF COPIES.                    CL**2
00030 *        IT WILL ALSO CONTAIN THE TEMP STORAGE ID TO USE IN ORDER    CL**2
00031 *        TO RETRIEVE THE DATA TO BE PRINTED.                         CL**2
00032                                                                   EL1522
00033                                                                   EL1522
00034      EJECT                                                        EL1522
00035  ENVIRONMENT DIVISION.                                            EL1522
00036  DATA DIVISION.                                                   EL1522
00037  WORKING-STORAGE SECTION.                                         EL1522
00038  77  FILLER  PIC X(32)  VALUE '********************************'. EL1522
00039  77  FILLER  PIC X(32)  VALUE '*   EL1522 WORKING STORAGE     *'. EL1522
00040  77  FILLER  PIC X(32)  VALUE '******* VMOD=2.009 *************'.    CL**9
00041                                                                   EL1522
00042  01  WS-CONSTANTS.                                                EL1522
00043      12  THIS-PGM                PIC X(8)    VALUE 'EL1522'.         CL**8
00044      12  PGM-EL152               PIC X(8)    VALUE 'EL152'.       EL1522
00045      12  ERROR-LINE              PIC X(70).                       EL1522
00046      12  TS-ITEM                 PIC S9(4)   COMP.                EL1522
00047      12  TS-LENGTH               PIC S9(4)   COMP VALUE +3650.       CL**3
00048      12  INDX-WORK               PIC 99.                          EL1522
00049      12  NUM-ENTRIES-PER-RECORD  PIC 9(3)  COMP-3 VALUE 50.       EL1522
00050      12  RECORDS-PRINTED         PIC 9(3)  COMP-3 VALUE 0.        EL1522
00051      12  TOP-OF-FORM-MESSAGE     PIC X(8)  VALUE '*****TOP'.      EL1522
00052      12  TOP-FORM                PIC X      VALUE '1'.            EL1522
00053      12  WS-NEXT-TRAN            PIC X(4).                        EL1522
00054      12  WS-TERMINAL-ID.                                          EL1522
00055          18  WS-TERM-PREFIX      PIC XX.                          EL1522
00056          18  FILLER              PIC XX.                          EL1522
00057      12  TS-WORK-AREA            PIC X(3650).                        CL**3
00058      12  REC-ENTRIES REDEFINES TS-WORK-AREA.                      EL1522
00059          16  REC-ENT OCCURS 50 TIMES INDEXED BY TB-INDX.          EL1522
00060              20  REC-TEXT.                                        EL1522
00061                24 REC-TEXT-TOP   PIC X(8).                        EL1522
00062                24 FILLER         PIC X(62).                       EL1522
00063              20  REC-PC          PIC 99.                          EL1522
00064              20  FILLER          PIC X.                              CL**3
CIDMOD     12  WS-CSO-TEMP-STOR-ID.                                          000
CIDMOD         18  FILLER              PIC X(04).                            000
CIDMOD         18  WS-CSO-PRINTER-ID   PIC X(04).                            000
CIDMOD     12  WS-REPORT-STARTED-SW    PIC X      VALUE 'N'.                 000
CIDMOD         88  REPORT-STARTED                 VALUE 'Y'.                 000
CIDMOD/                                                                 EL1522
CIDMOD                                     COPY ELCDMD34.                  CL**8
CIDMOD/                                                                 EL1522
CIDMOD                                     COPY ELCINTF.                   CL**6
CIDMOD/                                                                 EL1522
CIDMOD     12  PI-WA REDEFINES PI-PROGRAM-WORK-AREA.                    EL1522
CIDMOD/                                                                 EL1522
00069          COPY ELC1042.                                               CL**4
00070 **********************************************************           CL**4
00071 *    NOTE                                                *           CL**4
00072 *        THE WORK AREA IS USED BY EL152 AND EL1522       *           CL**4
00073 *        AND CANNOT BE REARRANGED WITHOUT COMPILING      *           CL**4
00074 *        BOTH PROGRAMS.                                  *           CL**4
00075 *                                                        *           CL**4
00076 **********************************************************           CL**4
00077          16  PI-EL152-WA.                                            CL**4
00078              20  PI-ALT-PRINTER-ID        PIC X(4).                  CL**9
00079              20  PI-ARCHIVE-NUMBER        PIC 9(8).                  CL**9
00080              20  PI-FORM-NUMBER           PIC X(4).                  CL**9
00081              20  PI-ADDR-TYPE             PIC XX.                    CL**9
00082              20  PI-TEMP-STOR-ID          PIC X(8).                  CL**9
00083              20  PI-NUM-PRINT-COPIES      PIC 9.                     CL**9
00084              20  PI-ADDR-SEQ              PIC S9(4)   COMP.          CL**9
00085              20  PI-PRINT-SW              PIC  X.                    CL**9
00086                  88 PRINT-PERFORMED     VALUE '1'.                   CL**9
00087              20  FILLER                   PIC X(543).                CL**9
00088      EJECT                                                        EL1522
00089                                  COPY ELPRTCVD.                      CL**6
00090                                                                      CL**6
00091  01  WS-DATA-SHIFT-AREA.                                             CL**6
00092      12  WS-DATA-FIL             PIC X(7).                           CL**9
00093      12  WS-DATA-SHIFT           PIC X(125).                         CL**9
00094      EJECT                                                        EL1522
00095  PROCEDURE DIVISION.                                              EL1522
00096                                                                      CL**8
00097      MOVE +132                   TO  WS-LINE-LEN.                    CL**8
00098      MOVE SPACES                 TO  DL34-PROCESS-TYPE.              CL**8
CIDMOD     MOVE 'N'                    TO  WS-REPORT-STARTED-SW.             000
00099                                                                      CL**8
00100  0100-RETRIEVE-LOOP.                                                 CL**8
00101                                                                      CL**8
00102      EXEC CICS HANDLE CONDITION                                   EL1522
00103           ENDDATA(200-END-DATA)                                   EL1522
00104           NOTFND (300-NOT-FOUND)                                     CL**6
00105      END-EXEC.                                                       CL**6
00106                                                                   EL1522
00107      EXEC CICS RETRIEVE                                           EL1522
00108           INTO  (PROGRAM-INTERFACE-BLOCK)                            CL**6
00109           LENGTH(PI-COMM-LENGTH)                                  EL1522
00110      END-EXEC.                                                       CL**6
00111                                                                      CL**8
00112 * DLO034 OPEN WHEN DMD OR CID                                        CL**8
pemuni     IF PI-COMPANY-ID = 'DMD' OR 'XXX'                               CL**8
00114          IF DL34-PROCESS-TYPE = SPACES                               CL**9
00115              MOVE 'O'                TO DL34-PROCESS-TYPE            CL**8
00116              MOVE PI-COMPANY-ID      TO DL34-COMPANY-ID              CL**8
00117              MOVE THIS-PGM           TO DL34-PRINT-PROGRAM-ID        CL**8
00118              MOVE PI-PROCESSOR-ID    TO DL34-USERID                  CL**8
00119              MOVE SPACES             TO DL34-PRINT-LINE              CL**8
00120              MOVE PI-ALT-DMD-PRT-ID  TO DL34-OVERRIDE-PRINTER-ID     CL**8
00121              EXEC CICS LINK                                          CL**8
00122                  PROGRAM    ('DLO034')                               CL**8
00123                  COMMAREA   (DLO034-COMMUNICATION-AREA)              CL**8
00124                  LENGTH     (DLO034-REC-LENGTH)                      CL**8
00125              END-EXEC                                                CL**8
00126              IF DL34-RETURN-CODE NOT = 'OK'                          CL**8
00127                  MOVE  '**DLO034 OPEN ERROR - ABORT**'               CL**8
00128                                      TO ERROR-LINE                   CL**8
00129                  PERFORM 400-SEND-TEXT                               CL**8
00130                  EXEC CICS RETURN                                    CL**8
00131                  END-EXEC.                                           CL**8
00132                                                                   EL1522
00133      IF PI-CALLING-PROGRAM = PGM-EL152                            EL1522
00134         GO TO 1000-PRINT-EL152.                                   EL1522
00135                                                                   EL1522
00136      GO TO 0100-RETRIEVE-LOOP.                                    EL1522
00137      EJECT                                                        EL1522
00138  200-END-DATA.                                                    EL1522
CIDMOD                                                                       000
CIDMOD*    MOVE 'L'                    TO DRS-SW.                            000
CIDMOD*    PERFORM ELPRTCVP THRU ELPRTCVP-EXIT.                              000
CIDMOD                                                                       000
00139      MOVE 'EX27'                 TO WS-NEXT-TRAN.                 EL1522
00140                                                                   EL1522
00141      MOVE EIBTRMID               TO WS-TERMINAL-ID.                  CL**6
00142                                                                      CL**8
00143 * DLO034 CLOSE                                                       CL**8
pemuni     IF PI-COMPANY-ID = 'DMD' OR 'XXX'                               CL**8
00145          MOVE 'C'                TO DL34-PROCESS-TYPE                CL**8
00146          MOVE PI-COMPANY-ID      TO DL34-COMPANY-ID                  CL**8
00147          MOVE THIS-PGM           TO DL34-PRINT-PROGRAM-ID            CL**8
00148          MOVE PI-PROCESSOR-ID    TO DL34-USERID                      CL**8
00149          MOVE SPACES             TO DL34-PRINT-LINE                  CL**8
00150                                     DL34-OVERRIDE-PRINTER-ID         CL**8
00151          EXEC CICS LINK                                              CL**8
00152              PROGRAM    ('DLO034')                                   CL**8
00153              COMMAREA   (DLO034-COMMUNICATION-AREA)                  CL**8
00154              LENGTH     (DLO034-REC-LENGTH)                          CL**8
00155          END-EXEC                                                    CL**8
00156          IF DL34-RETURN-CODE NOT = 'OK'                              CL**8
00157              MOVE  '**DLO034 CLOSE ERROR - ABORT**'                  CL**8
00158                                  TO ERROR-LINE                       CL**8
00159              PERFORM 400-SEND-TEXT                                   CL**8
00160              EXEC CICS RETURN                                        CL**8
00161                   TRANSID  (WS-NEXT-TRAN)                            CL**8
00162                   COMMAREA (PROGRAM-INTERFACE-BLOCK)                 CL**8
00163                   LENGTH   (PI-COMM-LENGTH)                          CL**8
00164              END-EXEC.                                               CL**8
00165                                                                      CL**6
00166      IF WS-TERM-PREFIX = 'DU'                                     EL1522
00167         EXEC CICS RETURN                                          EL1522
00168              TRANSID  (WS-NEXT-TRAN)                              EL1522
00169              COMMAREA (PROGRAM-INTERFACE-BLOCK)                   EL1522
00170              LENGTH   (PI-COMM-LENGTH)                            EL1522
00171         END-EXEC                                                     CL**6
00172        ELSE                                                          CL**6
00173         EXEC CICS RETURN                                          EL1522
00174         END-EXEC.                                                    CL**6
00175                                                                   EL1522
00176  300-NOT-FOUND.                                                   EL1522
00177      IF PI-COMPANY-ID NOT = 'MON'                                    CL**6
00178          MOVE 'NO COMMUNICATION AREA FOUND' TO ERROR-LINE            CL**6
00179          PERFORM 400-SEND-TEXT.                                      CL**6
00180                                                                      CL**6
00181      GO TO 200-END-DATA.                                          EL1522
00182                                                                   EL1522
00183  400-SEND-TEXT.                                                   EL1522
00184      EXEC CICS SEND TEXT                                          EL1522
00185           FROM(ERROR-LINE)                                        EL1522
00186           LENGTH(70)                                              EL1522
00187      END-EXEC.                                                       CL**6
00188      EJECT                                                        EL1522
00189  1000-PRINT-EL152.                                                EL1522
00190      EXEC CICS HANDLE CONDITION                                   EL1522
00191           QIDERR (1090-TS-QIDERR)                                    CL**9
00192           ITEMERR(1089-TS-ITEMERR)                                EL1522
00193      END-EXEC.                                                       CL**6
00194                                                                      CL**6
00195      SUBTRACT 7 FROM PI-TOTAL-LINES.                              EL1522
CIDMOD                                                                     CL**6
CIDMOD*    MOVE PI-TEMP-STOR-ID  TO  WS-CSO-TEMP-STOR-ID.                    000
CIDMOD*    IF REPORT-STARTED                                                 000
CIDMOD*      MOVE 'L'  TO  DRS-SW                                            000
CIDMOD*      PERFORM ELPRTCVP THRU ELPRTCVP-EXIT.                            000
CIDMOD*    MOVE 'F'     TO  DRS-SW.                                          000
CIDMOD*    PERFORM ELPRTCVP THRU ELPRTCVP-EXIT.                              000
CIDMOD*    MOVE ' '     TO  DRS-SW.                                          000
CIDMOD                                                                       000
00196                                                                      CL**6
00197  1005-READ-TEMP.                                                  EL1522
00198      PERFORM 1010-TEMP-READ THRU 1019-EXIT                        EL1522
00199              VARYING TS-ITEM FROM 1 BY 1 UNTIL                    EL1522
00200              TS-ITEM GREATER THAN PI-TEMP-STOR-ITEMS.                CL**6
00201                                                                      CL**6
00202      MOVE 'X'                    TO WS-PROG-END.                     CL**6
00203                                                                      CL**9
pemuni     IF PI-COMPANY-ID NOT = 'DMD' AND 'XXX'                          CL**9
00205          PERFORM ELPRTCVP THRU ELPRTCVP-EXIT.                        CL**9
00206                                                                      CL**9
00207      MOVE ZEROS                  TO RECORDS-PRINTED.              EL1522
00208      SUBTRACT 1 FROM PI-NUM-PRINT-COPIES.                            CL**6
00209                                                                      CL**6
00210      IF PI-NUM-PRINT-COPIES = 0                                   EL1522
00211         PERFORM 7750-DELETE-TEMP-STOR THRU 7750-EXIT              EL1522
00212         GO TO 0100-RETRIEVE-LOOP.                                 EL1522
00213                                                                      CL**6
00214      GO TO 1005-READ-TEMP.                                        EL1522
00215      EJECT                                                        EL1522
00216  1010-TEMP-READ.                                                  EL1522
00217      EXEC CICS READQ TS                                           EL1522
00218           INTO  (TS-WORK-AREA)                                       CL**6
00219           QUEUE (PI-TEMP-STOR-ID)                                    CL**6
00220           LENGTH(TS-LENGTH)                                       EL1522
00221           ITEM  (TS-ITEM)                                            CL**6
00222      END-EXEC.                                                       CL**6
00223                                                                      CL**6
00224      IF TS-ITEM = 1                                               EL1522
00225         MOVE TOP-FORM            TO WS-PASSED-CNTL-CHAR           EL1522
00226         PERFORM 1020-RECORD-PRINT THRU 1029-EXIT                  EL1522
00227                 VARYING TB-INDX FROM 8 BY 1 UNTIL                 EL1522
00228                  TB-INDX GREATER THAN NUM-ENTRIES-PER-RECORD      EL1522
00229         ELSE                                                      EL1522
00230         PERFORM 1020-RECORD-PRINT THRU 1029-EXIT                  EL1522
00231                 VARYING TB-INDX FROM 1 BY 1 UNTIL                 EL1522
00232                 TB-INDX GREATER THAN NUM-ENTRIES-PER-RECORD.      EL1522
00233                                                                   EL1522
00234  1019-EXIT.                                                       EL1522
00235       EXIT.                                                       EL1522
00236      EJECT                                                        EL1522
00237  1020-RECORD-PRINT.                                               EL1522
00238      IF REC-TEXT-TOP (TB-INDX) = TOP-OF-FORM-MESSAGE              EL1522
00239         MOVE TOP-FORM            TO WS-PASSED-CNTL-CHAR           EL1522
00240         ADD 1                    TO RECORDS-PRINTED               EL1522
00241         SET TB-INDX UP BY 1                                       EL1522
00242         GO TO 1029-EXIT.                                             CL**5
00243                                                                   EL1522
00244      IF PI-COMPANY-ID = 'MON'                                        CL**6
00245          MOVE SPACES             TO WS-DATA-SHIFT-AREA               CL**6
00246          MOVE REC-TEXT (TB-INDX) TO WS-DATA-SHIFT                    CL**6
00247          MOVE WS-DATA-SHIFT-AREA TO WS-PASSED-DATA                   CL**6
00248        ELSE                                                          CL**6
00249          MOVE REC-TEXT (TB-INDX) TO WS-PASSED-DATA.                  CL**6
00250                                                                      CL**6
00251      PERFORM ELPRTCVP THRU ELPRTCVP-EXIT.                         EL1522
00252      MOVE SPACE                  TO WS-PASSED-CNTL-CHAR.             CL**6
00253      ADD 1                       TO RECORDS-PRINTED.                 CL**6
00254                                                                   EL1522
00255      IF RECORDS-PRINTED = PI-TOTAL-LINES                          EL1522
00256         SET TB-INDX TO NUM-ENTRIES-PER-RECORD                     EL1522
00257         SET TB-INDX UP BY 1                                       EL1522
00258         GO TO 1029-EXIT.                                          EL1522
00259                                                                   EL1522
00260  1029-EXIT.                                                       EL1522
00261       EXIT.                                                       EL1522
00262      EJECT                                                        EL1522
00263  1089-TS-ITEMERR.                                                 EL1522
00264      MOVE 'TEMP STORAGE RECORD NOT FOUND' TO ERROR-LINE.             CL**6
00265      PERFORM 400-SEND-TEXT.                                          CL**6
00266      GO TO 0100-RETRIEVE-LOOP.                                    EL1522
00267                                                                   EL1522
00268  1090-TS-QIDERR.                                                  EL1522
00269      MOVE 'NO TEMP STORAGE RECORDS NOT FOUND' TO ERROR-LINE.         CL**6
00270      PERFORM 400-SEND-TEXT.                                          CL**6
00271      GO TO 0100-RETRIEVE-LOOP.                                    EL1522
00272      EJECT                                                        EL1522
00273  7750-DELETE-TEMP-STOR.                                           EL1522
00274      EXEC CICS HANDLE CONDITION                                   EL1522
00275           QIDERR(7750-EXIT)                                       EL1522
00276      END-EXEC.                                                       CL**6
00277                                                                      CL**6
00278      EXEC CICS DELETEQ TS                                         EL1522
00279           QUEUE(PI-TEMP-STOR-ID)                                  EL1522
00280      END-EXEC.                                                       CL**6
00281                                                                      CL**6
00282  7750-EXIT.                                                       EL1522
00283      EXIT.                                                        EL1522
00284      EJECT                                                        EL1522
CIDMOD*                          COPY ELPRTCVP.                            CL**7
uktdel*9800-PRINT-ROUTINE.       COPY ELPRTCVP.                              000
uktins 9800-PRINT-ROUTINE.
uktins     COPY ELPRTCVP.
00286                                                                   EL1522
