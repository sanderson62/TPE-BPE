00001 ***************************************************************** 10/17/86
00002 *                                                               * ELCNPD
00002 *                                                               * ELCNPD
00003 *                            ELCNPD                             *    LV001
00004 *                            VMOD=2.001                         * ELCNPD
00005 ***************************************************************** ELCNPD
00006                                                                   ELCNPD
00007  5200-MOVE-NAME.                                                  ELCNPD
00008 *                THE FOLLOWING ROUTINE MOVES THE INSURRED'S     * ELCNPD
00009 *            NAME TO A WORK AREA WITH NO EMBEDDED               * ELCNPD
00010 *            BLANKS.                                            * ELCNPD
00011 *                                                               * ELCNPD
00012 *                  FIELD               VALUE                    * ELCNPD
00013 *                                                               * ELCNPD
00014 *                LAST NAME (CL15)      SMITH                    * ELCNPD
00015 *                1ST NAME  (CL12)      JOHN                     * ELCNPD
00016 *                MID NAME  (CL1)       A                        * ELCNPD
00017 *                                                               * ELCNPD
00018 *                AFTER NAME HAS BEEN MOVED WS-NAME-WORK (CL30)  * ELCNPD
00019 *         *                                                     * ELCNPD
00020 *         *              JOHN A. SMITH                          * ELCNPD
00021 *         *                                                     * ELCNPD
00022 *         *      TO USE THIS ROUTINE YOU ALSO NEED A WORKING    * ELCNPD
00023 *         *  STORAGE COPYBOOK:                                  * ELCNPD
00024 *         *                                                     * ELCNPD
00025 *         *      01  WS-NAME-WORK-AREA COPY ELCNWA.             * ELCNPD
00026 *         *******************************************************.ELCNPD
00027                                                                   ELCNPD
00028      MOVE SPACES                 TO  WS-NAME-WORK                 ELCNPD
00029                                      WS-NAME-WORK2.               ELCNPD
00030      MOVE ZERO                   TO  WS-NAME-SW.                  ELCNPD
00031      SET NWA-INDEX TO +1.                                         ELCNPD
00032                                                                   ELCNPD
00033      IF WS-INSURED-1ST-NAME = SPACES  AND                         ELCNPD
00034         WS-INSURED-MID-INIT = SPACES                              ELCNPD
00035          MOVE WS-INSURED-LAST-NAME TO WS-NAME-WORK                ELCNPD
00036          GO TO 5200-EXIT.                                         ELCNPD
00037                                                                   ELCNPD
00038      MOVE WS-INSURED-1ST-NAME    TO  WS-NAME-WORK2.               ELCNPD
00039      PERFORM 5300-MOVE-NAME THRU 5390-EXIT.                       ELCNPD
00040                                                                   ELCNPD
00041      SET NWA-INDEX UP BY +1                                       ELCNPD
00042      IF WS-INSURED-MID-INIT NOT = SPACES                          ELCNPD
00043         MOVE WS-INSURED-MID-INIT   TO  WS-NW (NWA-INDEX)          ELCNPD
00044         SET NWA-INDEX UP BY +1                                    ELCNPD
00045         MOVE '.'                   TO  WS-NW (NWA-INDEX)          ELCNPD
00046         SET NWA-INDEX UP BY +2.                                   ELCNPD
00047                                                                   ELCNPD
00048      MOVE WS-INSURED-LAST-NAME  TO  WS-NAME-WORK2.                ELCNPD
00049      PERFORM 5300-MOVE-NAME THRU 5390-EXIT.                       ELCNPD
00050                                                                   ELCNPD
00051                                                                   ELCNPD
00052  5200-EXIT.                                                       ELCNPD
00053      EXIT.                                                        ELCNPD
00054                                                                   ELCNPD
00055      EJECT                                                        ELCNPD
00056  5300-MOVE-NAME SECTION.                                          ELCNPD
00057      IF WS-NAME-SW GREATER THAN +1                                ELCNPD
00058          GO TO 5390-EXIT.                                         ELCNPD
00059                                                                   ELCNPD
00060      IF WS-NAME-WORK2 = SPACES                                    ELCNPD
00061          GO TO 5390-EXIT.                                         ELCNPD
00062                                                                   ELCNPD
00063      SET NWA-INDEX2 TO +1.                                        ELCNPD
00064      SET NWA-INDEX3 TO +2.                                        ELCNPD
00065                                                                   ELCNPD
00066  5310-MOVE-NAME.                                                  ELCNPD
00067      MOVE WS-NW2 (NWA-INDEX2)  TO  WS-NW (NWA-INDEX).             ELCNPD
00068                                                                   ELCNPD
00069      IF NWA-INDEX LESS THAN +30                                   ELCNPD
00070         SET NWA-INDEX UP BY +1                                    ELCNPD
00071      ELSE                                                         ELCNPD
00072         ADD +2  TO  WS-NAME-SW                                    ELCNPD
00073         GO TO 5390-EXIT.                                          ELCNPD
00074                                                                   ELCNPD
00075      IF NWA-INDEX2 LESS THAN +20                                  ELCNPD
00076          SET NWA-INDEX2 UP BY +1                                  ELCNPD
00077          SET NWA-INDEX3 UP BY +1.                                 ELCNPD
00078                                                                   ELCNPD
00079      IF WS-NW2 (NWA-INDEX2) = SPACES AND                          ELCNPD
00080         WS-NW2 (NWA-INDEX3) = SPACES                              ELCNPD
00081         GO TO 5390-EXIT.                                          ELCNPD
00082                                                                   ELCNPD
00083      GO TO 5310-MOVE-NAME.                                        ELCNPD
00084                                                                   ELCNPD
00085  5390-EXIT.                                                       ELCNPD
00086      EXIT.                                                        ELCNPD
00087                                                                   ELCNPD
00088      EJECT                                                        ELCNPD
