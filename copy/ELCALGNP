00001 ******************************************************************12/03/86
00002 *                                                                *ELCALGNP
00003 *                            ELCALGNP.                           *   LV002
00004 *                            VMOD=2.002                          *   CL**2
00005 *                                                                *ELCALGNP
CIDMOD*   NO  CID  MODS  IN  COPYBOOK ELCALGNP                         *ELCALGNP
00006 ******************************************************************ELCALGNP
00007                                                                   ELCALGNP
00008 ******************************************************************ELCALGNP
00009 ***                 -ELCALGNP-                                    ELCALGNP
00010 ***     COPY MEMBER FOR FIELD ALIGNMENT ROUTINE                  *ELCALGNP
00011 ***     THIS ROUTINE WILL ACCEPT AN ALPHABETIC FIELD AND AFTER   *ELCALGNP
00012 ***      ALIGNING THE CONTENTS IN THE CENTER, RETURNS THE ALIGNED*ELCALGNP
00013 ***      FIELD IN A SEPARATE AREA.                                ELCALGNP
00014 ***     THIS ROUTINE TO BE USED ONLY WITH ACCOMPANIMENT          *ELCALGNP
00015 ***      OF THE WORKING-STORAGE COPY MEMBER ( ELCALGND )         *ELCALGNP
00016 ***     THE HOST PROGRAM MUST INITIALIZE THE FOLLOWING 2 FIELDS  *ELCALGNP
00017 ***      FROM THE ABOVE COPY MEMBER FOR THIS PROCEDURE TO BE     *ELCALGNP
00018 ***      SUCCESSFUL.                                             *ELCALGNP
00019 ***       WS-UNALIGNED-FIELD   PIC X(30)                          ELCALGNP
00020 ***                         THE ALPHANUMERIC FIELD TO BE ALIGNED  ELCALGNP
00021 ***       WS-NAME-LENGTH       PIC S9(4)                          ELCALGNP
00022 ***                         THE NUMERIC LENGTH OF THE ABOVE FIELD ELCALGNP
00023 ***                         DEFAULT VALUE IS 30                   ELCALGNP
00024 ******************************************************************ELCALGNP
00025  ELCALGNP.                                                        ELCALGNP
00026      MOVE SPACES   TO WS-ALIGNED-FIELD.                           ELCALGNP
00027                                                                   ELCALGNP
00028      IF WS-UNALIGNED-FIELD = SPACES                               ELCALGNP
00029          GO TO ELCALGNP-EXIT.                                     ELCALGNP
00030                                                                   ELCALGNP
00031      MOVE  ZEROS     TO WS-HALF-BLANKS                            ELCALGNP
00032                         WS-ACTUAL-NAME-LENGTH                     ELCALGNP
00033                         WS-SPACE-COUNTER.                         ELCALGNP
00034                                                                   ELCALGNP
00035      MOVE LOW-VALUES TO WS-LENGTH-FOUND-SW.                       ELCALGNP
00036      SET NAME-IND    TO WS-NAME-LENGTH.                           ELCALGNP
00037                                                                   ELCALGNP
00038      PERFORM S1-FIND-SPACE-LENGTH  THRU S1-EXIT                   ELCALGNP
00039                                     UNTIL  LENGTH-FOUND.          ELCALGNP
00040                                                                   ELCALGNP
00041      IF WS-SPACE-COUNTER LESS 2                                   ELCALGNP
00042          MOVE WS-UNALIGNED-FIELD  TO WS-ALIGNED-FIELD             ELCALGNP
00043          GO TO ELCALGNP-EXIT.                                     ELCALGNP
00044                                                                   ELCALGNP
00045      SUBTRACT WS-SPACE-COUNTER FROM WS-NAME-LENGTH                ELCALGNP
00046                                     GIVING WS-ACTUAL-NAME-LENGTH. ELCALGNP
00047                                                                   ELCALGNP
00048      DIVIDE  WS-SPACE-COUNTER BY 2  GIVING WS-HALF-BLANKS.        ELCALGNP
00049      SET  NAME-IND-A TO 1.                                        ELCALGNP
00050      PERFORM  S2-MOVE-LEADING-SPACE  THRU S2-EXIT                 ELCALGNP
00051                                     WS-HALF-BLANKS  TIMES.        ELCALGNP
00052      SET  NAME-IND   TO 1.                                        ELCALGNP
00053      PERFORM S3-MOVE-ALPHA           THRU S3-EXIT                 ELCALGNP
00054                                     WS-ACTUAL-NAME-LENGTH  TIMES. ELCALGNP
00055  ELCALGNP-EXIT.                                                   ELCALGNP
00056      EXIT.                                                        ELCALGNP
00057                                                                   ELCALGNP
00058  S1-FIND-SPACE-LENGTH.                                            ELCALGNP
00059      IF  WS-UNALIGNED-BYTE (NAME-IND) = SPACES                    ELCALGNP
00060          ADD 1  TO WS-SPACE-COUNTER                               ELCALGNP
00061      ELSE                                                         ELCALGNP
00062          MOVE HIGH-VALUES TO WS-LENGTH-FOUND-SW                   ELCALGNP
00063          GO TO S1-EXIT.                                           ELCALGNP
00064                                                                   ELCALGNP
00065      SET NAME-IND  DOWN BY 1.                                     ELCALGNP
00066      IF NAME-IND NOT  GREATER THAN ZERO                           ELCALGNP
00067          MOVE  HIGH-VALUES TO WS-LENGTH-FOUND-SW.                 ELCALGNP
00068                                                                   ELCALGNP
00069  S1-EXIT.                                                         ELCALGNP
00070      EXIT.                                                           CL**2
00071                                                                   ELCALGNP
00072  S2-MOVE-LEADING-SPACE.                                           ELCALGNP
00073      MOVE SPACE   TO WS-UNALIGNED-BYTE-A (NAME-IND-A)             ELCALGNP
00074      SET NAME-IND-A  UP  BY 1.                                    ELCALGNP
00075                                                                   ELCALGNP
00076  S2-EXIT.                                                         ELCALGNP
00077      EXIT.                                                           CL**2
00078                                                                   ELCALGNP
00079  S3-MOVE-ALPHA.                                                   ELCALGNP
00080      MOVE  WS-UNALIGNED-BYTE (NAME-IND) TO                        ELCALGNP
00081                          WS-UNALIGNED-BYTE-A (NAME-IND-A).        ELCALGNP
00082      SET NAME-IND   UP BY 1.                                      ELCALGNP
00083      SET NAME-IND-A UP BY 1.                                      ELCALGNP
00084                                                                   ELCALGNP
00085  S3-EXIT.                                                         ELCALGNP
00086      EXIT.                                                           CL**2
00087                                                                   ELCALGNP
