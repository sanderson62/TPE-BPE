00001 ******************************************************************08/12/97
00002 *                                                                *MPCPLCY
00003 *                           MPCPLCY                              *   LV001
00004 *                            VMOD=1.024                          *MPCPLCY
00005 *                                                                *MPCPLCY
00006 *   FILE DESCRIPTION = POLICY MASTER                             *MPCPLCY
00007 *                                                                *MPCPLCY
00008 *   FILE TYPE = VSAM,KSDS                                        *MPCPLCY
00009 *   RECORD SIZE = 1200 RECFORM = FIXED                           *MPCPLCY
00010 *                                                                *MPCPLCY
00011 *   BASE CLUSTER = MPPLCY                         RKP=2,LEN=42   *MPCPLCY
00012 *       ALTERNATE PATH2 = ** NOT USED **                         *MPCPLCY
00013 *       ALTERNATE PATH3 = MPPLCY3 (BY INSD SS NO) RKP=44,LEN=16  *MPCPLCY
00014 *       ALTERNATE PATH4 = MPPLCY4 (BY REF. NO.)   RKP=60,LEN=25  *MPCPLCY
00015 *       ALTERNATE PATH5 = MPPLCY5 (BY ACCOUNT )   RKP=85,LEN=27  *MPCPLCY
00016 *       ALTERNATE PATH6 = MPPLCY6 (BY TRANSIT )   RKP=112,LEN=15 *MPCPLCY
00017 *       ALTERNATE PATH7 = MPPLCY7 (BY LOAN NO.)   RKP=127,LEN=27 *MPCPLCY
00018 *                                                                *MPCPLCY
00019 *   LOG = YES                                                    *MPCPLCY
00020 *   SERVREQ = BROWSE, DELETE, UPDATE, NEWREC                     *MPCPLCY
00021 ******************************************************************MPCPLCY
00022 **WARNING*********************************************************MPCPLCY
00023 **ANY CHANGES TO THIS COPY BOOK MAY NEED CORRESPONDING CHANGES****MPCPLCY
00024 **TO THE FOLLOWING COPY BOOKS: MPCPOLUP                          *MPCPLCY
00025 **                             MPCPHSTD                          *MPCPLCY
00026 **                             MPCPHSTC                          *MPCPLCY
00027 **                             MPCPHSTT                          *MPCPLCY
00028 **                                                               *MPCPLCY
00029 ******************************************************************MPCPLCY
00030                                                                   MPCPLCY
00031  01  POLICY-MASTER.                                               MPCPLCY
00032      12  PM-RECORD-ID                      PIC XX.                MPCPLCY
00033          88  VALID-PM-ID                      VALUE 'PM'.         MPCPLCY
00034                                                                   MPCPLCY
00035 ******************************************************************MPCPLCY
00036 *   BASE CLUSTER = MPPLCY         (BASE KEY)      RKP=2,LEN=42   *MPCPLCY
00037 ******************************************************************MPCPLCY
00038                                                                   MPCPLCY
00039      12  PM-CONTROL-PRIMARY.                                      MPCPLCY
00040          16  PM-PRODUCER-PRIMARY.                                 MPCPLCY
00041              20  PM-PROD-PRIMARY.                                 MPCPLCY
00042                  24  PM-COMPANY-CD         PIC X.                 MPCPLCY
00043                  24  PM-CGSP-KEY.                                 MPCPLCY
00044                      28  PM-CARRIER        PIC X.                 MPCPLCY
00045                      28  PM-GROUPING.                             MPCPLCY
00046                          32  PM-GROUPING-PREFIX                   MPCPLCY
00047                                            PIC X(3).              MPCPLCY
00048                          32  PM-GROUPING-PRIME                    MPCPLCY
00049                                            PIC X(3).              MPCPLCY
00050                      28  PM-STATE          PIC X(2).              MPCPLCY
00051                      28  PM-PRODUCER.                             MPCPLCY
00052                          32  PM-PRODUCER-PREFIX                   MPCPLCY
00053                                            PIC X(4).              MPCPLCY
00054                          32  PM-PRODUCER-PRIME                    MPCPLCY
00055                                            PIC X(6).              MPCPLCY
00056              20  PM-POLICY-EFF-DT              PIC XX.            MPCPLCY
00057          16  PM-REFERENCE-NUMBER.                                 MPCPLCY
00058              20  PM-REFNO-PRIME            PIC X(18).             MPCPLCY
00059              20  PM-REFNO-SFX              PIC XX.                MPCPLCY
00060                                                                   MPCPLCY
00061 ******************************************************************MPCPLCY
00062 *       ALTERNATE PATH3 = MPPLCY3 (BY INSD SS NO) RKP=44,LEN=16  *MPCPLCY
00063 ******************************************************************MPCPLCY
00064                                                                   MPCPLCY
00065      12  PM-CONTROL-BY-SSN.                                       MPCPLCY
00066          16  PM-COMPANY-CD-A3              PIC X.                 MPCPLCY
00067          16  PM-SOC-SEC-NO.                                       MPCPLCY
00068              20  PM-SSN-STATE              PIC XX.                MPCPLCY
00069              20  PM-SSN-PRODUCER           PIC X(6).              MPCPLCY
00070              20  PM-SSN-LN3.                                      MPCPLCY
00071                  25  PM-INSURED-INITIALS-A3.                      MPCPLCY
00072                      30 PM-INSURED-INITIAL1-A3 PIC X.             MPCPLCY
00073                      30 PM-INSURED-INITIAL2-A3 PIC X.             MPCPLCY
00074                  25 PM-PART-LAST-NAME-A3         PIC X.           MPCPLCY
00075          16  PM-DATE-A3                     PIC XX.               MPCPLCY
00076          16  PM-TIME-A3                     PIC S9(04)   COMP.    MPCPLCY
00077                                                                   MPCPLCY
00078 ******************************************************************MPCPLCY
00079 *       ALTERNATE PATH4 = MPPLCY4 (BY REFRENCE)   RKP=60,LEN=25  *MPCPLCY
00080 ******************************************************************MPCPLCY
00081                                                                   MPCPLCY
00082      12  PM-CONTROL-BY-POLICY-NO.                                 MPCPLCY
00083          16  PM-COMPANY-CD-A4              PIC X.                 MPCPLCY
00084          16  PM-POLICY-NO-A4.                                     MPCPLCY
00085              20  PM-POLICY-PRIME-A4        PIC X(18).             MPCPLCY
00086              20  PM-POLICY-SFX-A4          PIC XX.                MPCPLCY
00087          16  PM-DATE-A4                    PIC XX.                MPCPLCY
00088          16  PM-TIME-A4                    PIC S9(04)   COMP.     MPCPLCY
00089                                                                   MPCPLCY
00090 ******************************************************************MPCPLCY
00091 *       ALTERNATE PATH5 = MPPLCY5 (BY ACCOUNT NO) RKP=85,LEN=27  *MPCPLCY
00092 ******************************************************************MPCPLCY
00093                                                                   MPCPLCY
00094      12  PM-CONTROL-BY-ACCOUNT.                                   MPCPLCY
00095          16  PM-COMPANY-CD-A5              PIC X.                 MPCPLCY
00096          16  PM-BANK-ACCOUNT-NUMBER        PIC X(20).             MPCPLCY
00097          16  PM-DATE-A5                    PIC XX.                MPCPLCY
00098          16  PM-TIME-A5                    PIC S9(07)   COMP.     MPCPLCY
00099                                                                   MPCPLCY
00100 ******************************************************************MPCPLCY
00101 *       ALTERNATE PATH6 = MPPLCY6 (BY TRANSIT NO) RKP=112,LEN=15 *MPCPLCY
00102 ******************************************************************MPCPLCY
00103                                                                   MPCPLCY
00104      12  PM-CONTROL-BY-TRANSIT.                                   MPCPLCY
00105          16  PM-COMPANY-CD-A6              PIC X.                 MPCPLCY
00106          16  PM-BANK-TRANSIT-NUMBER.                              MPCPLCY
00107              20  PM-FEDERAL-NUMBER         PIC X(4).              MPCPLCY
00108              20  PM-BANK-NUMBER            PIC X(4).              MPCPLCY
00109          16  PM-DATE-A6                    PIC XX.                MPCPLCY
00110          16  PM-TIME-A6                    PIC S9(07)   COMP.     MPCPLCY
00111                                                                   MPCPLCY
00112 ******************************************************************MPCPLCY
00113 *       ALTERNATE PATH7 = MPPLCY7 (BY LOAN NO)    RKP=127,LEN=27 *MPCPLCY
00114 ******************************************************************MPCPLCY
00115                                                                   MPCPLCY
00116      12  PM-CONTROL-BY-LOAN-NO.                                   MPCPLCY
00117          16  PM-COMPANY-CD-A7              PIC X.                 MPCPLCY
00118          16  PM-LOAN-NUMBER                PIC X(20).             MPCPLCY
00119          16  PM-DATE-A7                    PIC XX.                MPCPLCY
00120          16  PM-TIME-A7                    PIC S9(07)   COMP.     MPCPLCY
00121                                                                   MPCPLCY
00122 ******************************************************************MPCPLCY
00123 *                 FILE SYNCHRONIZATION DATA                      *MPCPLCY
00124 ******************************************************************MPCPLCY
00125                                                                   MPCPLCY
00126      12  FILLER                            PIC X(05).             MPCPLCY
00127      12  PM-FILE-SYNCH-DATA.                                      MPCPLCY
00128          16  PM-LAST-CHANGE-DT             PIC XX.                MPCPLCY
00129          16  PM-LAST-CHANGE-TIME           PIC S9(7)    COMP.     MPCPLCY
00130          16  PM-LAST-CHANGE-PROCESSOR      PIC X(4).              MPCPLCY
00131      12  FILLER                            PIC X(05).             MPCPLCY
00132                                                                   MPCPLCY
00133 ******************************************************************MPCPLCY
00134 *                    INSUREDS PROFILE DATA                       *MPCPLCY
00135 ******************************************************************MPCPLCY
00136                                                                   MPCPLCY
00137      12  PM-INSURED-PROFILE-DATA.                                 MPCPLCY
00138          16  PM-INSURED-NAME.                                     MPCPLCY
00139              20  PM-INSURED-LAST-NAME     PIC X(15).              MPCPLCY
00140              20  PM-INSURED-FIRST-NAME.                           MPCPLCY
00141                  24  PM-INSURED-1ST-INIT PIC X.                   MPCPLCY
00142                  24  FILLER               PIC X(9).               MPCPLCY
00143              20  PM-INSURED-MIDDLE-INIT PIC X.                    MPCPLCY
00144          16  PM-INSURED-ADDRESS.                                  MPCPLCY
00145              20  PM-ADDRESS-LINE-1         PIC X(30).             MPCPLCY
00146              20  PM-ADDRESS-LINE-2         PIC X(30).             MPCPLCY
00147              20  PM-CITY                   PIC X(25).             MPCPLCY
00148              20  PM-RESIDENT-STATE         PIC XX.                MPCPLCY
00149              20  PM-ZIP-CD.                                       MPCPLCY
00150                  24  PM-ZIP-FIRST-FIVE     PIC X(5).              MPCPLCY
00151                  24  PM-ZIP-PLUS-FOUR      PIC X(4).              MPCPLCY
00152          16  PM-INSURED-PERSONAL.                                 MPCPLCY
00153              20  PM-INSURED-OCC-CLASS      PIC X.                 MPCPLCY
00154                  88  PM-PREFERRED            VALUE '1'.           MPCPLCY
00155                  88  PM-STANDARD             VALUE '2'.           MPCPLCY
00156                  88  PM-HAZARDOUS            VALUE '3'.           MPCPLCY
00157                  88  PM-VERY-HAZARDOUS       VALUE '4'.           MPCPLCY
00158                  88  PM-EXTREME-HAZARDOUS VALUE '5'.              MPCPLCY
00159                  88  PM-NOT-OCC              VALUE '6'.           MPCPLCY
00160                  88  PM-OCC-UNKNOWN          VALUE '9'.           MPCPLCY
00161              20  PM-INSURED-OCC-CD         PIC X(3).              MPCPLCY
00162              20  PM-INSURED-OCC-CD-NUM REDEFINES                  MPCPLCY
00163                  PM-INSURED-OCC-CD         PIC 9(3).              MPCPLCY
00164              20  PM-INSURED-SEX            PIC X.                 MPCPLCY
00165                  88  PM-INSURED-SEX-MALE      VALUE 'M'.          MPCPLCY
00166                  88  PM-INSURED-SEX-FEMALE VALUE 'F'.             MPCPLCY
00167              20  PM-INSURED-BIRTH-DT       PIC XX.                MPCPLCY
00168              20  PM-INSURED-ISSUE-AGE      PIC S9(3)     COMP-3.  MPCPLCY
00169              20  PM-INSURED-HEIGHT-FT      PIC S9(3)     COMP-3.  MPCPLCY
00170              20  PM-INSURED-HEIGHT-IN      PIC S9(3)     COMP-3.  MPCPLCY
00171              20  PM-INSURED-WEIGHT         PIC S9(3)     COMP-3.  MPCPLCY
00172              20  PM-INSURED-BIRTH-STATE PIC XX.                   MPCPLCY
00173              20  PM-INSURED-PHONE-NO       PIC X(13).             MPCPLCY
00174              20  PM-INSURED-RATED-AGE      PIC S9(3)     COMP-3.  MPCPLCY
00175          16  PM-INS-LANGUAGE-IND           PIC X(01).             MPCPLCY
00176              88  PM-ENGLISH                           VALUE 'E'.  MPCPLCY
00177              88  PM-FRENCH                            VALUE 'F'.  MPCPLCY
00178              88  PM-SPANISH                           VALUE 'S'.  MPCPLCY
00179          16  PM-INSURED-TOT-BENEFIT        PIC S9(7)V99  COMP-3.  MPCPLCY
00180                                                                   MPCPLCY
00181          16  PM-INSURED-AGE-IND            PIC X(01).             MPCPLCY
00182              88  PM-INSURED-AGE-75-REACHED            VALUE 'Y'.  MPCPLCY
00183      12  FILLER                            PIC X(13).             MPCPLCY
00184                                                                   MPCPLCY
00185 ******************************************************************MPCPLCY
00186 *                JOINT INSUREDS PROFILE DATA                     *MPCPLCY
00187 ******************************************************************MPCPLCY
00188                                                                   MPCPLCY
00189      12  PM-JOINT-PROFILE-DATA.                                   MPCPLCY
00190          16  PM-JOINT-NAME.                                       MPCPLCY
00191              20  PM-JOINT-LAST-NAME        PIC X(15).             MPCPLCY
00192              20  PM-JOINT-FIRST-NAME.                             MPCPLCY
00193                  24  PM-JOINT-1ST-INIT     PIC X.                 MPCPLCY
00194                  24  FILLER                PIC X(9).              MPCPLCY
00195              20  PM-JOINT-MIDDLE-INIT      PIC X.                 MPCPLCY
00196          16  PM-JOINT-SOC-SEC-NO.                                 MPCPLCY
00197              20  PM-JT-SSN-STATE           PIC XX.                MPCPLCY
00198              20  PM-JT-SSN-PRODUCER        PIC X(6).              MPCPLCY
00199              20  PM-JT-SSN-LN3.                                   MPCPLCY
00200                  25  PM-JT-INSURED-INITIALS-A3.                   MPCPLCY
00201                      30 PM-JT-INSURED-INITIAL1-A3 PIC X.          MPCPLCY
00202                      30 PM-JT-INSURED-INITIAL2-A3 PIC X.          MPCPLCY
00203                  25 PM-JT-PART-LAST-NAME-A3        PIC X.         MPCPLCY
00204          16  PM-JOINT-PERSONAL.                                   MPCPLCY
00205              20  PM-JOINT-OCC-CLASS        PIC X.                 MPCPLCY
00206                  88 PM-JNT-PREFERRED          VALUE '1'.          MPCPLCY
00207                  88 PM-JNT-STANDARD           VALUE '2'.          MPCPLCY
00208                  88 PM-JNT-HAZARDOUS          VALUE '3'.          MPCPLCY
00209                  88 PM-JNT-VERY-HAZARDOUS     VALUE '4'.          MPCPLCY
00210                  88 PM-JNT-EXTREME-HAZARDOUS VALUE '5'.           MPCPLCY
00211                  88 PM-JNT-NOT-OCC            VALUE '6'.          MPCPLCY
00212                  88 PM-JNT-OCC-UNKNOWN        VALUE '9'.          MPCPLCY
00213              20  PM-JOINT-OCC-CD           PIC X(3).              MPCPLCY
00214              20  PM-JOINT-SEX              PIC X.                 MPCPLCY
00215                  88  PM-JOINT-SEX-MALE        VALUE 'M'.          MPCPLCY
00216                  88  PM-JOINT-SEX-FEMALE      VALUE 'F'.          MPCPLCY
00217              20  PM-JOINT-BIRTH-DT         PIC XX.                MPCPLCY
00218              20  PM-JOINT-ISSUE-AGE        PIC S9(3)     COMP-3.  MPCPLCY
00219              20  PM-JOINT-HEIGHT-FT        PIC S9(3)     COMP-3.  MPCPLCY
00220              20  PM-JOINT-HEIGHT-IN        PIC S9(3)     COMP-3.  MPCPLCY
00221              20  PM-JOINT-WEIGHT           PIC S9(3)     COMP-3.  MPCPLCY
00222              20  PM-JOINT-BIRTH-STATE      PIC XX.                MPCPLCY
00223              20  PM-JOINT-RATED-AGE        PIC S9(3)     COMP-3.  MPCPLCY
00224          16  PM-JOINT-TOT-BENEFIT          PIC S9(7)V99  COMP-3.  MPCPLCY
00225          16  PM-JOINT-AGE-IND              PIC X(01).             MPCPLCY
00226              88  PM-JOINT-AGE-75-REACHED              VALUE 'Y'.  MPCPLCY
00227                                                                   MPCPLCY
00228      12  FILLER                            PIC X(12).             MPCPLCY
00229                                                                   MPCPLCY
00230 ******************************************************************MPCPLCY
00231 *                  INSURANCE COVERAGE DATA                       *MPCPLCY
00232 ******************************************************************MPCPLCY
00233                                                                   MPCPLCY
00234      12  PM-INS-COVERAGE-DATA.                                    MPCPLCY
00235          16  PM-FREE-PERIOD                PIC S9(03)    COMP-3.  MPCPLCY
00236          16  PM-LOAN-TERM                  PIC S9(3)     COMP-3.  MPCPLCY
00237          16  PM-LOAN-APR                   PIC S9V9999   COMP-3.  MPCPLCY
00238          16  PM-LOAN-DT                    PIC XX.                MPCPLCY
00239          16  PM-LOAN-PYMT                  PIC S9(5)V99  COMP-3.  MPCPLCY
00240          16  PM-LOAN-BALC                  PIC S9(7)V99  COMP-3.  MPCPLCY
00241          16  PM-INS-BENEFIT-MONTHS         PIC S9(3)     COMP-3.  MPCPLCY
00242          16  PM-INS-MONTH-BENEFIT          PIC S9(7)V99  COMP-3.  MPCPLCY
00243          16  PM-INS-TOTAL-BENEFIT          PIC S9(7)V99  COMP-3.  MPCPLCY
00244          16  PM-INS-PLAN-TYPE              PIC X.                 MPCPLCY
00245              88  PM-AH-MORT-PLAN              VALUE 'A'.          MPCPLCY
00246              88  PM-AD-D-MORT-PLAN            VALUE 'E'.          MPCPLCY
00247              88  PM-DISMEM-MORT-PLAN          VALUE 'D'.          MPCPLCY
00248              88  PM-LIFE-MORT-PLAN            VALUE 'L'.          MPCPLCY
00249          16  PM-INS-PLAN-CD                PIC XX.                MPCPLCY
00250          16  PM-INS-PLAN-REVISION          PIC X(3).              MPCPLCY
00251          16  PM-INS-POLICY-FORM            PIC X(12).             MPCPLCY
00252          16  PM-INS-MSTR-POLICY.                                  MPCPLCY
00253              20  PM-FREE-TYPE              PIC X(04).             MPCPLCY
00254              20  FILLER                    PIC X(08).             MPCPLCY
00255          16  PM-INS-MSTR-APP.                                     MPCPLCY
00256              20  FILLER                    PIC X(11).             MPCPLCY
00257              20  PM-INS-B-C-TYPE           PIC X(01).             MPCPLCY
00258          16  PM-INS-RATE-CD                PIC X(5).              MPCPLCY
00259          16  PM-INS-SEX-RATING             PIC X.                 MPCPLCY
00260              88  PM-NOT-SEX-RATED              VALUE '1'.         MPCPLCY
00261              88  PM-SEX-RATED                  VALUE '2'.         MPCPLCY
00262          16  PM-INS-SUBSTANDARD-PCT        PIC S9V9999   COMP-3.  MPCPLCY
00263          16  PM-INS-SUBSTANDARD-TYPE       PIC X.                 MPCPLCY
00264          16  PM-INS-TERMINATION-DT         PIC XX.                MPCPLCY
00265          16  PM-INS-MONTH-PREMIUM      PIC S9(5)V999999  COMP-3.  MPCPLCY
00266          16  PM-INS-CALC-MO-PREM       PIC S9(5)V999999  COMP-3.  MPCPLCY
00267          16  PM-REINSURANCE-TABLE          PIC X(3).              MPCPLCY
00268          16  PM-MORTALITY-CD               PIC X(4).              MPCPLCY
00269          16  PM-INS-TYPE                   PIC X.                 MPCPLCY
00270              88  PM-INDIVIDUAL                VALUES ARE '1' 'I'. MPCPLCY
00271              88  PM-GROUP                     VALUES ARE '2' 'G'. MPCPLCY
00272          16  PM-LOAN-OFFICER               PIC X(5).              MPCPLCY
00273          16  PM-POLICY-FEE                 PIC S9(3)V99 COMP-3.   MPCPLCY
00274          16  PM-DEPENDENT-COUNT            PIC S99      COMP-3.   MPCPLCY
00275          16  PM-CWA-AMOUNT                 PIC S9(5)V99  COMP-3.  MPCPLCY
00276          16  PM-LAST-AUTO-RERATE-DT        PIC XX.                MPCPLCY
00277          16  PM-PREM-FINANCED-SW           PIC X.                 MPCPLCY
00278              88  PM-PREM-FINANCED              VALUE 'Y'.         MPCPLCY
00279              88  PM-PREM-NOT-FINANCED          VALUE 'N'.         MPCPLCY
00280                                                                   MPCPLCY
00281          16  PM-INS-TERM-LETTER-IND        PIC X.                 MPCPLCY
00282              88  PM-TERM-INITIALIZED           VALUE 'Y'.         MPCPLCY
00283          16  PM-INS-UNDERWRITER-MAX-BEN PIC S9(7)V99     COMP-3.  MPCPLCY
00284      12  FILLER                            PIC X(11).             MPCPLCY
00285                                                                   MPCPLCY
00286 ******************************************************************MPCPLCY
00287 *                    POLICY BILLING DATA                         *MPCPLCY
00288 ******************************************************************MPCPLCY
00289                                                                   MPCPLCY
00290      12  PM-BILLING-DATA.                                         MPCPLCY
00291          16  PM-BILLING-MODE               PIC X(1).              MPCPLCY
00292              88  PM-ANNUAL                    VALUE '1'.          MPCPLCY
00293              88  PM-SEMI-ANNUAL               VALUE '2'.          MPCPLCY
00294              88  PM-QUARTERLY                 VALUE '3'.          MPCPLCY
00295              88  PM-MONTHLY                   VALUE '4'.          MPCPLCY
00296              88  PM-BI-MONTHLY                VALUE '5'.          MPCPLCY
00297              88  PM-SINGLE-PREM               VALUE '6'.          MPCPLCY
00298          16  PM-BILLING-SCHEDULE           PIC X(1).              MPCPLCY
00299          16  PM-BILLING-SW                 PIC X(1).              MPCPLCY
00300              88  PM-FIRST-BILLING             VALUE 'Y'.          MPCPLCY
00301              88  PM-PAID-IN-ADVANCE           VALUE 'A'.          MPCPLCY
00302              88  PM-POLICY-FEE-REFUNDED       VALUE 'F'.          MPCPLCY
00303          16  PM-BILLING-TYPE               PIC X(1).              MPCPLCY
00304              88  PM-LIST-BILL                 VALUE '1'.          MPCPLCY
00305              88  PM-TAPE-BILL                 VALUE '2'.          MPCPLCY
00306              88  PM-TAPE-LIST-BILL            VALUE '3'.          MPCPLCY
00307              88  PM-GROUP-BILL          VALUE ARE '1' '2' '3'.    MPCPLCY
00308              88  PM-DIRECT-BILL               VALUE '4'.          MPCPLCY
00309              88  PM-PAC-BILL            VALUE ARE '5' 'C' 'S'.    MPCPLCY
00310              88  PM-CHARGE-CARD-BILL          VALUE '6'.          MPCPLCY
00311              88  PM-INDIV-BILL                                    MPCPLCY
00312                                   VALUE ARE '4' '5' '6' 'C' 'S'.  MPCPLCY
00313              88  PM-GRP-PLCY-BILL             VALUE '7'.          MPCPLCY
00314              88  PM-GRP-PLCY-PAC              VALUE '8'.          MPCPLCY
00315              88  PM-GRP-PLCY-CR-CRD           VALUE '9'.          MPCPLCY
00316              88  PM-GRP-PLCY            VALUE ARE '7' '8' '9'.    MPCPLCY
00317              88  PM-GRP-PROD                  VALUE 'A'.          MPCPLCY
00318              88  PM-EFT-CHECKING              VALUE 'C'.          MPCPLCY
00319              88  PM-EFT-SAVINGS               VALUE 'S'.          MPCPLCY
00320          16  PM-PAYMENT-AMT                PIC S9(5)V99  COMP-3.  MPCPLCY
00321          16  PM-OVER-SHORT-AMT             PIC S9(5)V99  COMP-3.  MPCPLCY
00322          16  PM-LAST-BILL-DT               PIC XX.                MPCPLCY
00323          16  PM-LAST-BILL-AMT              PIC S9(5)V99  COMP-3.  MPCPLCY
00324          16  PM-BILL-TO-DT                 PIC XX.                MPCPLCY
00325          16  PM-LAST-PYMT-DT               PIC XX.                MPCPLCY
00326          16  PM-PAID-TO-DT                 PIC XX.                MPCPLCY
00327          16  PM-PYMT-INVOICE-NUMBER        PIC X(6).              MPCPLCY
00328          16  PM-MONTHS-PAID                PIC S9(3)     COMP-3.  MPCPLCY
00329          16  PM-TOTAL-PREM-RECVD           PIC S9(7)V99  COMP-3.  MPCPLCY
00330          16  PM-BILLING-GROUPING-CODE      PIC X(6).              MPCPLCY
00331          16  PM-CHARGE-CARD-EXP-DT         PIC X(2).              MPCPLCY
00332          16  PM-CHARGE-CARD-TYPE           PIC X(2).              MPCPLCY
00333              88  PM-VISA                      VALUE 'VI'.         MPCPLCY
00334              88  PM-MSTR-CARD                 VALUE 'MC'.         MPCPLCY
00335              88  PM-DINERS-CLUB               VALUE 'DN'.         MPCPLCY
00336              88  PM-DISCOVER                  VALUE 'DS'.         MPCPLCY
00337              88  PM-CARTE-BLANCHE             VALUE 'CB'.         MPCPLCY
00338              88  PM-AMERICAN-EXPRESS          VALUE 'AE'.         MPCPLCY
00339          16  PM-BILL-INVOICE-NUMBER        PIC X(6).              MPCPLCY
00340          16  PM-BILL-DAY                   PIC S99       COMP-3.  MPCPLCY
00341          16  PM-RES-PREM-TAX           PIC S9(3)V999999  COMP-3.  MPCPLCY
00342      12  FILLER                            PIC X(15).             MPCPLCY
00343                                                                   MPCPLCY
00344 ******************************************************************MPCPLCY
00345 *                     CLAIM PAYMENT DATA                         *MPCPLCY
00346 ******************************************************************MPCPLCY
00347                                                                   MPCPLCY
00348      12  PM-CLAIM-PAYMENT-DATA.                                   MPCPLCY
00349          16  PM-CLAIM-BENEFICIARY-NAME     PIC X(25).             MPCPLCY
00350          16  PM-CLAIM-INTERFACE-SW         PIC X.                 MPCPLCY
00351              88  PM-NO-CLAIM-ATTACHED         VALUE SPACE.        MPCPLCY
00352              88  PM-POLICY-AND-CLAIM-ONLINE VALUE '1'.            MPCPLCY
00353              88  PM-POLICY-CREATED-FOR-CLAIM VALUE '2'.           MPCPLCY
00354              88  PM-CLAIM-CLOSED              VALUE '3'.          MPCPLCY
00355              88  PM-ACTIVE-CLAIM              VALUE '1' '2'.      MPCPLCY
00356              88  PM-CLAIM-ATTACHED            VALUE '1' '2' '3'.  MPCPLCY
00357          16  PM-CLAIM-INCURRED-DT          PIC XX.                MPCPLCY
00358          16  PM-CLAIM-PAID-TO-DT           PIC XX.                MPCPLCY
00359          16  PM-CLAIM-PAYMENT-CNT          PIC S9(3)     COMP-3.  MPCPLCY
00360          16  PM-CLAIM-LAST-PAYMENT-AMT     PIC S9(7)V99  COMP-3.  MPCPLCY
00361          16  PM-CLAIM-EXPENSES-ITD         PIC S9(7)V99  COMP-3.  MPCPLCY
00362          16  PM-CLAIM-PAYMENTS-ITD         PIC S9(7)V99  COMP-3.  MPCPLCY
00363          16  PM-CLAIM-ACCUMULATOR          PIC S9(7)V99  COMP-3.  MPCPLCY
00364          16  PM-CLAIM-ATTACH-CNT           PIC S9(3)     COMP-3.  MPCPLCY
00365          16  PM-CLAIM-LIFE-ITD             PIC S9(7)V99  COMP-3.  MPCPLCY
00366          16  PM-CLAIM-AH-ITD               PIC S9(7)V99  COMP-3.  MPCPLCY
00367          16  PM-CLAIM-RIDER-ITD            PIC S9(7)V99  COMP-3.  MPCPLCY
00368                                                                   MPCPLCY
00369      12  FILLER                            PIC X(03).             MPCPLCY
00370                                                                   MPCPLCY
00371 ******************************************************************MPCPLCY
00372 *                POLICY STATUS AND DISPOSITION                   *MPCPLCY
00373 ******************************************************************MPCPLCY
00374                                                                   MPCPLCY
00375      12  PM-STATUS-DISPOSITION-DATA.                              MPCPLCY
00376          16  PM-ISSUE-EOM-DT               PIC XX.                MPCPLCY
00377          16  PM-REPLACEMENT-SWITCH         PIC X.                 MPCPLCY
00378          16  PM-APPL-SIGN-DT               PIC XX.                MPCPLCY
00379          16  PM-UNDERWRITER                PIC X(3).              MPCPLCY
00380          16  PM-ENTRY-PROCESSOR            PIC X(4).              MPCPLCY
00381          16  PM-ENTRY-STATUS               PIC X.                 MPCPLCY
00382              88  PM-NORMAL                    VALUE '1'.          MPCPLCY
00383              88  PM-TAKE-OVER                 VALUE '2'.          MPCPLCY
00384              88  PM-CONVERSION                VALUE '4'.          MPCPLCY
00385              88  PM-RE-ISSUE                  VALUE '5'.          MPCPLCY
00386              88  PM-REINSURANCE-ONLY          VALUE '9'.          MPCPLCY
00387          16  PM-ENTRY-DT                   PIC XX.                MPCPLCY
00388          16  PM-ENTRY-TIME                 PIC S9(7) COMP-3.      MPCPLCY
00389          16  PM-EXIT-DT                    PIC XX.                MPCPLCY
00390          16  PM-CURRENT-STATUS             PIC X.                 MPCPLCY
00391              88  PM-LAPSE                     VALUE '0'.          MPCPLCY
00392              88  PM-ACTIVE                    VALUE '1'.          MPCPLCY
00393              88  PM-PENDING-ISSUE             VALUE '2'.          MPCPLCY
00394              88  PM-DECLINED                  VALUE '3'.          MPCPLCY
00395              88  PM-PENDING-CANCEL            VALUE '4'.          MPCPLCY
00396              88  PM-PENDING-ISSUE-ERROR       VALUE '5'.          MPCPLCY
00397              88  PM-CLAIM-APPLIED             VALUE '6'.          MPCPLCY
00398              88  PM-CANCEL                    VALUE '7'.          MPCPLCY
00399              88  PM-PENDING-UNWTR-REVW        VALUE '8'.          MPCPLCY
00400              88  PM-PENDING-CANCEL-ERROR      VALUE '9'.          MPCPLCY
00401              88  PM-CANCEL-TRANSFER           VALUE 'C'.          MPCPLCY
00402              88  PM-CLAIM-SETTLEMENT          VALUE 'F'.          MPCPLCY
00403              88  PM-TERMINATE                 VALUE 'T'.          MPCPLCY
00404 ** NOTE TYPE 1 IS ANYTHING THAT IS OR HAS BEEN ACTIVE.  TYPE 2 IS MPCPLCY
00405 ** EVERYTHING ELSE.  IF YOU ADD A STATUS ADD THE VALUE TO ONE OF  MPCPLCY
00406 ** THESE GROUPS.                                                  MPCPLCY
00407              88  PM-TYPE-STAT-1                                   MPCPLCY
00408                      VALUES ARE '0' '1' '4' '6' '7' '9'           MPCPLCY
00409                                 'C' 'F' 'T'.                      MPCPLCY
00410              88  PM-TYPE-STAT-2                                   MPCPLCY
00411                      VALUES ARE '2' '3' '5' '8'.                  MPCPLCY
00412              88  PM-BILLABLE-STATUS VALUES ARE '0' '1' '6'.       MPCPLCY
00413              88  PM-PENDING-STATUS                                MPCPLCY
00414                                 VALUES ARE '2' '4' '5' '8' '9'.   MPCPLCY
00415              88  PM-PENDING-ISSUE-STATUS                          MPCPLCY
00416                                 VALUES ARE '2' '5' '8'.           MPCPLCY
00417              88  PM-CANCEL-STATUS                                 MPCPLCY
00418                                 VALUES ARE '4' '7' '9' 'C'.       MPCPLCY
00419          16  PM-CANCEL-CAUSE-CD            PIC X(3).              MPCPLCY
00420          16  PM-CANCEL-DT                  PIC XX.                MPCPLCY
00421          16  PM-REFUND-AMT                 PIC S9(5)V99  COMP-3.  MPCPLCY
00422          16  PM-CALC-REFUND-AMT            PIC S9(5)V99  COMP-3.  MPCPLCY
00423          16  PM-DECLINE-CD                 PIC X(3).              MPCPLCY
00424          16  PM-DECLINE-DT                 PIC XX.                MPCPLCY
00425          16  PM-LAST-LAPSE-DT              PIC XX.                MPCPLCY
00426          16  PM-LAST-REINSTATE-DT          PIC XX.                MPCPLCY
00427          16  PM-SECURITY-ACCESS-CODE       PIC X.                 MPCPLCY
00428          16  PM-PREV-CONTROL-PRIMARY.                             MPCPLCY
00429              20  PM-PREV-COMPANY-CD             PIC X.            MPCPLCY
00430              20  PM-PREV-CARRIER                PIC X.            MPCPLCY
00431              20  PM-PREV-GROUPING.                                MPCPLCY
00432                  24  PM-PREV-GROUPING-PREFIX PIC X(3).            MPCPLCY
00433                  24  PM-PREV-GROUPING-PRIME     PIC X(3).         MPCPLCY
00434              20  PM-PREV-STATE                  PIC XX.           MPCPLCY
00435              20  PM-PREV-PRODUCER.                                MPCPLCY
00436                  24  PM-PREV-PRODUCER-PREFIX PIC X(4).            MPCPLCY
00437                  24  PM-PREV-PRODUCER-PRIME     PIC X(6).         MPCPLCY
00438              20  PM-PREV-POLICY-EFF-DT          PIC XX.           MPCPLCY
00439              20  PM-PREV-REFERENCE-NUMBER.                        MPCPLCY
00440                  24  PM-PREV-REFNO-PRIME        PIC X(18).        MPCPLCY
00441                  24  PM-PREV-REFNO-SFX          PIC XX.           MPCPLCY
00442          16  PM-ACTION-DT                  PIC XX.                MPCPLCY
00443          16  PM-ACTION-CODE                PIC X(3).              MPCPLCY
00444          16  PM-ACTION-DT-2                PIC XX.                MPCPLCY
00445          16  PM-ACTION-CODE-2              PIC X(3).              MPCPLCY
00446          16  PM-ACTION-DT-3                PIC XX.                MPCPLCY
00447          16  PM-ACTION-CODE-3              PIC X(3).              MPCPLCY
00448          16  PM-ACTION-DT-4                PIC XX.                MPCPLCY
00449          16  PM-ACTION-CODE-4              PIC X(3).              MPCPLCY
00450          16  PM-ACTION-DT-5                PIC XX.                MPCPLCY
00451          16  PM-ACTION-CODE-5              PIC X(3).              MPCPLCY
00452                                                                   MPCPLCY
00453          16  PM-KEY-CHANGE                 PIC X.                 MPCPLCY
00454                  88  PM-NO-KEY-CHG      VALUES ARE ' ' 'N'.       MPCPLCY
00455                  88  PM-KEY-CHG              VALUE 'Y'.           MPCPLCY
00456          16  PM-KEY-CHANGE-DT              PIC XX.                MPCPLCY
00457                                                                   MPCPLCY
00458          16  PM-RTI-INDICATOR              PIC X.                 MPCPLCY
00459          16  PM-REASON-CODE                PIC X(3).              MPCPLCY
00460          16  PM-IN-OUT-PROCESSING-IND      PIC X(1).              MPCPLCY
00461              88  PM-IN-OUT-PROCESSING      VALUE 'Y'.             MPCPLCY
00462              88  PM-NOT-IN-OUT-PROCESSING  VALUE SPACES.          MPCPLCY
00463                                                                   MPCPLCY
00464      12  FILLER                            PIC X(12).             MPCPLCY
00465                                                                   MPCPLCY
00466 ******************************************************************MPCPLCY
00467 *                 AGENT AND COMMISSION DATA                      *MPCPLCY
00468 ******************************************************************MPCPLCY
00469                                                                   MPCPLCY
00470      12  PM-COMMISSION-DATA.                                      MPCPLCY
00471          16  PM-REMIT-TO                   PIC S9(3) COMP-3.      MPCPLCY
00472          16  PM-COMM-CHANGE-SW             PIC X.                 MPCPLCY
00473                  88  PM-COMMISSION-CHANGE     VALUE 'Y'.          MPCPLCY
00474          16  PM-AGENT-INFORMATION OCCURS     5 TIMES.             MPCPLCY
00475              20  PM-AGENT-NUMBER           PIC X(10).             MPCPLCY
00476              20  PM-AGENT-TYPE             PIC X.                 MPCPLCY
00477                  88  PM-PRODUCER-LEVEL-AGENT                      MPCPLCY
00478                                               VALUES ARE 'C' 'D'. MPCPLCY
00479                  88  PM-AGENT-GROSS           VALUE 'C'.          MPCPLCY
00480                  88  PM-AGENT-REINS           VALUE 'R'.          MPCPLCY
00481                  88  PM-AGENT-GROSS-REINS     VALUE 'D'.          MPCPLCY
00482                  88  PM-OVERWRITE-GROSS       VALUE 'O'.          MPCPLCY
00483                  88  PM-OVERWRITE-GROSS-REINS VALUE 'P'.          MPCPLCY
00484                  88  PM-OVERWRITE-REINS       VALUE 'T'.          MPCPLCY
00485                  88  PM-REINS-ONLY            VALUE 'W'.          MPCPLCY
00486              20  PM-COMMISSION-BILL-PAID PIC X(1).                MPCPLCY
00487                  88  PM-GENERATE-BILL         VALUE 'B'.          MPCPLCY
00488                  88  PM-GENERATE-PAID         VALUE 'P'.          MPCPLCY
00489              20  PM-AGENT-COMP-1ST-YEAR PIC S99V999.              MPCPLCY
00490              20  PM-COMP-1ST-YEAR-TYPE     PIC X(1).              MPCPLCY
00491                  88  PM-COMP-1ST-YEAR-PERCENT VALUE '1'.          MPCPLCY
00492                  88  PM-COMP-1ST-YEAR-DOLLARS VALUE '2'.          MPCPLCY
00493                  88  PM-COMP-1ST-YEAR-NOT-USED VALUE '3'.         MPCPLCY
00494              20  PM-RENEWAL-DATA.                                 MPCPLCY
00495                  24  PM-AGENT-RENEWAL-DATA OCCURS 6 TIMES.        MPCPLCY
00496                      28  PM-RENEW-MONTHS     PIC S999    COMP-3.  MPCPLCY
00497                      28  PM-RENEW-COMMISSION                      MPCPLCY
00498                                              PIC S99V999 COMP-3.  MPCPLCY
00499                      28  PM-RENEW-TYPE       PIC X(1).            MPCPLCY
00500                          88  PM-COMP-RENEW-PERCENT      VALUE '1'.MPCPLCY
00501                          88  PM-COMP-RENEW-DOLLARS      VALUE '2'.MPCPLCY
00502                          88  PM-COMP-RENEW-NOT-USED     VALUE '3'.MPCPLCY
00503              20  PM-COMP-RECALC-FLAG       PIC X(1).              MPCPLCY
00504                  88  PM-BYPASS-RECALC         VALUE 'N'.          MPCPLCY
00505      12  FILLER                            PIC X(20).             MPCPLCY
00506 ******************************************************************MPCPLCY
00507 *             CUSTOMER DATA                                      *MPCPLCY
00508 ******************************************************************MPCPLCY
00509      12  PM-CUSTOMER-ID                    PIC X(20).             MPCPLCY
00510 ******************************************************************MPCPLCY
00511      12  FILLER                            PIC X(43).             MPCPLCY
00512 ******************************************************************MPCPLCY
