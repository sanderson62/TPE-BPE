00001 ***************************************************************** 04/16/98
00002 *                                                               * ELCRESP1
00002 *                                                               * ELCRESP1
00003 *                            ELCRESP1.                          *    LV003
00004 *           COPYBOOK REVIEWED FOR YEAR 2000 COMPLIANCE               CL**3
00005 *                            VMOD=2.015                         * ELCRESP1
00006 *                                                               * ELCRESP1
00007 *                PROCEDURE DIVISION USED TO CALCULATE THE       * ELCRESP1
00008 *            FUTURE, I.B.N.R., AND PAY TO CURRENT RESERVES.     * ELCRESP1
00009 *                                                               * ELCRESP1
00010 ***************************************************************** ELCRESP1
032612******************************************************************
032612*                   C H A N G E   L O G
032612*
032612* CHANGES ARE MARKED BY THE CHANGE EFFECTIVE DATE.
032612*-----------------------------------------------------------------
032612*  CHANGE   CHANGE REQUEST PGMR  DESCRIPTION OF CHANGE
032612* EFFECTIVE    NUMBER
032612*-----------------------------------------------------------------
032612* 032612  CR2011110200001  PEMA  AHL CHANGES
032612******************************************************************
00011                                                                   ELCRESP1
00012      MOVE ZERO                   TO  CP-CDT-TABLE                 ELCRESP1
00013                                      CP-CDT-FACTOR                ELCRESP1
00014                                      CP-PTC-RESERVE               ELCRESP1
00015                                      CP-IBNR-RESERVE              ELCRESP1
00016                                      CP-FUTURE-RESERVE.           ELCRESP1
00017                                                                   ELCRESP1
00018      IF LCP-ONCTR-01 =  0                                         ELCRESP1
00019          ADD 1 TO LCP-ONCTR-01                                    ELCRESP1
00020          MOVE WS-WORK-AREAS          TO  WS-INITIAL-WORK-AREAS    ELCRESP1
00021        ELSE                                                       ELCRESP1
00022          MOVE WS-INITIAL-WORK-AREAS  TO  WS-WORK-AREAS.           ELCRESP1
00023                                                                   ELCRESP1
00024      IF CP-LIFE-CLAIM                                             ELCRESP1
00025          MOVE CP-REMAINING-AMT   TO CP-PTC-RESERVE                ELCRESP1
00026          GO TO 0100-CALCULATE-AGE.                                ELCRESP1
00027                                                                   ELCRESP1
           IF CP-R-MAX-MON-BEN NOT = ZEROS
              MOVE CP-R-MAX-MON-BEN    TO CP-ORIGINAL-BENEFIT
           END-IF

00028      IF CP-CLAIM-STATUS = 'C'                                     ELCRESP1
00029          GO TO 0100-CALCULATE-AGE.                                ELCRESP1
00030                                                                   ELCRESP1
00031 *    NOTE ******************************************************* ELCRESP1
00032 *         *                                                     * ELCRESP1
00033 *         *      CALCULATE THE NUMBER OF MONTHS DISABLED TO     * ELCRESP1
00034 *         *  DETERMINE WHICH DISABILITY TABLE TO USE.           * ELCRESP1
00035 *         *                                                     * ELCRESP1
00036 *         *******************************************************.ELCRESP1
00037                                                                   ELCRESP1
00038      IF CP-COMPANY-ID = 'CVL'  OR  'CNL'                          ELCRESP1
00039         IF CP-PAID-THRU-DT EQUAL LOW-VALUES                       ELCRESP1
00040            MOVE CP-INCURRED-DT   TO  DC-BIN-DATE-2                ELCRESP1
00041         ELSE                                                      ELCRESP1
00042            MOVE CP-PAID-THRU-DT  TO  DC-BIN-DATE-2                ELCRESP1
00043      ELSE                                                         ELCRESP1
00044         MOVE CP-VALUATION-DT     TO  DC-BIN-DATE-2.               ELCRESP1
00045                                                                   ELCRESP1
00046      MOVE CP-INCURRED-DT         TO  DC-BIN-DATE-1.               ELCRESP1
00047      MOVE '1'                    TO  DC-OPTION-CODE.              ELCRESP1
00048      PERFORM 8500-DATE-CONVERSION.                                ELCRESP1
00049                                                                   ELCRESP1
00050      IF DC-ERROR-CODE NOT = SPACES                                ELCRESP1
00051          MOVE '2'                TO  CP-RETURN-CODE               ELCRESP1
00052          GO TO 0999-EXIT.                                         ELCRESP1
00053                                                                   ELCRESP1
00054      MOVE DC-ELAPSED-MONTHS      TO  WS-MONTHS-DISABLED.          ELCRESP1
00055                                                                   ELCRESP1
00056      IF WS-MONTHS-DISABLED LESS +1                                ELCRESP1
00057          MOVE TAB-01-VALUES      TO  TABLE-WORK-AREA              ELCRESP1
00058          MOVE +1                 TO  WS-DISABILITY-TABLE          ELCRESP1
00059          GO TO 0100-CALCULATE-AGE.                                ELCRESP1
00060                                                                   ELCRESP1
00061      IF WS-MONTHS-DISABLED LESS +2                                ELCRESP1
00062          MOVE TAB-02-VALUES      TO  TABLE-WORK-AREA              ELCRESP1
00063          MOVE +2                 TO  WS-DISABILITY-TABLE          ELCRESP1
00064          GO TO 0100-CALCULATE-AGE.                                ELCRESP1
00065                                                                   ELCRESP1
00066      IF WS-MONTHS-DISABLED LESS +6                                ELCRESP1
00067          MOVE TAB-03-VALUES      TO  TABLE-WORK-AREA              ELCRESP1
00068          MOVE +3                 TO  WS-DISABILITY-TABLE          ELCRESP1
00069          GO TO 0100-CALCULATE-AGE.                                ELCRESP1
00070                                                                   ELCRESP1
00071      IF WS-MONTHS-DISABLED LESS +12                               ELCRESP1
00072          MOVE TAB-04-VALUES      TO  TABLE-WORK-AREA              ELCRESP1
00073          MOVE +4                 TO  WS-DISABILITY-TABLE          ELCRESP1
00074          GO TO 0100-CALCULATE-AGE.                                ELCRESP1
00075                                                                   ELCRESP1
00076      IF WS-MONTHS-DISABLED LESS +24                               ELCRESP1
00077          MOVE TAB-05-VALUES      TO  TABLE-WORK-AREA              ELCRESP1
00078          MOVE +5                 TO  WS-DISABILITY-TABLE          ELCRESP1
00079          GO TO 0100-CALCULATE-AGE.                                ELCRESP1
00080                                                                   ELCRESP1
00081      IF WS-MONTHS-DISABLED LESS +36                               ELCRESP1
00082          MOVE TAB-06-VALUES      TO  TABLE-WORK-AREA              ELCRESP1
00083          MOVE +6                 TO  WS-DISABILITY-TABLE          ELCRESP1
00084          GO TO 0100-CALCULATE-AGE.                                ELCRESP1
00085                                                                   ELCRESP1
00086      IF WS-MONTHS-DISABLED LESS +48                               ELCRESP1
00087          MOVE TAB-07-VALUES      TO  TABLE-WORK-AREA              ELCRESP1
00088          MOVE +7                 TO  WS-DISABILITY-TABLE          ELCRESP1
00089          GO TO 0100-CALCULATE-AGE.                                ELCRESP1
00090                                                                   ELCRESP1
00091      IF WS-MONTHS-DISABLED LESS +60                               ELCRESP1
00092          MOVE TAB-08-VALUES      TO  TABLE-WORK-AREA              ELCRESP1
00093          MOVE +8                 TO  WS-DISABILITY-TABLE          ELCRESP1
00094          GO TO 0100-CALCULATE-AGE.                                ELCRESP1
00095                                                                   ELCRESP1
00096      MOVE TAB-09-VALUES          TO  TABLE-WORK-AREA.             ELCRESP1
00097      MOVE +9                     TO  WS-DISABILITY-TABLE.         ELCRESP1
00098                                                                   ELCRESP1
00099  0100-CALCULATE-AGE.                                              ELCRESP1
00100 *    NOTE ******************************************************* ELCRESP1
00101 *         *                                                     * ELCRESP1
00102 *         *      CALCULATE THE AGE AT THE TIME OF DISABLEMENT.  * ELCRESP1
00103 *         *                                                     * ELCRESP1
00104 *         *******************************************************.ELCRESP1
00105                                                                   ELCRESP1
00106      MOVE +0                     TO CP-INCURRED-AGE.              ELCRESP1
00107                                                                   ELCRESP1
032612     IF (CP-COMPANY-ID = 'MON' or 'AHL')
032612        AND (CP-INSURED-BIRTH-DT NOT = LOW-VALUES)
032612        CONTINUE
032612     ELSE                                                         ELCRESP1
032612        GO TO 0110-CONTINUE-CALC-AGE
032612     END-IF
00113                                                                   ELCRESP1
00114      MOVE CP-INSURED-BIRTH-DT    TO DC-BIN-DATE-1                 ELCRESP1
00115      MOVE CP-INCURRED-DT         TO DC-BIN-DATE-2                 ELCRESP1
00116      MOVE '1'                    TO DC-OPTION-CODE                ELCRESP1
00117      PERFORM 8500-DATE-CONVERSION.                                ELCRESP1
00118                                                                   ELCRESP1
00119      IF DATE-CONVERSION-ERROR                                     ELCRESP1
00120         GO TO 0110-CONTINUE-CALC-AGE.                             ELCRESP1
00121                                                                   ELCRESP1
00122      COMPUTE WS-DISABLED-AGE =                                    ELCRESP1
00123                  (DC-ELAPSED-MONTHS / +12).                       ELCRESP1
00124                                                                   ELCRESP1
00125      MOVE WS-DISABLED-AGE    TO CP-INCURRED-AGE.                  ELCRESP1
00126                                                                   ELCRESP1
00127      GO TO 0120-CONTINUE-CALC-AGE.                                ELCRESP1
00128                                                                   ELCRESP1
00129  0110-CONTINUE-CALC-AGE.                                          ELCRESP1
00130                                                                   ELCRESP1
00131      MOVE CP-CERT-EFF-DT         TO  DC-BIN-DATE-1.               ELCRESP1
00132      MOVE CP-INCURRED-DT         TO  DC-BIN-DATE-2.               ELCRESP1
00133      MOVE '1'                    TO  DC-OPTION-CODE.              ELCRESP1
00134      PERFORM 8500-DATE-CONVERSION.                                ELCRESP1
00135                                                                   ELCRESP1
00136      IF DC-ERROR-CODE NOT = SPACE                                 ELCRESP1
00137          MOVE '2'                TO  CP-RETURN-CODE               ELCRESP1
00138          GO TO 0999-EXIT.                                         ELCRESP1
00139                                                                   ELCRESP1
00140      IF CP-ISSUE-AGE NOT GREATER ZERO                             ELCRESP1
00141          MOVE +40                TO  CP-ISSUE-AGE.                ELCRESP1
00142                                                                   ELCRESP1
00143      COMPUTE WS-DISABLED-AGE =                                    ELCRESP1
00144                  (DC-ELAPSED-MONTHS / +12) + CP-ISSUE-AGE.        ELCRESP1
00145                                                                   ELCRESP1
00146      MOVE WS-DISABLED-AGE    TO CP-INCURRED-AGE.                  ELCRESP1
00147                                                                   ELCRESP1
00148  0120-CONTINUE-CALC-AGE.                                          ELCRESP1
00149                                                                   ELCRESP1
00150      IF CP-LIFE-CLAIM                                             ELCRESP1
00151          GO TO 0999-EXIT.                                         ELCRESP1
00152                                                                   ELCRESP1
00153      IF CP-CLAIM-STATUS = 'C'                                     ELCRESP1
00154          GO TO 0405-CALCULATE-IBNR-RESERVES.                      ELCRESP1
00155                                                                   ELCRESP1
00156      MOVE WS-DISABLED-AGE        TO  AGE-INDEX.                   ELCRESP1
00157                                                                   ELCRESP1
00158      IF AGE-INDEX LESS +22                                        ELCRESP1
00159          MOVE +22                TO  AGE-INDEX.                   ELCRESP1
00160                                                                   ELCRESP1
00161      IF AGE-INDEX GREATER +72                                     ELCRESP1
00162          MOVE +72                TO  AGE-INDEX.                   ELCRESP1
00163                                                                   ELCRESP1
00164      SUBTRACT +17 FROM AGE-INDEX.                                 ELCRESP1
00165      DIVIDE +5 INTO AGE-INDEX.                                    ELCRESP1
00166                                                                   ELCRESP1
00167 *    NOTE ******************************************************* ELCRESP1
00168 *         *                                                     * ELCRESP1
00169 *         *          CALCULATE COVERAGE EXPIRE DATE             * ELCRESP1
00170 *         *                                                     * ELCRESP1
00171 *         *******************************************************.ELCRESP1
00172                                                                   ELCRESP1
00173      MOVE CP-CERT-EFF-DT         TO  DC-BIN-DATE-1.               ELCRESP1
00174      MOVE CP-ORIGINAL-TERM       TO  DC-ELAPSED-MONTHS.           ELCRESP1
00175      MOVE ZERO                   TO  DC-ELAPSED-DAYS.             ELCRESP1
00176      MOVE '6'                    TO  DC-OPTION-CODE.              ELCRESP1
00177      PERFORM 8500-DATE-CONVERSION.                                ELCRESP1
00178                                                                   ELCRESP1
00179      IF DC-ERROR-CODE NOT = SPACES                                ELCRESP1
00180          MOVE '2'                TO  CP-RETURN-CODE               ELCRESP1
00181          GO TO 0999-EXIT.                                         ELCRESP1
00182                                                                   ELCRESP1
00183      MOVE DC-BIN-DATE-2          TO  WS-EXPIRE-DATE.              ELCRESP1
032612     if cp-company-id = 'AHL'
032612        if cp-expire-dt not = low-values and spaces
032612           move cp-expire-dt     to ws-expire-date
032612        end-if
032612     end-if
00185 *    NOTE ******************************************************* ELCRESP1
00186 *         *                                                     * ELCRESP1
00187 *         *      CALCULATE NUMBER OF MONTHS FROM VALUATION      * ELCRESP1
00188 *         *  DATE TO EXPIRY DATE (IE. REMAINING TERM).          * ELCRESP1
00189 *         *                                                     * ELCRESP1
00190 *         *      IF THE EXPIRE DATE HAS PAST BYPASS THE FUTURE  * ELCRESP1
00191 *         *  RESERVE CALCULATIONS AND ONLY CALCULATE THE PTC    * ELCRESP1
00192 *         *  RESERVE.                                           * ELCRESP1
00193 *         *                                                     * ELCRESP1
00194 *         ******************************************************* ELCRESP1
00195                                                                   ELCRESP1
00196      IF CP-COMPANY-ID = 'CVL'  OR  'CNL'                          ELCRESP1
00197         IF CP-PAID-THRU-DT EQUAL LOW-VALUES                       ELCRESP1
00198            MOVE CP-INCURRED-DT   TO  DC-BIN-DATE-1                ELCRESP1
00199         ELSE                                                      ELCRESP1
00200            MOVE CP-PAID-THRU-DT  TO  DC-BIN-DATE-1                ELCRESP1
00201      ELSE                                                         ELCRESP1
00202         MOVE CP-VALUATION-DT     TO  DC-BIN-DATE-1.               ELCRESP1
00203                                                                   ELCRESP1
00204 *    IF CP-COMPANY-ID = 'FIM'                                     ELCRESP1
00205 *        IF CP-PAID-THRU-DT = LOW-VALUES                          ELCRESP1
00206 *            MOVE CP-INCURRED-DT     TO  DC-BIN-DATE-1            ELCRESP1
00207 *        ELSE                                                     ELCRESP1
00208 *            MOVE CP-PAID-THRU-DT    TO  DC-BIN-DATE-1.           ELCRESP1
00209 *                                                                 ELCRESP1
00210      IF DC-BIN-DATE-1 NOT LESS WS-EXPIRE-DATE                     ELCRESP1
00211          GO TO 0405-CALCULATE-IBNR-RESERVES.                      ELCRESP1
00212                                                                   ELCRESP1
00213      MOVE WS-EXPIRE-DATE         TO  DC-BIN-DATE-2.               ELCRESP1
00214      MOVE '1'                    TO  DC-OPTION-CODE.              ELCRESP1
00215      PERFORM 8500-DATE-CONVERSION.                                ELCRESP1
00216                                                                   ELCRESP1
00217      IF DC-ERROR-CODE NOT = SPACES                                ELCRESP1
00218          MOVE '2'                TO  CP-RETURN-CODE               ELCRESP1
00219          GO TO 0999-EXIT.                                         ELCRESP1
00220                                                                   ELCRESP1
00221      MOVE DC-ELAPSED-MONTHS      TO  MONTH-INDEX                  ELCRESP1
00222                                      WS-REMAINING-TERM.           ELCRESP1
00223                                                                   ELCRESP1
00224      IF (CP-CRITICAL-PERIOD)
00225         AND (CP-CRITICAL-MONTHS > +0)
00226         AND (WS-REMAINING-TERM > (CP-CRITICAL-MONTHS -
                   WS-MONTHS-DISABLED))
00228         COMPUTE WS-REMAINING-TERM =
00229         (CP-CRITICAL-MONTHS - WS-MONTHS-DISABLED)                 ELCRESP1
00230         MOVE WS-REMAINING-TERM TO MONTH-INDEX.                    ELCRESP1
00231                                                                   ELCRESP1
00232                                                                   ELCRESP1
00233 *    NOTE ******************************************************* ELCRESP1
00234 *         *                                                     * ELCRESP1
00235 *         *      IF THE INSURED HAS BEEN DISABLED MORE THAN     * ELCRESP1
00236 *         *  TWELVE (12) MONTHS THE CALCULATION IS DONE IN      * ELCRESP1
00237 *         *  YEARS.                                             * ELCRESP1
00238 *         *                                                     * ELCRESP1
00239 *         *******************************************************.ELCRESP1
00240                                                                   ELCRESP1
00241      IF WS-DISABILITY-TABLE GREATER +4                            ELCRESP1
00242          GO TO 0200-CALCULATE-YEARS.                              ELCRESP1
00243                                                                   ELCRESP1
00244      DIVIDE DC-ODD-DAYS-OVER BY DC-DAYS-IN-MONTH                  ELCRESP1
00245                                  GIVING TERM-INDEX-DIFF.          ELCRESP1
00246                                                                   ELCRESP1
00247      MOVE DC-ODD-DAYS-OVER       TO  WS-ODD-DAYS-OVER.            ELCRESP1
00248      MOVE DC-DAYS-IN-MONTH       TO  WS-DAYS-IN-MONTH.            ELCRESP1
00249                                                                   ELCRESP1
00250      COMPUTE WS-EXPIRE-AGE =                                      ELCRESP1
00251                            CP-ISSUE-AGE + (CP-ORIGINAL-TERM / 12) ELCRESP1
00252                                                                   ELCRESP1
00253      IF MONTH-INDEX LESS +24                                      ELCRESP1
00254          GO TO 0300-CALCULATE-RESERVES.                           ELCRESP1
00255                                                                   ELCRESP1
00256      IF MONTH-INDEX NOT GREATER +180                              ELCRESP1
00257          COMPUTE MONTH-INDEX = ((MONTH-INDEX - 24) / 12) + 24     ELCRESP1
00258          MOVE MONTH-INDEX        TO  TERM-INDEX-DIFF              ELCRESP1
00259          GO TO 0305-CALCULATE-RESERVES.                           ELCRESP1
00260                                                                   ELCRESP1
00261      IF WS-EXPIRE-AGE GREATER +65                                 ELCRESP1
00262          MOVE +42                TO  MONTH-INDEX                  ELCRESP1
00263          MOVE ZERO               TO  TERM-INDEX-DIFF              ELCRESP1
00264          GO TO 0300-CALCULATE-RESERVES.                           ELCRESP1
00265                                                                   ELCRESP1
00266      IF WS-EXPIRE-AGE GREATER +60                                 ELCRESP1
00267          MOVE +41                TO  MONTH-INDEX                  ELCRESP1
00268          COMPUTE TERM-INDEX-DIFF = (WS-EXPIRE-AGE - +60) / +5     ELCRESP1
00269          IF AGE-INDEX GREATER +6                                  ELCRESP1
00270              MOVE +6             TO  AGE-INDEX                    ELCRESP1
00271              GO TO 0300-CALCULATE-RESERVES                        ELCRESP1
00272            ELSE                                                   ELCRESP1
00273              GO TO 0300-CALCULATE-RESERVES.                       ELCRESP1
00274                                                                   ELCRESP1
00275      IF WS-EXPIRE-AGE GREATER +55                                 ELCRESP1
00276          MOVE +40                TO  MONTH-INDEX                  ELCRESP1
00277          COMPUTE TERM-INDEX-DIFF = (WS-EXPIRE-AGE - +55) / +5     ELCRESP1
00278          IF AGE-INDEX GREATER +5                                  ELCRESP1
00279              MOVE +5             TO  AGE-INDEX                    ELCRESP1
00280              GO TO 0300-CALCULATE-RESERVES                        ELCRESP1
00281            ELSE                                                   ELCRESP1
00282              GO TO 0300-CALCULATE-RESERVES.                       ELCRESP1
00283                                                                   ELCRESP1
00284      IF WS-EXPIRE-AGE GREATER +50                                 ELCRESP1
00285          MOVE +39                TO  MONTH-INDEX                  ELCRESP1
00286          COMPUTE TERM-INDEX-DIFF = (WS-EXPIRE-AGE - +50) / +5     ELCRESP1
00287          IF AGE-INDEX GREATER +4                                  ELCRESP1
00288              MOVE +4             TO  AGE-INDEX                    ELCRESP1
00289              GO TO 0300-CALCULATE-RESERVES                        ELCRESP1
00290            ELSE                                                   ELCRESP1
00291              GO TO 0300-CALCULATE-RESERVES.                       ELCRESP1
00292                                                                   ELCRESP1
00293      MOVE +38                    TO  MONTH-INDEX.                 ELCRESP1
00294      MOVE ZERO                   TO  TERM-INDEX-DIFF.             ELCRESP1
00295                                                                   ELCRESP1
00296      IF AGE-INDEX GREATER +3                                      ELCRESP1
00297          MOVE +3                 TO  AGE-INDEX.                   ELCRESP1
00298                                                                   ELCRESP1
00299      GO TO 0300-CALCULATE-RESERVES.                               ELCRESP1
00300                                                                   ELCRESP1
00301      EJECT                                                        ELCRESP1
00302  0200-CALCULATE-YEARS.                                            ELCRESP1
00303      DIVIDE +12 INTO MONTH-INDEX.                                 ELCRESP1
00304                                                                   ELCRESP1
00305      MOVE MONTH-INDEX            TO  TERM-INDEX-DIFF.             ELCRESP1
00306                                                                   ELCRESP1
00307      IF YEAR-INDEX LESS +1  AND                                   ELCRESP1
00308         NOT CP-CDT-INTERPOLATED                                   ELCRESP1
00309          MOVE +1                 TO  YEAR-INDEX.                  ELCRESP1
00310                                                                   ELCRESP1
00311      IF (WS-DISABILITY-TABLE = +5 AND                             ELCRESP1
00312          YEAR-INDEX LESS +15)                                     ELCRESP1
00313        OR                                                         ELCRESP1
00314         (WS-DISABILITY-TABLE = +6 AND                             ELCRESP1
00315          YEAR-INDEX LESS +14)                                     ELCRESP1
00316        OR                                                         ELCRESP1
00317         (WS-DISABILITY-TABLE = +7 AND                             ELCRESP1
00318          YEAR-INDEX LESS +13)                                     ELCRESP1
00319        OR                                                         ELCRESP1
00320         (WS-DISABILITY-TABLE = +8 AND                             ELCRESP1
00321          YEAR-INDEX LESS +12)                                     ELCRESP1
00322        OR                                                         ELCRESP1
00323         (WS-DISABILITY-TABLE = +9 AND                             ELCRESP1
00324          YEAR-INDEX LESS +13)                                     ELCRESP1
00325              GO TO 0305-CALCULATE-RESERVES.                       ELCRESP1
00326                                                                   ELCRESP1
00327      COMPUTE WS-EXPIRE-AGE =                                      ELCRESP1
00328                            CP-ISSUE-AGE + (CP-ORIGINAL-TERM / 12).ELCRESP1
00329                                                                   ELCRESP1
00330      COMPUTE YEAR-INDEX = (WS-DISABILITY-TABLE * -1) + 5.         ELCRESP1
00331                                                                   ELCRESP1
00332      IF WS-EXPIRE-AGE GREATER +65                                 ELCRESP1
00333          ADD +19                 TO  YEAR-INDEX                   ELCRESP1
00334          MOVE ZERO               TO  TERM-INDEX-DIFF              ELCRESP1
00335          GO TO 0300-CALCULATE-RESERVES.                           ELCRESP1
00336                                                                   ELCRESP1
00337      IF WS-EXPIRE-AGE GREATER +60                                 ELCRESP1
00338          ADD +18  TO  YEAR-INDEX                                  ELCRESP1
00339          COMPUTE TERM-INDEX-DIFF = (WS-EXPIRE-AGE - +60) / +5     ELCRESP1
00340          IF AGE-INDEX GREATER +6                                  ELCRESP1
00341              MOVE +6             TO  AGE-INDEX                    ELCRESP1
00342              GO TO 0300-CALCULATE-RESERVES                        ELCRESP1
00343            ELSE                                                   ELCRESP1
00344              GO TO 0300-CALCULATE-RESERVES.                       ELCRESP1
00345                                                                   ELCRESP1
00346      IF WS-EXPIRE-AGE GREATER +55                                 ELCRESP1
00347          ADD +17  TO  YEAR-INDEX                                  ELCRESP1
00348          COMPUTE TERM-INDEX-DIFF = (WS-EXPIRE-AGE - +55) / +5     ELCRESP1
00349          IF AGE-INDEX GREATER +5                                  ELCRESP1
00350              MOVE +5             TO  AGE-INDEX                    ELCRESP1
00351              GO TO 0300-CALCULATE-RESERVES                        ELCRESP1
00352            ELSE                                                   ELCRESP1
00353              GO TO 0300-CALCULATE-RESERVES.                       ELCRESP1
00354                                                                   ELCRESP1
00355      IF WS-EXPIRE-AGE GREATER +50                                 ELCRESP1
00356          ADD +16  TO  YEAR-INDEX                                  ELCRESP1
00357          COMPUTE TERM-INDEX-DIFF = (WS-EXPIRE-AGE - +50) / +5     ELCRESP1
00358          IF AGE-INDEX GREATER +4                                  ELCRESP1
00359              MOVE +4             TO  AGE-INDEX                    ELCRESP1
00360              GO TO 0300-CALCULATE-RESERVES                        ELCRESP1
00361            ELSE                                                   ELCRESP1
00362              GO TO 0300-CALCULATE-RESERVES.                       ELCRESP1
00363                                                                   ELCRESP1
00364      ADD +15  TO  YEAR-INDEX.                                     ELCRESP1
00365      MOVE ZERO                   TO  TERM-INDEX-DIFF.             ELCRESP1
00366                                                                   ELCRESP1
00367      IF AGE-INDEX GREATER +3                                      ELCRESP1
00368          MOVE +3             TO  AGE-INDEX.                       ELCRESP1
00369                                                                   ELCRESP1
00370      GO TO 0300-CALCULATE-RESERVES.                               ELCRESP1
00371                                                                   ELCRESP1
00372      EJECT                                                        ELCRESP1
00373  0300-CALCULATE-RESERVES.                                         ELCRESP1
00374                                                                   ELCRESP1
00375      ADD TERM-INDEX-DIFF  TO  TERM-INDEX.                         ELCRESP1
00376                                                                   ELCRESP1
00377  0305-CALCULATE-RESERVES.                                         ELCRESP1
00378                                                                   ELCRESP1
00379      IF CP-CDT-METHOD EQUAL '4'                                   ELCRESP1
00380          ADD +0.5  AGE-INDEX  GIVING  AGE-INDEX2                  ELCRESP1
00381          ADD +0.9167   TERM-INDEX GIVING  TERM-INDEX2             ELCRESP1
00382      ELSE                                                         ELCRESP1
00383      IF CP-CDT-INTERPOLATED                                       ELCRESP1
00384          GO TO 0310-CALCULATE-RESERVES                            ELCRESP1
00385      ELSE                                                         ELCRESP1
00386      IF CP-CDT-ROUND-NEAR                                         ELCRESP1
00387          ADD +0.5  AGE-INDEX  GIVING  AGE-INDEX2                  ELCRESP1
00388          ADD +0.5  TERM-INDEX GIVING  TERM-INDEX2                 ELCRESP1
00389      ELSE                                                         ELCRESP1
00390      IF CP-CDT-ROUND-HIGH                                         ELCRESP1
00391          ADD +0.9167   AGE-INDEX  GIVING  AGE-INDEX2              ELCRESP1
00392          ADD +0.9167   TERM-INDEX GIVING  TERM-INDEX2.            ELCRESP1
00393                                                                   ELCRESP1
00394      IF CP-COMPANY-ID = 'UFL' OR 'UFR' OR 'WFL' OR 'WSL' OR 'CSL' ELCRESP1
00395          ADD +0.5  AGE-INDEX  GIVING  AGE-INDEX2                  ELCRESP1
00396          ADD +0.9167   TERM-INDEX GIVING  TERM-INDEX2.            ELCRESP1
00397                                                                   ELCRESP1
00398      SET TWA-INDEX1  TO  TERM-INDEX2.                             ELCRESP1
00399      SET TWA-INDEX2  TO  AGE-INDEX2.                              ELCRESP1
00400                                                                   ELCRESP1
00401      IF TWA-INDEX1 LESS +1                                        ELCRESP1
00402          MOVE ZERO               TO  RESERVE-FACTOR               ELCRESP1
00403        ELSE                                                       ELCRESP1
00404          MOVE TWA-FACTOR (TWA-INDEX1, TWA-INDEX2)                 ELCRESP1
00405                                  TO  RESERVE-FACTOR.              ELCRESP1
00406                                                                   ELCRESP1
00407      GO TO 0400-CALCULATE-FUTURE-RESERVES.                        ELCRESP1
00408                                                                   ELCRESP1
00409      EJECT                                                        ELCRESP1
00410  0310-CALCULATE-RESERVES.                                         ELCRESP1
00411      MOVE AGE-INDEX              TO  AGE-INDEX-LOW                ELCRESP1
00412                                      AGE-INDEX-HIGH               ELCRESP1
00413                                      AGE-INDEX-DIFF.              ELCRESP1
00414                                                                   ELCRESP1
00415      IF WS-DISABLED-AGE LESS +22                                  ELCRESP1
00416          MOVE ZERO               TO  AGE-INDEX-LOW  AGE-INDEX-HIGHELCRESP1
00417          DIVIDE WS-DISABLED-AGE BY +22                            ELCRESP1
00418                                  GIVING AGE-INDEX-DIFF ROUNDED.   ELCRESP1
00419                                                                   ELCRESP1
00420      MOVE TERM-INDEX             TO  TERM-INDEX-LOW               ELCRESP1
00421                                      TERM-INDEX-HIGH.             ELCRESP1
00422                                                                   ELCRESP1
00423      IF AGE-INDEX-DIFF GREATER ZERO                               ELCRESP1
00424          ADD +1 TO AGE-INDEX-HIGH.                                ELCRESP1
00425                                                                   ELCRESP1
00426      IF TERM-INDEX-DIFF GREATER ZERO                              ELCRESP1
00427          ADD +1 TO TERM-INDEX-HIGH.                               ELCRESP1
00428                                                                   ELCRESP1
00429      IF TERM-INDEX-LOW LESS +1                                    ELCRESP1
00430          MOVE ZERO               TO  FACTOR-A                     ELCRESP1
00431        ELSE                                                       ELCRESP1
00432      IF AGE-INDEX-LOW LESS +1                                     ELCRESP1
00433          COMPUTE FACTOR-A =                                       ELCRESP1
00434              TWA-FACTOR (TERM-INDEX-LOW, AGE-INDEX-HIGH) *        ELCRESP1
00435                                  AGE-INDEX-DIFF                   ELCRESP1
00436        ELSE                                                       ELCRESP1
00437          COMPUTE FACTOR-A =                                       ELCRESP1
00438              ((TWA-FACTOR (TERM-INDEX-LOW, AGE-INDEX-HIGH) -      ELCRESP1
00439                TWA-FACTOR (TERM-INDEX-LOW, AGE-INDEX-LOW)) *      ELCRESP1
00440                                                 AGE-INDEX-DIFF) + ELCRESP1
00441                TWA-FACTOR (TERM-INDEX-LOW, AGE-INDEX-LOW).        ELCRESP1
00442                                                                   ELCRESP1
00443      IF TERM-INDEX-HIGH LESS +1                                   ELCRESP1
00444          MOVE ZERO               TO  FACTOR-B                     ELCRESP1
00445        ELSE                                                       ELCRESP1
00446      IF AGE-INDEX-LOW LESS +1                                     ELCRESP1
00447          COMPUTE FACTOR-B =                                       ELCRESP1
00448              TWA-FACTOR (TERM-INDEX-HIGH, AGE-INDEX-HIGH) *       ELCRESP1
00449                                  AGE-INDEX-DIFF                   ELCRESP1
00450        ELSE                                                       ELCRESP1
00451          COMPUTE FACTOR-B =                                       ELCRESP1
00452              ((TWA-FACTOR (TERM-INDEX-HIGH, AGE-INDEX-HIGH) -     ELCRESP1
00453                TWA-FACTOR (TERM-INDEX-HIGH, AGE-INDEX-LOW)) *     ELCRESP1
00454                                                 AGE-INDEX-DIFF) + ELCRESP1
00455                TWA-FACTOR (TERM-INDEX-HIGH, AGE-INDEX-LOW).       ELCRESP1
00456                                                                   ELCRESP1
00457      COMPUTE RESERVE-FACTOR = ((FACTOR-B - FACTOR-A) *            ELCRESP1
00458                                TERM-INDEX-DIFF) + FACTOR-A.       ELCRESP1
00459                                                                   ELCRESP1
00460      EJECT                                                        ELCRESP1
00461  0400-CALCULATE-FUTURE-RESERVES.                                  ELCRESP1
00462                                                                   ELCRESP1
00463 *    NOTE ******************************************************* ELCRESP1
00464 *         *                                                     * ELCRESP1
00465 *         *          CALCULATE THE FUTURE RESERVE.              * ELCRESP1
00466 *         *******************************************************.ELCRESP1
00467                                                                   ELCRESP1
00468      COMPUTE CP-FUTURE-RESERVE =                                  ELCRESP1
00469                      (CP-ORIGINAL-BENEFIT / 100) * RESERVE-FACTOR.ELCRESP1
00470                                                                   ELCRESP1
00471      IF CP-CDT-PERCENT NOT = ZERO                                 ELCRESP1
00472          COMPUTE CP-FUTURE-RESERVE =                              ELCRESP1
00473                  CP-FUTURE-RESERVE * (CP-CDT-PERCENT / +100).     ELCRESP1
00474                                                                   ELCRESP1
00475      MOVE WS-DISABILITY-TABLE    TO  CP-CDT-TABLE.                ELCRESP1
00476      MOVE RESERVE-FACTOR         TO  CP-CDT-FACTOR.               ELCRESP1
00477                                                                   ELCRESP1
00478      IF CP-COMPANY-ID = 'POS'  AND                                ELCRESP1
00479         CP-CARRIER = ('3'  OR  '5')                               ELCRESP1
00480          NEXT SENTENCE                                            ELCRESP1
00481      ELSE                                                         ELCRESP1
00482          GO TO 0405-CALCULATE-IBNR-RESERVES.                      ELCRESP1
00483                                                                   ELCRESP1
00484      MOVE WS-MONTHS-DISABLED     TO FACTOR-INDEX.                 ELCRESP1
00485      MOVE ZEROS                  TO RESERVE-FACTOR.               ELCRESP1
00486      MOVE +0.99585               TO FACTOR-V.                     ELCRESP1
00487      PERFORM 0403-CALC-POS-RESERVE-FACTOR THRU 0403-POS-EXIT      ELCRESP1
00488                        WS-REMAINING-TERM TIMES.                   ELCRESP1
00489                                                                   ELCRESP1
00490      IF WS-MONTHS-DISABLED = ZEROS                                ELCRESP1
00491          MOVE +1.000             TO FACTOR-A                      ELCRESP1
00492      ELSE                                                         ELCRESP1
00493          IF WS-MONTHS-DISABLED GREATER THAN +40                   ELCRESP1
00494              MOVE +0.011         TO FACTOR-A                      ELCRESP1
00495          ELSE                                                     ELCRESP1
00496              MOVE POS-FACTOR (WS-MONTHS-DISABLED)  TO FACTOR-A.   ELCRESP1
00497                                                                   ELCRESP1
00498      COMPUTE CP-FUTURE-RESERVE = ((CP-ORIGINAL-BENEFIT * 2.50) +  ELCRESP1
00499             ((RESERVE-FACTOR / FACTOR-A) * CP-ORIGINAL-BENEFIT))  ELCRESP1
00500                                                        * 1.10.    ELCRESP1
00501                                                                   ELCRESP1
00502      MOVE ZEROS                  TO CP-CDT-TABLE.                 ELCRESP1
00503      COMPUTE CP-CDT-FACTOR = RESERVE-FACTOR / FACTOR-A.           ELCRESP1
00504                                                                   ELCRESP1
00505      GO TO 0405-CALCULATE-IBNR-RESERVES.                          ELCRESP1
00506                                                                   ELCRESP1
00507  0403-CALC-POS-RESERVE-FACTOR.                                    ELCRESP1
00508                                                                   ELCRESP1
00509      ADD +1 TO FACTOR-INDEX.                                      ELCRESP1
00510                                                                   ELCRESP1
00511      IF FACTOR-INDEX LESS THAN +41                                ELCRESP1
00512          COMPUTE RESERVE-FACTOR ROUNDED = RESERVE-FACTOR +        ELCRESP1
00513                         (POS-FACTOR (FACTOR-INDEX) * FACTOR-V)    ELCRESP1
00514      ELSE                                                         ELCRESP1
00515          COMPUTE RESERVE-FACTOR ROUNDED = RESERVE-FACTOR +        ELCRESP1
00516                                           (.011 * FACTOR-V).      ELCRESP1
00517                                                                   ELCRESP1
00518      COMPUTE FACTOR-V  =  FACTOR-V * .99585.                      ELCRESP1
00519                                                                   ELCRESP1
00520 * NOTE ********************************************************** ELCRESP1
00521 *      *   FACTOR-V  =  .99585  =  ( 1 / ( 1 + I ))             * ELCRESP1
00522 *      *                                 WHERE I = .05 / 12     * ELCRESP1
00523 *      **********************************************************.ELCRESP1
00524                                                                   ELCRESP1
00525  0403-POS-EXIT.                                                   ELCRESP1
00526      EXIT.                                                        ELCRESP1
00527                                                                   ELCRESP1
00528      EJECT                                                        ELCRESP1
00529  0405-CALCULATE-IBNR-RESERVES.                                    ELCRESP1
00530                                                                   ELCRESP1
00531 *    NOTE ******************************************************* ELCRESP1
00532 *         *                                                     * ELCRESP1
00533 *         *          CALCULATE THE I.B.N.R. RESERVE.            * ELCRESP1
00534 *         *******************************************************.ELCRESP1
00535                                                                   ELCRESP1
00536      MOVE CP-VALUATION-DT        TO  DC-BIN-DATE-1.               ELCRESP1
00537                                                                   ELCRESP1
00538      IF CP-IBNR-RESERVE-SW = '2'                                  ELCRESP1
00539          MOVE -6                 TO  DC-ELAPSED-MONTHS            ELCRESP1
00540      ELSE                                                         ELCRESP1
00541          MOVE -3                 TO  DC-ELAPSED-MONTHS.           ELCRESP1
00542                                                                   ELCRESP1
00543      MOVE ZERO                   TO  DC-ELAPSED-DAYS.             ELCRESP1
00544      MOVE '6'                    TO  DC-OPTION-CODE.              ELCRESP1
00545      PERFORM 8500-DATE-CONVERSION.                                ELCRESP1
00546                                                                   ELCRESP1
00547      IF DC-ERROR-CODE NOT = SPACES                                ELCRESP1
00548          MOVE '2'                TO  CP-RETURN-CODE               ELCRESP1
00549          GO TO 0999-EXIT.                                         ELCRESP1
00550                                                                   ELCRESP1
00551      MOVE DC-BIN-DATE-2          TO  WS-VALUATION-DATE-3.         ELCRESP1
00552                                                                   ELCRESP1
00553      IF CP-INCURRED-DT LESS WS-VALUATION-DATE-3  AND              ELCRESP1
00554         CP-REPORTED-DT GREATER WS-VALUATION-DATE-3                ELCRESP1
00555          MOVE CP-FUTURE-RESERVE  TO  CP-IBNR-RESERVE              ELCRESP1
00556      ELSE                                                         ELCRESP1
00557          MOVE ZERO               TO  CP-IBNR-RESERVE.             ELCRESP1
00558                                                                   ELCRESP1
00559      IF CP-CLAIM-STATUS = 'C'                                     ELCRESP1
00560          GO TO 0500-PTC-RESERVE.                                  ELCRESP1
00561                                                                   ELCRESP1
00562      IF CP-COMPANY-ID = 'POS'                                     ELCRESP1
00563        IF CP-CARRIER = '3'  OR  '5'                               ELCRESP1
00564            COMPUTE CP-IBNR-RESERVE = CP-ORIGINAL-BENEFIT * 1.10.  ELCRESP1
00565                                                                   ELCRESP1
00566      EJECT                                                        ELCRESP1
00567 *    NOTE ******************************************************* ELCRESP1
00568 *         *                                                     * ELCRESP1
00569 *         *        CALCULATE THE PAY-TO-CURRENT RESERVE.        * ELCRESP1
00570 *         ******************************************************* ELCRESP1
00571                                                                   ELCRESP1
00572  0415-CALCULATE-PTC-RESERVE.                                      ELCRESP1
00573                                                                   ELCRESP1
00574 *    IF CP-COMPANY-ID NOT = 'FIM'                                 ELCRESP1
00575 *        GO TO 0420-PTC-RESERVE.                                  ELCRESP1
00576                                                                   ELCRESP1
00577      GO TO 0420-PTC-RESERVE.                                      ELCRESP1
00578                                                                   ELCRESP1
00579      MOVE CP-CERT-EFF-DT         TO  DC-BIN-DATE-1.               ELCRESP1
00580      MOVE CP-VALUATION-DT        TO  DC-BIN-DATE-2.               ELCRESP1
00581      MOVE '1'                    TO  DC-OPTION-CODE.              ELCRESP1
00582      PERFORM 8500-DATE-CONVERSION.                                ELCRESP1
00583                                                                   ELCRESP1
00584      IF DC-ERROR-CODE NOT = SPACE                                 ELCRESP1
00585          MOVE '2'                TO  CP-RETURN-CODE               ELCRESP1
00586          GO TO 0999-EXIT.                                         ELCRESP1
00587                                                                   ELCRESP1
00588      MOVE DC-GREG-DATE-A-EDIT    TO  WS-DATE-WORK.                   CL**2
00589      MOVE WS-DW-DAY              TO  WS-WK-DAY.                   ELCRESP1
00590                                                                   ELCRESP1
00591      MOVE DC-GREG-DATE-B-EDIT    TO  WS-DATE-WORK.                   CL**2
00592      MOVE WS-DW-CCYR             TO  WS-WK-CCYR.                     CL**2
00593      MOVE WS-DW-MONTH            TO  WS-WK-MONTH.                 ELCRESP1
00594                                                                   ELCRESP1
00595      MOVE WS-WORK-CYMD           TO  DC-GREG-DATE-CYMD.              CL**2
00596      MOVE 'L'                    TO  DC-OPTION-CODE.                 CL**2
00597      PERFORM 8500-DATE-CONVERSION.                                ELCRESP1
00598                                                                   ELCRESP1
00599      IF DC-ERROR-CODE NOT = SPACES                                ELCRESP1
00600          MOVE '2'                TO  CP-RETURN-CODE               ELCRESP1
00601          GO TO 0999-EXIT.                                         ELCRESP1
00602                                                                   ELCRESP1
00603      MOVE DC-BIN-DATE-1          TO WS-VALUATION-DATE-2.          ELCRESP1
00604                                                                   ELCRESP1
00605  0420-PTC-RESERVE.                                                ELCRESP1
00606                                                                   ELCRESP1
00607      IF CP-COMPANY-ID = 'POS'                                     ELCRESP1
00608        IF CP-CARRIER = ('3'  OR  '5')                             ELCRESP1
00609            COMPUTE CP-PTC-RESERVE = CP-ORIGINAL-BENEFIT * 1.65    ELCRESP1
00610            GO TO 0500-PTC-RESERVE.                                ELCRESP1
00611                                                                   ELCRESP1
00612      IF CP-PAID-THRU-DT = LOW-VALUES                              ELCRESP1
00613          MOVE CP-INCURRED-DT     TO  DC-BIN-DATE-1                ELCRESP1
00614      ELSE                                                         ELCRESP1
00615          MOVE CP-PAID-THRU-DT    TO  DC-BIN-DATE-1.               ELCRESP1
00616                                                                   ELCRESP1
00617      MOVE CP-VALUATION-DT        TO  DC-BIN-DATE-2.               ELCRESP1
00618                                                                   ELCRESP1
00619 *    IF CP-COMPANY-ID = 'FIM'                                     ELCRESP1
00620 *        IF WS-VALUATION-DATE-2  LESS CP-VALUATION-DT             ELCRESP1
00621 *            MOVE WS-VALUATION-DATE-2 TO  DC-BIN-DATE-2.          ELCRESP1
00622 *                                                                 ELCRESP1
032612     if cp-company-id = 'AHL'
032612        if dc-bin-date-1 > dc-bin-date-2
032612           move dc-bin-date-1    to dc-bin-date-2
032612           move cp-valuation-dt  to dc-bin-date-1
032612           move '1'              to dc-option-code
032612           perform 8500-date-conversion
032612           if dc-error-code = spaces
032612              compute cp-ptc-reserve =
032612                 cp-original-benefit * (dc-elapsed-months +
032612                 (dc-odd-days-over / 30))
032612              compute cp-ptc-reserve = cp-ptc-reserve * -1
032612              move zero          to cp-return-code
032612              go to 0500-ptc-reserve
032612           end-if
032612        end-if
032612     end-if
00623      IF DC-BIN-DATE-2 GREATER WS-EXPIRE-DATE                      ELCRESP1
00624         MOVE WS-EXPIRE-DATE      TO  DC-BIN-DATE-2.               ELCRESP1
00625                                                                   ELCRESP1
00626      IF DC-BIN-DATE-1 NOT LESS DC-BIN-DATE-2                      ELCRESP1
00627          MOVE ZERO               TO  CP-RETURN-CODE               ELCRESP1
00628          GO TO 0500-PTC-RESERVE.                                  ELCRESP1
00629                                                                   ELCRESP1
00630      MOVE '1'                    TO  DC-OPTION-CODE.              ELCRESP1
00631      PERFORM 8500-DATE-CONVERSION.                                ELCRESP1
00632                                                                   ELCRESP1
00633      IF DC-ERROR-CODE NOT = SPACES                                ELCRESP1
00634          MOVE '2'                TO  CP-RETURN-CODE               ELCRESP1
00635          GO TO 0999-EXIT.                                         ELCRESP1
00636                                                                   ELCRESP1
           IF CP-CRITICAL-PERIOD
              IF DC-ELAPSED-MONTHS > CP-CRITICAL-MONTHS
                 MOVE CP-CRITICAL-MONTHS TO DC-ELAPSED-MONTHS
                 MOVE ZEROS            TO DC-ODD-DAYS-OVER
              ELSE
                 IF CP-CRITICAL-MONTHS = DC-ELAPSED-MONTHS
                    MOVE ZEROS         TO DC-ODD-DAYS-OVER
                 END-IF
              END-IF
           END-IF

00637      COMPUTE CP-PTC-RESERVE =                                     ELCRESP1
00638          CP-ORIGINAL-BENEFIT * (DC-ELAPSED-MONTHS +               ELCRESP1
00639                                    (DC-ODD-DAYS-OVER / 30)).      ELCRESP1
00640                                                                   ELCRESP1
00641      MOVE ZERO                   TO  CP-RETURN-CODE.              ELCRESP1
00642                                                                   ELCRESP1
00643  0500-PTC-RESERVE.                                                ELCRESP1
00644      IF CP-IBNR-RESERVE-SW NOT = '2'                              ELCRESP1
00645          GO TO 0999-EXIT.                                         ELCRESP1
00646                                                                   ELCRESP1
00647      IF CP-INCURRED-DT LESS WS-VALUATION-DATE-3  AND              ELCRESP1
00648         CP-REPORTED-DT GREATER WS-VALUATION-DATE-3                ELCRESP1
00649          NEXT SENTENCE                                            ELCRESP1
00650      ELSE                                                         ELCRESP1
00651          GO TO 0999-EXIT.                                         ELCRESP1
00652                                                                   ELCRESP1
00653      IF CP-CLAIM-STATUS = 'C'                                     ELCRESP1
00654          MOVE CP-TOTAL-PAID      TO  CP-IBNR-RESERVE              ELCRESP1
00655          GO TO 0999-EXIT.                                         ELCRESP1
00656                                                                   ELCRESP1
00657      MOVE CP-INCURRED-DT         TO  DC-BIN-DATE-1.               ELCRESP1
00658 *    IF CP-COMPANY-ID = 'FIM'                                     ELCRESP1
00659 *        MOVE WS-VALUATION-DATE-2 TO  DC-BIN-DATE-2               ELCRESP1
00660 *    ELSE                                                         ELCRESP1
00661      MOVE CP-VALUATION-DT     TO  DC-BIN-DATE-2.                  ELCRESP1
00662                                                                   ELCRESP1
00663      MOVE '1'                     TO  DC-OPTION-CODE.             ELCRESP1
00664                                                                   ELCRESP1
00665      PERFORM 8500-DATE-CONVERSION.                                ELCRESP1
00666                                                                   ELCRESP1
00667      IF DC-ERROR-CODE NOT = SPACES                                ELCRESP1
00668          MOVE '2'                TO  CP-RETURN-CODE               ELCRESP1
00669          GO TO 0999-EXIT.                                         ELCRESP1
00670                                                                   ELCRESP1
00671      COMPUTE CP-IBNR-RESERVE = CP-IBNR-RESERVE +                  ELCRESP1
00672              (CP-ORIGINAL-BENEFIT * (DC-ELAPSED-MONTHS +          ELCRESP1
00673                                  (DC-ODD-DAYS-OVER / 30))).       ELCRESP1
00674                                                                   ELCRESP1
00675  0999-EXIT.                                                       ELCRESP1

032612     if cp-company-cd = 'AHL'
032612        continue
032612     else
032612        IF CP-PTC-RESERVE <= ZERO
032612           MOVE ZERO             TO CP-PTC-RESERVE
032612        end-if
032612     end-if

00679 *    IF CP-COMPANY-ID = 'FIM'  AND  CP-AH-CLAIM                   ELCRESP1
00680 *        COMPUTE CP-FUTURE-RESERVE =                              ELCRESP1
00681 *                         CP-FUTURE-RESERVE - CP-PTC-RESERVE.     ELCRESP1
00682 *                                                                 ELCRESP1
00683      IF CP-FUTURE-RESERVE NOT GREATER ZERO                        ELCRESP1
00684          MOVE ZERO               TO  CP-FUTURE-RESERVE.           ELCRESP1
00685                                                                   ELCRESP1
00686      IF CP-IBNR-RESERVE NOT GREATER ZERO                          ELCRESP1
00687          MOVE ZERO               TO  CP-IBNR-RESERVE.             ELCRESP1
