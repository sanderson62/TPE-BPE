00001 ******************************************************************
00002 *                                                                *
00002 *                                                                *
00003 *                            ERCPNDB.                            *
00004 *           COPYBOOK REVIEWED FOR YEAR 2000 COMPLIANCE            
00005 *                            VMOD=2.025                          *
00006 *                                                                *
00007 *   FILE DESCRIPTION = PENDING NEW BUSINESS (ISSUES AND CANCELS) *
00008 *                                                                *
00009 ******************************************************************
00010 *   NOTE: IF THIS FORMAT IS CHANGED, THE SORT RECORD IN THE      *
00011 *         EL861 A/R SYSTEM MAY NEED TO BE CHANGED.               *
00012 ******************************************************************
00013 *                                                                *
00014 *                                                                *
00015 *   FILE TYPE = VSAM,KSDS                                        *
00016 *   RECORD SIZE = 585  RECFORM = FIXED                           *
00017 *                                                                *
00018 *   BASE CLUSTER = ERPNDB                         RKP=2,LEN=11   *
00019 *       ALTERNATE PATH1 = ERPNDB2  (BY CAR GRP STATE ACCOUNT     *
00020 *                                  EFF-DT CERT CHG-SEQ REC-TYPE) *
00021 *                                                 RKP=13,LEN=36  *
00022 *       ALTERNATE PATH2 = ERPNDB3  (BY CO, ORIGINAL BATCH, SEQ.  *
00023 *                                      AND CHG-SEQ.)             *
00024 *                                                RKP=49,LEN=11   *
00025 *       ALTERNATE PATH3 = ERPNDB4  (BY CO, CSR-ID, BATCH, SEQ.   *
00026 *                                      AND CHG-SEQ.)             *
00027 *                                                RKP=60,LEN=15   *
00028 *                                                                *
00029 *   LOG = NO                                                     *
00030 *   SERVREQ = BROWSE, DELETE, UPDATE, NEWREC                     *
00031 ******************************************************************
122002******************************************************************
122002*                   C H A N G E   L O G
122002*
122002* CHANGES ARE MARKED BY THE CHANGE EFFECTIVE DATE.
122002*-----------------------------------------------------------------
122002*  CHANGE   CHANGE REQUEST PGMR  DESCRIPTION OF CHANGE
122002* EFFECTIVE    NUMBER
122002*-----------------------------------------------------------------
122002* 122002                   PEMA  ADD MONTHLY PRODUCT PROCESSING  
122002******************************************************************
00032                                                                   
00033  01  PENDING-BUSINESS.                                            
00034      12  PB-RECORD-ID                     PIC XX.                 
00035          88  VALID-PB-ID                        VALUE 'PB'.       
00036                                                                   
00037      12  PB-CONTROL-PRIMARY.                                      
00038          16  PB-COMPANY-CD                PIC X.                  
00039          16  PB-ENTRY-BATCH               PIC X(6).               
00040          16  PB-BATCH-SEQ-NO              PIC S9(4)     COMP.     
00041          16  PB-BATCH-CHG-SEQ-NO          PIC S9(4)     COMP.     
00042                                                                   
00043      12  PB-CONTROL-BY-ACCOUNT.                                   
00044          16  PB-COMPANY-CD-A1             PIC X.                  
00045          16  PB-CARRIER                   PIC X.                  
00046          16  PB-GROUPING.                                         
00047              20  PB-GROUPING-PREFIX       PIC XXX.                
00048              20  PB-GROUPING-PRIME        PIC XXX.                
00049          16  PB-STATE                     PIC XX.                 
00050          16  PB-ACCOUNT.                                          
00051              20  PB-ACCOUNT-PREFIX        PIC X(4).               
00052              20  PB-ACCOUNT-PRIME         PIC X(6).               
00053          16  PB-CERT-EFF-DT               PIC XX.                 
00054          16  PB-CERT-NO.                                          
00055              20  PB-CERT-PRIME            PIC X(10).              
00056              20  PB-CERT-SFX              PIC X.                  
00057          16  PB-ALT-CHG-SEQ-NO            PIC S9(4)     COMP.     
00058                                                                   
00059          16  PB-RECORD-TYPE               PIC X.                  
00060              88  PB-MAILING-DATA                VALUE '0'.        
00061              88  PB-ISSUE                       VALUE '1'.        
00062              88  PB-CANCELLATION                VALUE '2'.        
00063              88  PB-BATCH-TRAILER               VALUE '9'.        
00064                                                                   
00065      12  PB-CONTROL-BY-ORIG-BATCH.                                
00066          16  PB-ORIGINAL-COMPANY-CD       PIC X.                  
00067          16  PB-ORIGINAL-ENTRY-BATCH      PIC X(6).               
00068          16  PB-ORIGINAL-SEQ-NO           PIC S9(4)     COMP.     
00069          16  PB-ORIGINAL-CHG-SEQ-NO       PIC S9(4)     COMP.     
00070                                                                   
00071      12  PB-CONTROL-BY-CSR.                                       
00072          16  PB-CSR-COMPANY-CD            PIC X.                  
00073          16  PB-CSR-ID                    PIC X(4).               
00074          16  PB-CSR-ENTRY-BATCH           PIC X(6).               
00075          16  PB-CSR-BATCH-SEQ-NO          PIC S9(4)     COMP.     
00076          16  PB-CSR-BATCH-CHG-SEQ-NO      PIC S9(4)     COMP.     
00077 ******************************************************************
00078 *    MAILING DATA IS PROCESSED IN ONLY PRGS. EL512 & EL513       *
00079 ******************************************************************
00080                                                                   
00081      12  PB-LAST-MAINT-DT                 PIC XX.                 
00082      12  PB-LAST-MAINT-BY                 PIC X(4).               
00083      12  PB-LAST-MAINT-HHMMSS             PIC S9(6)     COMP-3.   
00084                                                                   
00085      12  PB-RECORD-BODY                   PIC X(375).             
00086                                                                   
00087      12  PB-ISSUE-RECORD   REDEFINES PB-RECORD-BODY.              
00088          16  PB-CERT-ORIGIN               PIC X.                  
00089              88  CLASIC-CREATED-CERT         VALUE '1'.           
00090          16  PB-I-NAME.                                           
00091              20  PB-I-INSURED-LAST-NAME   PIC X(15).              
00092              20  PB-I-INSURED-FIRST-NAME.                         
00093                  24  PB-I-INSURED-1ST-INIT PIC X.                 
00094                  24  FILLER                PIC X(9).              
00095              20  PB-I-INSURED-MIDDLE-INIT PIC X.                  
00096          16  PB-I-AGE                     PIC S99   COMP-3.       
00097          16  PB-I-JOINT-AGE               PIC S99   COMP-3.       
00098          16  PB-I-BIRTHDAY                PIC XX.                 
00099          16  PB-I-INSURED-SEX             PIC X.                  
00100              88  PB-SEX-MALE     VALUE 'M'.                       
00101              88  PB-SEX-FEMALE   VALUE 'F'.                       
00102                                                                   
00103          16  PB-I-LF-TERM                 PIC S999   COMP-3.      
00104          16  PB-I-AH-TERM                 PIC S999   COMP-3.      
00105          16  PB-I-LOAN-TERM               PIC S999   COMP-3.      
00106          16  PB-I-PAY-FREQUENCY           PIC S99    COMP-3.      
00107          16  PB-I-SKIP-CODE               PIC X.                  
00108              88  PB-NO-MONTHS-SKIPPED      VALUE ' ' '0'.         
00109              88  PB-SKIP-JULY              VALUE '1'.             
00110              88  PB-SKIP-AUGUST            VALUE '2'.             
00111              88  PB-SKIP-SEPTEMBER         VALUE '3'.             
00112              88  PB-SKIP-JULY-AUG          VALUE '4'.             
00113              88  PB-SKIP-AUG-SEPT          VALUE '5'.             
00114              88  PB-SKIP-JULY-AUG-SEPT     VALUE '6'.             
00115              88  PB-SKIP-JUNE-JULY-AUG     VALUE '7'.             
00116              88  PB-SKIP-JUNE              VALUE '8'.             
00117              88  PB-SKIP-JUNE-JULY         VALUE '9'.             
00118              88  PB-SKIP-AUG-SEPT-OCT      VALUE 'A'.             
00119              88  PB-SKIP-BI-WKLY-3RD-PMT   VALUE 'X'.             
00120          16  PB-I-TERM-TYPE               PIC X.                  
00121              88  PB-PAID-MONTHLY           VALUE ' ' 'M'.         
00122              88  PB-PAID-WEEKLY            VALUE 'W'.             
00123              88  PB-PAID-SEMI-MONTHLY      VALUE 'S'.             
00124              88  PB-PAID-BI-WEEKLY         VALUE 'B'.             
00125              88  PB-PAID-13-YEARLY         VALUE 'T'.             
00126          16  PB-I-NO-OF-PAYMENTS          PIC S999   COMP-3.      
00127          16  PB-I-POLICY-FORM-NO          PIC X(12).              
00128          16  PB-I-DATA-ENTRY-SW           PIC X.                  
00129              88  PB-EFF-DT-PROCESSING      VALUE '1' ' '.         
00130              88  PB-EXT-DAYS-PROCESSING    VALUE '2'.             
00131              88  PB-EXPIRE-DT-PROCESSING   VALUE '3'.             
00132              88  PB-1ST-PMT-DT-PROCESSING  VALUE '4'.             
00133          16  PB-I-PAYMENT-AMOUNT          PIC S9(7)V99  COMP-3.   
00134          16  FILLER                       PIC X(4).               
00135          16  PB-I-MICROFILM-NO            PIC S9(9)      COMP-3.  
00136                                                                   
00137          16  PB-I-LIFE-BENEFIT-CD         PIC XX.                 
00138              88  PB-VALID-LIFE               VALUE '01' THRU '89'.
00139              88  PB-INVALID-LIFE             VALUE '  ' '00'      
00140                                                    '90' THRU '99'.
00141          16  PB-I-LF-BENEFIT-CD   REDEFINES PB-I-LIFE-BENEFIT-CD  
00142                                           PIC XX.                 
00143          16  PB-I-LF-BENEFIT-AMT          PIC S9(9)V99   COMP-3.  
00144          16  PB-I-LF-ALT-BENEFIT-AMT      PIC S9(9)V99   COMP-3.  
00145          16  PB-I-LF-PREMIUM-AMT          PIC S9(7)V99   COMP-3.  
00146          16  PB-I-LF-ALT-PREMIUM-AMT      PIC S9(7)V99   COMP-3.  
00147          16  PB-I-LF-CALC-FLAG            PIC X.                  
00148              88 PB-COMP-LF-PREM               VALUE '?'.          
00149          16  PB-I-LF-PREM-CALC            PIC S9(7)V99   COMP-3.  
00150          16  PB-I-LF-ALT-PREM-CALC        PIC S9(7)V99   COMP-3.  
00151          16  PB-I-LF-RATE                 PIC S99V9(5)   COMP-3.  
00152          16  PB-I-LF-ALT-RATE             PIC S99V9(5)   COMP-3.  
00153          16  PB-I-LF-POLICY-FEE           PIC S9(3)V99   COMP-3.  
00154          16  PB-I-LF-REI-RATE             PIC S99V9(5)   COMP-3.  
00155          16  PB-I-LF-ALT-REI-RATE         PIC S99V9(5)   COMP-3.  
00156          16  PB-I-LF-ABBR                 PIC XXX.                
00157          16  PB-I-LF-INPUT-CD             PIC XX.                 
00158                                                                   
00159          16  PB-I-AH-BENEFIT-CD           PIC XX.                 
00160              88  PB-VALID-AH                 VALUE '01' THRU '89'.
00161              88  PB-INVALID-AH               VALUE '  ' '00'      
00162                                                    '90' THRU '99'.
00163          16  PB-I-AH-BENEFIT-AMT          PIC S9(7)V99   COMP-3.  
00164          16  PB-I-AH-PREMIUM-AMT          PIC S9(7)V99   COMP-3.  
00165          16  PB-I-AH-CALC-FLAG            PIC X.                  
00166              88 PB-COMP-AH-PREM                  VALUE '?'.       
00167          16  PB-I-AH-PREM-CALC            PIC S9(7)V99   COMP-3.  
00168          16  PB-I-AH-RATE                 PIC S99V9(5)   COMP-3.  
00169          16  PB-I-AH-POLICY-FEE           PIC S9(3)V99   COMP-3.  
00170          16  PB-I-AH-REI-RATE             PIC S99V9(5)   COMP-3.  
00171          16  PB-I-AH-RATE-TRM             PIC S999       COMP-3.  
00172          16  PB-I-AH-ABBR                 PIC XXX.                
00173          16  PB-I-AH-INPUT-CD             PIC XXX.                
00174                                                                   
00175          16  PB-I-SPECIAL-REIN-CODE       PIC X.                  
00176          16  PB-I-REIN-TABLE              PIC XXX.                
00177          16  PB-I-BUSINESS-TYPE           PIC 99.                 
00178          16  PB-I-INDV-GRP-CD             PIC X.                  
00179          16  PB-I-MORT-CODE.                                      
00180              20  PB-I-TABLE               PIC X.                  
00181              20  PB-I-INTEREST            PIC XX.                 
00182              20  PB-I-MORT-TYP            PIC X.                  
00183          16  PB-I-LF-CRIT-PER             PIC S9(3)      COMP-3.  
00184          16  PB-I-AH-CRIT-PER             PIC S9(3)      COMP-3.  
00185          16  FILLER                       PIC X(4).               
00186          16  PB-I-INDV-GRP-OVRD           PIC X.                  
00187          16  PB-I-RATE-CLASS-OVRD         PIC XX.                 
00188          16  PB-I-SIG-SW                  PIC X.                  
00189              88  PB-POLICY-SIGNED             VALUE 'Y'.          
00190          16  PB-I-RATE-CLASS              PIC XX.                 
00191          16  PB-I-RATE-DEVIATION-LF       PIC XXX.                
00192          16  PB-I-RATE-DEVIATION-AH       PIC XXX.                
00193          16  PB-I-RATE-DEV-PCT-LF         PIC S9V9(6)    COMP-3.  
00194          16  PB-I-RATE-DEV-PCT-AH         PIC S9V9(6)    COMP-3.  
00195          16  PB-I-LIFE-COMMISSION         PIC SV9(5)     COMP-3.  
00196          16  PB-I-JOINT-COMMISSION        PIC SV9(5)     COMP-3.  
00197          16  PB-I-AH-COMMISSION           PIC SV9(5)     COMP-3.  
00198          16  PB-I-BENEFIT-TYPE            PIC XXX.                
00199          16  PB-I-OB-FLAG                 PIC X.                  
00200              88  PB-I-OB                      VALUE 'B'.          
00201              88  PB-I-SUMMARY                 VALUE 'Z'.          
00202          16  PB-I-ENTRY-STATUS            PIC X.                  
00203              88  PB-I-POLICY-IS-ACTIVE        VALUE '1' '3' '4'   
122002                                              'M' '5' '9' '2'.    
00205              88  PB-I-NORMAL-ENTRY            VALUE '1'.          
00206              88  PB-I-POLICY-PENDING          VALUE '2'.          
00207              88  PB-I-CONVERSION-ENTRY        VALUE '4'.          
00208              88  PB-I-POLICY-IS-REISSUE       VALUE '5'.          
122002             88  PB-I-POLICY-IS-MONTHLY       VALUE 'M'.          
00209              88  PB-I-REIN-ONLY               VALUE '9'.          
00210              88  PB-I-POLICY-IS-DECLINED      VALUE 'D'.          
00211              88  PB-I-POLICY-IS-VOIDED        VALUE 'V'.          
00212              88  PB-I-PREM-ACCTNG-ONLY        VALUE 'P'.          
00213              88  PB-I-UNDERWRITE-POLICY       VALUE 'U'.          
00214          16  PB-I-INT-CODE                PIC X.                  
00215              88  PB-ADD-ON-INTEREST           VALUE 'A'.          
00216              88  PB-SIMPLE-INTEREST           VALUE 'S'.          
00217          16  PB-I-LOAN-APR                PIC 9(3)V9(4)   COMP-3. 
00218          16  PB-I-SOC-SEC-NO              PIC X(11).              
00219          16  PB-I-MEMBER-NO               PIC X(12).              
00220          16  PB-I-CURR-SEQ                PIC S9(4)       COMP.   
00221          16  PB-I-LOAN-OFFICER            PIC XXX.                
00222          16  PB-I-LF-EXPIRE-DT            PIC XX.                 
00223          16  PB-I-AH-EXPIRE-DT            PIC XX.                 
00224          16  PB-I-EXTENTION-DAYS          PIC S999        COMP-3. 
00225          16  PB-I-TERM-IN-DAYS            PIC S9(5)       COMP-3. 
00226          16  PB-I-LIFE-INDICATOR          PIC X.                  
00227              88  PB-I-JOINT-COVERAGE         VALUE 'J'.           
00228          16  PB-I-LIVES                   PIC S9(7)       COMP-3. 
00229          16  PB-I-MAIL-ADDRS-SW           PIC X.                  
00230              88 PB-I-MAIL-ADDRS-NOT-PRESENT  VALUE ' '.           
00231              88 PB-I-MAIL-ADDRS-PRESENT      VALUE '1'.           
00232          16  PB-I-1ST-PMT-DT              PIC XX.                 
00233          16  PB-I-JOINT-INSURED.                                  
00234              20 PB-I-JOINT-LAST-NAME      PIC X(15).              
00235              20 PB-I-JOINT-FIRST-NAME.                            
00236                 24  PB-I-JOINT-FIRST-INIT PIC X.                  
00237                 24  FILLER                PIC X(9).               
00238              20 PB-I-JOINT-MIDDLE-INIT    PIC X.                  
00239          16  PB-I-BENEFICIARY-NAME        PIC X(25).              
00240          16  PB-I-LAST-ADD-ON-DT          PIC XX.                 
00241          16  PB-I-REFERENCE               PIC X(12).              
00242          16  PB-I-UNDERWRITING-STATUS     PIC X.                  
00243              88  PB-I-POLICY-ACCEPTED         VALUE 'A' 'N'.      
00244              88  PB-I-POLICY-DECLINED         VALUE 'D'.          
00245              88  PB-I-NEEDS-UNDERWRITING      VALUE 'U'.          
00246          16  PB-I-STATE-TAX               PIC S9(7)V99 COMP-3.    
00247          16  PB-I-MUNI-TAX                PIC S9(7)V99 COMP-3.    
00248          16  PB-I-RESIDENT-STATE          PIC XX.                 
00249          16  PB-I-RATE-CODE               PIC X(4).               
00250          16  PB-I-NUM-BILLED              PIC S9(7)    COMP-3.    
PEMMOD         16  PB-I-LF-PREM-TAX             PIC S9V9(4)  COMP-3.
PEMMOD         16  PB-I-AH-PREM-TAX             PIC S9V9(4)  COMP-3.
PEMMOD*        16  FILLER                       PIC X(17).              
00251          16  FILLER                       PIC X(11).              
00252                                                                   
00253      12  PB-CANCEL-RECORD   REDEFINES PB-RECORD-BODY.             
00254          16  PB-C-LF-CANCEL-VOID-SW       PIC X.                  
00255              88  PB-C-LF-CANCEL-VOIDED        VALUE '1'.          
00256          16  PB-C-CANCEL-ORIGIN           PIC X.                  
00257              88  PB-C-CLAIM-CREATED-CANCEL   VALUE '1'.           
00258          16  PB-C-LF-CANCEL-DT            PIC XX.                 
00259          16  PB-C-LF-CANCEL-AMT           PIC S9(7)V99    COMP-3. 
00260          16  PB-C-LF-CALC-REQ             PIC X.                  
00261              88 PB-COMP-LF-CANCEL            VALUE '?'.           
00262          16  PB-C-LF-REF-CALC             PIC S9(7)V99    COMP-3. 
00263          16  PB-C-LF-REM-TERM             PIC S9(3)       COMP-3. 
00264          16  PB-C-AH-CANCEL-VOID-SW       PIC X.                  
00265              88  PB-C-AH-CANCEL-VOIDED        VALUE '1'.          
00266          16  PB-C-AH-CANCEL-DT            PIC XX.                 
00267          16  PB-C-AH-CANCEL-AMT           PIC S9(7)V99    COMP-3. 
00268          16  PB-C-AH-CALC-REQ             PIC X.                  
00269              88 PB-COMP-AH-CANCEL            VALUE '?'.           
00270          16  PB-C-AH-REF-CALC             PIC S9(7)V99    COMP-3. 
00271          16  PB-C-AH-REM-TERM             PIC S9(3)       COMP-3. 
00272          16  PB-C-LAST-NAME               PIC X(15).              
00273          16  PB-C-REFUND-SW               PIC X.                  
00274              88  PB-C-REFUND-CREATED          VALUE 'Y'.          
00275              88  PB-C-REFUND-REQUESTED        VALUE 'R'.          
00276          16  PB-C-LIVES                   PIC S9(3)       COMP-3. 
00277          16  PB-C-PAYEE-CODE              PIC X(6).               
00278          16  PB-C-LF-REFUND-OVERRIDE      PIC X.                  
00279          16  PB-C-AH-REFUND-OVERRIDE      PIC X.                  
00280          16  PB-C-LF-COMM-CHARGEBACK      PIC X.                  
00281          16  PB-C-AH-COMM-CHARGEBACK      PIC X.                  
00282          16  PB-C-REFERENCE               PIC X(12).              
PEMMOD         16  PB-C-LF-PREM-TAX             PIC S9V9(4)  COMP-3.
PEMMOD         16  PB-C-AH-PREM-TAX             PIC S9V9(4)  COMP-3.
00283          16  FILLER                       PIC X(12).              
PEMMOD*        16  FILLER                       PIC X(18).              
00284          16  PB-C-POLICY-FORM-NO          PIC X(12).              
00285          16  PB-C-MICROFILM-NO            PIC S9(9)      COMP-3.  
00286          16  PB-CANCELED-CERT-DATA.                               
00287              20  PB-CI-INSURED-NAME.                              
00288                  24  PB-CI-LAST-NAME      PIC X(15).              
00289                  24  PB-CI-INITIALS       PIC XX.                 
00290              20  PB-CI-INSURED-AGE        PIC S99         COMP-3. 
00291              20  PB-CI-INSURED-SEX        PIC X.                  
00292              20  PB-CI-LF-TERM            PIC S999        COMP-3. 
00293              20  PB-CI-LF-BENEFIT-CD      PIC XX.                 
00294              20  PB-CI-LF-BENEFIT-AMT     PIC S9(9)V99    COMP-3. 
00295              20  PB-CI-LF-ALT-BENEFIT-AMT PIC S9(9)V99    COMP-3. 
00296              20  PB-CI-LF-PREMIUM-AMT     PIC S9(7)V99    COMP-3. 
00297              20  PB-CI-LF-ALT-PREMIUM-AMT PIC S9(7)V99    COMP-3. 
00298              20  PB-CI-AH-TERM            PIC S999        COMP-3. 
00299              20  PB-CI-AH-BENEFIT-CD      PIC XX.                 
00300              20  PB-CI-AH-BENEFIT-AMT     PIC S9(7)V99    COMP-3. 
00301              20  PB-CI-AH-PREMIUM-AMT     PIC S9(7)V99    COMP-3. 
00302              20  PB-CI-RATE-CLASS         PIC XX.                 
00303              20  PB-CI-RATE-DEV-LF        PIC XXX.                
00304              20  PB-CI-RATE-DEV-AH        PIC XXX.                
00305              20  PB-CI-RATE-DEV-PCT-LF    PIC S9V9(6)     COMP-3. 
00306              20  PB-CI-RATE-DEV-PCT-AH    PIC S9V9(6)     COMP-3. 
00307              20  PB-CI-LIFE-COMMISSION    PIC SV9(5)      COMP-3. 
00308              20  PB-CI-AH-COMMISSION      PIC SV9(5)      COMP-3. 
00309              20  PB-CI-LF-ABBR            PIC X(3).               
00310              20  PB-CI-AH-ABBR            PIC X(3).               
00311              20  PB-CI-OB-FLAG            PIC X.                  
00312                  88  PB-CI-OB                VALUE 'B'.           
00313              20  PB-CI-LF-POLICY-STATUS   PIC X.                  
00314                  88  PB-CI-LF-POLICY-IS-ACTIVE       VALUE '1' '3'
122002                                           'M' '4' '5' '9' '2'.   
00316                  88  PB-CI-LF-NORMAL-ENTRY           VALUE '1'.   
00317                  88  PB-CI-LF-POLICY-PENDING         VALUE '2'.   
00318                  88  PB-CI-LF-POLICY-IS-RESTORE      VALUE '3'.   
00319                  88  PB-CI-LF-CONVERSION-ENTRY       VALUE '4'.   
00320                  88  PB-CI-LF-POLICY-IS-REISSUE      VALUE '5'.   
122002                 88  PB-CI-LF-POLICY-IS-MONTHLY      VALUE 'M'.   
00321                  88  PB-CI-LF-LUMP-SUM-DISAB         VALUE '6'.   
00322                  88  PB-CI-LF-DEATH-CLAIM-APPLIED    VALUE '7'.   
00323                  88  PB-CI-LF-CANCEL-APPLIED         VALUE '8'.   
00324                  88  PB-CI-LF-REIN-ONLY              VALUE '9'.   
00325                  88  PB-CI-LF-POLICY-IS-DECLINED     VALUE 'D'.   
00326                  88  PB-CI-LF-POLICY-IS-VOID         VALUE 'V'.   
00327              20  PB-CI-AH-POLICY-STATUS   PIC X.                  
00328                  88  PB-CI-AH-POLICY-IS-ACTIVE       VALUE '1' '3'
122002                                           'M' '4' '5' '9' '2'.   
00330                  88  PB-CI-AH-NORMAL-ENTRY           VALUE '1'.   
00331                  88  PB-CI-AH-POLICY-PENDING         VALUE '2'.   
00332                  88  PB-CI-AH-POLICY-IS-RESTORE      VALUE '3'.   
00333                  88  PB-CI-AH-CONVERSION-ENTRY       VALUE '4'.   
00334                  88  PB-CI-AH-POLICY-IS-REISSUE      VALUE '5'.   
122002                 88  PB-CI-AH-POLICY-IS-MONTHLY      VALUE 'M'.   
00335                  88  PB-CI-AH-LUMP-SUM-DISAB         VALUE '6'.   
00336                  88  PB-CI-AH-DEATH-CLAIM-APPLIED    VALUE '7'.   
00337                  88  PB-CI-AH-CANCEL-APPLIED         VALUE '8'.   
00338                  88  PB-CI-AH-REIN-ONLY              VALUE '9'.   
00339                  88  PB-CI-AH-POLICY-IS-DECLINED     VALUE 'D'.   
00340                  88  PB-CI-AH-POLICY-IS-VOID         VALUE 'V'.   
00341              20  PB-CI-PAY-FREQUENCY      PIC 99.                 
00342              20  PB-CI-LOAN-APR           PIC 9(3)V9(4)   COMP-3. 
00343              20  PB-CI-SOC-SEC-NO         PIC X(11).              
00344              20  PB-CI-MEMBER-NO          PIC X(12).              
00345              20  PB-CI-INT-CODE           PIC X.                  
00346                  88  PB-CI-ADD-ON                  VALUE 'A'.     
00347                  88  PB-CI-SIMPLE                  VALUE 'S'.     
00348              20  PB-CI-LOAN-TERM          PIC S999        COMP-3. 
00349              20  PB-CI-LOAN-1ST-PMT-DT    PIC X(2).               
00350              20  PB-CI-COMP-EXCP-SW       PIC X.                  
00351                  88  PB-CI-NO-COMP-EXCP            VALUE ' '.     
00352                  88  PB-CI-CERT-HAS-ERCOMM-ENTRY   VALUE '1'.     
00353              20  PB-CI-ENTRY-STATUS       PIC X.                  
00354              20  PB-CI-CURR-SEQ           PIC S9(4)       COMP.   
00355              20  PB-CI-AH-PAID-THRU-DT    PIC XX.                 
00356              20  PB-CI-AH-SETTLEMENT-DT   PIC XX.                 
00357              20  PB-CI-DEATH-DT           PIC XX.                 
00358              20  PB-CI-LF-PRIOR-CANCEL-DT PIC XX.                 
00359              20  PB-CI-AH-PRIOR-CANCEL-DT PIC XX.                 
00360              20  PB-CI-AH-CANCEL-AMT      PIC S9(7)V99    COMP-3. 
00361              20  PB-CI-LF-CANCEL-AMT      PIC S9(7)V99    COMP-3. 
00362              20  PB-CI-CREDIT-INTERFACE-SW-1 PIC X.               
00363              20  PB-CI-CREDIT-INTERFACE-SW-2 PIC X.               
00364              20  PB-CI-ENTRY-DT              PIC XX.              
00365              20  PB-CI-ENTRY-BATCH           PIC X(6).            
00366              20  PB-CI-LF-EXPIRE-DT          PIC XX.              
00367              20  PB-CI-AH-EXPIRE-DT          PIC XX.              
00368              20  PB-CI-EXTENTION-DAYS        PIC S999     COMP-3. 
00369              20  PB-CI-TERM-IN-DAYS          PIC S9(5)    COMP-3. 
00370              20  PB-CI-LOAN-OFFICER          PIC XXX.             
00371              20  PB-CI-LIVES                 PIC S9(3)    COMP-3. 
00372              20  PB-CI-LF-CRIT-PER           PIC S9(3)    COMP-3. 
00373              20  PB-CI-AH-CRIT-PER           PIC S9(3)    COMP-3. 
00374              20  PB-CI-INDV-GRP-CD           PIC X.               
00375              20  PB-CI-BENEFICIARY-NAME      PIC X(25).           
00376              20  PB-CI-NOTE-SW               PIC X.               
00377              20  PB-CI-CANCEL-FEE            PIC S9(3)V99 COMP-3. 
00378              20  PB-CI-MUNI-TAX              PIC S9(7)V99 COMP-3. 
00379              20  PB-CI-STATE-TAX             PIC S9(7)V99 COMP-3. 
00380                                                                   
00381          16  FILLER                       PIC X(50).              
00382                                                                   
00383      12  PB-MAIL-RECORD    REDEFINES PB-RECORD-BODY.              
00384          16  FILLER                       PIC X(10).              
00385          16  PB-M-INSURED-LAST-NAME       PIC X(15).              
00386          16  PB-M-INSURED-FIRST-NAME      PIC X(10).              
00387          16  PB-M-INSURED-MID-INIT        PIC X.                  
00388          16  PB-M-INSURED-AGE             PIC 99.                 
00389          16  PB-M-INSURED-BIRTHDAY        PIC XX.                 
00390          16  PB-M-INSURED-SEX             PIC X.                  
00391          16  PB-M-INSURED-SOC-SEC-NO      PIC X(11).              
00392          16  PB-M-INSURED-ADDRESS-1       PIC X(30).              
00393          16  PB-M-INSURED-ADDRESS-2       PIC X(30).              
00394          16  PB-M-INSURED-CITY-STATE      PIC X(30).              
00395          16  PB-M-INSURED-ZIP-CODE.                               
00396              20  PB-M-INSURED-ZIP-PRIME.                          
00397                  24  PB-M-INSURED-ZIP-1   PIC X.                  
00398                      88  PB-M-CANADIAN-POST-CODE                  
00399                                              VALUE 'A' THRU 'Z'.  
00400                  24  FILLER               PIC X(4).               
00401              20  PB-M-INSURED-ZIP-PLUS4   PIC X(4).               
00402          16  PB-M-INSURED-CANADIAN-ZIP  REDEFINES                 
00403                                         PB-M-INSURED-ZIP-CODE.    
00404              20  PM-M-INS-CAN-POST1       PIC XXX.                
00405              20  PM-M-INS-CAN-POST2       PIC XXX.                
00406              20  FILLER                   PIC XXX.                
00407          16  PB-M-INSURED-PHONE-NO        PIC 9(10).              
00408          16  FILLER                       PIC X(214).             
00409                                                                   
00410      12  PB-BATCH-RECORD   REDEFINES PB-RECORD-BODY.              
00411          16  FILLER                       PIC X(10).              
00412          16  PB-B-LF-ISS-PRM-REMITTED     PIC S9(9)V99 COMP-3.    
00413          16  PB-B-LF-ISS-PRM-ENTERED      PIC S9(9)V99 COMP-3.    
00414          16  PB-B-LF-ISS-PRM-COMPUTED     PIC S9(9)V99 COMP-3.    
00415          16  PB-B-LF-CAN-PRM-REMITTED     PIC S9(9)V99 COMP-3.    
00416          16  PB-B-LF-CAN-PRM-ENTERED      PIC S9(9)V99 COMP-3.    
00417          16  PB-B-LF-CAN-PRM-COMPUTED     PIC S9(9)V99 COMP-3.    
00418          16  PB-B-AH-ISS-PRM-REMITTED     PIC S9(9)V99 COMP-3.    
00419          16  PB-B-AH-ISS-PRM-ENTERED      PIC S9(9)V99 COMP-3.    
00420          16  PB-B-AH-ISS-PRM-COMPUTED     PIC S9(9)V99 COMP-3.    
00421          16  PB-B-AH-CAN-PRM-REMITTED     PIC S9(9)V99 COMP-3.    
00422          16  PB-B-AH-CAN-PRM-ENTERED      PIC S9(9)V99 COMP-3.    
00423          16  PB-B-AH-CAN-PRM-COMPUTED     PIC S9(9)V99 COMP-3.    
00424          16  PB-B-ISSUE-CNT-REMITTED      PIC S9(5)    COMP-3.    
00425          16  PB-B-ISSUE-CNT-ENTERED       PIC S9(5)    COMP-3.    
00426          16  PB-B-CANCEL-CNT-REMITTED     PIC S9(5)    COMP-3.    
00427          16  PB-B-CANCEL-CNT-ENTERED      PIC S9(5)    COMP-3.    
00428          16  PB-B-HIGHEST-SEQ-NO          PIC S9(4)    COMP.      
00429          16  PB-ACCOUNT-NAME              PIC X(30).              
00430          16  PB-PREM-REF-RPT-FLAG         PIC X.                  
00431          16  PB-REFERENCE                 PIC X(12).              
00432          16  PB-B-RECEIVED-DT             PIC XX.                 
00433          16  FILLER                       PIC X(234).             
00434                                                                   
00435      12  PB-RECORD-STATUS.                                        
00436          16  PB-CREDIT-SELECT-DT          PIC XX.                 
00437          16  PB-CREDIT-ACCEPT-DT          PIC XX.                 
00438          16  PB-BILLED-DT                 PIC XX.                 
00439          16  PB-BILLING-STATUS            PIC X.                  
00440              88  PB-ENTRY-REVERSED            VALUE 'R'.          
00441              88  PB-EL860-INTERNAL-ERROR      VALUE 'E'.          
00442              88  PB-EL860-INTERNAL-PROCESS    VALUE 'P'.          
00443          16  PB-RECORD-BILL               PIC X.                  
00444              88  PB-RECORD-ON-HOLD            VALUE 'H'.          
00445              88  PB-RECORD-RETURNED           VALUE 'R'.          
00446              88  PB-RECORD-ENDORSED           VALUE 'E'.          
00447              88  PB-OVERRIDE-LIFE             VALUE 'L'.          
00448              88  PB-OVERRIDE-AH               VALUE 'A'.          
00449              88  PB-OVERRIDE-BOTH             VALUE 'B'.          
00450          16  PB-BATCH-ENTRY               PIC X.                  
00451              88  PB-POLICY-IS-DECLINED        VALUE 'D'.          
00452              88  PB-REIN-ONLY-CERT            VALUE 'R'.          
00453              88  PB-REISSUED-CERT             VALUE 'E'.          
122002             88  PB-MONTHLY-CERT              VALUE 'M'.          
00454              88  PB-PREM-ACCTNG-ONLY          VALUE 'P'.          
00455              88  PB-NEEDS-UNDERWRITING        VALUE 'U'.          
00456              88  PB-POLICY-IS-VOIDED          VALUE 'V'.          
00457          16  PB-FORCE-CODE                PIC X.                  
00458              88  PB-FORCE-OFF                 VALUE ' ' '0'.      
00459              88  PB-ISSUE-FORCE               VALUE 'A'.          
00460              88  PB-CANCEL-FORCE              VALUE '8'.          
00461              88  PB-ALL-ISSUE-FORCED          VALUE 'A'.          
00462              88  PB-ALL-CANCEL-FORCED         VALUE '8'.          
00463              88  PB-ALL-CANCEL-FORCED-NO-FEE  VALUE '9'.          
00464              88  PB-CANCEL-DATE-FORCED        VALUE 'D'.          
00465              88  PB-CANCEL-DATE-FORCED-NO-FEE VALUE 'E'.          
00466              88  PB-ISSUE-DATE-FORCED         VALUE 'D'.          
00467          16  PB-FATAL-FLAG                PIC X.                  
00468              88  PB-FATAL-ERRORS              VALUE 'X'.          
00469          16  PB-FORCE-ER-CD               PIC X.                  
00470              88  PB-FORCE-ERRORS              VALUE 'F'.          
00471              88  PB-UNFORCED-ERRORS           VALUE 'X'.          
00472          16  PB-WARN-ER-CD                PIC X.                  
00473              88  PB-WARNING-ERRORS            VALUE 'W'.          
00474          16  FILLER                       PIC X.                  
00475          16  PB-OUT-BAL-CD                PIC X.                  
00476              88  PB-OUT-OF-BAL                VALUE 'O'.          
00477          16  PB-LIFE-OVERRIDE-L1          PIC X.                  
00478          16  PB-AH-OVERRIDE-L1            PIC X.                  
00479          16  PB-INPUT-DT                  PIC XX.                 
00480          16  PB-INPUT-BY                  PIC X(4).               
00481          16  PB-CHG-COUNT                 PIC 9(3)        COMP-3. 
00482          16  PB-CALC-TOLERANCE            PIC 9(3)V99     COMP-3. 
00483          16  PB-TOLERANCE-REJECT-SW       PIC X.                  
00484          16  PB-LF-EARNING-METHOD         PIC X.                  
00485          16  PB-AH-EARNING-METHOD         PIC X.                  
00486          16  PB-LF-TERM-CALC-METHOD       PIC X.                  
00487          16  PB-AH-TERM-CALC-METHOD       PIC X.                  
00488          16  PB-REIN-CD                   PIC XXX.                
00489          16  PB-LF-REFUND-TYPE            PIC X.                  
00490          16  PB-AH-REFUND-TYPE            PIC X.                  
00491          16  PB-ACCT-EFF-DT               PIC XX.                 
00492          16  PB-ACCT-EXP-DT               PIC XX.                 
00493          16  PB-COMPANY-ID                PIC X(3).               
00494          16  PB-LF-BILLED-AMTS            PIC S9(7)V99  COMP-3.   
00495          16  PB-AH-BILLED-AMTS            PIC S9(7)V99  COMP-3.   
00496          16  PB-SV-CARRIER                PIC X.                  
00497          16  PB-SV-GROUPING               PIC X(6).               
00498          16  PB-SV-STATE                  PIC XX.                 
00499          16  PB-CONFIRMATION-REPT-DT      PIC XX.                 
00500          16  PB-GA-BILLING-INFO.                                  
00501              20  PB-GA-BILL-DT OCCURS 5 TIMES                     
00502                                           PIC XX.                 
00503          16  PB-SV-REMIT-TO  REDEFINES                            
00504              PB-GA-BILLING-INFO           PIC X(10).              
00505          16  PB-NO-OF-ERRORS              PIC S9(3) COMP-3.       
00506                                                                   
00507          16  FILLER                       PIC X(27).              
00508                                                                   
00509 ******************************************************************
00510 *                COMMON EDIT ERRORS                              *
00511 ******************************************************************
00512                                                                   
00513      12  PB-COMMON-ERRORS.                                        
00514          16  PB-COMMON-ERROR    OCCURS 10 TIMES                   
00515                                            PIC S9(4)     COMP.    
00516                                                                   
00517 ******************************************************************
