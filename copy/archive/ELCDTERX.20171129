00001 ******************************************************************04/15/98
00002 *                                                                *ELCDTERX
00002 *                                                                *ELCDTERX
00003 *                            ELCDTERX.                           *   LV002
00004 *           COPYBOOK REVIEWED FOR YEAR 2000 COMPLIANCE               CL**2
00005 *                            VMOD=2.007                          *ELCDTERX
00006 *                                                                *ELCDTERX
00007 *      LOAD DATE CARD DISK FILE INTO WORKING STORAGE AREA        *ELCDTERX
00008 *      (COPY CODE FOR W/S = ELCDTECX AND COPY CODE FOR FD        *ELCDTERX
00009 *      = ELCDTEFD)                                               *ELCDTERX
00010 *                                                                *ELCDTERX
00011 ******************************************************************ELCDTERX
00012                                                                   ELCDTERX
00013      ACCEPT WS-ACCEPT-DATE       FROM  DATE.                      ELCDTERX
00014                                                                   ELCDTERX
00015      MOVE WS-AD-YY               TO  WS-CD-YY.                    ELCDTERX
00016      MOVE WS-AD-MM               TO  WS-CD-MM.                    ELCDTERX
00017      MOVE WS-AD-DD               TO  WS-CD-DD.                    ELCDTERX
00018                                                                   ELCDTERX
00019      ACCEPT WS-TIME-OF-DAY       FROM  TIME.                      ELCDTERX
00020                                                                   ELCDTERX
00021 ******************************************************************ELCDTERX
00022 *    NOTE: THE FOLLOWING STATEMENT IS USED TO ELIMINATE COB II    ELCDTERX
00023 *          WARNING MESSAGES.                                      ELCDTERX
00024 ******************************************************************ELCDTERX
00025      IF WS-CD-YY = LOW-VALUES                                     ELCDTERX
00026          COMPUTE PGM-SUB = PGM-SUB + 1.                           ELCDTERX
00027                                                                   ELCDTERX
00028      OPEN INPUT DISK-DATE.                                        ELCDTERX
00029                                                                   ELCDTERX
00030      MOVE SPACE TO DATE-CARD         CLAS-BUSINESS-CLASSES        ELCDTERX
00031                    STATE-NAMES       CLAS-MORTALITY-CODES         ELCDTERX
00032                    CLAS-INS-TYPES    COMPANY-NAME-TABLE           ELCDTERX
00033                    DATE-CARD-OPTIONS DATE-CARD-FACTORS            ELCDTERX
00034                    CLAX-ID           CLAS-MISC                    ELCDTERX
00035                    CARRIER-NAMES     CLASIC-SYSTEM-CODES          ELCDTERX
00036                    CARRIER-OPT-CLAIM-DATA                         ELCDTERX
00037                    STATE-TARGET-LOSS-RATIOS                       ELCDTERX
00038                    BUSINESS-TRGT-LOSS-RATIO-MODS                  ELCDTERX
00039                    MISC-OPT-CLM-RSV-DATA.                         ELCDTERX
00040                                                                   ELCDTERX
00041      MOVE LOW-VALUES TO CLAS-INDEX-TBL CLAS-TYPE-MISC.            ELCDTERX
00042                                                                   ELCDTERX
00043      MOVE ZERO TO CLAS-CN DTE-TOT-LINES CLAS-CO                   ELCDTERX
00044                   INDEXCA INDEXBS INDEXST.                        ELCDTERX
00045                                                                   ELCDTERX
00046      COMPUTE CLAS-CN = PGM-SUB / +50.                             ELCDTERX
00047      COMPUTE CLAS-CO = PGM-SUB / +20.                             ELCDTERX
00048      COMPUTE CLAS-INDEXEN = PGM-SUB - (CLAS-CN * +50).            ELCDTERX
00049      COMPUTE CLAS-INDEXON = PGM-SUB - (CLAS-CO * +20).            ELCDTERX
00050                                                                   ELCDTERX
00051      IF CLAS-INDEXEN = ZERO                                       ELCDTERX
00052          MOVE +50               TO CLAS-INDEXEN                   ELCDTERX
00053      ELSE                                                         ELCDTERX
00054          ADD +1                 TO CLAS-CN.                       ELCDTERX
00055                                                                   ELCDTERX
00056      IF CLAS-INDEXON = ZERO                                       ELCDTERX
00057          MOVE +20               TO CLAS-INDEXON                   ELCDTERX
00058      ELSE                                                         ELCDTERX
00059          ADD +1                 TO CLAS-CO.                       ELCDTERX
00060                                                                   ELCDTERX
00061  0005-READ--CARD.                                                 ELCDTERX
00062      READ DISK-DATE                                               ELCDTERX
00063          AT END CLOSE DISK-DATE                                   ELCDTERX
00064              GO TO 0060-READ--TEST.                               ELCDTERX
00065                                                                   ELCDTERX
00066      MOVE ZERO                   TO CLAS-INDEX.                   ELCDTERX
00067                                                                   ELCDTERX
00068      IF DD-ID = 'CLAS' OR 'LIFE' OR 'GLED'                        ELCDTERX
00069          MOVE DATE-DISK          TO DATE-CARD                     ELCDTERX
00070          MOVE RUN-DATE           TO WS-RUN-DATE-N                    CL**2
00071          MOVE EP-DT              TO WS-EP-DT-N                       CL**2
00072          GO TO 0005-READ--CARD.                                   ELCDTERX
00073                                                                   ELCDTERX
00074      IF DD-ID = 'CLAD'                                            ELCDTERX
00075          MOVE DATE-DISK          TO DATE-CARD-OPTIONS             ELCDTERX
00076          GO TO 0005-READ--CARD.                                   ELCDTERX
00077                                                                   ELCDTERX
00078      IF DD-ID = 'CLAX'                                            ELCDTERX
00079          MOVE DATE-DISK          TO CLAS-INDEX-TBL                ELCDTERX
00080          GO TO 0005-READ--CARD.                                   ELCDTERX
00081                                                                   ELCDTERX
00082      IF DD-ID = 'CLAF'                                            ELCDTERX
00083          MOVE DATE-DISK          TO DATE-CARD-FACTORS             ELCDTERX
00084          GO TO 0005-READ--CARD.                                   ELCDTERX
00085                                                                   ELCDTERX
00086      IF DD-ID = 'CLAC'                                            ELCDTERX
00087          GO TO 0010-READ--CLAC.                                   ELCDTERX
00088                                                                   ELCDTERX
00089      IF DD-ID = 'CARR'                                            ELCDTERX
00090          GO TO 0015-READ--CARR.                                   ELCDTERX
00091                                                                   ELCDTERX
00092      IF DD-ID = 'STAT'                                            ELCDTERX
00093          GO TO 0020-READ--STAT.                                   ELCDTERX
00094                                                                   ELCDTERX
00095      IF DD-ID = 'BUSC'                                            ELCDTERX
00096          GO TO 0025-READ--BUSC.                                   ELCDTERX
00097                                                                   ELCDTERX
00098      IF DD-ID = 'MORT'                                            ELCDTERX
00099          GO TO 0030-READ--MORT.                                   ELCDTERX
00100                                                                   ELCDTERX
00101      IF DD-ID = 'CLAI'                                            ELCDTERX
00102          GO TO 0040-READ--CLAI.                                   ELCDTERX
00103                                                                   ELCDTERX
00104      IF DD-ID = 'COLC'                                            ELCDTERX
00105          GO TO 0065-READ--COLC.                                   ELCDTERX
00106                                                                   ELCDTERX
00107      IF DD-IDX NUMERIC AND                                        ELCDTERX
00108         DD-IDA = 'CL'                                             ELCDTERX
00109          GO TO 0035-READ--CLXX.                                   ELCDTERX
00110                                                                   ELCDTERX
00111      IF DD-IDX NUMERIC AND                                        ELCDTERX
00112         DD-IDA = 'PO'                                             ELCDTERX
00113          GO TO 0050-READ--POXX.                                   ELCDTERX
00114                                                                   ELCDTERX
00115      IF DD-ID = 'OPCA'                                            ELCDTERX
00116          GO TO 0070-READ--OPCA.                                   ELCDTERX
00117                                                                   ELCDTERX
00118      IF DD-ID = 'OPST'                                            ELCDTERX
00119          GO TO 0072-READ--OPST.                                   ELCDTERX
00120                                                                   ELCDTERX
00121      IF DD-ID = 'OPBS'                                            ELCDTERX
00122          GO TO 0074-READ--OPBS.                                   ELCDTERX
00123                                                                   ELCDTERX
00124      IF DD-ID = 'OPMS'                                            ELCDTERX
00125          MOVE DD-MISC-DATA       TO MISC-OPT-CLM-RSV-DATA.        ELCDTERX
00126                                                                   ELCDTERX
00127      GO TO 0005-READ--CARD.                                       ELCDTERX
00128                                                                   ELCDTERX
00129  0010-READ--CLAC.                                                 ELCDTERX
00130      ADD 1                       TO CLAS-INDEXC.                  ELCDTERX
00131      MOVE DD-CR1                 TO CNT-ID (CLAS-INDEXC).         ELCDTERX
00132      MOVE DD-CR1N                TO CNT-NAME (CLAS-INDEXC).       ELCDTERX
00133      ADD 1                       TO CLAS-INDEXC.                  ELCDTERX
00134      MOVE DD-CR2                 TO CNT-ID (CLAS-INDEXC).         ELCDTERX
00135      MOVE DD-CR2N                TO CNT-NAME (CLAS-INDEXC).       ELCDTERX
00136      GO TO 0005-READ--CARD.                                       ELCDTERX
00137                                                                   ELCDTERX
00138  0015-READ--CARR.                                                 ELCDTERX
00139      ADD 1  TO  CLAS-INDEXCN.                                     ELCDTERX
00140      IF CLAS-INDEXCN LESS THAN +26                                ELCDTERX
00141         MOVE DD-CARR1         TO CARRIER-SUB (CLAS-INDEXCN)
00142         MOVE DD-CARR1S        TO CARRIER-DOM-ST (CLAS-INDEXCN)
00143         MOVE DD-CARR1N        TO CARRIER-PIC (CLAS-INDEXCN)
00144         MOVE DD-CARR1-UEP-PCT TO CARRIER-UEP-PCT (CLAS-INDEXCN)
00145         MOVE DD-CARR1-R78-PCT TO CARRIER-R78-PCT (CLAS-INDEXCN)
00146         MOVE DD-CARR1-PRO-PCT TO CARRIER-PRO-PCT (CLAS-INDEXCN)
011904        MOVE DD-CARR1-SECPAY-SW
011904                               TO CARRIER-SEC-PAY (CLAS-INDEXCN)
011904     END-IF
00147                                                                   ELCDTERX
00148      ADD 1  TO  CLAS-INDEXCN.                                     ELCDTERX
00149      IF CLAS-INDEXCN LESS THAN +26                                ELCDTERX
00150         MOVE DD-CARR2         TO CARRIER-SUB (CLAS-INDEXCN)
00151         MOVE DD-CARR2S        TO CARRIER-DOM-ST (CLAS-INDEXCN)
00152         MOVE DD-CARR2N        TO CARRIER-PIC (CLAS-INDEXCN)
00153         MOVE DD-CARR2-UEP-PCT TO CARRIER-UEP-PCT (CLAS-INDEXCN)
00154         MOVE DD-CARR2-R78-PCT TO CARRIER-R78-PCT (CLAS-INDEXCN)
00155         MOVE DD-CARR2-PRO-PCT TO CARRIER-PRO-PCT (CLAS-INDEXCN)
011904        MOVE DD-CARR2-SECPAY-SW
011904                              TO CARRIER-SEC-PAY (CLAS-INDEXCN)
011904     END-IF
00156                                                                   ELCDTERX
00157                                                                   ELCDTERX
00158      GO TO 0005-READ--CARD.                                       ELCDTERX
00159                                                                   ELCDTERX
00160  0020-READ--STAT.                                                 ELCDTERX
00161      ADD 1                       TO CLAS-INDEX.                   ELCDTERX
00162      IF CLAS-INDEX GREATER 3  OR                                  ELCDTERX
00163         DD-ST (CLAS-INDEX) = SPACE                                ELCDTERX
00164          GO TO 0005-READ--CARD.                                   ELCDTERX
00165                                                                   ELCDTERX
00166      ADD 1                       TO CLAS-INDEXS.                  ELCDTERX
00167      MOVE DD-ST (CLAS-INDEX)     TO STATE-NAME-FLD (CLAS-INDEXS). ELCDTERX
00168      GO TO 0020-READ--STAT.                                       ELCDTERX
00169                                                                   ELCDTERX
00170  0025-READ--BUSC.                                                 ELCDTERX
00171      ADD 1                       TO CLAS-INDEX.                   ELCDTERX
00172      IF CLAS-INDEX GREATER 3  OR                                  ELCDTERX
00173         DD-ST (CLAS-INDEX) = SPACE                                ELCDTERX
00174          GO TO 0005-READ--CARD.                                   ELCDTERX
00175                                                                   ELCDTERX
00176      ADD 1                       TO CLAS-INDEXB.                  ELCDTERX
00177      MOVE DD-ST (CLAS-INDEX)     TO CLAS-BUSC-FLD (CLAS-INDEXB).  ELCDTERX
00178      GO TO 0025-READ--BUSC.                                       ELCDTERX
00179                                                                   ELCDTERX
00180  0030-READ--MORT.                                                 ELCDTERX
00181      ADD 1                       TO CLAS-INDEX.                   ELCDTERX
00182      IF CLAS-INDEX GREATER 2  OR                                  ELCDTERX
00183         DD-MORT (CLAS-INDEX) = SPACE                              ELCDTERX
00184          GO TO 0005-READ--CARD.                                   ELCDTERX
00185                                                                   ELCDTERX
00186      ADD 1                       TO CLAS-INDEXM.                  ELCDTERX
00187      MOVE DD-MORT (CLAS-INDEX)   TO CLAS-MORT-FLD (CLAS-INDEXM).  ELCDTERX
00188      GO TO 0030-READ--MORT.                                       ELCDTERX
00189                                                                   ELCDTERX
00190  0035-READ--CLXX.                                                 ELCDTERX
00191      IF DD-IDX NOT = CLAS-CN                                      ELCDTERX
00192           GO TO 0005-READ--CARD.                                  ELCDTERX
00193                                                                   ELCDTERX
00194      IF DD-PS-PIC (CLAS-INDEXEN) = 'X'                            ELCDTERX
00195          DISPLAY '***********************************************'ELCDTERX
00196          DISPLAY 'PROGRAM ECS-', PGM-SUB, ' NOT SCHEDULED TO RUN' ELCDTERX
00197          DISPLAY '***********************************************'ELCDTERX
00198          GO TO 0055-READ--STOP.                                   ELCDTERX
00199                                                                   ELCDTERX
00200      IF DD-PS-PIC (CLAS-INDEXEN) NOT LESS 'A'  AND                ELCDTERX
00201         DD-PS-PIC (CLAS-INDEXEN) NOT GREATER 'I'                  ELCDTERX
00202          MOVE '1'                TO DTE-FICH.                     ELCDTERX
00203                                                                   ELCDTERX
00204      IF DD-PS-PIC (CLAS-INDEXEN) NOT LESS 'J'  AND                ELCDTERX
00205         DD-PS-PIC (CLAS-INDEXEN) NOT GREATER 'R'                  ELCDTERX
00206          MOVE '2'                TO DTE-FICH.                     ELCDTERX
00207                                                                   ELCDTERX
pemuni*    MOVE DD-PS-PIC (CLAS-INDEXEN) TO DTE-PGM-OPT.                ELCDTERX
00209      GO TO 0005-READ--CARD.                                       ELCDTERX
00210                                                                   ELCDTERX
00211  0040-READ--CLAI.                                                 ELCDTERX
00212      ADD 1                       TO CLAS-INDEX.                   ELCDTERX
00213      IF CLAS-INDEX GREATER 2  OR                                  ELCDTERX
00214         DD-TYPE (CLAS-INDEX) = SPACE                              ELCDTERX
00215          GO TO 0005-READ--CARD.                                   ELCDTERX
00216                                                                   ELCDTERX
00217      ADD 1                       TO CLAS-INDEXA.                  ELCDTERX
00218      MOVE DD-TYPE (CLAS-INDEX) TO CLAS-ALL-TYPES (CLAS-INDEXA).   ELCDTERX
00219      GO TO 0040-READ--CLAI.                                       ELCDTERX
00220                                                                   ELCDTERX
00221  0045-READ--DUMP.                                                 ELCDTERX
00222      MOVE +999 TO WS-RETURN-CODE                                  ELCDTERX
00223      MOVE 'MISSING AND/OR INVALID CLAS DATE CARD'                 ELCDTERX
00224                                  TO  WS-ABEND-MESSAGE             ELCDTERX
00225      PERFORM ABEND-PGM.                                           ELCDTERX
00226                                                                   ELCDTERX
00227  0050-READ--POXX.                                                 ELCDTERX
00228      IF DD-IDX NOT = CLAS-CO                                      ELCDTERX
00229          GO TO 0005-READ--CARD.                                   ELCDTERX
00230                                                                   ELCDTERX
00231      IF DD-PS-PRC (CLAS-INDEXON) = 'X'                            ELCDTERX
00232          DISPLAY '***********************************************'ELCDTERX
00233          DISPLAY 'ECS - ', PGM-SUB, ' IS NOT SCHEDULED TO RUN'    ELCDTERX
00234          DISPLAY '***********************************************'ELCDTERX
00235          GO TO 0055-READ--STOP.                                   ELCDTERX
00236                                                                   ELCDTERX
00237      MOVE DD-PS-PRT (CLAS-INDEXON) TO DTE-PRT-OPT.                ELCDTERX
00238      MOVE DD-PS-FMT (CLAS-INDEXON) TO DTE-FMT-OPT.                ELCDTERX
00239      MOVE DD-PS-PRC (CLAS-INDEXON) TO DTE-PRC-OPT DTE-PGM-OPT.    ELCDTERX
00240      MOVE DD-PS-TOT (CLAS-INDEXON) TO DTE-TOT-OPT.                ELCDTERX
00241                                                                   ELCDTERX
00242      IF DTE-PRT-OPT = 'B'                                         ELCDTERX
00243          MOVE '2'                  TO DTE-FICH.                   ELCDTERX
00244      IF DTE-PRT-OPT = 'F'                                         ELCDTERX
00245          MOVE '1'                  TO DTE-FICH.                   ELCDTERX
00246                                                                   ELCDTERX
00247      GO TO 0005-READ--CARD.                                       ELCDTERX
00248                                                                   ELCDTERX
00249  0055-READ--STOP.                                                 ELCDTERX
00250      GOBACK.                                                      ELCDTERX
00251                                                                   ELCDTERX
00252  0060-READ--TEST.                                                 ELCDTERX
00253      IF DATX-COD        = SPACE  OR                               ELCDTERX
00254         CLAX-ID         = SPACE  OR                               ELCDTERX
00255         DATE-OPTIONS-ID = SPACE                                   ELCDTERX
00256          GO TO 0045-READ--DUMP.                                   ELCDTERX
00257                                                                   ELCDTERX
00258      MOVE LOW-VALUE TO CLAS-TYPE-MISC.                            ELCDTERX
00259      GO TO 0079-READ--EXIT.                                       ELCDTERX
00260                                                                   ELCDTERX
00261  0065-READ--COLC.                                                 ELCDTERX
00262      MOVE DATE-DISK              TO CLASIC-SYSTEM-CODES.          ELCDTERX
00263      GO TO 0005-READ--CARD.                                       ELCDTERX
00264                                                                   ELCDTERX
00265  0070-READ--OPCA.                                                 ELCDTERX
00266                                                                   ELCDTERX
00267      ADD 1                       TO CLAS-INDEX.                   ELCDTERX
00268                                                                   ELCDTERX
00269      IF  CLAS-INDEX GREATER 13                                    ELCDTERX
00270              OR                                                   ELCDTERX
00271          DD-CARR-FLDS (CLAS-INDEX) = SPACE                        ELCDTERX
00272          GO TO 0005-READ--CARD.                                   ELCDTERX
00273                                                                   ELCDTERX
00274      ADD 1                       TO INDEXCA.                      ELCDTERX
00275      MOVE DD-CARR-FLDS (CLAS-INDEX)                               ELCDTERX
00276                                  TO CARRIER-FLDS (INDEXCA).       ELCDTERX
00277      GO TO 0070-READ--OPCA.                                       ELCDTERX
00278                                                                   ELCDTERX
00279  0072-READ--OPST.                                                 ELCDTERX
00280                                                                   ELCDTERX
00281      ADD 1                       TO CLAS-INDEX.                   ELCDTERX
00282                                                                   ELCDTERX
00283      IF  CLAS-INDEX GREATER 16                                    ELCDTERX
00284              OR                                                   ELCDTERX
00285          DD-TLR (CLAS-INDEX) = SPACE                              ELCDTERX
00286          GO TO 0005-READ--CARD.                                   ELCDTERX
00287                                                                   ELCDTERX
00288      ADD 1                       TO INDEXST.                      ELCDTERX
00289      MOVE DD-STATE-FLDS (CLAS-INDEX)                              ELCDTERX
00290                                  TO STATE-TLR-FLD (INDEXST).      ELCDTERX
00291      GO TO 0072-READ--OPST.                                       ELCDTERX
00292                                                                   ELCDTERX
00293  0074-READ--OPBS.                                                 ELCDTERX
00294                                                                   ELCDTERX
00295      ADD 1                       TO CLAS-INDEX.                   ELCDTERX
00296                                                                   ELCDTERX
00297      IF  CLAS-INDEX GREATER 32                                    ELCDTERX
00298              OR                                                   ELCDTERX
00299          DD-TLR (CLAS-INDEX) = SPACE                              ELCDTERX
00300          GO TO 0005-READ--CARD.                                   ELCDTERX
00301                                                                   ELCDTERX
00302      ADD 1                       TO INDEXBS.                      ELCDTERX
00303      MOVE DD-TLR (CLAS-INDEX)    TO BUS-TLRM-FLD (INDEXBS).       ELCDTERX
00304      GO TO 0074-READ--OPBS.                                       ELCDTERX
00305                                                                   ELCDTERX
00306  0079-READ--EXIT.                                                 ELCDTERX
00307 *    EXIT.                                                        ELCDTERX
00308 ******************************************************************ELCDTERX
