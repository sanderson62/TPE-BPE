00001 ****************************************************************  
00002 *                                                              *  
00003 *                            ELCUPRMP.                         *  
00004 *           COPYBOOK REVIEWED FOR YEAR 2000 COMPLIANCE            
00005 *                            VMOD=2.009                        *  
00006 *                                                              *  
00007 ****************************************************************. 
100703*                   C H A N G E   L O G
100703*
100703* CHANGES ARE MARKED BY THE CHANGE EFFECTIVE DATE.
100703*-----------------------------------------------------------------
100703*  CHANGE   CHANGE REQUEST PGMR  DESCRIPTION OF CHANGE
100703* EFFECTIVE    NUMBER
100703*-----------------------------------------------------------------
100703* 100703    2003080800002  PEMA  ADD SECURE PAY PLUS PROCESSING
040504* 040504    2003080800002  PEMA  ADD ACTUARIAL METHOD FOR SPP
010405* 010405                   PEMA  CHANGE VA TO MEAN > 6/30/02
092705* 092705  CR2005050300006  PEMA  ADD SPP LEASES
043008* 043008  CR2008030600001  PEMA  VA RULE OF ANTIC
100703******************************************************************
00008                                                                   
00009                                                                   
00010      MOVE ZERO                   TO  CP-RETURN-CODE               
00011                                      CP-R78-U-PRM                 
00012                                      CP-R78-U-PRM-ALT             
00013                                      CP-PRORATA-U-PRM             
00014                                      CP-PRORATA-U-PRM-ALT         
00015                                      CP-STATE-U-PRM               
00016                                      CP-DOMICILE-U-PRM            
00017                                      PR-UN-PREMIUM                
00018                                      PR-UN-PREM-ALT               
00019                                      STATE-UN-PREMIUM             
00020                                      DOMI-UN-PREMIUM              
00021                                      R78-UN-PREMIUM               
00022                                      R78-UN-PREM-ALT.             
00023                                                                   
00024                                                                   
00025  0001-GET-EFFECTIVE-DATE.                                         
00026                                                                   
00027      MOVE CP-CERT-EFF-DT     TO DC-BIN-DATE-1.                    
00028      MOVE SPACE              TO DC-OPTION-CODE.                   
00029      PERFORM 9100-CONVERT-DATE THRU 9100-EXIT.                    
00030                                                                   
00031      IF DATE-CONVERSION-ERROR                                     
00032           DISPLAY 'BAD RET  '                                     
00033          MOVE '2' TO CP-RETURN-CODE                               
00034           GO TO 1999-EXIT-CALC-ROUTINE.                           
00035                                                                   
00036      IF CP-ORIGINAL-TERM EQUAL 0                                  
00037          MOVE '1'           TO CP-RETURN-CODE                     
00038           GO TO 1999-EXIT-CALC-ROUTINE.                           
00039                                                                   
00040      IF CP-AH                                                     
00041          PERFORM 1500-CALC-UNEARNED-AH THRU                       
00042                                1500-CALC-UNEARNED-X               
00043      ELSE                                                         
00044          PERFORM 1000-CALC-UNEARNED-LIFE THRU                     
00045                                1000-CALC-UNEARNED-X.              
00046                                                                   
00047      GO TO 1999-EXIT-CALC-ROUTINE.                                
00048                                                                   
00049  0009-EXIT.                                                       
00050      EXIT.                                                        
00051  EJECT                                                            
00052 ******************** UNEARNED LIFE PREMIUM ***********************
00053  1000-CALC-UNEARNED-LIFE SECTION.                                 
00054                                                                   
00055      IF CP-SPECIAL-CALC-CD EQUAL 'G'                              
00056          MOVE 'N' TO CP-EARNING-METHOD.                           
00057                                                                   
00058      IF CP-TERM-OR-EXT-DAYS NOT NUMERIC                           
00059          MOVE +0                TO CP-TERM-OR-EXT-DAYS.           
00060                                                                   
00061      IF CP-LOAN-TERM NOT NUMERIC                                  
00062          MOVE +0                TO CP-LOAN-TERM.                  
00063                                                                   
00064      IF CP-EARNING-METHOD EQUAL 'T'                               
00065          PERFORM 1160-CALC-TEXAS-UN   THRU 1160-EXIT              
00066          GO TO 1000-CALC-UNEARNED-X1.                             
00067                                                                   
00068      PERFORM 1100-LIFE-PRORATA-UNEARNED THRU 1111-EXIT.           
00069      PERFORM 1120-LIFE-RULE78S-UNEARNED THRU 1120-EXIT.           
00070                                                                   
00071      IF CP-COMPANY-ID EQUAL 'NCL'                                 
00072         IF CP-EARN-AS-REG-BALLOON                                 
00073            IF CP-SPECIAL-CALC-CD EQUAL 'A'                        
00074               PERFORM 1180-CALC-NET-PAY-UN THRU 1180-EXIT         
00075               GO TO 1000-DO-STATE-DOM-REGS.                       
00076                                                                   
00077      IF (CP-EARNING-METHOD = 'B' OR 'K' OR 'L')                   
00078          GO TO 1000-DO-STATE-DOM-REGS.                            
00079                                                                   
00080      IF CP-COMPANY-ID = 'NCL'                                     
00081          IF DC-GREG-DATE-CYMD LESS THAN 19910101                  
00082              MOVE ZEROS           TO CP-LOAN-APR                  
00083              IF CP-EARN-AS-NET-PAY                                
00084                  MOVE 'R'         TO CP-EARNING-METHOD.           
00085                                                                   
CIDMOD     IF CP-BENEFIT-CD     EQUAL 'QD'                              
CIDMOD       MOVE 'N' TO CP-EARNING-METHOD                              
CIDMOD       MOVE +15.0 TO N-P-APR
CIDMOD         PERFORM 1180-CALC-NET-PAY-UN THRU 1180-EXIT              
CIDMOD         GO TO 1000-DO-STATE-DOM-REGS.                            
CIDMOD                                                                  
00086      IF CP-EARNING-METHOD EQUAL 'N'                               
00087          PERFORM 1180-CALC-NET-PAY-UN THRU 1180-EXIT              
00088          GO TO 1000-DO-STATE-DOM-REGS.                            
00089                                                                   
00090      IF CP-STATE-STD-ABBRV = 'OH'                                 
00091          IF (CP-ORIGINAL-TERM GREATER THAN +60) AND               
CIDMOD            (CP-CLASS-CODE NOT = 'L ')           AND
00092             (DC-GREG-DATE-CYMD GREATER 19831031) AND              
00093             (CP-LOAN-APR GREATER THAN ZERO)                       
00094             PERFORM 1180-CALC-NET-PAY-UN THRU 1180-EXIT.          
00095                                                                   
00096      IF CP-STATE-STD-ABBRV = 'MT'                                 
00097          IF (CP-ORIGINAL-TERM GREATER THAN +61) AND               
00098             (DC-GREG-DATE-CYMD GREATER 19830318) AND              
00099             (CP-LOAN-APR GREATER THAN ZERO)                       
00100             PERFORM 1180-CALC-NET-PAY-UN THRU 1180-EXIT.          
00101                                                                   
00102      IF CP-STATE-STD-ABBRV = 'UT'                                 
00103          IF (CP-ORIGINAL-TERM GREATER THAN +62) AND               
00104             (DC-GREG-DATE-CYMD GREATER 19810831) AND              
00105             (DC-GREG-DATE-CYMD LESS 19830901) AND                 
00106             (CP-LOAN-APR GREATER THAN ZERO)                       
00107             PERFORM 1180-CALC-NET-PAY-UN THRU 1180-EXIT.          
00108                                                                   
00109      IF CP-STATE-STD-ABBRV  = 'RI'                                
00110          IF (CP-ORIGINAL-TERM GREATER THAN +60) AND               
00111             (DC-GREG-DATE-CYMD GREATER 19831231) AND              
00112             (CP-LOAN-APR GREATER THAN ZERO)                       
00113              PERFORM 1180-CALC-NET-PAY-UN THRU 1180-EXIT.         
00114                                                                   
00115  1000-DO-STATE-DOM-REGS.                                          
00116                                                                   
00117      PERFORM 1200-DO-STATE-REGS    THRU 1200-EXIT.                
00118      PERFORM 1300-DO-DOMICILE      THRU 1300-EXIT.                
00119                                                                   
00120  1000-CALC-UNEARNED-X1.                                           
00121                                                                   
00122      MOVE DOMI-UN-PREMIUM         TO CP-DOMICILE-U-PRM.           
00123      MOVE STATE-UN-PREMIUM        TO CP-STATE-U-PRM.              
00124      MOVE PR-UN-PREMIUM           TO CP-PRORATA-U-PRM.            
00125      MOVE PR-UN-PREM-ALT          TO CP-PRORATA-U-PRM-ALT.        
00126      MOVE R78-UN-PREMIUM          TO CP-R78-U-PRM.                
00127      MOVE R78-UN-PREM-ALT         TO CP-R78-U-PRM-ALT.            
00128                                                                   
00129  1000-CALC-UNEARNED-X.                                            
00130 *---------------------------------------------------------------  
00131      EXIT.                                                        
00132  EJECT                                                            
00133 *********************************************                     
00134 *                                           *                     
00135 * PERFORMED: LIFE UNEARNED PREMIUM ROUTINES *                     
00136 *                                           *                     
00137 *********************************************                     
00138                                                                   
00139  1100-LIFE-PRORATA-UNEARNED.                                      
00140                                                                   
00141      IF CP-COMPANY-ID EQUAL 'FLI' OR 'FLU' OR 'MIC' OR 'MCC'      
00142          COMPUTE PR-FACTOR ROUNDED =                              
00143                    (CP-REMAINING-TERM - .5) / CP-ORIGINAL-TERM    
00144      ELSE                                                         
00145          COMPUTE PR-FACTOR ROUNDED =                              
00146                        CP-REMAINING-TERM / CP-ORIGINAL-TERM.      
00147                                                                   
00148      COMPUTE PR-UN-PREMIUM ROUNDED =                              
00149                      CP-ORIGINAL-PREMIUM * PR-FACTOR.             
00150                                                                   
00151                                                                   
00152      IF (CP-EARNING-METHOD NOT = 'B' AND 'K' AND 'L')             
00153          GO TO 1111-EXIT.                                         
00154                                                                   
00155      IF CP-SPECIAL-CALC-CD EQUAL 'L'                              
00156          GO TO 1110-ALT-LIFE-PRO-UNEARNED.                        
00157                                                                   
00158      IF CP-COMPANY-ID EQUAL 'FLI' OR 'FLU' OR 'MIC' OR 'MCC'      
00159          COMPUTE PR-FACTOR  ROUNDED =                             
00160                (CP-REMAINING-TERM + .5) / (CP-ORIGINAL-TERM + 1)  
00161      ELSE                                                         
00162          COMPUTE PR-FACTOR  ROUNDED =                             
00163               (CP-REMAINING-TERM + 1) / (CP-ORIGINAL-TERM + 1).   
00164                                                                   
00165  1110-ALT-LIFE-PRO-UNEARNED.                                      
00166                                                                   
00167      COMPUTE PR-UN-PREM-ALT ROUNDED =                             
00168                    CP-ALTERNATE-PREMIUM * PR-FACTOR.              
00169                                                                   
00170                                                                   
00171      COMPUTE PR-UN-PREMIUM = PR-UN-PREMIUM +                      
00172                     PR-UN-PREM-ALT.                               
00173                                                                   
00174                                                                   
00175  1111-EXIT.                                                       
00176  EJECT                                                            
00177 *---------------------------------------------------------------  
00178  1120-LIFE-RULE78S-UNEARNED.                                      
00179                                                                   
00180      COMPUTE R78-TEMP-1 =                                         
00181                 CP-REMAINING-TERM * (CP-REMAINING-TERM + +1).     
00182                                                                   
00183      IF CP-R78-OPTION = '1'                                       
00184          COMPUTE R78-TEMP-1 =                                     
00185                   CP-REMAINING-TERM * CP-REMAINING-TERM.          
00186                                                                   
00187      IF CP-COMPANY-ID = 'FLI'                                     
00188          COMPUTE R78-TEMP-1 =                                     
00189                   CP-REMAINING-TERM * CP-REMAINING-TERM.          
00190                                                                   
00191      IF CP-COMPANY-ID = 'MPP'                                     
00192          COMPUTE R78-TEMP-1 =                                     
00193                  (CP-ORIGINAL-TERM - CP-REMAINING-TERM)           
00194                  * ((CP-ORIGINAL-TERM - CP-REMAINING-TERM) + +1). 
00195                                                                   
00196      COMPUTE R78-TEMP-2 =                                         
00197                   CP-ORIGINAL-TERM * (CP-ORIGINAL-TERM + +1).     
00198                                                                   
00199      COMPUTE R78-FACTOR ROUNDED = R78-TEMP-1 / R78-TEMP-2.        
00200                                                                   
00201      COMPUTE R78-UN-PREMIUM ROUNDED =                             
00202                      CP-ORIGINAL-PREMIUM * R78-FACTOR.            
00203                                                                   
00204                                                                   
00205                                                                   
00206      IF (CP-EARNING-METHOD NOT = 'B' AND 'K' AND 'L')             
00207          GO TO 1120-EXIT.                                         
00208                                                                   
00209      IF CP-SPECIAL-CALC-CD EQUAL 'L'                              
00210          GO TO 1120-ALT-LIFE-R78-UNEARNED.                        
00211                                                                   
00212      IF CP-R78-OPTION = '1' OR CP-COMPANY-ID = 'FLI'              
00213          COMPUTE R78-TEMP-1 =                                     
00214                 (CP-REMAINING-TERM + 1) *                         
00215                  (CP-REMAINING-TERM + 1 )                         
00216      ELSE                                                         
00217          COMPUTE R78-TEMP-1 =                                     
00218               (CP-REMAINING-TERM + 1) *                           
00219               (CP-REMAINING-TERM + 2).                            
00220                                                                   
00221      IF CP-COMPANY-ID = 'MPP'                                     
00222          COMPUTE R78-TEMP-1 =                                     
00223             ((CP-ORIGINAL-TERM + 1) - (CP-REMAINING-TERM + 1))    
00224            * ((CP-ORIGINAL-TERM + 1) - (CP-REMAINING-TERM + 1)).  
00225                                                                   
00226      COMPUTE R78-TEMP-2 =                                         
00227            (CP-ORIGINAL-TERM + 1) * (CP-ORIGINAL-TERM + 2).       
00228                                                                   
00229      COMPUTE R78-FACTOR ROUNDED =                                 
00230                R78-TEMP-1 / R78-TEMP-2.                           
00231                                                                   
00232  1120-ALT-LIFE-R78-UNEARNED.                                      
00233                                                                   
00234      COMPUTE R78-UN-PREM-ALT ROUNDED =                            
00235              CP-ALTERNATE-PREMIUM * R78-FACTOR.                   
00236                                                                   
00237                                                                   
00238      COMPUTE R78-UN-PREMIUM ROUNDED =                             
00239              R78-UN-PREMIUM + R78-UN-PREM-ALT.                    
00240                                                                   
00241                                                                   
00242  1120-EXIT.                                                       
00243  EJECT                                                            
00244 *---------------------------------------------------------------  
00245  1160-CALC-TEXAS-UN.                                              
00246                                                                   
00247      IF CP-PAY-FREQUENCY = ZERO                                   
00248         MOVE '5'                 TO CP-RETURN-CODE                
00249         GO TO 1160-EXIT.                                          
00250                                                                   
00251      COMPUTE TEX-FACT-4 = (CP-ORIGINAL-TERM * CP-ORIGINAL-TERM) + 
00252          (CP-PAY-FREQUENCY * CP-ORIGINAL-TERM).                   
00253                                                                   
00254      DIVIDE CP-REMAINING-TERM BY CP-PAY-FREQUENCY                 
00255          GIVING TEX-FACT-5                                        
00256              REMAINDER TEX-FACT-6.                                
00257                                                                   
00258      COMPUTE TEX-FACT-5 = TEX-FACT-5 * CP-PAY-FREQUENCY.          
00259                                                                   
00260      COMPUTE TEX-FACT-7 = (TEX-FACT-5 * TEX-FACT-5) +             
00261          (TEX-FACT-5 * CP-PAY-FREQUENCY) +                        
00262          (2 * (TEX-FACT-6 * (TEX-FACT-5 + CP-PAY-FREQUENCY))).    
00263                                                                   
00264      COMPUTE TEX-FACT-8 ROUNDED = TEX-FACT-7 / TEX-FACT-4.        
00265                                                                   
00266      COMPUTE STATE-UN-PREMIUM ROUNDED =                           
00267               CP-ORIGINAL-PREMIUM * TEX-FACT-8.                   
00268                                                                   
00269      MOVE STATE-UN-PREMIUM       TO  PR-UN-PREMIUM                
00270                                      R78-UN-PREMIUM               
00271                                      DOMI-UN-PREMIUM.             
00272                                                                   
00273  1160-EXIT.                                                       
00274  EJECT                                                            
00275                                                                   
00276 *---------------------------------------------------------------  
00277  1180-CALC-NET-PAY-UN.                                            
00278                                                                   
00279      IF CP-LOAN-APR = ZERO                                        
00280         MOVE '5'            TO  CP-EARNING-METHOD                 
00281         MOVE ZEROS          TO  DOMI-UN-PREMIUM                   
00282         MOVE ZEROS          TO  STATE-UN-PREMIUM                  
00283         GO TO 1180-EXIT.                                          
00284                                                                   
00285      MOVE CP-LOAN-APR       TO N-P-APR.                           
00286      MOVE CP-ORIGINAL-TERM  TO N-P-ORIG                           
00287                                N-P-CAP.                           
00288      MOVE CP-REMAINING-TERM TO N-P-REM.                           
00289                                                                   
00290      IF CP-TRUNCATED-LIFE                                         
00291         MOVE CP-LOAN-TERM   TO N-P-ORIG.                          
00292                                                                   
042904     IF CP-EARNING-METHOD = 'S'
      *       DISPLAY ' 1180 S '
042904        MOVE 'S'                 TO N-P-OPT
042904     ELSE
00293         MOVE 'R'                 TO N-P-OPT
042904     END-IF
00294                                                                   
00295      CALL 'ECSNETRM'                                              
00296           USING N-P-APR N-P-ORIG N-P-REM N-P-OPT N-P-CAP          
00297            N-P-FACTOR.                                            
00298                                                                   
00299 *****PERFORM 10000-NET-TERM THRU 99999-EXIT.                      
00300                                                                   
00301      COMPUTE STATE-UN-PREMIUM ROUNDED =                           
00302                  N-P-FACTOR * CP-ORIGINAL-PREMIUM.                
00303                                                                   
00304      MOVE STATE-UN-PREMIUM      TO  DOMI-UN-PREMIUM.              
00305                                                                   
040504     IF CP-EARNING-METHOD EQUAL 'B' OR 'S'
00307         NEXT SENTENCE                                             
00308      ELSE                                                         
00309         MOVE '5'                TO  CP-EARNING-METHOD.            
00310                                                                   
00311  1180-EXIT.                                                       
00312  EJECT                                                            
00313 *---------------------------------------------------------------  
00314  1200-DO-STATE-REGS.                                              
00315                                                                   
00316      IF CP-STATE-STD-ABBRV = 'AL'                                 
00317          MOVE R78-UN-PREMIUM       TO STATE-UN-PREMIUM            
00318          GO TO 1200-EXIT.                                         
00319                                                                   
00320      IF CP-STATE-STD-ABBRV = 'WY'                                 
00321          MOVE PR-UN-PREMIUM        TO STATE-UN-PREMIUM            
00322          GO TO 1200-EXIT.                                         
00323                                                                   
00324      IF CP-COMPANY-ID EQUAL 'NCL'                                 
00325         IF CP-EARN-AS-REG-BALLOON                                 
00326            IF CP-SPECIAL-CALC-CD EQUAL 'A'                        
00327               COMPUTE STATE-UN-PREMIUM EQUAL STATE-UN-PREMIUM +   
00328                                  PR-UN-PREM-ALT                   
00329               GO TO 1200-EXIT.                                    
00330                                                                   
00331      IF (CP-EARNING-METHOD = 'B' OR 'K' OR 'L')                   
00332          COMPUTE STATE-UN-PREMIUM =                               
00333             (R78-UN-PREMIUM  - R78-UN-PREM-ALT) + PR-UN-PREM-ALT  
00334          GO TO 1200-EXIT.                                         
00335                                                                   
00336      IF CP-EARN-AS-NET-PAY                                        
00337         AND CP-TRUNCATED-LIFE                                     
00338         AND CP-LOAN-TERM  LESS THAN CP-ORIGINAL-TERM              
00339          GO TO 1200-EXIT.                                         
00340                                                                   
00341      IF CP-EARN-AS-NET-PAY                                        
00342          IF CP-LOAN-APR NOT = ZEROS                               
00343              GO TO 1200-EXIT.                                     
00344                                                                   
00345      IF CP-BENEFIT-TYPE  = 'L' OR 'P'                             
00346          MOVE PR-UN-PREMIUM        TO STATE-UN-PREMIUM            
00347      ELSE                                                         
00348          MOVE R78-UN-PREMIUM       TO STATE-UN-PREMIUM.           
00349                                                                   
00350  1200-EXIT.                                                       
00351  EJECT                                                            
00352                                                                   
00353 *---------------------------------------------------------------  
00354  1300-DO-DOMICILE.                                                
00355                                                                   
00356      IF CP-DOMICILE-STATE = '  '  OR  '00'                        
00357          GO TO 1300-EXIT.                                         
00358                                                                   
00359      IF CP-DOMICILE-STATE = 'AL'                                  
00360          MOVE R78-UN-PREMIUM      TO DOMI-UN-PREMIUM              
00361          GO TO 1300-EXIT.                                         
00362                                                                   
00363      IF CP-DOMICILE-STATE = 'WY'                                  
00364          MOVE PR-UN-PREMIUM       TO DOMI-UN-PREMIUM              
00365          GO TO 1300-EXIT.                                         
00366                                                                   
00367      IF (CP-EARNING-METHOD = 'B' OR 'K' OR 'L')                   
00368         MOVE STATE-UN-PREMIUM   TO DOMI-UN-PREMIUM                
00369          GO TO 1300-EXIT.                                         
00370                                                                   
00371      IF CP-EARN-AS-NET-PAY                                        
00372         AND CP-TRUNCATED-LIFE                                     
00373         AND CP-LOAN-TERM  LESS THAN CP-ORIGINAL-TERM              
00374          GO TO 1300-EXIT.                                         
00375                                                                   
00376      IF CP-EARN-AS-NET-PAY                                        
00377          IF CP-LOAN-APR NOT = ZEROS                               
00378              GO TO 1300-EXIT.                                     
00379                                                                   
00380      IF CP-BENEFIT-TYPE  = 'L' OR 'P'                             
00381          MOVE PR-UN-PREMIUM       TO DOMI-UN-PREMIUM              
00382      ELSE                                                         
00383          MOVE R78-UN-PREMIUM      TO DOMI-UN-PREMIUM.             
00384                                                                   
00385  1300-EXIT.                                                       
00386                                                                   
00387  EJECT                                                            
00388 ******************** UNEARNED A&H  PREMIUM ***********************
00389  1500-CALC-UNEARNED-AH SECTION.                                   
00390                                                                   
00391      PERFORM 1510-AH-PRORATA-UNEARNED THRU 1510-EXIT.             
00392                                                                   
00393      PERFORM 1550-AH-RULE78S-UNEARNED  THRU 1550-EXIT.            
00394                                                                   
00395      IF CP-COMPANY-ID = 'NCL'                                     
00396          IF DC-GREG-DATE-CYMD LESS THAN 19910101                  
00397              MOVE ZEROS           TO CP-LOAN-APR                  
00398              IF CP-EARN-AS-NET-PAY                                
00399                  MOVE 'R'         TO CP-EARNING-METHOD.           
00400                                                                   
00401      IF CP-EARNING-METHOD EQUAL 'N'
00402         PERFORM 1180-CALC-NET-PAY-UN THRU 1180-EXIT
040504     END-IF
00403                                                                   
00404      PERFORM 1600-DO-STATE-AH         THRU 1699-EXIT.             
00405                                                                   
00406      PERFORM 1700-DO-DOM-STATE-AH     THRU 1799-EXIT.             
00407                                                                   
00408      MOVE DOMI-UN-PREMIUM         TO CP-DOMICILE-U-PRM.           
00409      MOVE STATE-UN-PREMIUM        TO CP-STATE-U-PRM.              
00410      MOVE PR-UN-PREMIUM           TO CP-PRORATA-U-PRM.            
00411      MOVE ZEROS                   TO CP-PRORATA-U-PRM-ALT.        
00412      MOVE R78-UN-PREMIUM          TO CP-R78-U-PRM.                
00413      MOVE ZEROS                   TO CP-R78-U-PRM-ALT.            
00414                                                                   
00415                                                                   
00416  1500-CALC-UNEARNED-X.                                            
00417      EXIT.                                                        
00418  EJECT                                                            
00419 *********************************************                     
00420 *                                           *                     
00421 * PERFORMED: A&H  UNEARNED PREMIUM ROUTINES *                     
00422 *                                           *                     
00423 *********************************************                     
00424                                                                   
00425  1510-AH-PRORATA-UNEARNED.                                        
00426                                                                   
00427      IF CP-COMPANY-ID EQUAL 'FLI' OR 'FLU' OR 'MIC' OR 'MCC'      
00428          COMPUTE PR-FACTOR ROUNDED =                              
00429                    (CP-REMAINING-TERM - .5) / CP-ORIGINAL-TERM    
00430      ELSE                                                         
00431          COMPUTE PR-FACTOR ROUNDED =                              
00432                        CP-REMAINING-TERM / CP-ORIGINAL-TERM.      
00433                                                                   
00434      COMPUTE PR-UN-PREMIUM ROUNDED =                              
00435                      CP-ORIGINAL-PREMIUM * PR-FACTOR.             
00436                                                                   
00437  1510-EXIT.                                                       
00438  EJECT                                                            
00439  1550-AH-RULE78S-UNEARNED.                                        
00440                                                                   
00441      COMPUTE R78-TEMP-1 =                                         
00442                 CP-REMAINING-TERM * (CP-REMAINING-TERM + +1).     
00443                                                                   
00444      IF CP-R78-OPTION = '1'                                       
00445          COMPUTE R78-TEMP-1 =                                     
00446                   CP-REMAINING-TERM * CP-REMAINING-TERM.          
00447                                                                   
00448      IF CP-COMPANY-ID = 'FLI'                                     
00449          COMPUTE R78-TEMP-1 =                                     
00450                   CP-REMAINING-TERM * CP-REMAINING-TERM.          
00451                                                                   
00452      IF CP-COMPANY-ID = 'MPP'                                     
00453          COMPUTE R78-TEMP-1 =                                     
00454                  (CP-ORIGINAL-TERM - CP-REMAINING-TERM)           
00455                  * ((CP-ORIGINAL-TERM - CP-REMAINING-TERM) + +1). 
00456                                                                   
00457      COMPUTE R78-TEMP-2 =                                         
00458                   CP-ORIGINAL-TERM * (CP-ORIGINAL-TERM + +1).     
00459                                                                   
00460      COMPUTE R78-FACTOR ROUNDED = R78-TEMP-1 / R78-TEMP-2.        
00461                                                                   
00462      COMPUTE R78-UN-PREMIUM ROUNDED =                             
00463                      CP-ORIGINAL-PREMIUM * R78-FACTOR.            
00464                                                                   
00465                                                                   
00466  1550-EXIT.                                                       
00467  EJECT                                                            
00468                                                                   
00469  1600-DO-STATE-AH.                                                
00470                                                                   
042904*    DISPLAY ' ELUPRMX MADE IT TO 1600 '
021904     IF CP-COMPANY-ID = 'CID'
021904        SET TABLE-INDEX TO +1                                        
021904                                                                  
021904        SEARCH MR-TABLE VARYING TABLE-INDEX                          
021904              AT END                                              
021904         MOVE 'P'                TO WS-CP-METHOD
021904                                    WS-AH-METHOD
021904              WHEN                                                
021904         T-STATE (TABLE-INDEX) = CP-STATE-STD-ABBRV
021904             MOVE CP-METHOD-CODE (TABLE-INDEX) TO WS-CP-METHOD
021904             MOVE AH-METHOD-CODE (TABLE-INDEX) TO WS-AH-METHOD
021904        END-SEARCH

021904        IF CP-CRITICAL-PERIOD
021904           IF WS-CP-METHOD = 'P'
021904              MOVE PR-UN-PREMIUM TO STATE-UN-PREMIUM
021904           ELSE
021904              IF WS-CP-METHOD = 'R'
021904                 MOVE R78-UN-PREMIUM
021904                                 TO STATE-UN-PREMIUM
021904              ELSE
021904                 GO TO 1680-DO-AHMEAN
021904              END-IF
021904           END-IF
021904        END-IF

021904        IF CP-CRITICAL-PERIOD
021904           IF (CP-STATE-STD-ABBRV = 'FL' OR 'MO')
021904              AND (DC-GREG-DATE-CYMD > 20030630)
021904              MOVE PR-UN-PREMIUM TO STATE-UN-PREMIUM
021904           END-IF
021904           GO TO 1699-EXIT
021904        END-IF

021904        IF WS-AH-METHOD = 'P'
021904           MOVE PR-UN-PREMIUM    TO STATE-UN-PREMIUM
021904        ELSE
021904           IF WS-AH-METHOD = 'R'
021904              MOVE R78-UN-PREMIUM
                                       TO STATE-UN-PREMIUM
021904           ELSE
021904              IF WS-AH-METHOD = 'M'
021904                 GO TO 1680-DO-AHMEAN
021904              END-IF
021904           END-IF
021904        END-IF
021904           
010405*       IF (CP-STATE-STD-ABBRV = 'VA')
010405*          AND (DC-GREG-DATE-CYMD > 20020630)
010405*          GO TO 1680-DO-AHMEAN
010405*       END-IF
010405
021904        IF (CP-STATE-STD-ABBRV = 'IN')
021904           AND (DC-GREG-DATE-CYMD > 20021231)
021904              MOVE PR-UN-PREMIUM TO STATE-UN-PREMIUM
021904        ELSE
021904           IF (CP-STATE-STD-ABBRV = 'ND')
021904              AND (DC-GREG-DATE-CYMD > 20011231)
021904                 MOVE PR-UN-PREMIUM
                                       TO STATE-UN-PREMIUM
062806*          ELSE
062806*             IF (CP-STATE-STD-ABBRV = 'OH')
062806*                AND (CP-EARN-AS-NET-PAY)
062806*                   MOVE PR-UN-PREMIUM
062806*                                TO STATE-UN-PREMIUM
062806*             END-IF
021904           END-IF
021904        END-IF
              GO TO 1699-EXIT
           END-IF

00401      IF CP-EARNING-METHOD EQUAL 'S'
      *       DISPLAY ' ELUPRMX EM S '
00402         PERFORM 1180-CALC-NET-PAY-UN
                                       THRU 1180-EXIT
040504        GO TO 1699-EXIT
040504     END-IF
00403                                                                   
100703     IF (CP-COMPANY-ID = 'DCC')
092705        AND (CP-BEN-CATEGORY = 'G' OR 'L')
100703        MOVE R78-UN-PREMIUM      TO STATE-UN-PREMIUM
100703        GO TO 1699-EXIT
100703     END-IF

00471      IF (CP-COMPANY-ID = 'CSO'  OR  'CID')  AND                   
00472          CP-STATE-STD-ABBRV = 'CO'                                
00473          IF DC-GREG-DATE-CYMD < 19970101                  
00474              MOVE R78-UN-PREMIUM  TO STATE-UN-PREMIUM             
00475              GO TO 1699-EXIT                                      
00476          ELSE                                                     
CIDMOD*            COMPUTE STATE-UN-PREMIUM =                           
CIDMOD             COMPUTE STATE-UN-PREMIUM ROUNDED =                   
00478                  (PR-UN-PREMIUM + R78-UN-PREMIUM ) / +2           
00479              GO TO 1699-EXIT.                                     
00480                                                                   
00481 *    IF (CP-STATE-STD-ABBRV = 'VA')
021904*       AND (DC-GREG-DATE-CYMD > 20020630)
021904*       GO TO 1680-DO-AHMEAN
021904*    END-IF
00486                                                                   
021904     IF (CP-STATE-STD-ABBRV = 'NM')
021904        AND (DC-GREG-DATE-CYMD > 19980831)
021904        GO TO 1680-DO-AHMEAN
021904     END-IF
021904                                                                  
021904     IF CP-STATE-STD-ABBRV = 'GA'                                 
021904        GO TO 1680-DO-AHMEAN
021904     END-IF
021904                                                                  
021904     IF CP-STATE-STD-ABBRV = 'IN'
021904        IF DC-GREG-DATE-CYMD > 20021231
021904           MOVE PR-UN-PREMIUM    TO STATE-UN-PREMIUM
021904        ELSE
021904           MOVE R78-UN-PREMIUM   TO STATE-UN-PREMIUM
021904        END-IF
021904        GO TO 1699-EXIT
021904     END-IF
021904                                                                  
00487      IF CP-STATE-STD-ABBRV  = 'OH'                                
00488          GO TO 1650-DO-OHIO.                                      
00489                                                                   
00490      IF CP-STATE-STD-ABBRV  = 'OK'                                
00491          GO TO 1660-DO-OKLAHOMA.                                  
00492                                                                   
00493      IF CP-STATE-STD-ABBRV  = 'TX'                                
00494          GO TO 1670-DO-TEXAS.                                     
00495                                                                   
00496      IF CP-COMPANY-ID EQUAL 'CSL'                                 
00497         IF CP-CARRIER EQUAL '2'                                   
021904           GO TO 1680-DO-AHMEAN.                                  
00499                                                                   
00500      IF CP-COMPANY-ID EQUAL 'GIC'                                 
00501        IF (CP-EARNING-METHOD NOT = 'B' AND 'K' AND 'L')           
00502          COMPUTE STATE-UN-PREMIUM =                               
00503             (PR-UN-PREMIUM * .80) + (R78-UN-PREMIUM  * .20)       
00504          GO TO 1699-EXIT.                                         
00505                                                                   
00506      IF CP-EARN-AS-NET-PAY                                        
00507          IF CP-LOAN-APR NOT = ZEROS                               
00508              GO TO 1699-EXIT.                                     
00509                                                                   
00510  1600-CONTINUE.                                                   
00511                                                                   
00512      MOVE R78-UN-PREMIUM           TO STATE-UN-PREMIUM.           
00513      MOVE +0                       TO SRL.                        
00514                                                                   
00515  1610-TABLE-LOOKUP.                                               
00516      ADD +1                        TO SRL.                        
00517                                                                   
00518      IF AH-RES-STATE-ENT (SRL) = HIGH-VALUES                      
00519          GO TO 1699-EXIT.                                         
00520                                                                   
00521      IF CP-STATE-STD-ABBRV  = AH-RES-STATE-ENT (SRL)              
00522          MOVE PR-UN-PREMIUM        TO STATE-UN-PREMIUM            
00523          GO TO 1699-EXIT.                                         
00524                                                                   
00525      GO TO 1610-TABLE-LOOKUP.                                     
00526                                                                   
00527  1650-DO-OHIO.                                                    
00528                                                                   
00529      IF DC-GREG-DATE-CYMD LESS THAN 19831101                      
00530          MOVE PR-UN-PREMIUM        TO STATE-UN-PREMIUM            
00531      ELSE                                                         
00532          COMPUTE STATE-UN-PREMIUM =                               
00533              (PR-UN-PREMIUM + R78-UN-PREMIUM ) / +2.              
00534                                                                   
00535      GO TO 1699-EXIT.                                             
00536                                                                   
00537  1660-DO-OKLAHOMA.                                                
00538      IF DC-GREG-DATE-CYMD LESS THAN 19820630                      
00539          MOVE R78-UN-PREMIUM        TO STATE-UN-PREMIUM           
00540      ELSE                                                         
00541          COMPUTE STATE-UN-PREMIUM =                               
00542              (PR-UN-PREMIUM + R78-UN-PREMIUM ) / +2.              
00543                                                                   
00544      GO TO 1699-EXIT.                                             
00545                                                                   
00546  1670-DO-TEXAS.                                                   
00547                                                                   
00548      IF CP-COMPANY-ID  = 'ABL' AND CP-CARRIER NOT EQUAL '2'       
00549          COMPUTE STATE-UN-PREMIUM =                               
00550              (PR-UN-PREMIUM + R78-UN-PREMIUM ) / +2               
00551          GO TO 1699-EXIT.                                         
00552                                                                   
00553      IF DC-GREG-DATE-CYMD LESS THAN 19830901                      
00554          MOVE R78-UN-PREMIUM        TO STATE-UN-PREMIUM           
00555      ELSE                                                         
00556          COMPUTE STATE-UN-PREMIUM =                               
00557              (PR-UN-PREMIUM + R78-UN-PREMIUM ) / +2.              
00558                                                                   
00559      GO TO 1699-EXIT.                                             
00560                                                                   
00561  1680-DO-AHMEAN.
00562                                                                   
00563      COMPUTE STATE-UN-PREMIUM EQUAL                               
00564              (PR-UN-PREMIUM + R78-UN-PREMIUM) / +2.               
00565                                                                   
00566  1699-EXIT.                                                       
00567  EJECT                                                            
00568                                                                   
00569  1700-DO-DOM-STATE-AH.                                            
00570                                                                   
00571      IF CP-DOMICILE-STATE = '  '  OR  '00'                        
00572          GO TO 1799-EXIT.                                         
00573                                                                   
00574      IF (CP-COMPANY-ID = 'CSO'  OR  'CID')  AND                   
00575          CP-DOMICILE-STATE = 'CO'                                 
00576          IF DC-GREG-DATE-CYMD LESS THAN 19970101                  
00577              MOVE R78-UN-PREMIUM  TO DOMI-UN-PREMIUM              
00578              GO TO 1799-EXIT                                      
00579          ELSE                                                     
CIDMOD*            COMPUTE DOMI-UN-PREMIUM =                            
CIDMOD             COMPUTE DOMI-UN-PREMIUM ROUNDED =                    
00581                  (PR-UN-PREMIUM + R78-UN-PREMIUM ) / +2           
00582              GO TO 1799-EXIT.                                     
00583                                                                   
00584 *    IF (CP-STATE-STD-ABBRV = 'VA') AND                           
00585 *       (DC-GREG-DATE-CYMD GREATER THAN 19921231) AND             
00586 *       (CP-ORIGINAL-TERM LESS THAN 62)                           
00587 *         MOVE 'R'               TO  CP-EARNING-METHOD            
00588 *         GO TO 1700-CONTINUE.                                    
00589                                                                   
00590      IF CP-DOMICILE-STATE = 'OH'                                  
00591          GO TO 1750-DO-DOM-OHIO.                                  
00592                                                                   
00593      IF CP-DOMICILE-STATE = 'OK'                                  
00594          GO TO 1760-DO-DOM-OKLAHOMA.                              
00595                                                                   
00596      IF CP-DOMICILE-STATE = 'TX'                                  
00597          GO TO 1770-DO-DOM-TEXAS.                                 
00598                                                                   
00599      IF CP-EARN-AS-NET-PAY                                        
00600          IF CP-LOAN-APR NOT = ZEROS                               
00601              GO TO 1799-EXIT.                                     
00602                                                                   
00603  1700-CONTINUE.                                                   
00604                                                                   
00605      MOVE R78-UN-PREMIUM        TO DOMI-UN-PREMIUM.               
00606      MOVE +0                    TO SRL.                           
00607                                                                   
00608  1710-TABLE-LOOKUP.                                               
00609      ADD +1                     TO SRL.                           
00610                                                                   
00611      IF AH-RES-STATE-ENT (SRL) = HIGH-VALUES                      
00612          GO TO 1799-EXIT.                                         
00613                                                                   
00614      IF CP-DOMICILE-STATE = AH-RES-STATE-ENT (SRL)                
00615          MOVE PR-UN-PREMIUM      TO DOMI-UN-PREMIUM               
00616          GO TO 1799-EXIT.                                         
00617                                                                   
00618      GO TO 1710-TABLE-LOOKUP.                                     
00619                                                                   
00620  1750-DO-DOM-OHIO.                                                
00621      IF DC-GREG-DATE-CYMD LESS THAN 19831101                      
00622          MOVE PR-UN-PREMIUM      TO DOMI-UN-PREMIUM               
00623      ELSE                                                         
00624          COMPUTE DOMI-UN-PREMIUM =                                
00625              (PR-UN-PREMIUM + R78-UN-PREMIUM ) / +2.              
00626                                                                   
00627      GO TO 1799-EXIT.                                             
00628                                                                   
00629  1760-DO-DOM-OKLAHOMA.                                            
00630                                                                   
00631      IF DC-GREG-DATE-CYMD LESS THAN 19820630                      
00632          MOVE R78-UN-PREMIUM    TO DOMI-UN-PREMIUM                
00633      ELSE                                                         
00634          COMPUTE DOMI-UN-PREMIUM =                                
00635              (PR-UN-PREMIUM + R78-UN-PREMIUM ) / +2.              
00636                                                                   
00637      GO TO 1799-EXIT.                                             
00638                                                                   
00639  1770-DO-DOM-TEXAS.                                               
00640                                                                   
00641      IF DC-GREG-DATE-CYMD LESS THAN 19830901                      
00642          MOVE R78-UN-PREMIUM    TO DOMI-UN-PREMIUM                
00643      ELSE                                                         
00644          COMPUTE DOMI-UN-PREMIUM =                                
00645              (PR-UN-PREMIUM + R78-UN-PREMIUM ) / +2.              
00646                                                                   
00647  1799-EXIT.                                                       
00648                                                                   
00649                                                                   
00650  1999-EXIT-CALC-ROUTINE.                                          
00651                                                                   
