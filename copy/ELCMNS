00001 ***************************************************************** 04/15/98
00002 *                                                               * ELCMNS
00002 *                                                               * ELCMNS
00003 *                            ELCMNS.                            *    LV003
00004 *           COPYBOOK REVIEWED FOR YEAR 2000 COMPLIANCE               CL**2
00005 *                            VMOD=2.002                         *    CL**3
00006 *                                                               * ELCMNS
00007 *                     M O V E   N A M E   R O U T I N E         * ELCMNS
00008 *                                                               * ELCMNS
00009 *                THE FOLLOWING ROUTINE MOVES THE INSURRED'S     * ELCMNS
00010 *            NAME FROM THE CLAIM MASTER TO A WORK AREA WITH     * ELCMNS
00011 *            NO EMBEDDED BLANKS.                                * ELCMNS
00012 *                                                               * ELCMNS
00013 *                  FIELD               VALUE                    * ELCMNS
00014 *                                                               * ELCMNS
00015 *                LAST NAME (CL15)      SMITH                    * ELCMNS
00016 *                1ST NAME  (CL12)      JOHN                     * ELCMNS
00017 *                MID NAME  (CL12)      ALLEN                    * ELCMNS
00018 *                                                               * ELCMNS
00019 *                AFTER NAME HAS BEEN MOVED WS-NAME-WORK (CL30)  * ELCMNS
00020 *                                                               * ELCMNS
00021 *                        SMITH, JOHN ALLEN                      * ELCMNS
00022 *                                                               * ELCMNS
00023 *                TO USE THIS ROUTINE YOU ALSO NEED A WORKING    * ELCMNS
00024 *            STORAGE COPYBOOK:                                  * ELCMNS
00025 *                                                               * ELCMNS
00026 *                01  WS-NAME-WORK-AREA COPY ELCNWA.             * ELCMNS
00027 *                                                               * ELCMNS
00028 *****************************************************************.ELCMNS
00029                                                                   ELCMNS
00030      MOVE SPACES                 TO  WS-NAME-WORK-AREA.           ELCMNS
00031      MOVE ZERO                   TO  WS-NAME-SW.                  ELCMNS
00032      SET NWA-INDEX TO +1.                                         ELCMNS
00033                                                                   ELCMNS
00034      IF CL-INSURED-1ST-NAME = SPACES  AND                         ELCMNS
00035         CL-INSURED-MID-INIT = SPACES                              ELCMNS
00036          MOVE +1                 TO  WS-NAME-SW.                  ELCMNS
00037                                                                   ELCMNS
00038      MOVE CL-INSURED-LAST-NAME  TO  WS-NAME-WORK2.                ELCMNS
00039      PERFORM 5100-MOVE-NAME THRU 5190-EXIT.                       ELCMNS
00040                                                                   ELCMNS
00041      MOVE CL-INSURED-1ST-NAME   TO  WS-NAME-WORK2.                ELCMNS
00042      PERFORM 5100-MOVE-NAME THRU 5190-EXIT.                       ELCMNS
00043                                                                   ELCMNS
00044      SET NWA-INDEX UP BY +1.                                      ELCMNS
00045      MOVE CL-INSURED-MID-INIT   TO  WS-NAME-WORK2.                ELCMNS
00046      PERFORM 5100-MOVE-NAME THRU 5190-EXIT.                       ELCMNS
00047                                                                   ELCMNS
00048  5000-EXIT.                                                       ELCMNS
00049      EXIT.                                                        ELCMNS
00050                                                                   ELCMNS
00051      EJECT                                                        ELCMNS
00052  5100-MOVE-NAME SECTION.                                          ELCMNS
00053      IF WS-NAME-SW GREATER THAN +1                                ELCMNS
00054          GO TO 5190-EXIT.                                         ELCMNS
00055                                                                   ELCMNS
00056      IF WS-NAME-WORK2 = SPACES                                    ELCMNS
00057          GO TO 5190-EXIT.                                         ELCMNS
00058                                                                   ELCMNS
00059      SET NWA-INDEX2 TO +1.                                        ELCMNS
00060      SET NWA-INDEX3 TO +2.                                        ELCMNS
00061                                                                   ELCMNS
00062  5110-MOVE-NAME.                                                  ELCMNS
00063      MOVE WS-NW2 (NWA-INDEX2)  TO  WS-NW (NWA-INDEX).             ELCMNS
00064                                                                   ELCMNS
00065      IF NWA-INDEX LESS THAN +30                                   ELCMNS
00066          SET NWA-INDEX UP BY +1                                   ELCMNS
00067        ELSE                                                       ELCMNS
00068          ADD +2  TO  WS-NAME-SW                                   ELCMNS
00069          GO TO 5190-EXIT.                                         ELCMNS
00070                                                                   ELCMNS
00071      IF NWA-INDEX2 LESS THAN +20                                  ELCMNS
00072          SET NWA-INDEX3 UP BY +1                                  ELCMNS
00073          SET NWA-INDEX2 UP BY +1.                                 ELCMNS
00074                                                                   ELCMNS
00075      IF WS-NW2 (NWA-INDEX2) = SPACES AND                          ELCMNS
00076         WS-NW2 (NWA-INDEX3) = SPACES                              ELCMNS
00077          IF WS-NAME-SW = ZERO                                     ELCMNS
00078              MOVE ','            TO  WS-NW (NWA-INDEX)            ELCMNS
00079              SET NWA-INDEX UP BY +2                               ELCMNS
00080              MOVE +1             TO  WS-NAME-SW                   ELCMNS
00081              GO TO 5190-EXIT                                      ELCMNS
00082            ELSE                                                   ELCMNS
00083              GO TO 5190-EXIT.                                     ELCMNS
00084                                                                   ELCMNS
00085      GO TO 5110-MOVE-NAME.                                        ELCMNS
00086                                                                   ELCMNS
00087  5190-EXIT.                                                       ELCMNS
00088      EXIT.                                                        ELCMNS
00089                                                                   ELCMNS
