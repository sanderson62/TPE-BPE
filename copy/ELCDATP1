00001 ***************************************************************** 05/29/98
00002 *                                                               * ELCDATP1
00002 *                                                               * ELCDATP1
00003 *                            ELCDATP1.                          *    LV023
00004 *           COPYBOOK REVIEWED FOR YEAR 2000 COMPLIANCE               CL**4
00005 *                            VMOD=2.002                         * ELCDATP1
00006 *                                                               * ELCDATP1
00007 ***************************************************************** ELCDATP1
00008                                                                   ELCDATP1
CIDMOD     IF NOT FIND-CENTURY
CIDMOD        MOVE GREG-ALPHA-MASK   TO DC-GREG-DATE-1-ALPHA
CIDMOD     END-IF
CIDMOD
00009      MOVE 2                    TO FIND-CENTURY-FLAG.                 CL**5
00010      MOVE CURRENT-CENTURY-1-N  TO SEARCH-CENTURY-N                ELCDATP1
00011      PERFORM 0800-FIND-CENTURY.                                   ELCDATP1
00012                                                                      CL**6
00013      MOVE FOUND-CENTURY-CD-N   TO CONTROL-YEAR-CD                    CL**5
00014                                   W-CD-YEAR-CD.                      CL*18
00015 *Y2K                                                                 CL*18
00016 *                                 W-FLOAT-YEAR-CD.                   CL*18
00017                                                                      CL*19
00018      COMPUTE BREAK-PERIOD = (FOUND-CENTURY-CD-N * 100) + 69          CL*20
00019                                                                      CL*19
00020      EVALUATE TRUE                                                   CL*11
00021         WHEN W-CD-CCYY < BREAK-PERIOD                                CL*22
00022            COMPUTE CONTROL-YEAR-CD = FOUND-CENTURY-CD-N - 1          CL*16
00023            ADD +1 TO CURRENT-CENTURY-1-N                             CL*16
00024                             GIVING NEXT-CENTURY                      CL*16
00025            SUBTRACT +1 FROM CURRENT-CENTURY-1-N                      CL*16
00026                             GIVING PREVIOUS-CENTURY                  CL*16
00027         WHEN OTHER                                                   CL*11
00028            MOVE FOUND-CENTURY-CD-N TO CONTROL-YEAR-CD                CL*11
00029            COMPUTE CENTURY-MARK = 100 +                              CL*15
00030                            (FOUND-CENTURY-CD-N * 100)                CL*16
00031            ADD +1 TO CURRENT-CENTURY-1-N                             CL*11
00032                             GIVING NEXT-CENTURY                      CL*11
00033            SUBTRACT +1 FROM CURRENT-CENTURY-1-N                      CL*11
00034                             GIVING PREVIOUS-CENTURY                  CL*11
00035      END-EVALUATE.                                                   CL*11
00036                                                                   ELCDATP1
00037      MOVE SPACES                 TO DC-ERROR-CODE.                ELCDATP1
00038                                                                   ELCDATP1
00039      MOVE ZERO                   TO  BIN-DATE.                    ELCDATP1
00040                                                                      CL**6
00041      EVALUATE TRUE                                                ELCDATP1
00042         WHEN BIN-TO-GREG                                          ELCDATP1
00043              OR ELAPSED-BETWEEN-BIN                               ELCDATP1
00044              OR BIN-PLUS-ELAPSED                                  ELCDATP1
00045            MOVE DC-BIN-DATE-1    TO  BIN-DATE-WORK                ELCDATP1
00046         WHEN BIN-3-TO-GREG                                        ELCDATP1
00047              OR ELAPSED-BETWEEN-BIN-3                             ELCDATP1
00048              OR BIN-TO-JULIAN-EXPANDED                            ELCDATP1
00049              OR BIN-PLUS-ELAPSED-3                                ELCDATP1
00050            MOVE DC-BIN-DATE-EXPAND-1 TO BIN-DATE-3-WORK           ELCDATP1
00051      END-EVALUATE.                                                ELCDATP1
00052                                                                      CL**6
00053      MOVE BIN-DATE               TO  WS-BINARY-DATE-1.            ELCDATP1
00054                                                                   ELCDATP1
00055      MOVE ZERO                   TO  BIN-DATE.                    ELCDATP1
00056                                                                      CL**6
00057      EVALUATE TRUE                                                ELCDATP1
00058         WHEN ELAPSED-BETWEEN-BIN                                  ELCDATP1
00059            MOVE DC-BIN-DATE-2          TO  BIN-DATE-WORK          ELCDATP1
00060         WHEN ELAPSED-BETWEEN-BIN-3                                ELCDATP1
00061            MOVE DC-BIN-DATE-EXPAND-2 TO BIN-DATE-3-WORK           ELCDATP1
00062      END-EVALUATE.                                                ELCDATP1
00063                                                                   ELCDATP1
00064      MOVE BIN-DATE               TO  WS-BINARY-DATE-2.            ELCDATP1
00065                                                                   ELCDATP1
00066      MOVE DC-ELAPSED-MONTHS      TO  WS-ELAPSED-MONTHS.           ELCDATP1
00067      MOVE DC-ELAPSED-DAYS        TO  WS-ELAPSED-DAYS.             ELCDATP1
00068                                                                   ELCDATP1
00069      EVALUATE TRUE                                                ELCDATP1
00070         WHEN JULIAN-TO-BIN OR                                        CL**6
00071              JULIAN-TO-BIN-3                                         CL**6
00072            MOVE DC-JULIAN-YEAR   TO  JULIAN-YEAR1                 ELCDATP1
00073            MOVE DC-JULIAN-DAYS   TO  JULIAN-DAY1                  ELCDATP1
00074         WHEN JULIAN-EXPANDED-TO-BIN OR                               CL**6
00075              JULIAN-EXPANDED-TO-BIN-3                                CL**6
00076            MOVE DC-JULIAN-1-CC   TO DC-ALPHA-CEN-N                ELCDATP1
00077            MOVE DC-JULIAN-1-YR   TO JULIAN-YEAR1                  ELCDATP1
00078            MOVE DC-JULIAN-DA-1   TO JULIAN-DAY1                      CL**5
00079      END-EVALUATE.                                                ELCDATP1
00080                                                                   ELCDATP1
00081      EVALUATE TRUE                                                ELCDATP1
00082         WHEN BIN-PLUS-ELAPSED OR                                     CL**6
00083              BIN-PLUS-ELAPSED-3                                      CL**6
00084            PERFORM 0700-CALCULATE-END-DATE                        ELCDATP1
00085         WHEN BIN-TO-GREGORIAN                                        CL**2
00086            PERFORM 0300-REFORMAT-BINARY-DAYS                      ELCDATP1
00087         WHEN GREGORIAN-TO-BIN                                        CL**2
00088            PERFORM 0400-REFORMAT-GREGORIAN-DATE                   ELCDATP1
00089         WHEN JULIAN-TO-BINARY                                        CL**2
00090            PERFORM 0500-REFORMAT-JULIAN-DATE                      ELCDATP1
00091         WHEN FIND-CENTURY                                         ELCDATP1
00092            MOVE SPACE         TO DC-ERROR-CODE                       CL**5
00093            MOVE 0             TO YEAR1                               CL**8
00094            MOVE 3             TO FIND-CENTURY-FLAG                   CL**8
00095            MOVE DC-ALPHA-YEAR TO YEAR1                               CL**9
pemuni*          display ' cpdbebug task prior if ' eibtaskn
00096            IF YEAR1 < W-FLOAT-YEAR                                   CL*10
pemuni*             display ' cpdebug task inside if ' eibtaskn
00097               ADD 100 TO YEAR1                                       CL*10
00098            END-IF                                                    CL*10
pemuni*          display ' cpdebug task prior  ' eibtaskn
00099            PERFORM 0800-FIND-CENTURY                              ELCDATP1
pemuni*          display ' cpdebug task after  ' eibtaskn
00100            IF NO-CONVERSION-ERROR                                 ELCDATP1
00101               MOVE 'B'        TO DC-ERROR-CODE                       CL**5
00102            END-IF                                                 ELCDATP1
00103         WHEN CHECK-LEAP-YEAR                                      ELCDATP1
00104            MOVE HOLD-CEN-1-YY TO CHECK-YEAR                       ELCDATP1
00105            MOVE HOLD-CEN-1-CC TO SEARCH-CENTURY                   ELCDATP1
00106            MOVE 2             TO FIND-CENTURY-FLAG                   CL*23
00107            PERFORM 0800-FIND-CENTURY                              ELCDATP1
00108            IF NO-CONVERSION-ERROR                                 ELCDATP1
00109               PERFORM 0900-CHECK-FOR-LEAP-YEAR THRU 0900-EXIT     ELCDATP1
00110               IF A-LEAP-YEAR                                      ELCDATP1
00111                  MOVE LEAP-YR-DAYS (2) TO HOLD-CEN-1-DA              CL**3
00112               ELSE                                                ELCDATP1
00113                  MOVE REGULAR-DAYS (2) TO HOLD-CEN-1-DA              CL**3
00114               END-IF                                              ELCDATP1
00115               MOVE 'C'        TO DC-ERROR-CODE                       CL**5
00116            END-IF                                                 ELCDATP1
00117         WHEN OTHER                                                ELCDATP1
00118            MOVE '9'           TO DC-ERROR-CODE                       CL**5
00119      END-EVALUATE.                                                ELCDATP1
00120                                                                   ELCDATP1
00121      IF NO-CONVERSION-ERROR                                       ELCDATP1
00122          PERFORM 0200-DATE-CONVERSION-ROUTINES.                   ELCDATP1
00123                                                                   ELCDATP1
