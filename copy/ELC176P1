00001 ***************************************************************** 04/19/98
00002 *                                                               * ELC176P1
00003 *                            ELC176P1.                          *    LV002
00004 *           COPYBOOK REVIEWED FOR YEAR 2000 COMPLIANCE               CL**2
00005 *                            VMOD=2.003                         *    CL**2
00006 *                                                               * ELC176P1
00007 * THIS SECTION CONVERTS A DOLLAR FIGURE INTO A SPELLED OUT AMT. * ELC176P1
00002 *                                                               * ELC176P1
00008 ***************************************************************** ELC176P1
00009                                                                   ELC176P1
00010  SDS-010.                                                         ELC176P1
00011      MOVE SPACES                 TO  WS-SPELLED-AMOUNT            ELC176P1
00012                                      SD-PASS-SPELLED-AMOUNT       ELC176P1
00013                                      WS-SPELLED-LINE1             ELC176P1
00014                                      WS-SPELLED-LINE2.            ELC176P1
00015                                                                   ELC176P1
00016      SET SA-INDEX TO +1.                                          ELC176P1
00017                                                                   ELC176P1
00018      MOVE SD-PASS-AMOUNT         TO  WS-AMOUNT.                   ELC176P1
00019                                                                   ELC176P1
00020      IF WS-MILLIONS GREATER ZERO                                  ELC176P1
00021          MOVE WS-MILLIONS        TO  WS-AMOUNT-WORK               ELC176P1
00022          PERFORM SPELL-AMOUNT                                     ELC176P1
00023          MOVE 'MILLION'          TO  WS-WORD                      ELC176P1
00024          PERFORM MOVE-WORD.                                       ELC176P1
00025                                                                   ELC176P1
00026      IF WS-THOUSANDS GREATER ZERO                                 ELC176P1
00027          MOVE WS-THOUSANDS       TO  WS-AMOUNT-WORK               ELC176P1
00028          PERFORM SPELL-AMOUNT                                     ELC176P1
00029          MOVE 'THOUSAND'         TO  WS-WORD                      ELC176P1
00030          PERFORM MOVE-WORD.                                       ELC176P1
00031                                                                   ELC176P1
00032      IF WS-HUNDREDS GREATER ZERO                                  ELC176P1
00033          MOVE WS-HUNDREDS        TO  WS-AMOUNT-WORK               ELC176P1
00034          PERFORM SPELL-AMOUNT.                                    ELC176P1
00035                                                                   ELC176P1
00036      IF WS-AMOUNT LESS +1.00                                      ELC176P1
00037          MOVE 'NO'               TO  WS-WORD                      ELC176P1
00038          PERFORM MOVE-WORD.                                       ELC176P1
00039                                                                   ELC176P1
00040      IF WS-CENTS NOT GREATER ZERO                                 ELC176P1
00041          MOVE 'NO'               TO  WS-CENTS-X.                  ELC176P1
00042                                                                   ELC176P1
00043      MOVE WS-CENTS-X             TO  WS-PENNEYS.                  ELC176P1
00044                                                                   ELC176P1
00045      MOVE WS-DOLLARS-AND-CENTS   TO  WS-WORD.                     ELC176P1
00046      PERFORM MOVE-WORD.                                           ELC176P1
00047                                                                   ELC176P1
00048      INSPECT WS-SPELLED-AMOUNT REPLACING ALL '-' BY SPACES.       ELC176P1
00049                                                                   ELC176P1
00050      MOVE WS-SPELLED-AMOUNT      TO  SD-PASS-SPELLED-AMOUNT.      ELC176P1
00051                                                                   ELC176P1
00052      PERFORM MOVE-SPELLED-AMOUNT.                                 ELC176P1
00053                                                                   ELC176P1
00054  SDS-EXIT.                                                        ELC176P1
00055      EXIT.                                                        ELC176P1
00056                                                                   ELC176P1
00057      EJECT                                                        ELC176P1
00058  SPELL-AMOUNT SECTION.                                            ELC176P1
00059                                                                   ELC176P1
00060 *SAS-NOTE.                                                        ELC176P1
00061 *                                                                 ELC176P1
00062 *    NOTE ******************************************************* ELC176P1
00063 *         *      THIS SECTION CONVERTS A THREE DIGIT NUMBER     * ELC176P1
00064 *         *  INTO A SPELLED AMOUNT.                             * ELC176P1
00065 *         ******************************************************* ELC176P1
00066                                                                   ELC176P1
00067  SAS-010.                                                         ELC176P1
00068      IF WS-HUNDRED GREATER ZERO                                   ELC176P1
00069          SET SINGLE-INDEX        TO  WS-HUNDRED                   ELC176P1
00070          MOVE WS-SINGLE-DESC (SINGLE-INDEX)  TO  WS-WORD          ELC176P1
00071          PERFORM MOVE-WORD                                        ELC176P1
00072          MOVE 'HUNDRED'          TO  WS-WORD                      ELC176P1
00073          PERFORM MOVE-WORD.                                       ELC176P1
00074                                                                   ELC176P1
00075      IF WS-TEEN GREATER ZERO                                      ELC176P1
00076          IF WS-TEEN LESS +20                                      ELC176P1
00077              SET SINGLE-INDEX TO WS-TEEN                          ELC176P1
00078              MOVE WS-SINGLE-DESC (SINGLE-INDEX)  TO  WS-WORD      ELC176P1
00079              PERFORM MOVE-WORD                                    ELC176P1
00080            ELSE                                                   ELC176P1
00081              SET UPPER-INDEX TO WS-TEN                            ELC176P1
00082              MOVE WS-UPPER-DESC (UPPER-INDEX)  TO  WS-WORD        ELC176P1
00083              PERFORM MOVE-WORD                                    ELC176P1
00084              IF WS-ONE GREATER ZERO                               ELC176P1
00085                  SET SINGLE-INDEX TO WS-ONE                       ELC176P1
00086                  MOVE WS-SINGLE-DESC (SINGLE-INDEX)  TO  WS-WORD  ELC176P1
00087                  PERFORM MOVE-WORD.                               ELC176P1
00088                                                                   ELC176P1
00089  SAS-EXIT.                                                        ELC176P1
00090      EXIT.                                                        ELC176P1
00091                                                                   ELC176P1
00092      EJECT                                                        ELC176P1
00093  MOVE-WORD SECTION.                                               ELC176P1
00094                                                                   ELC176P1
00095 *MWS-NOTE.                                                        ELC176P1
00096 *                                                                 ELC176P1
00097 *    NOTE ******************************************************* ELC176P1
00098 *         *      THIS SECTION MOVES ONE WORD TO THE SPELLED     * ELC176P1
00099 *         *  AMOUNT OUTPUT LINE.                                * ELC176P1
00100 *         *******************************************************.ELC176P1
00101                                                                   ELC176P1
00102  MWD-010.                                                         ELC176P1
00103      PERFORM MOVE-CHARACTERS                                      ELC176P1
00104          VARYING CHAR-INDEX FROM +1 BY +1                         ELC176P1
00105              UNTIL WS-CHAR2 (CHAR-INDEX) = SPACES.                ELC176P1
00106                                                                   ELC176P1
00107      SET SA-INDEX UP BY +1.                                       ELC176P1
00108                                                                   ELC176P1
00109  MWD-EXIT.                                                        ELC176P1
00110      EXIT.                                                        ELC176P1
00111                                                                   ELC176P1
00112  MOVE-CHARACTERS SECTION.                                         ELC176P1
00113                                                                   ELC176P1
00114 *MCS-NOTE.                                                        ELC176P1
00115 *                                                                 ELC176P1
00116 *    NOTE ******************************************************* ELC176P1
00117 *         *      THIS SECTION MOVES ONE CHARACTER TO THE SPELLED* ELC176P1
00118 *         *  AMOUNT OUTPUT LINE.                                * ELC176P1
00119 *         *******************************************************.ELC176P1
00120                                                                   ELC176P1
00121  MCD-010.                                                         ELC176P1
00122      MOVE WS-CHAR2 (CHAR-INDEX)  TO  WS-CHAR (SA-INDEX).          ELC176P1
00123                                                                   ELC176P1
00124      SET SA-INDEX UP BY +1.                                       ELC176P1
00125                                                                   ELC176P1
00126  MCD-EXIT.                                                        ELC176P1
00127      EXIT.                                                        ELC176P1
00128                                                                   ELC176P1
00129      EJECT                                                        ELC176P1
00130  MOVE-SPELLED-AMOUNT SECTION.                                     ELC176P1
00131                                                                   ELC176P1
00132 *MSA-NOTE.                                                        ELC176P1
00133 *                                                                 ELC176P1
00134 *    NOTE ******************************************************* ELC176P1
00135 *         *      THIS SECTION MOVES THE SPELLED DOLLAR AMOUNT   * ELC176P1
00136 *         *  TO TWO LINES IF NECESSARY.                         * ELC176P1
00137 *         *******************************************************.ELC176P1
00138                                                                   ELC176P1
00139  MSA-010.                                                         ELC176P1
00140      ADD WS-1ST-LINE-LENGTH +1 GIVING WS-1ST-LINE-LENGTH-PLUS-1.  ELC176P1
00141      ADD WS-1ST-LINE-LENGTH +2 GIVING WS-1ST-LINE-LENGTH-PLUS-2.  ELC176P1
00142      ADD WS-1ST-LINE-LENGTH -1 GIVING WS-1ST-LINE-LENGTH-MINUS-1. ELC176P1
00143                                                                   ELC176P1
00144      MOVE WS-SPELLED-AMOUNT  TO  WS-SPELLED-LINE1.                ELC176P1
00145                                                                   ELC176P1
00146      IF SA-INDEX GREATER WS-1ST-LINE-LENGTH-PLUS-1                ELC176P1
00147          SET SL2-INDEX TO +1                                      ELC176P1
00148          IF WS-CHAR (WS-1ST-LINE-LENGTH-PLUS-1) = SPACES          ELC176P1
00149              PERFORM MOVE-LINE2 VARYING SA-INDEX2                 ELC176P1
00150                FROM WS-1ST-LINE-LENGTH-PLUS-2 BY +1               ELC176P1
00151                  UNTIL SL2-INDEX GREATER WS-2ND-LINE-LENGTH       ELC176P1
00152            ELSE                                                   ELC176P1
00153              PERFORM CLEAR-LINE1 VARYING SL1-INDEX                ELC176P1
00154                  FROM WS-1ST-LINE-LENGTH BY -1                    ELC176P1
00155                      UNTIL WS-SL1 (SL1-INDEX) = SPACES            ELC176P1
00156              SET SL1-INDEX UP BY +1                               ELC176P1
00157              PERFORM MOVE-LINE2                                   ELC176P1
00158                VARYING SA-INDEX2 FROM SL1-INDEX BY +1             ELC176P1
00159                  UNTIL SL2-INDEX GREATER WS-2ND-LINE-LENGTH.      ELC176P1
00160                                                                   ELC176P1
00161      MOVE WS-SPELLED-LINE1       TO  SD-PSA-LINE1.                ELC176P1
00162      MOVE WS-SPELLED-LINE2       TO  SD-PSA-LINE2.                ELC176P1
00163                                                                   ELC176P1
00164  MSA-EXIT.                                                        ELC176P1
00165      EXIT.                                                        ELC176P1
00166                                                                   ELC176P1
00167      EJECT                                                        ELC176P1
00168  CLEAR-LINE1 SECTION.                                             ELC176P1
00169                                                                   ELC176P1
00170 *CLS-NOTE.                                                        ELC176P1
00171 *                                                                 ELC176P1
00172 *    NOTE ******************************************************* ELC176P1
00173 *         *      THIS SECTION CLEARS THE TRAILING WORD IN THE   * ELC176P1
00174 *         *  SPELLED LINE 1 IF THE AMOUNT IS GREATER THAN 78.   * ELC176P1
00175 *         *******************************************************.ELC176P1
00176                                                                   ELC176P1
00177  CLS-010.                                                         ELC176P1
00178      MOVE SPACES                 TO  WS-SL1 (SL1-INDEX).          ELC176P1
00179                                                                   ELC176P1
00180  CLS-EXIT.                                                        ELC176P1
00181      EXIT.                                                        ELC176P1
00182                                                                   ELC176P1
00183  MOVE-LINE2 SECTION.                                              ELC176P1
00184                                                                   ELC176P1
00185 *MLS-NOTE.                                                        ELC176P1
00186 *                                                                 ELC176P1
00187 *    NOTE ******************************************************* ELC176P1
00188 *         *      THIS SECTION MOVES ONE CHARACTER TO THE SPELLED* ELC176P1
00189 *         *  AMOUNT OUTPUT LINE.                                * ELC176P1
00190 *         *******************************************************.ELC176P1
00191                                                                   ELC176P1
00192  MLS-010.                                                         ELC176P1
00193      MOVE WS-CHAR (SA-INDEX2)    TO  WS-SL2 (SL2-INDEX).          ELC176P1
00194                                                                   ELC176P1
00195      SET SL2-INDEX UP BY +1.                                      ELC176P1
00196                                                                   ELC176P1
00197      IF WS-CHAR (SA-INDEX2)     = SPACES   AND                    ELC176P1
00198         WS-CHAR (SA-INDEX2 + 1) = SPACES                          ELC176P1
00199          SET SL2-INDEX TO +99.                                    ELC176P1
00200                                                                   ELC176P1
00201  MLS-EXIT.                                                        ELC176P1
00202      EXIT.                                                        ELC176P1
00203                                                                   ELC176P1
