00001 ******************************************************************04/19/98
00002 *                                                                *ELC53PD
00002 *                                                                *ELC53PD
00003 *                            ELC53PD.                            *   LV004
00004 *           COPYBOOK REVIEWED FOR YEAR 2000 COMPLIANCE               CL**4
00005 *                            VMOD=2.009                          *   CL**3
00006 *                                                                *ELC53PD
00007 ******************************************************************ELC53PD
00008 ************* PROCEDURE DIVISION FOR EL053 & EL518 ***************ELC53PD
00009 ******************************************************************ELC53PD
00010      MOVE SPACES                 TO PC-FATAL-FLAG                 ELC53PD
00011                                     PC-FORCE-ER-CD                ELC53PD
00012                                     PC-WARN-ER-CD                 ELC53PD
00013                                     PC-VOID-SW                    ELC53PD
00014                                     PC-ERROR-FLAGS.               ELC53PD
00015      MOVE ZERO                   TO PC-REMAINING-TERM             ELC53PD
00016                                     PC-REMAINING-BENEFIT.         ELC53PD
00017                                                                   ELC53PD
00018  0050-CHECK-COMPANY-FOUND.                                        ELC53PD
00019      IF WK-PC-CNTL-RECORD-FOUND-SW = 'Y'                          ELC53PD
00020          MOVE WK-PC-CERT-ACCESS-CNTL     TO WS-CERT-ACCESS-CONTROLELC53PD
00021          MOVE WK-PC-CO-CLAIM-REJECT-SW   TO WS-CLAIM-SW           ELC53PD
00022          MOVE WK-PC-CLAIM-SYSTEM-SW      TO CLAIM-SYSTEM-SW       ELC53PD
00023          MOVE WK-PC-CO-TOL-CLAIM         TO WS-CLAIM-TOL          ELC53PD
00024          MOVE WK-PC-REM-TRM-CALC-OPTION  TO WS-REM-TRM-CALC-OPTIONELC53PD
00025          MOVE WK-PC-CREDIT-EDIT-CONTROLS TO                       ELC53PD
00026                                            WS-CREDIT-EDIT-CONTROLSELC53PD
00027          GO TO 0100-READ-ACCT.                                    ELC53PD
00028                                                                   ELC53PD
00029      MOVE PC-COMPANY-ID          TO CNTL-COMP-ID.                 ELC53PD
00030      MOVE SPACES                 TO CNTL-ACCESS.                  ELC53PD
00031      MOVE '1'                    TO CNTL-REC-TYPE.                ELC53PD
00032      MOVE +0                     TO CNTL-SEQ-NO.                  ELC53PD
00033      MOVE 'C'                    TO CTL-READ.                     ELC53PD
00034                                                                   ELC53PD
00035      PERFORM 9000-READ-CONTROL THRU 9000-EXIT.                    ELC53PD
00036                                                                   ELC53PD
00037      IF NOT CF-COMPANY-MASTER                                     ELC53PD
00038           GO TO 0099-COMPANY-NOT-FOUND.                           ELC53PD
00039                                                                   ELC53PD
00040      MOVE CF-CREDIT-EDIT-CONTROLS  TO WK-PC-CREDIT-EDIT-CONTROLS. ELC53PD
00041      MOVE CF-CERT-ACCESS-CONTROL   TO WK-PC-CERT-ACCESS-CNTL.     ELC53PD
00042                                                                   ELC53PD
00043      IF CO-HAS-CLAS-IC-CLAIM                                      ELC53PD
00044           MOVE '1' TO WK-PC-CLAIM-SYSTEM-SW                       ELC53PD
00045        ELSE                                                       ELC53PD
00046           MOVE ' ' TO WK-PC-CLAIM-SYSTEM-SW.                      ELC53PD
00047                                                                   ELC53PD
00048      IF CF-VALID-REM-TRM-OPTION                                   ELC53PD
00049          MOVE CF-REM-TRM-CALC-OPTION TO WK-PC-REM-TRM-CALC-OPTION ELC53PD
00050      ELSE                                                         ELC53PD
00051          MOVE SPACES                 TO WK-PC-REM-TRM-CALC-OPTION.ELC53PD
00052                                                                   ELC53PD
00053      MOVE CF-CO-TOL-CLAIM        TO WK-PC-CO-TOL-CLAIM.           ELC53PD
00054      MOVE CF-CO-CLAIM-REJECT-SW  TO WK-PC-CO-CLAIM-REJECT-SW.     ELC53PD
00055      MOVE 'Y'                    TO WK-PC-CNTL-RECORD-FOUND-SW.   ELC53PD
00056      MOVE CF-JOURNAL-FILE-ID     TO WS-JOURNAL-FILE-ID.           ELC53PD
00057                                                                   ELC53PD
00058      GO TO 0050-CHECK-COMPANY-FOUND.                              ELC53PD
00059                                                                   ELC53PD
00060  0098-COMPANY-NOT-OPEN.                                           ELC53PD
00061      MOVE X2841                  TO PC-ERR-FLAG (41).             ELC53PD
00062      GO TO 9990-RETURN.                                           ELC53PD
00063                                                                   ELC53PD
00064  0099-COMPANY-NOT-FOUND.                                          ELC53PD
00065      MOVE X2840                  TO PC-ERR-FLAG (40).             ELC53PD
00066                                                                   ELC53PD
00067  0100-READ-ACCT.                                                  ELC53PD
00068      IF PC-ACCOUNT = SPACES                                       ELC53PD
00069          MOVE X2849              TO PC-ERR-FLAG (49)              ELC53PD
00070          GO TO 0340-CONTINUE-EDIT.                                ELC53PD
00071                                                                   ELC53PD
00072  0105-ACCT-GETMAIN.                                               ELC53PD
00073      PERFORM 8900-GETMAIN THRU 8900-EXIT.                         ELC53PD
00074                                                                   ELC53PD
00075  0110-READ-ACCT.                                                  ELC53PD
00076      MOVE PC-COMPANY-CD          TO ACCT-COMP-CD.                 ELC53PD
00077      MOVE PC-CARRIER             TO ACCT-CARRIER.                 ELC53PD
00078      MOVE PC-GROUPING            TO ACCT-GROUPING.                ELC53PD
00079      MOVE PC-STATE               TO ACCT-STATE.                   ELC53PD
00080      MOVE PC-ACCOUNT             TO ACCT-ACCOUNT.                 ELC53PD
00081      MOVE PC-CERT-EFF-DT         TO ACCT-EXP-DT.                  ELC53PD
00082                                                                   ELC53PD
00083      PERFORM 8200-START-BR THRU 8200-EXIT.                        ELC53PD
00084                                                                   ELC53PD
00085      MOVE 'Y'                    TO ACCOUNT-BROWSE-STARTED-SW.    ELC53PD
00086                                                                   ELC53PD
00087  0150-READ-ACCT-LOOP.                                             ELC53PD
00088      PERFORM 8300-READ-ACCT THRU 8300-EXIT.                       ELC53PD
00089                                                                   ELC53PD
00090      IF (PC-COMPANY-CD   NOT = AM-COMPANY-CD-A1)   OR             ELC53PD
00091         (PC-CARRIER      NOT = AM-VG-CARRIER)      OR             ELC53PD
00092         (PC-GROUPING     NOT = AM-VG-GROUPING)     OR             ELC53PD
00093         (PC-STATE        NOT = AM-VG-STATE)        OR             ELC53PD
00094         (PC-ACCOUNT      NOT = AM-VG-ACCOUNT)                     ELC53PD
00095         GO TO 0199-ACCT-NOT-FOUND.                                ELC53PD
00096                                                                   ELC53PD
00097      IF PC-CERT-EFF-DT LESS THAN AM-EFFECTIVE-DT                  ELC53PD
00098          GO TO 0199-ACCT-NOT-FOUND.                               ELC53PD
00099                                                                   ELC53PD
00100      IF PC-CERT-EFF-DT NOT LESS THAN AM-EXPIRATION-DT             ELC53PD
00101          GO TO 0150-READ-ACCT-LOOP.                               ELC53PD
00102                                                                   ELC53PD
00103  0160-ACCOUNT-FOUND.                                              ELC53PD
00104      IF AM-TOL-CLM NOT = ZERO                                     ELC53PD
00105         MOVE AM-TOL-CLM          TO WS-CLAIM-TOL.                 ELC53PD
00106                                                                   ELC53PD
00107      MOVE AM-CARRIER             TO PC-SV-CARRIER.                ELC53PD
00108      MOVE AM-GROUPING            TO PC-SV-GROUPING.               ELC53PD
00109      MOVE AM-STATE               TO PC-SV-STATE.                  ELC53PD
00110                                                                   ELC53PD
00111      GO TO 0200-READ-CERT.                                        ELC53PD
00112                                                                   ELC53PD
00113  0198-ACCT-NOT-OPEN.                                              ELC53PD
00114      MOVE X2850                  TO PC-ERR-FLAG (50).             ELC53PD
00115                                                                   ELC53PD
00116  0199-ACCT-NOT-FOUND.                                             ELC53PD
00117      MOVE X2851                  TO PC-ERR-FLAG (51).             ELC53PD
00118      EJECT                                                        ELC53PD
00119  0200-READ-CERT.                                                  ELC53PD
00120      MOVE PC-COMPANY-CD          TO CERT-COMP-CD                  ELC53PD
00121      MOVE PC-SV-CARRIER          TO CERT-CARRIER                  ELC53PD
00122      MOVE PC-SV-GROUPING         TO CERT-GROUPING                 ELC53PD
00123      MOVE PC-SV-STATE            TO CERT-STATE                    ELC53PD
00124      MOVE PC-ACCOUNT             TO CERT-ACCOUNT                  ELC53PD
00125      MOVE PC-CERT-EFF-DT         TO CERT-EFF-DT                   ELC53PD
00126      MOVE PC-CERT-NO             TO CERT-CERT-NO                  ELC53PD
00127                                                                   ELC53PD
00128      PERFORM 8700-SRL-CERT-MASTER THRU 8700-EXIT.                 ELC53PD
00129                                                                   ELC53PD
00130      IF CERT-WAS-CREATED-FOR-CLAIM                                ELC53PD
00131          MOVE X2859              TO PC-ERR-FLAG (59)              ELC53PD
00132          GO TO 0250-UNLOCK.                                       ELC53PD
00133                                                                   ELC53PD
00134      IF CERT-PURGED-OFFLINE                                       ELC53PD
00135          MOVE X2863              TO PC-ERR-FLAG (63)              ELC53PD
00136          GO TO 0250-UNLOCK.                                       ELC53PD
00137                                                                   ELC53PD
00138      IF CERT-PEND-ISSUE-ERROR OR CERT-PEND-ISSUE-RETURNED         ELC53PD
00139          MOVE X2867              TO PC-ERR-FLAG (67)              ELC53PD
00140          GO TO 0250-UNLOCK.                                       ELC53PD
00141                                                                   ELC53PD
00142      GO TO 0270-CONT.                                             ELC53PD
00143                                                                   ELC53PD
00144  0250-UNLOCK.                                                     ELC53PD
00145      GO TO 9250-UNLOCK.                                           ELC53PD
00146                                                                   ELC53PD
00147  0270-CONT.                                                       ELC53PD
00148      MOVE 'B'                    TO JP-RECORD-TYPE.               ELC53PD
00149      PERFORM 8100-JOURNAL-CERT THRU 8100-EXIT.                    ELC53PD
00150                                                                   ELC53PD
00151      IF (CM-ENTRY-DT GREATER THAN PC-CREDIT-SELECT-DT)            ELC53PD
00152         AND (NOT CERT-ADDED-BATCH)                                ELC53PD
00153          MOVE X2844            TO PC-ERR-FLAG (44).               ELC53PD
00154                                                                   ELC53PD
00155      MOVE CM-INSURED-LAST-NAME   TO PC-CC-LAST-NAME.              ELC53PD
00156      MOVE CM-INSURED-INITIALS    TO PC-CC-INITIALS.               ELC53PD
00157      MOVE CM-SOC-SEC-NO          TO PC-CC-SOC-SEC-NO.             ELC53PD
00158      MOVE CM-MEMBER-NO           TO PC-CC-MEMBER-NO.              ELC53PD
00159      MOVE CM-INSURED-ISSUE-AGE   TO PC-CC-INSURED-AGE.            ELC53PD
00160      MOVE CM-INSURED-SEX         TO PC-CC-INSURED-SEX.            ELC53PD
00161      MOVE CM-ENTRY-STATUS        TO PC-CC-CERT-ENTRY-STATUS.      ELC53PD
00162                                                                   ELC53PD
00163      IF PC-CLAIM-TYPE = '2' OR '4'                                ELC53PD
00164          MOVE CM-AH-ORIG-TERM      TO PC-CC-ORIG-TERM             ELC53PD
00165          MOVE CM-AH-CANCEL-DT      TO PC-CC-CANCEL-DT             ELC53PD
00166          MOVE CM-AH-CURRENT-STATUS TO PC-CC-AH-POLICY-STATUS      ELC53PD
00167      ELSE                                                         ELC53PD
00168          MOVE CM-LF-CURRENT-STATUS TO PC-CC-LF-POLICY-STATUS      ELC53PD
00169          MOVE CM-LF-CANCEL-DT      TO PC-CC-CANCEL-DT             ELC53PD
00170          MOVE CM-LF-ORIG-TERM      TO PC-CC-ORIG-TERM.            ELC53PD
00171                                                                   ELC53PD
00172      MOVE CM-AH-BENEFIT-CD      TO PC-CC-AH-BENEFIT-CD            ELC53PD
00173      MOVE CM-AH-BENEFIT-AMT     TO PC-CC-AH-BENEFIT-AMT           ELC53PD
00174      MOVE CM-AH-PREMIUM-AMT     TO PC-CC-AH-PREMIUM-AMT           ELC53PD
00175      MOVE CM-LF-BENEFIT-CD      TO PC-CC-LF-BENEFIT-CD            ELC53PD
00176      MOVE CM-LF-BENEFIT-AMT     TO PC-CC-LIFE-BENEFIT-AMT         ELC53PD
00177      MOVE CM-LF-ALT-BENEFIT-AMT TO PC-CC-ALT-LF-BENEFIT-AMT       ELC53PD
00178      MOVE CM-LF-PREMIUM-AMT     TO PC-CC-LIFE-PREMIUM             ELC53PD
00179      MOVE CM-LF-ALT-PREMIUM-AMT TO PC-CC-LIFE-PREMIUM             ELC53PD
00180      MOVE CM-LOAN-APR           TO PC-CC-LOAN-APR.                ELC53PD
00181      MOVE CM-PAY-FREQUENCY      TO PC-CC-PAY-FREQUENCY.           ELC53PD
00182                                                                   ELC53PD
00183      IF PC-CERT-DATA-CAPTURED                                     ELC53PD
00184         NEXT SENTENCE                                             ELC53PD
00185       ELSE                                                        ELC53PD
00186          MOVE CM-LF-DEATH-DT      TO PC-CC-DEATH-DT               ELC53PD
00187          MOVE CM-AH-SETTLEMENT-DT TO PC-CC-SETTLEMENT-DT          ELC53PD
00188          MOVE CM-LF-ITD-DEATH-AMT TO PC-CC-PRIOR-DEATH-AMT        ELC53PD
00189          MOVE CM-AH-ITD-LUMP-PMT  TO PC-CC-PRIOR-LUMP-PMT         ELC53PD
00190          MOVE '1'                 TO PC-CERT-UPDATE-SW            ELC53PD
00191          IF PC-CLAIM-TYPE = '1' OR '3'                            ELC53PD
00192             MOVE CM-LF-STATUS-AT-DEATH    TO PC-CC-PRIOR-STATUS   ELC53PD
00193           ELSE                                                    ELC53PD
00194             MOVE CM-AH-STATUS-AT-SETTLEMENT TO PC-CC-PRIOR-STATUS.ELC53PD
00195                                                                   ELC53PD
00196      GO TO 0300-CONTINUE-EDIT.                                    ELC53PD
00197                                                                   ELC53PD
00198  0298-CERT-NOT-OPEN.                                              ELC53PD
00199      MOVE X2842                  TO PC-ERR-FLAG (42).             ELC53PD
00200      GO TO 9990-RETURN.                                           ELC53PD
00201                                                                   ELC53PD
00202  0299-CERT-NOT-FOUND.                                             ELC53PD
00203      MOVE X2843                  TO PC-ERR-FLAG (43).             ELC53PD
00204      GO TO 9990-RETURN.                                           ELC53PD
00205      EJECT                                                        ELC53PD
00206                                                                   ELC53PD
00207  0300-CONTINUE-EDIT.                                              ELC53PD
00208      IF PC-SV-CARRIER = WK-PC-LAST-CARRIER                        ELC53PD
00209          MOVE WK-PC-RESERVE-CONTROLS  TO WS-RESERVE-CONTROLS      ELC53PD
00210          GO TO 0305-CONTINUE-EDIT.                                ELC53PD
00211                                                                   ELC53PD
00212      MOVE PC-COMPANY-ID          TO CNTL-COMP-ID.                 ELC53PD
00213      MOVE PC-SV-CARRIER          TO WS-CARR.                      ELC53PD
00214      MOVE ZEROS                  TO CNTL-SEQ-NO.                  ELC53PD
00215      MOVE WS-CARR-ACCESS         TO CNTL-ACCESS.                  ELC53PD
00216      MOVE '6'                    TO CNTL-REC-TYPE.                ELC53PD
00217                                                                   ELC53PD
00218      MOVE 'M' TO CTL-READ.                                        ELC53PD
00219                                                                   ELC53PD
00220      PERFORM 9000-READ-CONTROL THRU 9000-EXIT.                    ELC53PD
00221                                                                   ELC53PD
00222      IF NOT CF-CARRIER-MASTER                                     ELC53PD
00223         GO TO 0301-CARRIER-NOT-FOUND.                             ELC53PD
00224                                                                   ELC53PD
00225      MOVE CF-RESERVE-CONTROLS    TO WS-RESERVE-CONTROLS           ELC53PD
00226                                     WK-PC-RESERVE-CONTROLS.       ELC53PD
00227      MOVE CF-CARRIER-CNTL        TO WK-PC-LAST-CARRIER.           ELC53PD
00228      GO TO 0305-CONTINUE-EDIT.                                    ELC53PD
00229                                                                   ELC53PD
00230  0301-CARRIER-NOT-FOUND.                                          ELC53PD
00231      MOVE X2845                  TO PC-ERR-FLAG (45).             ELC53PD
00232                                                                   ELC53PD
00233  0305-CONTINUE-EDIT.                                              ELC53PD
00234      IF  PC-GROUPING = SPACES                                     ELC53PD
00235          IF WS-CARR-GROUP-ST-ACCNT-CNTL                           ELC53PD
00236              MOVE X2846          TO PC-ERR-FLAG (46).             ELC53PD
00237                                                                   ELC53PD
00238      IF PC-STATE = SPACES                                         ELC53PD
00239          IF NOT WS-ACCNT-CNTL AND NOT WS-CARR-ACCNT-CNTL          ELC53PD
00240              MOVE X2847          TO PC-ERR-FLAG (47)              ELC53PD
00241              GO TO 0340-CONTINUE-EDIT.                            ELC53PD
00242                                                                   ELC53PD
00243      PERFORM 9200-CONTROL-GETMAIN THRU 9200-EXIT.                 ELC53PD
00244                                                                   ELC53PD
00245      MOVE PC-COMPANY-ID          TO CNTL-COMP-ID.                 ELC53PD
00246      MOVE PC-SV-STATE            TO CNTL-ACCESS.                  ELC53PD
00247      MOVE '3'                    TO CNTL-REC-TYPE.                ELC53PD
00248      MOVE +0                     TO CNTL-SEQ-NO.                  ELC53PD
00249                                                                   ELC53PD
00250      PERFORM 9100-READ-CONTROL THRU 9100-EXIT.                    ELC53PD
00251                                                                   ELC53PD
00252      IF (CNTL-COMP-ID  NOT = CF-COMPANY-ID)  OR                   ELC53PD
00253         (CNTL-REC-TYPE NOT = CF-RECORD-TYPE) OR                   ELC53PD
00254         (PC-SV-STATE   NOT = CF-STATE-CODE)                       ELC53PD
00255          GO TO 0310-STATE-NOT-FOUND.                              ELC53PD
00256                                                                   ELC53PD
00257  0308-STATE-FOUND.                                                ELC53PD
00258      MOVE CF-STATE-ABBREVIATION  TO WS-STATE-ABBREVIATION.        ELC53PD
00259      IF CF-ST-TOL-CLAIM NOT = ZERO                                ELC53PD
00260         MOVE CF-ST-TOL-CLAIM        TO WS-CLAIM-TOL               ELC53PD
00261         MOVE CF-ST-CLAIM-REJECT-SW  TO WS-CLAIM-SW.               ELC53PD
00262                                                                   ELC53PD
00263      GO TO 0340-CONTINUE-EDIT.                                    ELC53PD
00264                                                                   ELC53PD
00265  0310-STATE-NOT-FOUND.                                            ELC53PD
00266      MOVE X2848                  TO PC-ERR-FLAG (48).             ELC53PD
00267                                                                   ELC53PD
00268  0340-CONTINUE-EDIT.                                              ELC53PD
00269      IF PC-FORCE-OFF OR PC-CLAIM-FORCE                            ELC53PD
00270          NEXT SENTENCE                                            ELC53PD
00271        ELSE                                                       ELC53PD
00272          MOVE X2815              TO PC-ERR-FLAG (15).             ELC53PD
00273                                                                   ELC53PD
00274      IF PC-CLAIM-NO = SPACES                                      ELC53PD
00275          MOVE X2852              TO PC-ERR-FLAG (52).             ELC53PD
00276                                                                   ELC53PD
00277      MOVE PC-CLAIM-TYPE          TO WS-VALID-CLAIM-TYPES.         ELC53PD
00278                                                                   ELC53PD
00279      IF NOT VALID-CLAIM-TYPE                                      ELC53PD
00280              MOVE X2817          TO PC-ERR-FLAG (17).             ELC53PD
00281                                                                   ELC53PD
00282      IF PC-RESERVES                                               ELC53PD
00283          GO TO 0500-EDIT-RESERVE-RECORD.                          ELC53PD
00284                                                                   ELC53PD
00285      IF PC-CLAIM-PAYMENT = ZEROS                                  ELC53PD
00286          MOVE X2801              TO PC-ERR-FLAG (1).              ELC53PD
00287                                                                   ELC53PD
00288      MOVE PC-PAYMENT-TYPE        TO WS-VALID-PAY-TYPES.           ELC53PD
00289      IF NOT VALID-PAY-TYPE                                        ELC53PD
00290          MOVE X2853              TO PC-ERR-FLAG (53).             ELC53PD
00291                                                                   ELC53PD
00292      MOVE EIBDATE                TO DC-JULIAN-DATE.                  CL**2
00293      MOVE '5'                    TO DC-OPTION-CODE.               ELC53PD
00294      PERFORM 8500-DATE-CONVERT THRU 8500-EXIT.                    ELC53PD
00295      IF DATE-CONVERSION-ERROR                                     ELC53PD
00296         MOVE ZERO TO WS-YEAR-1                                    ELC53PD
00297       ELSE                                                        ELC53PD
00298          MOVE DC-BIN-DATE-1  TO WS-YEAR-1                         ELC53PD
00299                                 WS-CURRENT-BIN-DT.                ELC53PD
00300                                                                   ELC53PD
00301      IF CERT-WAS-NOT-FOUND                                        ELC53PD
00302          GO TO 0350-DATE-CHECKS.                                  ELC53PD
00303                                                                   ELC53PD
00304      EJECT                                                        ELC53PD
00305                                                                   ELC53PD
00306  0345-CHECK-FOR-VOIDS.                                            ELC53PD
00307      IF (PC-CLAIM-TYPE = '1')                                     ELC53PD
00308         AND ((PC-PAYMENT-TYPE = '9')                              ELC53PD
00309             OR (PC-CLAIM-PAYMENT NEGATIVE))                       ELC53PD
00310           NEXT SENTENCE                                           ELC53PD
00311        ELSE                                                       ELC53PD
00312             GO TO 0350-DATE-CHECKS.                               ELC53PD
00313                                                                   ELC53PD
00314      MOVE PC-CC-DEATH-DT         TO WS-YEAR-2.                    ELC53PD
00315                                                                   ELC53PD
00316      IF PC-PAYMENT-TYPE = '9'                                     ELC53PD
00317         IF PC-CLAIM-PAYMENT NOT NEGATIVE                          ELC53PD
00318             MOVE X2861           TO PC-ERR-FLAG (61).             ELC53PD
00319                                                                   ELC53PD
00320      IF (PC-CLAIM-PAYMENT NEGATIVE) AND                           ELC53PD
00321         (PC-CLAIM-TYPE = '1' OR '3') AND                          ELC53PD
00322         ((PC-CC-PRIOR-DEATH-AMT + PC-CLAIM-PAYMENT) = ZERO)       ELC53PD
00323           MOVE X2838           TO PC-ERR-FLAG (38)                ELC53PD
00324           MOVE '1' TO PC-VOID-SW                                  ELC53PD
00325        ELSE                                                       ELC53PD
00326          IF PC-PAYMENT-TYPE = '9'                                 ELC53PD
00327            OR PC-CLAIM-PAYMENT IS NEGATIVE                        ELC53PD
00328             MOVE X2839           TO PC-ERR-FLAG (39).             ELC53PD
00329                                                                   ELC53PD
00330      IF PC-VOID-SW = '1'                                          ELC53PD
00331         IF WS-YEAR-1 NOT = WS-YEAR-2 AND                          ELC53PD
00332           WS-YEAR-2 NOT = ZEROS                                   ELC53PD
00333               MOVE SPACE          TO PC-ERR-FLAG (38)             ELC53PD
00334               MOVE X2862          TO PC-ERR-FLAG (62).            ELC53PD
00335                                                                   ELC53PD
00336      IF PC-VOID-SW = '1'                                          ELC53PD
00337         IF PC-PAYMENT-TYPE = '1' OR '3'                           ELC53PD
00338            GO TO 3999-END-EDIT.                                   ELC53PD
00339                                                                   ELC53PD
00340      EJECT                                                        ELC53PD
00341                                                                   ELC53PD
00342  0350-DATE-CHECKS.                                                ELC53PD
00343      IF PC-PAYMENT-DT = LOW-VALUES                                ELC53PD
00344          MOVE X2803              TO PC-ERR-FLAG (3).              ELC53PD
00345                                                                   ELC53PD
00346      IF PC-COMPANY-ID NOT = 'MIL'  OR  'CSL'                      ELC53PD
00347         IF PC-PAID-THRU-DT = LOW-VALUES                           ELC53PD
00348            IF VALID-AH-TYPE                                       ELC53PD
00349                MOVE X2805        TO PC-ERR-FLAG (5).              ELC53PD
00350                                                                   ELC53PD
00351      IF PC-INCURRED-DT = LOW-VALUES                               ELC53PD
00352          MOVE X2802              TO PC-ERR-FLAG (2).              ELC53PD
00353                                                                   ELC53PD
00354      IF PC-COMPANY-ID NOT = 'MIL'                                 ELC53PD
00355         IF PC-NO-OF-DAYS-PAID = ZEROS                             ELC53PD
00356            IF VALID-AH-TYPE                                       ELC53PD
00357                MOVE X2812        TO PC-ERR-FLAG (12).             ELC53PD
00358                                                                   ELC53PD
00359      IF PC-INCURRED-DT LESS THAN PC-CERT-EFF-DT                   ELC53PD
00360          MOVE X2806              TO PC-ERR-FLAG (6).              ELC53PD
00361                                                                   ELC53PD
00362      IF PC-REPORTED-DT = LOW-VALUES                               ELC53PD
00363          MOVE X2804              TO PC-ERR-FLAG (4)               ELC53PD
00364          GO TO 0355-FINISH-DATE-CHECKS.                           ELC53PD
00365                                                                   ELC53PD
00366      IF PC-REPORTED-DT LESS THAN PC-INCURRED-DT                   ELC53PD
00367          MOVE X2809         TO PC-ERR-FLAG (9).                   ELC53PD
00368                                                                   ELC53PD
00369      IF PC-REPORTED-DT GREATER THAN PC-PAYMENT-DT                 ELC53PD
00370          MOVE X2810         TO PC-ERR-FLAG (10).                  ELC53PD
00371                                                                   ELC53PD
00372  0355-FINISH-DATE-CHECKS.                                         ELC53PD
00373      IF PC-PAYMENT-DT LESS THAN PC-CERT-EFF-DT                    ELC53PD
00374          MOVE X2807              TO PC-ERR-FLAG (7).              ELC53PD
00375                                                                   ELC53PD
00376      IF PC-COMPANY-ID NOT = 'MIL'                                 ELC53PD
00377         IF PC-CLAIM-TYPE = '2' OR '4'                             ELC53PD
00378            IF PC-PAID-THRU-DT LESS THAN PC-INCURRED-DT            ELC53PD
00379               MOVE X2811         TO PC-ERR-FLAG (11).             ELC53PD
00380                                                                   ELC53PD
00381      IF PC-PAYMENT-DT GREATER WS-CURRENT-BIN-DT                   ELC53PD
00382              MOVE X2868 TO PC-ERR-FLAG (68).                      ELC53PD
00383                                                                   ELC53PD
00384      EJECT                                                        ELC53PD
00385      IF PC-CLAIM-TYPE = '2' OR '4'                                ELC53PD
00386          GO TO 0500-EDIT-AH-CLAIM                                 ELC53PD
00387        ELSE                                                       ELC53PD
00388            GO TO 0400-EDIT-LIFE-CLAIM.                            ELC53PD
00389                                                                   ELC53PD
00390  0400-EDIT-LIFE-CLAIM.                                            ELC53PD
00391      IF CM-LF-BENEFIT-CD = '00'                                   ELC53PD
00392         IF PC-CHARGEBLE-EXPENSE OR PC-NON-CHARGEBLE-EXPENSE       ELC53PD
00393            GO TO 3999-END-EDIT                                    ELC53PD
00394           ELSE                                                    ELC53PD
00395            MOVE X2818          TO PC-ERR-FLAG (18)                ELC53PD
00396            GO TO 3999-END-EDIT.                                   ELC53PD
00397                                                                   ELC53PD
00398      IF PC-CHARGEBLE-EXPENSE OR PC-NON-CHARGEBLE-EXPENSE          ELC53PD
00399         GO TO 0420-CONT.                                          ELC53PD
00400                                                                   ELC53PD
00401      IF PC-COMPANY-ID NOT = 'MIL'                                 ELC53PD
00402         IF PC-CC-DEATH-DT = LOW-VALUE                             ELC53PD
00403            IF NOT PC-FINAL-PAYMENT                                ELC53PD
00404               MOVE X2864        TO PC-ERR-FLAG (64).              ELC53PD
00405                                                                   ELC53PD
00406      IF PC-COMPANY-ID NOT = 'MIL'                                 ELC53PD
00407         IF PC-CC-DEATH-DT NOT = LOW-VALUE                         ELC53PD
00408            IF PC-FINAL-PAYMENT                                    ELC53PD
00409               MOVE X2860        TO PC-ERR-FLAG (60).              ELC53PD
00410                                                                   ELC53PD
00411  0420-CONT.                                                       ELC53PD
00412      MOVE SPACES                 TO WS-ACCESS.                    ELC53PD
00413      MOVE PC-COMPANY-ID          TO CNTL-COMP-ID.                 ELC53PD
00414      MOVE '4'                    TO CNTL-REC-TYPE.                ELC53PD
00415      MOVE +0                     TO CNTL-SEQ-NO.                  ELC53PD
00416      MOVE CM-LF-BENEFIT-CD       TO WS-BEN-CD.                    ELC53PD
00417      MOVE WS-ACCESS              TO CNTL-ACCESS.                  ELC53PD
00418                                                                   ELC53PD
00419      PERFORM 7200-FIND-BENEFIT THRU 7200-EXIT.                    ELC53PD
00420                                                                   ELC53PD
00421      IF NO-BENEFIT-FOUND                                          ELC53PD
00422          MOVE X2833          TO PC-ERR-FLAG (33)                  ELC53PD
00423          GO TO 3999-END-EDIT.                                     ELC53PD
00424                                                                   ELC53PD
00425      MOVE CF-LF-COVERAGE-TYPE  (SUB3) TO WS-LF-COVERAGE-TYPE.     ELC53PD
00426      MOVE CF-SPECIAL-CALC-CD   (SUB3) TO WS-LF-SPECIAL-CALC-CD.   ELC53PD
00427      MOVE CF-JOINT-INDICATOR   (SUB3) TO WS-LF-JOINT-INDICATOR.   ELC53PD
00428      MOVE CF-CO-EARNINGS-CALC  (SUB3) TO WS-LF-EARNINGS-CALC      ELC53PD
00429                                          WS-LF-REFUND-CALC.       ELC53PD
00430                                                                   ELC53PD
00431      IF LF-IS-OB-TYPE AND PC-OB-LF-CLAIM                          ELC53PD
00432          GO TO 0469-PART-DEATH.                                   ELC53PD
00433                                                                   ELC53PD
00434      IF LF-IS-OB-TYPE OR PC-OB-LF-CLAIM                           ELC53PD
00435          MOVE X2825          TO PC-ERR-FLAG (25)                  ELC53PD
00436          GO TO 3999-END-EDIT.                                     ELC53PD
00437                                                                   ELC53PD
00438      MOVE PC-CERT-EFF-DT         TO DC-BIN-DATE-1.                ELC53PD
00439      MOVE PC-CC-ORIG-TERM        TO DC-ELAPSED-MONTHS.            ELC53PD
00440      MOVE 6                      TO DC-OPTION-CODE.               ELC53PD
00441      PERFORM 8500-DATE-CONVERT THRU 8500-EXIT.                    ELC53PD
00442                                                                   ELC53PD
00443      IF PC-INCURRED-DT GREATER DC-BIN-DATE-2                      ELC53PD
00444              MOVE X2819          TO PC-ERR-FLAG (19).             ELC53PD
00445                                                                   ELC53PD
00446      IF PC-CC-CANCEL-DT NOT = LOW-VALUE                           ELC53PD
00447          IF PC-CC-CANCEL-DT LESS THAN   PC-INCURRED-DT            ELC53PD
00448              MOVE X2820          TO PC-ERR-FLAG (20).             ELC53PD
00449                                                                   ELC53PD
00450 *    IF PC-CC-PRIOR-STATUS NOT = SPACE                            ELC53PD
00451 *       IF PC-PARTIAL-PAYMENT OR PC-ADDITIONAL-PAYMENT            ELC53PD
00452 *          NEXT SENTENCE                                          ELC53PD
00453 *        ELSE                                                     ELC53PD
00454 *            MOVE X2821          TO PC-ERR-FLAG (21).             ELC53PD
00455                                                                   ELC53PD
00456      IF PC-CC-SETTLEMENT-DT NOT = LOW-VALUE                       ELC53PD
00457              MOVE X2822          TO PC-ERR-FLAG (22).             ELC53PD
00458                                                                   ELC53PD
00459      MOVE PC-CERT-EFF-DT         TO CP-CERT-EFF-DT.               ELC53PD
00460      MOVE CM-LOAN-1ST-PMT-DT     TO CP-FIRST-PAY-DATE.            ELC53PD
00461      MOVE PC-INCURRED-DT         TO CP-VALUATION-DT.              ELC53PD
00462      MOVE CM-LF-ORIG-TERM        TO CP-ORIGINAL-TERM.             ELC53PD
00463      MOVE '1'                    TO CP-REM-TERM-METHOD.           ELC53PD
00464      MOVE WS-REM-TRM-CALC-OPTION TO CP-REM-TRM-CALC-OPTION.       ELC53PD
00465      MOVE PC-COMPANY-ID          TO CP-COMPANY-ID.                ELC53PD
00466      MOVE PC-COMPANY-CD          TO CP-COMPANY-CD.                ELC53PD
00467                                                                   ELC53PD
00468      PERFORM 7600-GET-REMAIN-TERM THRU 7600-EXIT.                 ELC53PD
00469                                                                   ELC53PD
00470      IF (WS-STATE-ABBREVIATION = 'OH')                            ELC53PD
00471          AND (PC-LF-OVERRIDE-L1 = 'L')                            ELC53PD
00472          AND (PC-CC-ORIG-TERM GREATER THAN 60)                    ELC53PD
00473          AND (PC-CERT-EFF-DT GREATER WS-831031)                   ELC53PD
00474             MOVE '5'             TO WS-LF-EARNINGS-CALC.          ELC53PD
00475                                                                   ELC53PD
00476      IF (WS-STATE-ABBREVIATION = 'RI')                            ELC53PD
00477          AND (PC-LF-OVERRIDE-L1 = 'L')                            ELC53PD
00478          AND (PC-CC-ORIG-TERM GREATER THAN 60)                    ELC53PD
00479          AND (PC-CERT-EFF-DT GREATER WS-831231)                   ELC53PD
00480             MOVE '5'             TO WS-LF-EARNINGS-CALC.          ELC53PD
00481                                                                   ELC53PD
00482      IF (WS-STATE-ABBREVIATION = 'UT')                            ELC53PD
00483          AND (PC-LF-OVERRIDE-L1 = 'L')                            ELC53PD
00484          AND (PC-CC-ORIG-TERM GREATER THAN 62)                    ELC53PD
00485          AND (PC-CERT-EFF-DT GREATER WS-810831)                   ELC53PD
00486          AND (PC-CERT-EFF-DT LESS WS-830901)                      ELC53PD
00487             MOVE '5'             TO WS-LF-EARNINGS-CALC.          ELC53PD
00488                                                                   ELC53PD
00489      MOVE CP-REMAINING-TERM-3    TO CP-REMAINING-TERM.            ELC53PD
00490      MOVE CM-LF-BENEFIT-AMT      TO CP-ORIGINAL-BENEFIT.          ELC53PD
00491      MOVE CM-LF-ORIG-TERM        TO CP-ORIGINAL-TERM.             ELC53PD
00492      MOVE CM-LOAN-APR            TO CP-LOAN-APR.                  ELC53PD
00493      MOVE CM-LOAN-TERM           TO CP-LOAN-TERM                  ELC53PD
00494      MOVE CM-PAY-FREQUENCY       TO CP-PAY-FREQUENCY.             ELC53PD
00495      MOVE WS-LF-COVERAGE-TYPE    TO CP-BENEFIT-TYPE.              ELC53PD
00496      MOVE WS-LF-EARNINGS-CALC    TO CP-EARNING-METHOD.            ELC53PD
00497                                                                   ELC53PD
00498      PERFORM 7700-GET-REMAIN-AMT THRU 7700-EXIT.                  ELC53PD
00499                                                                   ELC53PD
00500      MOVE CP-REMAINING-AMT       TO WS-REMAINING-AMT.             ELC53PD
00501      IF CP-REMAINING-TERM GREATER ZERO                            ELC53PD
00502          IF WS-LF-SPECIAL-CALC-CD = 'B'                           ELC53PD
00503             ADD PC-CC-ALT-LF-BENEFIT-AMT TO WS-REMAINING-AMT.     ELC53PD
00504                                                                   ELC53PD
00505      COMPUTE WK-C3 = (PC-CLAIM-PAYMENT +                          ELC53PD
00506                    PC-CC-PRIOR-DEATH-AMT) - WS-REMAINING-AMT.     ELC53PD
00507                                                                   ELC53PD
00508      MOVE CP-REMAINING-TERM      TO PC-REMAINING-TERM.            ELC53PD
00509      COMPUTE PC-REMAINING-BENEFIT = WS-REMAINING-AMT -            ELC53PD
00510                                     PC-CC-PRIOR-DEATH-AMT.        ELC53PD
00511                                                                   ELC53PD
00512      IF WS-REDUCING                                               ELC53PD
00513          GO TO 0467-CHECK-REDUCE-CLAIM.                           ELC53PD
00514                                                                   ELC53PD
00515  0460-CHECK-LEVEL-CLAIM.                                          ELC53PD
00516      MOVE WK-C3 TO UNSIGN-WRK3.                                   ELC53PD
00517      IF UNSIGN-WRK3 GREATER THAN WS-CLAIM-TOL                     ELC53PD
00518          MOVE X2823              TO PC-ERR-FLAG (23).             ELC53PD
00519                                                                   ELC53PD
00520      GO TO 0469-PART-DEATH.                                       ELC53PD
00521                                                                   ELC53PD
00522  0467-CHECK-REDUCE-CLAIM.                                         ELC53PD
00523      MOVE WK-C3 TO UNSIGN-WRK3.                                   ELC53PD
00524      IF UNSIGN-WRK3 GREATER THAN WS-CLAIM-TOL                     ELC53PD
00525          MOVE X2823              TO PC-ERR-FLAG (23).             ELC53PD
00526                                                                   ELC53PD
00527  0469-PART-DEATH.                                                 ELC53PD
00528      GO TO 3999-END-EDIT.                                         ELC53PD
00529      EJECT                                                        ELC53PD
00530                                                                   ELC53PD
00531  0500-EDIT-AH-CLAIM.                                              ELC53PD
00532      MOVE PC-CERT-EFF-DT         TO CP-CERT-EFF-DT.               ELC53PD
00533      MOVE CM-LOAN-1ST-PMT-DT     TO CP-FIRST-PAY-DATE.            ELC53PD
00534                                                                   ELC53PD
00535      IF PC-PAID-THRU-DT NOT = LOW-VALUES                          ELC53PD
00536         MOVE PC-PAID-THRU-DT     TO CP-VALUATION-DT               ELC53PD
00537        ELSE                                                       ELC53PD
00538         MOVE PC-INCURRED-DT      TO CP-VALUATION-DT.              ELC53PD
00539                                                                   ELC53PD
00540      MOVE '1'                    TO CP-REM-TERM-METHOD.           ELC53PD
00541      MOVE WS-REM-TRM-CALC-OPTION TO CP-REM-TRM-CALC-OPTION.       ELC53PD
00542      MOVE PC-COMPANY-ID          TO CP-COMPANY-ID.                ELC53PD
00543      MOVE PC-COMPANY-CD          TO CP-COMPANY-CD.                ELC53PD
00544      MOVE CM-AH-ORIG-TERM        TO CP-ORIGINAL-TERM.             ELC53PD
00545                                                                   ELC53PD
00546      PERFORM 7600-GET-REMAIN-TERM THRU 7600-EXIT.                 ELC53PD
00547                                                                   ELC53PD
00548      MOVE CP-REMAINING-TERM-3    TO PC-REMAINING-TERM.            ELC53PD
00549      COMPUTE PC-REMAINING-BENEFIT = CP-REMAINING-TERM-3 *         ELC53PD
00550                                     CM-AH-BENEFIT-AMT.            ELC53PD
00551                                                                   ELC53PD
00552      IF PC-CLAIM-PAYMENT GREATER THAN PC-REMAINING-BENEFIT        ELC53PD
00553         MOVE X2865               TO PC-ERR-FLAG (65).             ELC53PD
00554                                                                   ELC53PD
00555      COMPUTE WS-PAY-TOL = PC-CLAIM-PAYMENT - (CM-AH-BENEFIT-AMT   ELC53PD
00556                           / 30 * PC-NO-OF-DAYS-PAID)              ELC53PD
00557                                                                   ELC53PD
00558      IF PC-COMPANY-ID NOT = 'MIL'                                 ELC53PD
00559         IF WS-PAY-TOL GREATER THAN CM-AH-BENEFIT-AMT              ELC53PD
00560            MOVE X2866               TO PC-ERR-FLAG (66).          ELC53PD
00561                                                                   ELC53PD
00562      IF CM-AH-BENEFIT-CD = '00'                                   ELC53PD
00563         IF PC-CHARGEBLE-EXPENSE OR PC-NON-CHARGEBLE-EXPENSE       ELC53PD
00564            NEXT SENTENCE                                          ELC53PD
00565          ELSE                                                     ELC53PD
00566            MOVE X2824          TO PC-ERR-FLAG (24)                ELC53PD
00567            GO TO 3999-END-EDIT.                                   ELC53PD
00568                                                                   ELC53PD
00569      MOVE SPACES                 TO WS-ACCESS.                    ELC53PD
00570      MOVE PC-COMPANY-ID          TO CNTL-COMP-ID.                 ELC53PD
00571      MOVE '5'                    TO CNTL-REC-TYPE.                ELC53PD
00572      MOVE +0                     TO CNTL-SEQ-NO.                  ELC53PD
00573      MOVE CM-AH-BENEFIT-CD       TO WS-BEN-CD.                    ELC53PD
00574      MOVE WS-ACCESS              TO CNTL-ACCESS.                  ELC53PD
00575                                                                   ELC53PD
00576      PERFORM 7200-FIND-BENEFIT THRU 7200-EXIT.                    ELC53PD
00577                                                                   ELC53PD
00578      IF NO-BENEFIT-FOUND                                          ELC53PD
00579          MOVE X2834          TO PC-ERR-FLAG (34)                  ELC53PD
00580          GO TO 3999-END-EDIT.                                     ELC53PD
00581                                                                   ELC53PD
00582      MOVE CF-SPECIAL-CALC-CD   (SUB3) TO WS-AH-SPECIAL-CALC-CD.   ELC53PD
00583      MOVE CF-JOINT-INDICATOR   (SUB3) TO WS-AH-JOINT-INDICATOR.   ELC53PD
00584      MOVE CF-CO-EARNINGS-CALC  (SUB3) TO WS-AH-EARNINGS-CALC      ELC53PD
00585                                          WS-AH-REFUND-CALC.       ELC53PD
00586                                                                   ELC53PD
00587 *    IF CLASIC-CLAIMS-USED                                        ELC53PD
00588 *        IF AH-IS-OB-TYPE                                         ELC53PD
00589 *           MOVE '4' TO PC-CLAIM-TYPE                             ELC53PD
00590 *           GO TO 0575-PROCESS-REG.                               ELC53PD
00591                                                                   ELC53PD
00592      IF AH-IS-OB-TYPE AND PC-OB-AH-CLAIM                          ELC53PD
00593         MOVE SPACE               TO PC-ERR-FLAG (65)              ELC53PD
00594          GO TO 0575-PROCESS-REG.                                  ELC53PD
00595                                                                   ELC53PD
00596      IF AH-IS-OB-TYPE OR PC-OB-AH-CLAIM                           ELC53PD
00597          MOVE X2825                TO PC-ERR-FLAG (25)            ELC53PD
00598          GO TO 3999-END-EDIT.                                     ELC53PD
00599                                                                   ELC53PD
00600      MOVE PC-CERT-EFF-DT         TO DC-BIN-DATE-1.                ELC53PD
00601      MOVE PC-CC-ORIG-TERM        TO DC-ELAPSED-MONTHS.            ELC53PD
00602      MOVE 6                      TO DC-OPTION-CODE.               ELC53PD
00603      PERFORM 8500-DATE-CONVERT THRU 8500-EXIT.                    ELC53PD
00604                                                                   ELC53PD
00605      IF PC-INCURRED-DT GREATER DC-BIN-DATE-2                      ELC53PD
00606              MOVE X2826          TO PC-ERR-FLAG (26).             ELC53PD
00607                                                                   ELC53PD
00608      IF PC-CC-CANCEL-DT NOT = LOW-VALUE                           ELC53PD
00609          IF PC-CC-CANCEL-DT NOT GREATER PC-INCURRED-DT            ELC53PD
00610              MOVE X2831          TO PC-ERR-FLAG (31).             ELC53PD
00611                                                                   ELC53PD
00612      IF PC-LUMP-SUM-PAYMENT                                       ELC53PD
00613          GO TO 0574-PROCESS-LUMP-SUM.                             ELC53PD
00614                                                                   ELC53PD
00615      IF PC-CC-DEATH-DT NOT = LOW-VALUE                            ELC53PD
00616          IF PC-CC-DEATH-DT NOT GREATER PC-INCURRED-DT             ELC53PD
00617              MOVE X2828          TO PC-ERR-FLAG (28).             ELC53PD
00618                                                                   ELC53PD
00619      IF PC-CC-CANCEL-DT NOT = LOW-VALUE                           ELC53PD
00620          IF PC-CC-CANCEL-DT NOT GREATER PC-INCURRED-DT            ELC53PD
00621              MOVE X2829          TO PC-ERR-FLAG (29).             ELC53PD
00622                                                                   ELC53PD
00623      IF PC-CC-SETTLEMENT-DT NOT = LOW-VALUE                       ELC53PD
00624          IF PC-CC-SETTLEMENT-DT NOT GREATER PC-INCURRED-DT        ELC53PD
00625              MOVE X2827          TO PC-ERR-FLAG (27).             ELC53PD
00626                                                                   ELC53PD
00627  0573-CHECK-PAID-THRU.                                            ELC53PD
00628      IF PC-CC-DEATH-DT NOT = LOW-VALUE                            ELC53PD
00629          IF PC-CC-DEATH-DT NOT GREATER PC-PAID-THRU-DT            ELC53PD
00630              MOVE X2836          TO PC-ERR-FLAG (36).             ELC53PD
00631                                                                   ELC53PD
00632      IF PC-CC-CANCEL-DT NOT = LOW-VALUE                           ELC53PD
00633          IF PC-CC-CANCEL-DT NOT GREATER PC-PAID-THRU-DT           ELC53PD
00634              MOVE X2837          TO PC-ERR-FLAG (37).             ELC53PD
00635                                                                   ELC53PD
00636      IF PC-CC-SETTLEMENT-DT NOT = LOW-VALUE                       ELC53PD
00637          IF PC-CC-SETTLEMENT-DT NOT GREATER PC-PAID-THRU-DT       ELC53PD
00638              MOVE X2835          TO PC-ERR-FLAG (35).             ELC53PD
00639                                                                   ELC53PD
00640      GO TO 0575-PROCESS-REG.                                      ELC53PD
00641                                                                   ELC53PD
00642  0574-PROCESS-LUMP-SUM.                                           ELC53PD
00643      IF PC-CC-DEATH-DT NOT = LOW-VALUE                            ELC53PD
00644          IF PC-CC-DEATH-DT NOT GREATER PC-INCURRED-DT             ELC53PD
00645              MOVE X2830          TO PC-ERR-FLAG (30).             ELC53PD
00646                                                                   ELC53PD
00647      IF PC-CC-CANCEL-DT NOT = LOW-VALUE                           ELC53PD
00648          IF PC-CC-CANCEL-DT NOT GREATER PC-INCURRED-DT            ELC53PD
00649              MOVE X2831          TO PC-ERR-FLAG (31).             ELC53PD
00650                                                                   ELC53PD
00651      IF PC-CC-SETTLEMENT-DT NOT = LOW-VALUE                       ELC53PD
00652          IF PC-CC-SETTLEMENT-DT NOT GREATER PC-INCURRED-DT        ELC53PD
00653              MOVE X2832          TO PC-ERR-FLAG (32).             ELC53PD
00654                                                                   ELC53PD
00655  0575-PROCESS-REG.                                                ELC53PD
00656      GO TO 3999-END-EDIT.                                         ELC53PD
00657      EJECT                                                        ELC53PD
00658                                                                   ELC53PD
00659  0500-EDIT-RESERVE-RECORD.                                        ELC53PD
00660      IF PC-CLAIM-TYPE = '1' OR '2'                                ELC53PD
00661          NEXT SENTENCE                                            ELC53PD
00662        ELSE                                                       ELC53PD
00663          MOVE X2816           TO PC-ERR-FLAG (16).                ELC53PD
00664                                                                   ELC53PD
00665      IF PC-LF-CLAIM                                               ELC53PD
00666          IF CM-LF-BENEFIT-CD = '00'                               ELC53PD
00667              MOVE X2857            TO PC-ERR-FLAG (57).           ELC53PD
00668                                                                   ELC53PD
00669      IF PC-AH-CLAIM                                               ELC53PD
00670          IF CM-AH-BENEFIT-CD = '00'                               ELC53PD
00671              MOVE X2858            TO PC-ERR-FLAG (58).           ELC53PD
00672                                                                   ELC53PD
00673      IF (PC-FUTURE-RESERVE-AMT +                                  ELC53PD
00674          PC-PTC-RESERVE-AMT +                                     ELC53PD
00675          PC-IBNR-RESERVE-AMT +                                    ELC53PD
00676          PC-MANUAL-RESERVE-AMT) = ZEROS                           ELC53PD
00677           MOVE X2813             TO PC-ERR-FLAG (13).             ELC53PD
00678                                                                   ELC53PD
00679      GO TO 3999-END-EDIT.                                         ELC53PD
00680                                                                   ELC53PD
00681      EJECT                                                        ELC53PD
00682  3999-END-EDIT.                                                   ELC53PD
00683      PERFORM 6000-UPDATE-CERT THRU 6999-EXIT.                     ELC53PD
00684      PERFORM 7800-REWRITE-CERT THRU 7800-EXIT.                    ELC53PD
00685      MOVE 'C'                    TO JP-RECORD-TYPE.               ELC53PD
00686      PERFORM 8100-JOURNAL-CERT THRU 8100-EXIT.                    ELC53PD
00687      GO TO 9990-RETURN.                                           ELC53PD
00688      EJECT                                                        ELC53PD
00689                                                                   ELC53PD
00690  6000-UPDATE-CERT.                                                ELC53PD
00691      IF PC-RESERVES OR                                            ELC53PD
00692         PC-CHARGEBLE-EXPENSE OR                                   ELC53PD
00693         PC-NON-CHARGEBLE-EXPENSE                                  ELC53PD
00694          GO TO 6999-EXIT.                                         ELC53PD
00695                                                                   ELC53PD
00696      IF PC-PUT-CERT-INFORCE                                       ELC53PD
00697         GO TO 6500-VOID-CLAIM.                                    ELC53PD
00698                                                                   ELC53PD
00699      IF PC-LF-CLAIM OR PC-OB-LF-CLAIM                             ELC53PD
00700         GO TO 6300-UPDATE-LIFE.                                   ELC53PD
00701                                                                   ELC53PD
00702   6100-UPDATE-AH.                                                 ELC53PD
00703      MOVE PC-PAID-THRU-DT         TO CM-AH-PAID-THRU-DT.          ELC53PD
00704                                                                   ELC53PD
00705      IF NOT PC-LUMP-SUM-PAYMENT                                   ELC53PD
00706         GO TO 6999-EXIT.                                          ELC53PD
00707                                                                   ELC53PD
00708      MOVE PC-INCURRED-DT          TO CM-AH-SETTLEMENT-DT.         ELC53PD
00709                                                                   ELC53PD
00710      COMPUTE CM-AH-ITD-LUMP-PMT = PC-CC-PRIOR-LUMP-PMT +          ELC53PD
00711                                   PC-CLAIM-PAYMENT.               ELC53PD
00712                                                                   ELC53PD
00713      ADD PC-CLAIM-PAYMENT          TO CM-AH-ITD-AH-PMT.           ELC53PD
00714                                                                   ELC53PD
00715      MOVE WS-CURRENT-BIN-DT        TO CM-AH-SETTLEMENT-EXIT-DT    ELC53PD
00716                                       CM-LF-CANCEL-EXIT-DT.       ELC53PD
00717                                                                   ELC53PD
00718      IF (CM-LF-STATUS-AT-DEATH    = SPACE) AND                    ELC53PD
00719         (CM-AH-STATUS-AT-SETTLEMENT = SPACE)                      ELC53PD
00720          MOVE CM-LF-CURRENT-STATUS TO CM-LF-STATUS-AT-CANCEL      ELC53PD
00721          MOVE CM-AH-CURRENT-STATUS TO CM-AH-STATUS-AT-SETTLEMENT  ELC53PD
00722          MOVE '8'                  TO CM-LF-CURRENT-STATUS        ELC53PD
00723          MOVE '6'                  TO CM-AH-CURRENT-STATUS.       ELC53PD
00724                                                                   ELC53PD
00725      GO TO 6999-EXIT.                                             ELC53PD
00726                                                                   ELC53PD
00727                                                                   ELC53PD
00728   6300-UPDATE-LIFE.                                               ELC53PD
00729      IF (CM-LF-STATUS-AT-DEATH    = SPACE) AND                    ELC53PD
00730         (CM-AH-STATUS-AT-SETTLEMENT = SPACE)                      ELC53PD
00731          MOVE CM-LF-CURRENT-STATUS     TO CM-LF-STATUS-AT-DEATH   ELC53PD
00732          MOVE CM-AH-CURRENT-STATUS     TO CM-AH-STATUS-AT-CANCEL  ELC53PD
00733          MOVE '7'                      TO CM-LF-CURRENT-STATUS    ELC53PD
00734          MOVE '8'                      TO CM-AH-CURRENT-STATUS    ELC53PD
00735          MOVE WS-CURRENT-BIN-DT        TO CM-AH-CANCEL-EXIT-DT    ELC53PD
00736                                           CM-LF-DEATH-EXIT-DT.    ELC53PD
00737                                                                   ELC53PD
00738      MOVE PC-INCURRED-DT           TO CM-LF-DEATH-DT.             ELC53PD
00739                                                                   ELC53PD
00740      COMPUTE CM-LF-ITD-DEATH-AMT = PC-CC-PRIOR-DEATH-AMT +        ELC53PD
00741                                    PC-CLAIM-PAYMENT.              ELC53PD
00742                                                                   ELC53PD
00743      MOVE '2'                      TO CM-CREDIT-INTERFACE-SW-2.   ELC53PD
00744                                                                   ELC53PD
00745      GO TO 6999-EXIT.                                             ELC53PD
00746                                                                   ELC53PD
00747  6500-VOID-CLAIM.                                                 ELC53PD
00748      MOVE ZERO                     TO CM-LF-ITD-DEATH-AMT.        ELC53PD
00749      MOVE LOW-VALUES               TO CM-LF-DEATH-DT              ELC53PD
00750                                       CM-LF-DEATH-EXIT-DT         ELC53PD
00751                                       CM-AH-CANCEL-EXIT-DT.       ELC53PD
00752      MOVE CM-LF-STATUS-AT-DEATH    TO CM-LF-CURRENT-STATUS.       ELC53PD
00753      MOVE CM-AH-STATUS-AT-CANCEL   TO CM-AH-CURRENT-STATUS.       ELC53PD
00754      MOVE SPACE                    TO CM-LF-STATUS-AT-DEATH       ELC53PD
00755                                       CM-AH-STATUS-AT-CANCEL.     ELC53PD
00756                                                                   ELC53PD
00757  6999-EXIT.                                                       ELC53PD
00758      EXIT.                                                        ELC53PD
00759      EJECT                                                        ELC53PD
00760                                                                   ELC53PD
00761 ******************************************************************ELC53PD
00762 ************* PROCEDURE DIVISION FOR EL053 & EL518 ***************ELC53PD
00763 ********************** ELC53PD END *******************************ELC53PD
00764                                                                   ELC53PD
